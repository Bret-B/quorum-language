package Libraries.Data.Formats.ScalableVectorGraphics
use Libraries.Containers.Array
use Libraries.Containers.Support.Pair

/*
    The Polyline class is an implementation of the polyline shape in the SVG standard. The 
    polyline is used to make open shapes using a set of points. Note that when using a set
    of points the points are pairs of absolute coordinates. Like other ScalableVectorGraphicsObjects 
    the polyline class inherits global attributes but it also has its own attributes.

    Attribute: Author Gabriel Contreras

    Attribute: Example

    use Libraries.Data.Formats.SVG.all

    Polyline polyline
    polyline:AddPoint(100,100)
    polyline:AddPoint(50,25)
    polyline:AddPoint(50,75)
    polyline:AddPoint(100,0)
    polyline:SetFill("none")
    polyline:SetStroke("black")

*/
class Polyline is ScalableVectorGraphicsObject
    constant text objectName = "polyline"
    private Array<Pair<integer>> points

    /*
        This action writes out all of the necessary tags and attributes that make up this
        polyline. This action will be called automatically by the parent ScalableVectorGraphics,
        as such this action does not need to be used directly by the user.

        Attribute: Returns the formatted string of all tags and attributes for this shape

    */
    action ToText returns text
        text result = ""
        text newLine = result:GetLineFeed()
        text tab = result:GetTab()
        text dq = result:GetDoubleQuote()

        result = "<" + objectName 
        result = result + GlobalAttributesToText()
        if points:IsEmpty() = false
            result = result + " points=" + dq
            integer i = 0
            repeat while i < points:GetSize()
                result = result + points:Get(i):GetFirstValue() + "," + points:Get(i):GetSecondValue() + " "
                i = i + 1
            end
            result = result + dq
        end
        if GetTitle() not= "" or GetDescription() not= ""
            result = result + ">" + newLine
            if GetTitle() not= ""
                Title title
                title:SetTitle(GetTitle())
                result = result + tab + tab + title:ToText() + newLine
            end
            if GetDescription() not= ""
                Description desc
                desc:SetDescription(GetDescription())
                result = result + tab + tab + desc:ToText() + newLine
            end
            result = result + tab
        end
        result = result + GetCloseTag(objectName, IsContainer()) + newLine
        return result
    end

    /*
        This action adds a point with a given x and y to the list of points that construct the polyline.
        Note that these points must be absolute coordinates. 

        Attribute: Parameter x The x position of the point.
        Attribute: Parameter y The y position of the point.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Polyline myLine
        myLine:AddPoint(50,50)
    */
    action AddPoint(integer x, integer y)
        Pair<integer> point
        point:Set(x, y)
        points:Add(point)
    end

    /*
        This action empties the list of points that construct the polyline.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Polyline myLine
        myLine:Empty()
    */
    action Empty
        points:Empty()
    end

end