package Libraries.Data.Formats.ScalableVectorGraphics

/*
    The Rectangle class is an implementation of the rectangle shape in the SVG standard. Like other
    ScalableVectorGraphicsObjects the rectangle class inherits global attributes but it also has
    its own attributes such as the position, width, and height.

    Attribute: Author Gabriel Contreras

    Attribute: Example

    use Libraries.Data.Formats.SVG.all

    // make an svg ellipse
    Rectangle rectangle

    rectangle:SetPosition(50, 50)
    rectangle:SetSize(75,75)
    rectangle:SetFill("red")

*/
class Rectangle is ScalableVectorGraphicsObject
    constant text objectName = "rect"
    private text width = ""
    private text height = ""
    private text x = ""
    private text y = ""
    private text rx = ""
    private text ry = ""

    /*
        This action writes out all of the necessary tags and attributes that make up this
        ellipse. This action will be called automatically by the parent ScalableVectorGraphics,
        as such this action does not need to be used directly by the user.

        Attribute: Returns the formatted string of all tags and attributes for this shape

    */
    action ToText returns text
        text result = ""
        text newLine = result:GetLineFeed()
        text tab = result:GetTab()
        text dq = result:GetDoubleQuote()

        result = "<" + objectName 
        result = result +" width=" +dq+ width +dq+" height=" +dq+ height +dq
        result = result + " x=" +dq+ x +dq+" y=" +dq+ y +dq
        if rx not= ""
            result = result + " rx=" +dq+ rx +dq
        end
        if ry not= ""
            result = result + " ry=" +dq+ ry +dq
        end
        result = result + GlobalAttributesToText()
        if GetTitle() not= "" or GetDescription() not= ""
            result = result + ">" + newLine
            if GetTitle() not= ""
                Title title
                title:SetTitle(GetTitle())
                result = result + tab + tab + title:ToText() + newLine
            end
            if GetDescription() not= ""
                Description desc
                desc:SetDescription(GetDescription())
                result = result + tab + tab + desc:ToText() + newLine
            end
            result = result + tab
        end
        result = result + GetCloseTag(objectName, IsContainer()) + newLine
        return result
    end

    /*
        This action sets the width and height of the rectangle.

        Attribute: Parameter width The width of the rectangle. 
        Attribute: Parameter height The height of the rectangle.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetSize(75,75)
    */
    action SetSize(number width, number height)
        me:width = "" + width
        me:height = "" + height
    end

    /*
        This action sets the width and height of the rectangle along with a unit.

        Attribute: Parameter width The width of the rectangle. 
        Attribute: Parameter height The height of the rectangle.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetSize(75,75)
    */
    action SetSize(number width, number height, text unit)
        me:width = "" + width + unit
        me:height = "" + height + unit
    end

    /*
        This action sets the width of the rectangle.

        Attribute: Parameter width The width of the rectangle. 

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetWidth(75)
    */
    action SetWidth(number width)
        me:width = "" + width
    end

    /*
        This action sets the height of the rectangle.

        Attribute: Parameter height The height of the rectangle. 

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetHeight(75)
    */
    action SetHeight(number height)
        me:height = "" + height
    end

    /*
        This action sets the width of the rectangle along with a unit.

        Attribute: Parameter width The width of the rectangle. 
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetWidth(50,"%")
    */
    action SetWidth(number width, text unit)
        me:width = "" + width + unit
    end

    /*
        This action sets the height of the rectangle along with a unit.

        Attribute: Parameter height The height of the rectangle.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetHeight(50,"%")
    */
    action SetHeight(number height, text unit)
        me:height = "" + height + unit
    end

    /*
        This action sets the position of the rectangle. Note that rectangles in SVG are positioned based
        on the top left vertex.

        Attribute: Parameter x The x position of the rectangle. 
        Attribute: Parameter y The y position of the rectangle.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetPosition(75,75)
    */
    action SetPosition(number x, number y)
        me:x = "" + x
        me:y = "" + y
    end

    /*
        This action sets the x position of the rectangle. Note that rectangles in SVG are positioned based
        on the top left vertex.

        Attribute: Parameter x The x position of the rectangle.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetXPosition(75)
    */
    action SetPositionX(number x)
        me:x = "" + x
    end

    /*
        This action sets the y position of the rectangle. Note that rectangles in SVG are positioned based
        on the top left vertex.

        Attribute: Parameter y The y position of the rectangle.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetPositionY(75)
    */
    action SetPositionY(number y)
        me:y = "" + y
    end

    /*
        This action sets the position of the rectangle. Note that rectangles in SVG are positioned based
        on the top left vertex.

        Attribute: Parameter x The x position of the rectangle. 
        Attribute: Parameter y The y position of the rectangle.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetPosition(15, 15, "%")
    */
    action SetPosition(number x, number y, text unit)
        me:x = "" + x + unit
        me:y = "" + y + unit
    end

    /*
        This action sets the x position of the rectangle. Note that rectangles in SVG are positioned based
        on the top left vertex.

        Attribute: Parameter x The x position of the rectangle.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetPositionX(75, "%")
    */
    action SetPositionX(number x, text unit)
        me:x = "" + x + unit
    end

    /*
        This action sets the y position of the rectangle. Note that rectangles in SVG are positioned based
        on the top left vertex.

        Attribute: Parameter y The y position of the rectangle.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetPositionY(75, "%")
    */
    action SetPositionY(number y, text unit)
        me:y = "" + y + unit
    end

    /*
        This action returns the x position of the rectangle with the unit appended. 

        Attribute: Returns the x position of the rectangle. 

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        rectangle rectangle
        output rectangle:GetPositionX()
    */
    action GetPositionX returns text
        return x
    end

    /*
        This action returns the y position of the rectangle with the unit appended. 

        Attribute: Returns the y position of the rectangle. 

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        rectangle rectangle
        output rectangle:GetPositionY()
    */
    action GetPositionY returns text
        return y
    end

    /*
        This action sets the corner radius of the rectangle. This will give the rectangle rounded edges.

        Attribute: Parameter rx The radius on the x axis.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetHorizontalCornerRadius(15)
    */
    action SetHorizontalCornerRadius(number rx)
        me:rx = "" + rx
    end

    /*
        This action sets the corner radius of the rectangle. This will give the rectangle rounded edges.

        Attribute: Parameter ry The radius on the y axis.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetVerticalCornerRadius(15)
    */
    action SetVerticalCornerRadius(number ry)
        me:ry = "" + ry
    end

    /*
        This action sets the corner radius of the rectangle. This will give the rectangle rounded edges.

        Attribute: Parameter rx The radius on the x axis.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetHorizontalCornerRadius(15, "%")
    */
    action SetHorizontalCornerRadius(number rx, text unit)
        me:rx = "" + rx + unit
    end

    /*
        This action sets the corner radius of the rectangle. This will give the rectangle rounded edges.

        Attribute: Parameter ry The radius on the y axis.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        use Libraries.Data.Formats.SVG.all

        Rectangle rectangle
        rectangle:SetVerticalCornerRadius(15, "%")
    */
    action SetVerticalCornerRadius(number ry, text unit)
        me:ry = "" + ry + unit
    end

end