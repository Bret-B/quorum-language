package Libraries.Data.Formats.ScalableVectorGraphics

use Libraries.Containers.Array
/*
    The Rectangle class is an implementation of the rectangle shape in the SVG standard. Like other
    ScalableVectorGraphicsObjects the rectangle class inherits global attributes but it also has
    its own attributes such as the position, width, and height.

    Attribute: Author Tim Kluthe

    Attribute: Example

    Libraries.Data.Formats.ScalableVectorGraphics.all

    // make an svg group
    Group group
    Rectangle rectangle1
    Rectangle rectangle2

    rectangle1:SetPosition(50, 50)
    rectangle1:SetSize(75,75)
    rectangle1:SetFill("red")

    rectangle1:SetPosition(100, 100)
    rectangle1:SetSize(50,50)
    rectangle1:SetFill("red")

    group:Add(rectangle1)
    group:Add(rectangle2)

*/
class Group is ScalableVectorGraphicsObject
    constant text objectName = "g"
    Array<ScalableVectorGraphicsObject> children


    on create
        SetIsContainer(true)
    end

    /*
        This action writes out all of the necessary tags and attributes that make up this
        ellipse. This action will be called automatically by the parent ScalableVectorGraphics,
        as such this action does not need to be used directly by the user.

        Attribute: Returns the formatted string of all tags and attributes for this shape

    */
    action ToText returns text
        text result = ""
        text newLine = result:GetLineFeed()
        text tab = result:GetTab()
        text dq = result:GetDoubleQuote()

        result = "<" + objectName 
        
        result = result + GlobalAttributesToText()
        result = result + ">" + newLine + newLine
        if GetTitle() not= "" or GetDescription() not= ""
            if GetTitle() not= ""
                Title title
                title:SetTitle(GetTitle())
                result = result + tab+ title:ToText() + newLine
            end
            if GetDescription() not= ""
                Description desc
                desc:SetDescription(GetDescription())
                result = result + tab + desc:ToText() + newLine
            end
        end
        integer i = 0
        repeat while i < children:GetSize()
            result = result + tab + tab + children:Get(i):ToText()
            i = i + 1
        end
        result = result + tab + GetCloseTag(objectName, IsContainer()) + newLine
        return result
    end


    /*
        This action adds other ScalableVectorGraphicsObjects as children to this object. When Group
        is written out it will also write out all of the content of its children. This is used to help
        group SVG elements together. Note that the order at which objects are drawn on the
        document corresponds to the order they were added using this action.

        Attribute: Parameter object The ScalableVectorGraphicsObject to add as a child.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        ScalableVectorGraphics canvas
        Group group
        Circle circle
        canvas:Add(group)
        group:Add(circle)
    */
    action Add(ScalableVectorGraphicsObject object)
        children:Add(object)
    end

    /*
        This action removes all of the children that have been added. 

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Group group
        Circle circle
        group:Add(circle)

        group:Empty()
    */
    action Empty
        children:Empty()
    end

end