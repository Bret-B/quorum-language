package Libraries.Data.Database

/*
    The DropTable class is a query to either remove a table from a database or to remove all data from it. 
    DropTable contains only two attributes of importance, the name of the table to manipulate and whether 
    or not we are deleting all of the data or the data and the table itself. 

    Attribute: Author Andreas Stefik

    Attribute: Example

    use Libraries.Data.Database

    DropTable table
    table:SetTable("Users")
    
*/
class DropTable is Query
    private constant text TABLE_ERROR = "Cannot drop a table with an empty or undefined name."
    private constant text TABLE_NOT_FOUND = "Could not find table"
    private constant text VIEW_ERROR = "Cannot empty a view only remove it"

    boolean empty = false
    
    action ConvertToStructuredQueryLanguage returns text
        text table = GetTable()
        if table = undefined or table:IsEmpty()
            alert(TABLE_ERROR)
        end

        DatabaseMetaData meta = GetDatabaseMetaData()
        DatabaseTable search = meta:GetTable(table)
        if search = undefined
            alert(TABLE_NOT_FOUND)
        end



        text result = ""

        //are we being asked to get rid of the entire table, or just the data.
        if search:IsView()
            if empty
                alert(VIEW_ERROR)
            end
            result = "DROP VIEW" + table
        else
            if not empty
                result = "DROP TABLE " + table + ";"
            else
                result = "TRUNCATE TABLE " + table + ";"
            end
        end
        return result
    end

     /*
        If this flag is true, when issued this DropTable query will drop the data inside the table, but not the table itself.
        This is equivalent to the TRUNCATE command in Structured Query Language (SQL).
    */
    action IsEmptyTableOnly returns boolean
        return empty
    end

    /*
        If this flag is true, when issued this DropTable query will drop the data inside the table, but not the table itself.
        This is equivalent to the TRUNCATE command in Structured Query Language (SQL).
    */
    action SetEmptyOnly(boolean empty)
        me:empty = empty
    end

end