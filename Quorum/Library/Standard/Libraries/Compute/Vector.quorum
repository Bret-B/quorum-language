package Libraries.Compute

use Libraries.Containers.Number64BitArray
use Libraries.Containers.Support.Comparison

/*
    This class represents a mathematical vector of number values. 
    
    Attribute: Author Andreas Stefik
    Attribute: Example
    
    use Libraries.Compute.Vector

    Vector vector
    vector:SetSize(2)
    vector:Set(0, 4.8)
    vector:Set(1, 3.2)
    
    output vector:ToText()
*/
class Vector
    Number64BitArray array
    constant text ERROR = "Operation cannot be performed unless the vectors are the same size."

    /* 
        This action returns a number at a particular point in the vector.

        Attribute: Parameter index where in the vector to get a number
        Attribute: Returns the value to return

        Attribute: Example

        use Libraries.Compute.Vector
        Vector vector
        vector:SetSize(3)
        output vector:Get(0)
    */
    action Get(integer index) returns number
        return array:Get(index)
    end

    /* 
        This action sets a number at a particular point in the vector.

        Attribute: Parameter index where in the vector to get a number
        Attribute: Parameter value the value to put in the vector.

        Attribute: Returns the value to return

        Attribute: Example

        use Libraries.Compute.Vector
        Vector vector
        vector:SetSize(3)
        vector:Set(0, 2)
        output vector:Get(0)
    */
    action Set(integer index, number value)
        array:Set(index, value)
    end

    /* 
        This action returns the size of the vector

        Attribute: Returns the size

        Attribute: Example

        use Libraries.Compute.Vector
        Vector vector
        vector:SetSize(3)
        output vector:GetSize()
    */
    action GetSize returns integer
        return array:GetSize()
    end

    /*
        This action returns a boolean value, true if the container is empty and false if it contains any items. 

        Attribute: Returns Returns true when the container is empty and false when it is not. 

        Attribute: Example
        use Libraries.Containers.Array
        Vector myArray
        myArray:SetSize(10)
        myArray:Set(0, 22) 
        myArray:Set(1, 33)
        myArray:Set(2, 45)
        myArray:Set(3, 22)
        boolean result = myArray:IsEmpty()
        output result
    */
    action IsEmpty returns boolean
        return GetSize() = 0
    end

    /* 
        This action sets the size of the vector. Note that this action must be called before the vector
        is used. This is because the vector class is optimized for memory.

        Attribute: size Sets the size of the vector.

        Attribute: Example

        use Libraries.Compute.Vector
        Vector vector
        vector:SetSize(3)
        output vector:GetSize()
    */
    action SetSize(integer size)
        array:SetSize(size)
    end

    /*
        This action adds this vector onto the current one. Because this action changes the vector
        being added on, a copy may be needed for non-destructive operations.

        Attribute: vector The vector to add on to this one.

        Attribute: Example

        use Libraries.Compute.Vector
        Vector vector
        vector:SetSize(3)

        Vector vector2
        vector2:SetSize(3)
        vector:Add(vector2)

        //this will output 0s because both vectors are 0
        output vector:ToText()
    */
    action Add(Vector vector)
        if GetSize() not= vector:GetSize()
            alert(ERROR)
        end

        i = 0
        repeat while i < GetSize()
            Set(i, Get(i) + vector:Get(i))
            i = i + 1
        end
    end

    /*
        This action subtracts this vector onto the current one. Because this action changes the vector
        being added on, a copy may be needed for non-destructive operations.

        Attribute: vector The vector to subtract on to this one.

        Attribute: Example

        use Libraries.Compute.Vector
        Vector vector
        vector:SetSize(3)

        Vector vector2
        vector2:SetSize(3)
        vector:Subtract(vector2)

        //this will output 0s because both vectors are 0
        output vector:ToText()
    */
    action Subtract(Vector vector)
        if GetSize() not= vector:GetSize()
            alert(ERROR)
        end

        i = 0
        repeat while i < GetSize()
            Set(i, Get(i) - vector:Get(i))
            i = i + 1
        end
    end

    /*
        This action calculates the dot product between the two vectors. More information 
        can be found about dot products here: https://en.wikipedia.org/wiki/Dot_product

        Attribute: vector The vector to dot product with

        Attribute: Example

        use Libraries.Compute.Vector
        Vector vector
        vector:SetSize(3)

        Vector vector2
        vector2:SetSize(3)
        output vector:DotProduct(vector2)
    */
    action DotProduct(Vector vector) returns number
        if GetSize() not= vector:GetSize()
            alert(ERROR)
        end

        number sum = 0
        i = 0
        repeat while i < GetSize()
            sum = sum + Get(i) * vector:Get(i)
            i = i + 1
        end
        return sum
    end
    /*
        This action returns a copy of the current vector. The new vector's
        components are a deep copy.
    
        Attribute: Returns a new vector that is a deep copy.
    
        Attribute: Example
        
        use Libraries.Compute.Vector
    
        Vector vector
        vector:SetSize(2)
        vector:Set(0, 4.8)
        vector:Set(1, 3.2)
        
        Vector copyVector
        copyVector = vector:Copy()
        output copyVector:ToText()
    */
    action Copy returns Vector
        Vector vector
        vector:SetSize(me:GetSize())
        i = 0
        repeat while i < me:GetSize()
            vector:Set(i, Get(i))
            i = i + 1
        end
        return vector
    end

    /*
        This action returns a text representation of the vector.
    
        Attribute: Returns a text representation of the vector
    
        Attribute: Example
        
        use Libraries.Compute.Vector
    
        Vector vector
        vector:SetSize(2)
        vector:Set(0, 4.8)
        vector:Set(1, 3.2)
        
        output vector:ToText()
    */
    action ToText returns text
        text result = "|"
        i = 0
        repeat while i < me:GetSize()
            result = result + Get(i)
            if i + 1 < GetSize()
                result = result + ", "
            else
                result = result + "|"
            end
            i = i + 1
        end
        return result
    end

    /*
        This action sorts the values of the array using a merge sort algorithm.
        It is guaranteed to execute in O(n log n).

        Attribute: Example
        use Libraries.Containers.Array
        Vector myArray
        myArray:Add(33)
        myArray:Add(13)
        myArray:Add(43)
        myArray:Sort()
    */
    action Sort
        integer size = GetSize()
        if size = 0 or size = 1
            return now //it is already sorted
        end
        Vector temp
        temp:SetSize(me:GetSize())
        MergeSort(me, temp, 0, me:GetSize() - 1)
    end


    private action MergeSort(Vector original, Vector temp,
        integer left, integer right)

        if left < right 
           center = (left + right) / 2
           MergeSort(original, temp, left, center)
           MergeSort(original, temp, center + 1, right)
           Merge(original, temp, left, center + 1, right)
        end
    end
    
    private action Merge(Vector original, Vector temp,
        integer left, integer center, integer right)
        leftEnd = center - 1
        leftTemp = left
        rightTemp = right
        centerTemp = center
        position = left

        repeat while leftTemp <= leftEnd and centerTemp <= right 
            number l = original:Get(leftTemp)
            number r = original:Get(centerTemp)
            integer result = l:Compare(r)
            if  result = -1 or result = 0
                temp:Set(position, original:Get(leftTemp))
                leftTemp = leftTemp + 1
            else 
                temp:Set(position, original:Get(centerTemp))
                centerTemp = centerTemp + 1
            end
            position = position + 1
        end

        repeat while leftTemp <= leftEnd
            temp:Set(position, original:Get(leftTemp))
            leftTemp = leftTemp + 1
            position = position + 1
        end

        repeat while centerTemp <= right
            temp:Set(position, original:Get(centerTemp))
            centerTemp = centerTemp + 1
            position = position + 1
        end

        size = right - left + 1
        i = left
        repeat size times
            original:Set(i, temp:Get(i))
            i = i + 1
        end
    end
end