package Libraries.Compute.Statistics

use Libraries.Containers.Array
use Libraries.Containers.HashTable
use Libraries.Containers.Stack
use Libraries.Containers.Iterator
use Libraries.System.File
use Libraries.Compute.Statistics.DataFrameCalculation
use Libraries.Compute.Statistics.Loaders.CommaSeparatedLoader
use Libraries.Compute.Statistics.Loaders.CommaSeparatedSaver
use Libraries.Compute.Matrix
use Libraries.Compute.Statistics.Tests.CompareGroups
use Libraries.Compute.Statistics.Inputs.ColumnInput
use Libraries.Compute.Statistics.Tests.CompareGroupToMean
use Libraries.Compute.Statistics.Tests.CorrelateGroups
use Libraries.Compute.Statistics.Tests.CorrelateRanks
use Libraries.Compute.Statistics.Tests.CompareVariances
use Libraries.Compute.Statistics.Tests.Regression
use Libraries.Compute.Statistics.Inputs.FactorInput
use Libraries.Compute.Statistics.Calculations.Mean
use Libraries.Compute.Statistics.Calculations.Variance
use Libraries.Compute.Statistics.Calculations.StandardDeviation
use Libraries.Compute.Statistics.Calculations.InterQuartileRange
use Libraries.Compute.Statistics.Calculations.Median
use Libraries.Compute.Statistics.Calculations.Summarize
use Libraries.Compute.Statistics.Calculations.Kurtosis
use Libraries.Compute.Statistics.Calculations.Skew
use Libraries.Interface.Controls.Charts.Chart
use Libraries.Compute.Statistics.Charts.HistogramCreator
use Libraries.Interface.Controls.Charts.Histogram
use Libraries.Interface.Controls.Charts.BarChart
use Libraries.Compute.Statistics.Charts.BarChartCreator
use Libraries.Language.Compile.Parsing.QuorumLexer
use Libraries.Language.Compile.CompilerErrorManager
use Libraries.Language.Compile.Parsing.QuorumParser
use Libraries.Language.Compile.Context.ParseContext
use Libraries.Compute.Statistics.Transforms.DataFrameExpressionListener
use Libraries.Language.Compile.Interpreter.Runtime
use Libraries.Language.Compile.Translate.QuorumOpcode
use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Interpreter.Result
use Libraries.Language.Compile.Interpreter.RuntimeBlock
use Libraries.Compute.Statistics.Charts.LineChartCreator
use Libraries.Interface.Controls.Charts.LineChart
use Libraries.Compute.Statistics.Charts.ViolinChartCreator
use Libraries.Interface.Controls.Charts.ViolinChart
use Libraries.Compute.Statistics.Transforms.CrossTab
use Libraries.Interface.Controls.Charts.ScatterPlot
use Libraries.Compute.Statistics.Charts.ScatterPlotCreator
use Libraries.Compute.Statistics.Columns.NumberColumn
use Libraries.Compute.Statistics.Transforms.DataTuple
use Libraries.Compute.Statistics.Columns.IntegerColumn
use Libraries.Compute.Statistics.Columns.TextColumn
use Libraries.Compute.Statistics.Columns.BooleanColumn
use Libraries.Compute.Statistics.Transforms.DataTupleComparison

/*
    The DataFrame class is a collection of columns and rows, like a spreadsheet, 
    that can be used for statistics and other calculations. By default, it can load
    comma separated files. Other file types can be supported using the Load action with 
    a file loader for the custom type. DataFrame objects can also be transformed
    using the Transform action, which is useful for sorting, filtering, or other
    operations. Transforms generally make a copy of the data frame and act on that copy,
    not the original.

    Attribute: Author Andreas Stefik

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame

    //Load a comma separated file
    DataFrame frame
    frame:Load("Data.csv") 
*/
class DataFrame
    Array<DataFrameColumn> columns
    DataFrameSelection selection = undefined
    HashTable<integer, DataFrameColumn> columnsOnLoad = undefined

    /*
        This action adds a column that, when the DataFrame is loaded
        will be used for processing a particular column. This will allow
        the loader to use customized type information specific to a particular
        file or situation.

        Attribute: Parameter index the position of the index on loading. For example, 
            an index of means the column at index 0, if one is loaded.
        Attribute: Parameter column the DataFrameColumn to use and enter into the 
            DataFrame.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        DataFrame frame
        NumberColumn column
        frame:AddColumnOnLoad(0, column)

        frame:Load("Data/Sheet.csv")
        output frame:ToText()
    */
    action AddColumnOnLoad(integer index, DataFrameColumn column)
        if columnsOnLoad = undefined
            HashTable<integer, DataFrameColumn> col
            columnsOnLoad = col
        end

        columnsOnLoad:Add(index, column)
    end

    /*
        This action returns a column to load with at a particular index, if one
        exists.
    */
    action GetColumnOnLoad(integer index) returns DataFrameColumn 
        if columnsOnLoad not= undefined
            return columnsOnLoad:GetValue(index)
        end
        return undefined
    end
    
    /*
        This action removes a column from the on load procedure. 
    */
    action RemoveColumnOnLoad(integer index)
        columnsOnLoad:RemoveAllKeys(index)
    end

    /*
        This action empties the columns loaded and frees up memory from 
        the initialization. After any load operation, this action is automatically
        called.
    */
    action EmptyColumnsOnLoad
        columnsOnLoad = undefined //free up memory
    end

    /*
        This action loads a data frame from a file. 

        Attribute: Parameter file The file to load.
        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        use Libraries.System.File

        //Load a comma separated file
        DataFrame frame
        File file
        file:SetPath("Data.csv")
        frame:Load(file) 
    */
    action Load(File file)
        if file not= undefined and file:Exists()
            text extension = file:GetFileExtension()
            if extension = "csv"
                CommaSeparatedLoader loader
                Load(file, loader)
            end
        elseif file = undefined
            alert("I cannot load an undefined file.")
        elseif not file:Exists()
            alert("I could not locate the file at " + file:GetAbsolutePath() + ". Did you spell it correctly?")
        end
        EmptyColumnsOnLoad()
    end
    
    
    /*
        This action takes a Quorum expression text value and then filters the DataFrame on a copy. 
        The expression follows the normal rules for Quorum, using the DataFrame's columns as the allowable variables. 
        For example, if a DataFrame has a column, Group, and is an integer, then a value like Group = 2
        would look for any value in the column that has the integer 2 and retain only those rows. 

        The filter call is on a copy, meaning it does not change the original DataFrame. If we instead want
        to filter destructively, we should call Filter instead.

        Attribute: text A Quorum expression, which will be parsed and executed on the DataFrame.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        DataFrame frame
        frame:Load("file.csv")
        DataFrame newFrame = frame:Copy("Group > 3")
        output newFrame:ToText()
    */
    action Copy(text source) returns DataFrame
        DataFrame copy = cast(DataFrame, me:Copy())
        copy:Filter(source)

        return copy
    end

    /*
        This action takes a Quorum expression text value and then filters the DataFrame. The expression
        follows the normal rules for Quorum, using the DataFrame's columns as the allowable variables. 
        For example, if a DataFrame has a column, Group, and is an integer, then a value like Group = 2
        would look for any value in the column that has the integer 2 and retain only those rows. 

        The filter call is destructive, meaning it changes the DataFrame itself. If we instead want
        to obtain a new DataFrame with only the non-filtered data, but to retain this one, the same
        expression can be used in the Copy action.

        Attribute: text A Quorum expression, which will be parsed and executed on the DataFrame.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        DataFrame frame
        frame:Load("file.csv")
        frame:Filter("Group > 3")
        output frame:ToText()
    */
    action Filter(text source)
        QuorumLexer luthor
        CompilerErrorManager errors
        luthor:SetCompilerErrorManager(errors)
        luthor:Read(source)

        if not errors:IsCompilationErrorFree()
            alert(errors:GetFirstMessageAsText())
        end

        QuorumParser parser
        parser:SetCompilerErrorManager(errors)
        parser:SetLexer(luthor)

        ParseContext expression = parser:Expression()

        if expression = undefined or not errors:IsCompilationErrorFree()
            alert(errors:GetFirstMessageAsText())
        end
    
        //Now do semantic analysis on the thing and get it setup for runtime processing
        DataFrameExpressionListener listener
        listener:SetCompilerErrorManager(errors)
        listener:SetDataFrame(me)
        expression:Traverse(listener)
        
        if not errors:IsCompilationErrorFree()
            alert(errors:GetFirstMessageAsText())
        end

        //the top-most item on the stack is the final expression
        Stack<QuorumOpcode> stack = listener:GetOpcodeStack()

        if stack:IsEmpty()
            alert("The expression contained an unidentified error and could not be evaluated.")
        end
        QuorumOpcode op = stack:Pop()

        Type result = op:GetType()
        if not result:IsBoolean()
            alert("The expression passed must evaluate to a boolean, but evaluated to a " + result:GetStaticKey() + ".")
        end

        Array<text> variables
        integer startingPosition = 0

        Array<integer> sizes
        integer change = 0
        repeat while change < me:GetSize()
            sizes:Add(startingPosition)
            change = change + 1
        end

        //if we still look good, then create a runtime context
        //and check it against every row, changing the value for every column
        Runtime runtime
        row = startingPosition
        integer skip = 0
        boolean done = false
        repeat while not done
            //first check that all the variables in the list are there
            //if there are zero variables, then just process the expression for now, even though technically it's a constant
            boolean missingValue = SetupRuntime(listener, runtime, me, row)
            boolean markForRemoval = false
            
            //if there are missing values, this row can't be evaluated
            //so mark it for removal
            if not missingValue
                op:Evaluate(runtime)
                
                Result value = runtime:PopFromStack()
                if value = undefined or not value:booleanValue
                    markForRemoval = true
                    skip = skip + 1
                end
            else
                markForRemoval = true
                skip = skip + 1
            end

            //now move the row if necessary, possibly skipping along
            MoveRow(me, row, skip, sizes, markForRemoval)

            row = row + 1
            done = not HasAnotherRow(me, row)
            runtime:GetCurrentBlock():Empty()
        end

        i = 0
        repeat while i < sizes:GetSize()
            integer size = sizes:Get(i)
            DataFrameColumn col = me:GetColumn(i)
            col:SetSize(size)
            i = i + 1
        end
    end

    private action SetupRuntime(DataFrameExpressionListener listener, Runtime runtime, DataFrame frame, integer row) returns boolean
        integer column = 0
        RuntimeBlock block = runtime:GetCurrentBlock()
        boolean hasMissingValue = false

        //get the row for each column
        repeat while column < frame:GetSize()
            DataFrameColumn dc = frame:GetColumn(column)
            if row < dc:GetSize() //get the value at this point and put it in as a variable name
                text header = dc:GetHeader()
                boolean hasVar = listener:HasVariable(header)
                Result result = GetResultFromColumn(dc, row)
                if result not= undefined
                    block:SetVariableResult(header, result)
                elseif hasVar and result = undefined
                    hasMissingValue = true
                end
            end
            column = column + 1
        end
        return hasMissingValue
    end

    private action GetResultFromColumn(DataFrameColumn column, integer row) returns Result
        if column:IsUndefined(row)
            return undefined
        end

        Result result
        Type type
        result:type = type
        if column:IsIntegerColumn()
            type:SetToInteger()
            result:integerValue = column:GetAsInteger(row)
        elseif column:IsNumberColumn()
            type:SetToNumber()
            result:numberValue = column:GetAsNumber(row)
        elseif column:IsBooleanColumn()
            type:SetToBoolean()
            result:booleanValue = column:GetAsBoolean(row)
        elseif column:IsTextColumn()
            type:SetToText()
            result:textValue = column:GetAsText(row)
        end

        return result
    end

    private action GetTypeFromColumn(DataFrameColumn column) returns Type
        Type type
        if column:IsIntegerColumn()
            type:SetToInteger()
        elseif column:IsNumberColumn()
            type:SetToNumber()
        elseif column:IsBooleanColumn()
            type:SetToBoolean()
        elseif column:IsTextColumn()
            type:SetToText()
        end

        return type
    end

    private action HasAnotherRow(DataFrame frame, integer row) returns boolean
        integer column = 0
        //get the row for each column
        repeat while column < frame:GetSize()
            DataFrameColumn dc = frame:GetColumn(column)
            if row < dc:GetSize()
                return true
            end
            column = column + 1
        end
        return false
    end

    private action MoveRow(DataFrame frame, integer row, integer skip, Array<integer> sizes, boolean markForRemoval)
        if skip = 0
            i = 0
            repeat while i < sizes:GetSize()
                sizes:Set(i, sizes:Get(i) + 1)
                i = i + 1
            end
            return now
        end

        integer column = 0
        integer i = row
        repeat while column < frame:GetSize()
            DataFrameColumn col = frame:GetColumn(column)

            //make sure there are actually rows here
            if i + 1 < col:GetSize()
                col:Move(i+1, i + 1 - skip)
                if not markForRemoval
                    sizes:Set(column, sizes:Get(column) + 1)
                end
            elseif i = col:GetSize() - 1 //only move this if it's not removed
                if not markForRemoval
                    col:Move(i, i - skip)
                    sizes:Set(column, sizes:Get(column) + 1)
                end
            end
            column = column + 1
        end
    end

    private action MoveRow(integer row1, integer row2)
        integer column = 0
        integer startIndex = 0
        integer endIndex = 0

        if row1 < row2
            startIndex = row1
            endIndex = row2
        elseif row1 > row2
            startIndex = row2
            endIndex = row1
        else
            return
        end

        repeat while column < GetSize()
            DataFrameColumn col = GetColumn(column)

            //make sure there are actually rows here
            if startIndex < col:GetSize() and endIndex < col:GetSize()
                col:Move(startIndex, endIndex)
            end
            column = column + 1
        end
    end


    /*
        This action reads a comma separated list of header names and determines the indices 
        from this list. This action is inherently strict, where if the parsing fails, the headers are 
        not unique, or there are other issues in the list, this action throws an error. 

        Attribute: Parameter headers the columns to sort
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:Sort("name1,name2")
        output frame:ToText()
    */
    action Sort(text headers)
        Array<integer> sortOrder = GetColumnsFromHeaderNames(headers)
        if(sortOrder:GetSize() > 0)
            integer size = GetColumn(sortOrder:Get(0)):GetSize()
            if size = 0 or size = 1
                return now //it is already sorted
            end
            //Array<Type> types = GetTypesFromColumns(sortOrder)
            //Type type = types:Get(sortDepth)
            Array<integer> newPositions
            DataFrame temp = me:Copy()
            MergeSort(sortOrder, temp, 0, GetColumn(0):GetSize() - 1)
        end
    end

    /*
        This action takes an Array of column indexes and returns the Type associated to each of the DataFrameColumns.
    */
    /*
    private action GetTypesFromColumns(Array<integer> columns) returns Array<Type>
        Array<Type> temp
        integer i = 0
        repeat while i < columns:GetSize()
            temp:Add(GetTypeFromColumn(GetColumn(columns:Get(i))))
            i = i + 1
        end
        return temp
    end
    */

    private action MergeSort(Array<integer> sortOrder, DataFrame temp,
        integer left, integer right)

        if left < right 
           center = (left + right) / 2
           MergeSort(sortOrder, temp, left, center)
           MergeSort(sortOrder, temp, center + 1, right)
           Merge(sortOrder, temp, left, center + 1, right)
        end
    end

    private action Merge(Array<integer> sortOrder, DataFrame temp,
        integer left, integer center, integer right)
        leftEnd = center - 1
        leftTemp = left
        rightTemp = right
        centerTemp = center
        position = left
        
        repeat while leftTemp <= leftEnd and centerTemp <= right
            DataTuple l = GetSortingTuple(sortOrder, leftTemp)
            DataTuple r = GetSortingTuple(sortOrder, centerTemp)
            DataTupleComparison comparison
            integer result = comparison:Compare(l,r)
            if  result = -1 or result = 0
                temp:SetRow(me, position, leftTemp)
                leftTemp = leftTemp + 1
            else 
                temp:SetRow(me, position, centerTemp)
                centerTemp = centerTemp + 1
            end
            position = position + 1
        end

        repeat while leftTemp <= leftEnd
            temp:SetRow(me, position, leftTemp)
            leftTemp = leftTemp + 1
            position = position + 1
        end

        repeat while centerTemp <= right
            temp:SetRow(me, position, centerTemp)
            centerTemp = centerTemp + 1
            position = position + 1
        end

        size = right - left + 1
        i = left
        repeat size times
            SetRow(temp,i,i)
            i = i + 1
        end
    end

    /*
        This set an entire row of the calling DataFrame (me) from a row in the the passed DataFrame (frame).
    */
    private action SetRow(DataFrame frame, integer to, integer from)
        integer col = 0
        if GetSize() not= frame:GetSize()
            alert("Mismatched size in SetRow.")
        end
        repeat while col < GetSize()
            if me:GetColumn(col):IsNumberColumn() and frame:GetColumn(col):IsNumberColumn()
                NumberColumn numCol1 = cast(NumberColumn, me:GetColumn(col))
                NumberColumn numCol2 = cast(NumberColumn, frame:GetColumn(col))
                numCol1:SetAsNumber(to,numCol2:Get(from))
            elseif me:GetColumn(col):IsIntegerColumn() and frame:GetColumn(col):IsIntegerColumn()
                IntegerColumn intCol1 = cast(IntegerColumn, me:GetColumn(col))
                IntegerColumn intCol2 = cast(IntegerColumn, frame:GetColumn(col))
                intCol1:SetAsInteger(to,intCol2:Get(from))
            elseif me:GetColumn(col):IsTextColumn() and frame:GetColumn(col):IsTextColumn()
                TextColumn textCol1 = cast(TextColumn, me:GetColumn(col))
                TextColumn textCol2 = cast(TextColumn, frame:GetColumn(col))
                textCol1:SetAsText(to,textCol2:Get(from))
            elseif me:GetColumn(col):IsBooleanColumn() and frame:GetColumn(col):IsBooleanColumn()
                BooleanColumn boolCol1 = cast(BooleanColumn, me:GetColumn(col))
                BooleanColumn boolCol2 = cast(BooleanColumn, frame:GetColumn(col))
                boolCol1:SetAsBoolean(to,boolCol2:Get(from))
            else
                alert("Potential type mismatch in SetRow.")
            end
            col = col + 1
        end
    end

    private action GetSortingTuple(Array<integer> sortOrder, integer row) returns DataTuple
        DataTuple tuple
        integer i = 0
        repeat while i < sortOrder:GetSize()
            DataFrameColumn col = GetColumn(sortOrder:Get(i))
            if col:IsNumberColumn()
                NumberColumn numCol = cast(NumberColumn, col)
                tuple:values:Add(numCol:Get(row))
            elseif col:IsIntegerColumn()
                IntegerColumn intCol = cast(IntegerColumn, col)
                tuple:values:Add(intCol:Get(row))
            elseif col:IsTextColumn()
                TextColumn textCol = cast(TextColumn, col)
                tuple:values:Add(textCol:Get(row))
            elseif col:IsBooleanColumn()
                BooleanColumn boolCol = cast(BooleanColumn, col)
                tuple:values:Add(boolCol:Get(row))
            else
                alert("Could not find the column type of column index: " + sortOrder:Get(i))
            end
            i = i + 1
        end
        return tuple
    end

    /*
        This action runs a calculation on the data frame. Calculations are not intended
        to be destructive to the original data.

        Attribute: Parameter calculation The calculation we want conducted on this frame.
        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        use Libraries.System.File

        //Load a comma separated file
        DataFrame frame
        File file
        file:SetPath("Data.csv")
        frame:Load(file)
    */
    action Calculate(DataFrameCalculation calculation)
        calculation:Calculate(me)
    end

    /*
        This action loads a data frame from a file relative to the working directory,
        which is typically where the executable lives.

        Attribute: Parameter location The file to load, parsed as text relative to the working directly. This is
                usually the directory of the executable.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv") 
    */
    action Load(text location)
        File file
        file:SetPath(location)
        Load(file)
    end

    /*
        This action obtains a column from the DataFrame. This column is the original, not a copy,
        so modifications made to the column will be permanent. If the goal is to obtain a copy, 
        then either the Copy actions or the Transform classes should be used.


        Attribute: Parameter index The column we want back.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv") 
        DataFrameColumn column = frame:GetColumn(0)
    */
    action GetColumn(integer index) returns DataFrameColumn
        return columns:Get(index)
    end

    /*
        This action replaces the columns in the DataFrame. It is needed by
        the Loader infrastructure in order to change the columns. However, for most users,
        the Transform infrastructure should be used instead of adjusting these manually.
        In other words, do not use this action unless you know what you are doing.

        Attribute: Parameter columns The new columns.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv") 
        Array<DataFrameColumn> col
        frame:SetColumns(col)
    */
    action SetColumns(Array<DataFrameColumn> columns)
        me:columns = columns
    end

    /*
        This action adds a column to the data frame. It is destructive in that it changes
        the existing DataFrame without making a copy.

        Attribute: Parameter column The column to add.

        Attribute: Example

        //We need the DataFrame class to load in files for Data Science operations.
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Statistics.Columns.NumberColumn
        use Libraries.Containers.Array
        use Libraries.Compute.Statistics.DataFrameColumn
        
        //Create a DataFrame, which is essentially a table that understands 
        //more information about the data that is being loaded.
        DataFrame frame
        
        //This creates a NumberColumn, which contains numbers
        NumberColumn column
        column:SetHeader("My Column")
        column:Add(1)
        column:Add(2)
        column:Add(3)
        column:Add(4)
        column:Add(5)
        column:Add(6)
        frame:AddColumn(column)
        
        //The system loaded the file, but can also output it a text value, or the console, if we want that.
        output frame:ToText()
    */
    action AddColumn(DataFrameColumn column)
        me:columns:Add(column)
    end

    /*
        This action adds a column to the data frame. It is destructive in that it changes
        the existing DataFrame without making a copy.

        Attribute: Parameter index the position of the column
        Attribute: Parameter column The column to add.
        Attribute: Example

        //We need the DataFrame class to load in files for Data Science operations.
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Statistics.Columns.NumberColumn
        use Libraries.Containers.Array
        use Libraries.Compute.Statistics.DataFrameColumn
        
        //Create a DataFrame, which is essentially a table that understands 
        //more information about the data that is being loaded.
        DataFrame frame
        
        //This creates a NumberColumn, which contains numbers
        NumberColumn column
        column:SetHeader("My Column")
        column:Add(1)
        column:Add(2)
        column:Add(3)
        column:Add(4)
        column:Add(5)
        column:Add(6)
        frame:AddColumn(0, column)
        
        //The system loaded the file, but can also output it a text value, or the console, if we want that.
        output frame:ToText()
    */
    action AddColumn(integer index, DataFrameColumn column)
        me:columns:Add(index, column)
    end

    /*
        This action removes a column from the data frame. It is destructive in that it changes
        the existing DataFrame without making a copy.

        Attribute: Parameter column The column to add.

        Attribute: Example

        //We need the DataFrame class to load in files for Data Science operations.
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Statistics.Columns.NumberColumn
        use Libraries.Containers.Array
        use Libraries.Compute.Statistics.DataFrameColumn
        
        //Create a DataFrame, which is essentially a table that understands 
        //more information about the data that is being loaded.
        DataFrame frame
        
        //This creates a NumberColumn, which contains numbers
        NumberColumn column
        column:SetHeader("My Column")
        column:Add(1)
        column:Add(2)
        column:Add(3)
        column:Add(4)
        column:Add(5)
        column:Add(6)
        frame:AddColumn(column)
        
        //The system loaded the file, but can also output it a text value, or the console, if we want that.
        output frame:ToText()
    */
    action RemoveColumnAt(integer index)
        me:columns:RemoveAt(index)
    end

    /*
        This action gets the columns in the DataFrame. This allows direct
        control of the columns for this particular data frame. We suggest not using 
        these values directly unless required by an application.

        Attribute: Parameter columns The new columns.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv") 
        Array<DataFrameColumn> col = frame:GetColumns()
    */
    action GetColumns returns Array<DataFrameColumn>
        return me:columns
    end
    /*
        This action returns the number of columns in the data frame. This value
        is not related to the number of rows in any particular column.

        Attribute: Returns the number of columns in the DataFrame.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv") 
        output frame:GetSize()
    */
    action GetSize returns integer
        return columns:GetSize()
    end

    /*
        This action returns true if the number of columns is zero

        Attribute: Returns true if the number of columns is zero.

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv") 
        output frame:IsEmpty()
    */
    action IsEmpty returns boolean
        return columns:GetSize() = 0
    end

    /*
        This action returns the first column with the name "header" in
        its header row. If multiple columns have the same name, then to 
        get them all, you will need to iterate and find each one using GetColumn(integer) instead. 


        Attribute: Parameter header The column we want back.
        Attribute: Returns the column, possibly undefined if no column of that name exists
        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv") 
        DataFrameColumn column = frame:GetColumn("Gender")
    */
    action GetColumn(text header) returns DataFrameColumn
        i = 0
        repeat while i < columns:GetSize()
            DataFrameColumn column = columns:Get(i)
            if column not= undefined and column:GetHeader() = header
                return column
            end
            i = i + 1
        end

        return undefined
    end

    /*
        This action loads data from a file and then places it into the existing data frame. 
        If data already exists in this data frame, it is discarded and replaced.

        Attribute: Parameter file The file we want processed
        Attribute: Parameter loader The object we want to manage loading the file type
        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Statistics.Loaders.CommaSeparatedLoader
        use Libraries.System.File

        //Load a comma separated file
        DataFrame frame
        File file
        file:SetPath("Data.csv")
        CommaSeparatedLoader loader
        frame:Load(file, loader) 
    */
    action Load(File file, DataFrameLoader loader)
        loader:Load(me, file)
        EmptyColumnsOnLoad()
    end

    /*
        This action data to a file from a data frame. 

        Attribute: Parameter file The file we want processed
        Attribute: Parameter saver The object we want to manage saving the file type
        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Statistics.Loaders.CommaSeparatedSaver
        use Libraries.System.File

        //Load a comma separated file
        DataFrame frame
        File file
        file:SetPath("Data.csv")
        CommaSeparatedLoader loader
        frame:Load(file, loader) 

        CommaSeparatedSaver saver
        frame:Save(file, saver)
    */
    action Save(File file, DataFrameSaver saver)
        saver:Save(me, file)
    end

    /*
        This action saves a data frame from a file relative to the working directory,
        which is typically where the executable lives. The file must have a csv file extension
        for this to save. Otherwise, it fails silently.

        Attribute: Parameter location The file to save. This action uses the default format of comma separate values (CSV).

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Save("Data.csv") 
    */
    action Save(text location)
        File file
        file:SetPath(location)

        text extension = file:GetFileExtension()
        if extension = "csv"
            CommaSeparatedSaver saver
            Save(file, saver)
        end
    end

    /*
        This action takes a DataFrame and converts it into a matrix with number values. If
        the data frame contains columns that cannot be converted to numbers, this action
        throws an exception. Finally, all columns must have the same size for this conversion to work.

        Attribute: Returns A matrix of real number values
        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Matrix

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv")

        //reverse the data frame
        Matrix matrix = frame:ConvertToMatrix()
        output matrix:ToText()
    */
    action ConvertToMatrix returns Matrix
        Matrix matrix

        if IsEmpty()
            return matrix
        end
        DataFrameColumn first = GetColumn(0)
        integer firstSize = first:GetSize()
        matrix:SetSize(first:GetSize(), GetSize())

        i = 0
        repeat while i < GetSize()
            DataFrameColumn column = GetColumn(i)
            if column:GetSize() not= firstSize
                alert("Cannot create an n x m Matrix, as the columns have different sizes")
            end
            
            integer j = 0
            repeat while j < column:GetSize()
                number value = column:GetAsNumber(j)
                matrix:Set(j, i, value)
                j = j + 1
            end
            i = i + 1
        end
        return matrix
    end

    /*
        This action takes the data from the current DataFrame then transforms into 
        a copy of this data frame. While custom Transforms can choose to adjust the original, 
        by default they do not. 

        Attribute: Parameter transform The object we want to copy and transform the frame
        Attribute: Returns Typically a copy of the DataFrame, transformed by the transformer.
        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Statistics.Transforms.ReverseTransform
        use Libraries.System.File

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv")

        //reverse the data frame
        ReverseTransform reverse
        frame:Transform(reverse) 
    */
    action Transform(DataFrameTransform transform) returns DataFrame
        return transform:Transform(me)
    end

    /*
        This action creates a chart, given a particular DataFrameChartCreator instance
        and returns a chart from it for this particular data.

        Attribute: Parameter creator The object that converts the DataFrame into a chart
        Attribute: Returns A chart object, which can be embedded into a user interface
        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Statistics.Charts.BarChartCreator
        use Libraries.System.File

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv")

        //We might instantiate an object to create a bar chart, setting some properties if we want to
        BarChartCreator create
        frame:CreateChart(create) 
    */
    action CreateChart(DataFrameChartCreator creator) returns Chart
        return creator:Create(me)
    end

    /*
        Returns a copy of the data frame, except that it only copies certain columns and rows

        Attribute: Parameter columnStart The 0-indexed first column
        Attribute: Parameter columnEnd The 0-indexed last column
        Attribute: Parameter rowStart The 0-indexed first row
        Attribute: Parameter rowEnd The 0-indexed last row
        Attribute: Returns A copy of the data frame with constrained columns and rows

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv")

        //return a copy with a max of the first five rows
        DataFrame copy = frame:Copy(0, frame:GetSize(), 0, 5)
    */
    action Copy(integer columnStart, integer columnEnd, integer rowStart, integer rowEnd) returns DataFrame
        DataFrame newFrame
        
        i = columnStart
        repeat while i < columnEnd
            DataFrameColumn column = GetColumn(i)
            DataFrameColumn copy = column:Copy(rowStart, rowEnd)
            newFrame:columns:Add(copy)
            i = i + 1
        end

        return newFrame
    end

    /*
        This action returns a copy of the data frame, which deep copies every column.

        Attribute: Returns A copy of the data frame with constrained columns and rows

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv")

        //return a copy of the entire DataFrame
        DataFrame copy = frame:Copy()
    */
    action Copy returns DataFrame
        DataFrame newFrame
        
        i = 0
        repeat while i < GetSize()
            DataFrameColumn column = GetColumn(i)
            DataFrameColumn copy = column:Copy()
            newFrame:columns:Add(copy)
            i = i + 1
        end

        return newFrame
    end

    
    /* 
        Classes can register as listeners of the selection in the DataFrame.
    */
    action Add(DataFrameSelectionListener listener)
        selection:Add(listener)
    end

    /* 
        Classes can also de-register as listeners of the selection in the DataFrame.
    */
    action Remove(DataFrameSelectionListener listener)
        selection:Remove(listener)
    end

    /* 
        This action returns an iterator of the listeners on the DataFrame's selection.
    */
    action GetListeners returns Iterator<DataFrameSelectionListener>
        return selection:GetListeners()
    end

    /*
        This adds adds columns to the selected range, starting from start and ending
        at finish, inclusive. In this case, this means that calculations will be conducted
        across this entire range.
    
        Attribute: Parameter start the start of the range
        Attribute: Parameter finish the end of the range
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0, 2)
        output frame:ToText()
    */
    action AddSelectedColumnRange(integer start, integer finish)
        CheckDefaultSelection()
        integer i = start
        repeat while i <= finish //inclusive
            AddSelectedColumn(i)
            i = i + 1
        end
    end

    /*
        This adds sets columns to the selected range, starting from start and ending
        at finish, inclusive. In this case, this means that calculations will be conducted
        across this entire range. If any previous range was indicated, it is removed.
    
        Attribute: Parameter start the start of the range
        Attribute: Parameter finish the end of the range
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:SetSelectedColumnRange(0, 2)
        output frame:ToText()
    */
    action SetSelectedColumnRange(integer start, integer finish)
        CheckDefaultSelection()
        selection:EmptyColumns()
        AddSelectedColumnRange(start, finish)
    end

    /*
        This adds removes columns from the selected range, starting from start and ending
        at finish, inclusive. In this case, this means that calculations will be conducted
        across this entire range.
    
        Attribute: Parameter start the start of the range
        Attribute: Parameter finish the end of the range
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0, 2)
        frame:RemoveSelectedColumnRange(1, 2)
        output frame:ToText()
    */
    action RemoveSelectedColumnRange(integer start, integer finish)
        if selection = undefined or selection:IsEmptyColumns()
            return now
        end
        integer i = start
        repeat while i <= finish //inclusive
            RemoveSelectedColumn(i)
            i = i + 1
        end
    end
    
    /*
        This adds adds factors to the selected range, starting from start and ending
        at finish, inclusive. In this case, this means that calculations will be conducted
        across this entire range.
    
        Attribute: Parameter start the start of the range
        Attribute: Parameter finish the end of the range
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactorRange(0, 2)
        output frame:ToText()
    */
    action AddSelectedFactorRange(integer start, integer finish)
        CheckDefaultSelection()
        integer i = start
        repeat while i <= finish //inclusive
            AddSelectedFactor(i)
            i = i + 1
        end
    end

    /*
        This adds sets factors to the selected range, starting from start and ending
        at finish, inclusive. In this case, this means that calculations will be conducted
        across this entire range. If any previous range was indicated, it is removed.
    
        Attribute: Parameter start the start of the range
        Attribute: Parameter finish the end of the range
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:SetSelectedFactorRange(0, 2)
        output frame:ToText()
    */
    action SetSelectedFactorRange(integer start, integer finish)
        CheckDefaultSelection()
        selection:EmptyColumns()
        AddSelectedFactorRange(start, finish)
    end

    /*
        This adds removes factors from the selected range, starting from start and ending
        at finish, inclusive. In this case, this means that calculations will be conducted
        across this entire range.
    
        Attribute: Parameter start the start of the range
        Attribute: Parameter finish the end of the range
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactorRange(0, 2)
        frame:RemoveSelectedFactorRange(1, 2)
        output frame:ToText()
    */
    action RemoveSelectedFactorRange(integer start, integer finish)
        if selection = undefined or selection:IsEmptyColumns()
            return now
        end
        integer i = start
        repeat while i <= finish //inclusive
            RemoveSelectedFactor(i)
            i = i + 1
        end
    end

    /*
        This action gets the selection in the DataFrame. 
    
        Attribute: Returns the selection of the frame
    */
    action GetSelection returns DataFrameSelection
        return selection
    end

    /* Private undefined checker. */
    private action CheckDefaultSelection
        if selection = undefined
            DataFrameSelection select
            select:SetFrame(me)
            me:selection = select
        end
    end

    /*
        This adds a column to the selected range.
    
        Attribute: Parameter index the column index of the column to add
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        output frame:ToText()
    */
    action AddSelectedColumn(integer index) 
        CheckDefaultSelection()

        selection:AddColumn(index)
    end

    /*
        This removes a column at the index of the selection.
    
        Attribute: Parameter index the selection index of the column to remove
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        frame:RemoveSelectedColumnAt(0)
        output frame:ToText()
    */
    action RemoveSelectedColumnAt(integer index)
        if selection = undefined or index < 0 or index >= selection:GetColumnSize()
            return now
        end

        selection:RemoveColumnAt(index)
    end

    /*
        This removes a column of a particular index anywhere from the selection.
    
        Attribute: Parameter index the column index of the column to remove
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        frame:RemoveSelectedColumn(0)
        output frame:ToText()
    */
    action RemoveSelectedColumn(integer index)
        if selection = undefined
            return now
        end

        selection:RemoveColumn(index)
    end

    /*
        This adds a factor of a particular index anywhere from the selection.
    
        Attribute: Parameter index the index of the factor to add
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactor(0)
        output frame:ToText()
    */
    action AddSelectedFactor(integer index) 
        CheckDefaultSelection()

        selection:AddFactor(index)
    end

    /*
        This removes a factor at a particular index from the selection.
    
        Attribute: Parameter index the selection index of the factor to remove
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactor(0)
        frame:RemoveSelectedFactorAt(0)
        output frame:ToText()
    */
    action RemoveSelectedFactorAt(integer index)
        if selection = undefined or index < 0 or index >= selection:GetFactorSize()
            return now
        end

        selection:RemoveFactorAt(index)
    end

    /*
        This removes a factor of a particular index anywhere from the selection.
    
        Attribute: Parameter index the factor index of the factor to remove
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactor(0)
        frame:RemoveSelectedFactor(0)
        output frame:ToText()
    */
    action RemoveSelectedFactor(integer index)
        if selection = undefined
            return now
        end

        selection:RemoveFactor(index)
    end

    /*
        This action empties the selection, so that no columns are selected.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactor(0)
        frame:EmptySelectedColumns()
        output frame:ToText()
    */
    action EmptySelectedColumns
        if selection = undefined
            return now
        end

        selection:EmptyColumns()
    end

    /*
        This action empties the selection, so that no factors are selected.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactor(0)
        frame:EmptySelectedFactors()
        output frame:ToText()
    */
    action EmptySelectedFactors
        if selection = undefined
            return now
        end

        selection:EmptyFactors()
    end

    /*
        This action obtains how many columns are selected in the selection.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        output frame:GetSelectedColumnSize()
    */
    action GetSelectedColumnSize returns integer
        if selection = undefined
            return 0
        end
        return selection:GetColumnSize()
    end

    /*
        This action obtains how many factors are selected in the selection.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactor(0)
        output frame:GetSelectedFactorSize()
    */
    action GetSelectedFactorSize returns integer
        if selection = undefined
            return 0
        end
        return selection:GetFactorSize()
    end

    /*
        This action receives a list of comma separated header values. It returns a list of all
        columns that would be selected by this list in sorted order by index. It ignores duplicate keys,
        but throws an error if either a header is not found or if the original DataFrame contains duplicate
        header names. As such, it is intentionally rigid to prevent the user from making accidental mistakes.
        The columns do not return in the same order as the header list, nor does it need to because
        it is designed to change the selection, which is not order dependent.
    */
    private action GetColumnsFromHeaderNames(text headers) returns Array<integer> 
        Array<integer> columns
        if headers = undefined or headers:IsEmpty()
            return columns
        end

        Array<text> split = headers:Split(",")
        if split = undefined or split:IsEmpty()
            return columns
        end

        //Hash all of the header names
        HashTable<text, integer> head
        i = 0
        repeat while i < GetSize()
            DataFrameColumn column = GetColumn(i)
            text key = column:GetHeader()

            //Don't allow duplicates, as this could lead to really easy user mistakes. If a user wants to use this system
            //it is on them to give unique header names. Otherwise they can specify columns by index
            if head:HasKey(key)
                alert("Detected non-unique column names in this data set, " + key + ". To use this helper action, all column names must be unique in the loaded DataFrame.")
            else
                head:Add(key, i)
            end
            i = i + 1
        end

        //now hash all of the values into an array, then sort it
        HashTable<text, integer> splitHash
        i = 0
        repeat while i < split:GetSize()
            text key = split:Get(i)
            if not head:HasKey(key)
                alert("Could not find key " + key + " in the DataFrame. Are you sure it exists?")
            else
                integer value = head:GetValue(key)
                splitHash:Add(key, value) //this automatically ignores duplicates, since it would hash the same
            end
            i = i + 1
        end

        Iterator<text> keys = splitHash:GetKeyIterator()
        repeat while keys:HasNext()
            text key = keys:Next()
            integer value = splitHash:GetValue(key)
            columns:Add(value)
        end
        columns:Sort()
        return columns
    end

    /*
        This action reads a comma separated list of header names and determines the indices 
        from this list. This action is inherently strict, where if the parsing fails, the headers are 
        not unique, or there are other issues in the list, this action throws an error. 

        Attribute: Parameter headers the columns to select
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumns("name1,name2")
        output frame:GetSelectedColumnSize()
    */
    action AddSelectedColumns(text headers) 
        CheckDefaultSelection()
        Array<integer> selected = GetColumnsFromHeaderNames(headers)
        integer i = 0
        repeat while i < selected:GetSize()
            AddSelectedColumn(selected:Get(i))
            i = i + 1
        end
    end

    /*
        This action reads a comma separated list of header names and determines the indices 
        from this list. This action is inherently strict, where if the parsing fails, the headers are 
        not unique, or there are other issues in the list, this action throws an error. 

        Attribute: Parameter headers the columns to select
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedFactors("name1,name2")
        output frame:GetSelectedColumnSize()
    */
    action AddSelectedFactors(text headers) 
        CheckDefaultSelection()
        Array<integer> selected = GetColumnsFromHeaderNames(headers)
        integer i = 0
        repeat while i < selected:GetSize()
            AddSelectedFactor(selected:Get(i))
            i = i + 1
        end
    end

    /*
        This action reads a comma separated list of header names and determines the indices 
        from this list. This action is inherently strict, where if the parsing fails, the headers are 
        not unique, or there are other issues in the list, this action throws an error. This 
        action removes any previous selection.

        Attribute: Parameter headers the columns to select
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:SetSelectedColumns("name1,name2")
        output frame:GetSelectedColumnSize()
    */
    action SetSelectedColumns(text headers) 
        CheckDefaultSelection()
        selection:EmptyColumns()
        AddSelectedColumns(headers)
    end

    /*
        This action reads a comma separated list of header names and determines the indices 
        from this list. This action is inherently strict, where if the parsing fails, the headers are 
        not unique, or there are other issues in the list, this action throws an error. This 
        action removes any previous selection.

        Attribute: Parameter headers the columns to select
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:SetSelectedFactors("name1,name2")
        output frame:GetSelectedColumnSize()
    */
    action SetSelectedFactors(text headers) 
        CheckDefaultSelection()
        selection:EmptyFactors()
        AddSelectedFactors(headers)
    end

    /*
        This action reads a comma separated list of header names and determines the indices 
        from this list. This action is inherently strict, where if the parsing fails, the headers are 
        not unique, or there are other issues in the list, this action throws an error. This 
        action removes the selection from the list of headers.

        Attribute: Parameter headers the columns to select
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:SetSelectedColumns("name1,name2")
        frame:RemoveSelectedColumns("name2")
        output frame:GetSelectedColumnSize()
    */
    action RemoveSelectedColumns(text headers)
        if selection = undefined
            return now
        end

        Array<integer> selected = GetColumnsFromHeaderNames(headers)

        i = 0
        repeat while i < selected:GetSize()
            integer value = selected:Get(i)
            RemoveSelectedColumn(value)
            i = i + 1    
        end
    end

    /*
        This action reads a comma separated list of header names and determines the indices 
        from this list. This action is inherently strict, where if the parsing fails, the headers are 
        not unique, or there are other issues in the list, this action throws an error. This 
        action removes the selection from the list of headers.

        Attribute: Parameter headers the columns to select
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:SetSelectedFactors("name1,name2")
        frame:RemoveSelectedFactors("name2")
        output frame:GetSelectedColumnSize()
    */
    action RemoveSelectedFactors(text headers)
        if selection = undefined
            return now
        end

        Array<integer> selected = GetColumnsFromHeaderNames(headers)

        i = 0
        repeat while i < selected:GetSize()
            integer value = selected:Get(i)
            RemoveSelectedFactor(value)
            i = i + 1    
        end
    end

    /* Checks if the selection is valid. */
    private action IsInvalidSelection returns boolean
        if selection = undefined or (selection:IsEmptyColumns() and selection:IsEmptyFactors())
            return true
        end
        return false
    end

    /*
        This action uses the selection to conduct a comparison between two or more columns. 
        The comparison is traditionally called a 1-way ANOVA. The CompareGroups object 
        returned gives information back in several formats, including text formatted in the 
        American Psychological Association (APA) style.

        Attribute: Returns an object representing the comparison
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        CompareGroups groups = CompareSelectedColumns()
        output groups:GetFormalSummary()
    */
    action CompareSelectedColumns returns CompareGroups
        if IsInvalidSelection()
            alert("Invalid Selection")
        end

        if selection:GetColumnSize() < 2
            alert("Must compare 2 or more columns")
        end

        CompareGroups groups
        //the cast isn't technically necessary, but I'm future proofing, as it could be down the road in case some tests allow both
        selection:CopyTo(cast(ColumnInput, groups))
        groups:Calculate(me)
        return groups
    end

    /*
        This action uses the selection to conduct a comparison between a column and a mean.
        The comparison is traditionally called a one sample t-test. The CompareGroupToMean object
        returned gives information back in several formats, including text formatted in the
        American Psychological Association (APA) style.

        Attribute: Returns an object representing the comparison
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        CompareGroupToMean result = CompareSelectedColumnToMean(0)
        output result:GetFormalSummary()
    */
    action CompareSelectedColumnToMean(number mean) returns CompareGroupToMean
        if IsInvalidSelection()
            alert("Invalid Selection")
        end

        if selection:GetColumnSize() not= 1
            alert("Must compare the mean to 1 column")
        end

        CompareGroupToMean groups
        groups:SetGroup(selection:GetColumn(0))
        groups:SetMean(mean)
        groups:Calculate(me)
        return groups
    end

    /*
        This action uses the selection to conduct a correlation between two or more columns. 
        The comparison is traditionally called a Pearson Correlation. The CompareGroups object 
        returned gives information back in several formats, including text formatted in the 
        American Psychological Association (APA) style.

        Attribute: Returns an object representing the comparison
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        CorrelateGroups groups = CorrelateSelectedColumns()
        output groups:GetFormalSummary()
    */
    action CorrelateSelectedColumns returns CorrelateGroups
        if IsInvalidSelection()
            alert("Invalid Selection")
        end

        if selection:GetColumnSize() < 2
            alert("Must compare 2 or more columns")
        end

        CorrelateGroups groups
        //the cast isn't technically necessary, but I'm future proofing, as it could be down the road in case some tests allow both
        selection:CopyTo(cast(ColumnInput, groups))
        groups:Calculate(me)
        return groups
    end

    /*
        This action uses the selection to conduct a correlation between the ranks of values
        between two or more columns. 
        The comparison is traditionally called a Spearman Correlation. The CompareGroups object 
        returned gives information back in several formats, including text formatted in the 
        American Psychological Association (APA) style.

        Attribute: Returns an object representing the comparison
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        CorrelateRanks groups = CorrelateSelectedRanks()
        output groups:GetFormalSummary()
    */
    action CorrelateSelectedRanks returns CorrelateRanks
        if IsInvalidSelection()
            alert("Invalid Selection")
        end

        if selection:GetColumnSize() < 2
            alert("Must compare 2 or more columns")
        end

        CorrelateRanks groups
        //the cast isn't technically necessary, but I'm future proofing, as it could be down the road in case some tests allow both
        selection:CopyTo(cast(ColumnInput, groups))
        groups:Calculate(me)
        return groups
    end

    /*
        This action uses the selection to conduct a comparison variances in columns. 
        The comparison is traditionally called a Levene's Test. The CompareGroups object 
        returned gives information back in several formats, including text formatted in the 
        American Psychological Association (APA) style.

        Attribute: Returns an object representing the comparison
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        CompareVariances groups = frame:CompareSelectedVariances()
        output groups:GetFormalSummary()
    */
    action CompareSelectedVariances returns CompareVariances
        if IsInvalidSelection()
            alert("Invalid Selection")
        end

        if selection:GetColumnSize() < 2
            alert("Must compare 2 or more columns")
        end

        CompareVariances groups
        //the cast isn't technically necessary, but I'm future proofing, as it could be down the road in case some tests allow both
        selection:CopyTo(cast(ColumnInput, groups))
        groups:Calculate(me)
        return groups
    end

    /*
        This action uses the selection to conduct a regression.
        The comparison is traditionally called a Regression. The CompareGroups object
        returned gives information back in several formats, including text formatted in the
        American Psychological Association (APA) style.

        Attribute: Returns an object representing the comparison
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        frame:AddSelectedFactor(1)
        frame:AddSelectedFactor(2)
        frame:AddSelectedFactor(3)
        Regression result = frame:RegressionOnSelected()
        output result:GetFormalSummary()
    */
    action RegressionOnSelected returns Regression
        if IsInvalidSelection()
            alert("Invalid Selection")
        end

        if selection:GetColumnSize() = 1
            alert("Must have a dependent variable having a column")
        end

        if selection:GetFactorSize() < 1
            alert("Must have at least one independent variable (factor column)")
        end

        Regression groups
        //the cast isn't technically necessary, but I'm future proofing, as it could be down the road in case some tests allow both
        selection:CopyTo(cast(FactorInput, groups))
        groups:Calculate(me)
        return groups
    end

    /* Checks if a single column is selected. */
    private action IsSingleColumnSelection returns boolean
        if IsInvalidSelection()
            return false
        end

        if selection:GetColumnSize() = 1
            return true
        end

        return false
    end

    /* Detects if more than one columns is selected. */
    private action IsMultiColumnSelection returns boolean
        if IsInvalidSelection()
            return false
        end

        if selection:GetColumnSize() >= 1
            return true
        end

        return false
    end

    /* Detects if more than one columns is selected. */
    private action IsMultiColumnSelection(integer columns) returns boolean
        if IsInvalidSelection()
            return false
        end

        if selection:GetColumnSize() > 1 and selection:GetColumnSize() = columns
            return true
        end

        return false
    end

    /*
        This action calculates the mean of the selected column.

        Attribute: Returns the mean
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        output frame:Mean()
    */
    action Mean returns number
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        Mean calculation
        GetColumn(selection:GetColumn(0)):Calculate(calculation)
        return calculation:GetMean()
    end

    /*
        This action calculates the mean of the selected columns. In this case, the full
        calculation objects are returned.

        Attribute: Returns an array of the calculations
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        Array<Mean> values = frame:MeanSelectedColumns()
    */
    action MeanSelectedColumns returns Array<Mean>
        if not IsMultiColumnSelection()
            alert("Can only conduct this calculation on one or more columns")
        end

        Array<Mean> calculations
        integer i = 0
        repeat while i < selection:GetColumnSize()
            Mean calculation
            GetColumn(selection:GetColumn(i)):Calculate(calculation)
            calculations:Add(calculation)
            i = i + 1
        end
        
        return calculations
    end

    /*
        This action calculates the variance of the selected column.

        Attribute: Returns the variance
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        output frame:Variance()
    */
    action Variance returns number
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        Variance calculation
        GetColumn(selection:GetColumn(0)):Calculate(calculation)
        return calculation:GetVariance()
    end

    /*
        This action calculates the variance of the selected columns. In this case, the full
        calculation objects are returned.

        Attribute: Returns an array of the calculations
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        Array<Variance> values = frame:VarianceSelectedColumns()
    */
    action VarianceSelectedColumns returns Array<Variance>
        if not IsMultiColumnSelection()
            alert("Can only conduct this calculation on one or more columns")
        end

        Array<Variance> calculations
        integer i = 0
        repeat while i < selection:GetColumnSize()
            Variance calculation
            GetColumn(selection:GetColumn(i)):Calculate(calculation)
            calculations:Add(calculation)
            i = i + 1
        end
        
        return calculations
    end

    /*
        This action calculates the standard deviation of the selected column.

        Attribute: Returns the standard deviation
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        output frame:StandardDeviation()
    */
    action StandardDeviation returns number
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        StandardDeviation calculation
        GetColumn(selection:GetColumn(0)):Calculate(calculation)
        return calculation:GetStandardDeviation()
    end

    /*
        This action calculates the standard deviation of the selected columns. In this case, the full
        calculation objects are returned.

        Attribute: Returns an array of the calculations
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        Array<StandardDeviation> values = frame:StandardDeviationSelectedColumns()
    */
    action StandardDeviationSelectedColumns returns Array<StandardDeviation>
        if not IsMultiColumnSelection()
            alert("Can only conduct this calculation on one or more columns")
        end

        Array<StandardDeviation> calculations
        integer i = 0
        repeat while i < selection:GetColumnSize()
            StandardDeviation calculation
            GetColumn(selection:GetColumn(i)):Calculate(calculation)
            calculations:Add(calculation)
            i = i + 1
        end
        
        return calculations
    end

    /*
        This action calculates the InterQuartileRange of the selected column.

        Attribute: Returns the interquartile range
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        InterQuartileRange range = frame:InterQuartileRange()
    */
    action InterQuartileRange returns InterQuartileRange
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        InterQuartileRange calculation
        GetColumn(selection:GetColumn(0)):Calculate(calculation)
        return calculation
    end

    /*
        This action calculates the interquartile range of the selected columns. 
        In this case, the full calculation objects are returned.

        Attribute: Returns an array of the calculations
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        Array<InterQuartileRange> values = frame:InterQuartileRangeSelectedColumns()
    */
    action InterQuartileRangeSelectedColumns returns Array<InterQuartileRange>
        if not IsMultiColumnSelection()
            alert("Can only conduct this calculation on one or more columns")
        end

        Array<InterQuartileRange> calculations
        integer i = 0
        repeat while i < selection:GetColumnSize()
            InterQuartileRange calculation
            GetColumn(selection:GetColumn(i)):Calculate(calculation)
            calculations:Add(calculation)
            i = i + 1
        end
        
        return calculations
    end

    /*
        This action calculates the median of the selected column.

        Attribute: Returns the median
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        output frame:Median()
    */
    action Median returns number
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        Median calculation
        GetColumn(selection:GetColumn(0)):Calculate(calculation)
        return calculation:GetMedian()
    end

    /*
        This action calculates the median of the selected columns. In this case, the full
        calculation objects are returned.

        Attribute: Returns an array of the calculations
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        Array<Median> values = frame:MedianSelectedColumns()
    */
    action MedianSelectedColumns returns Array<Median>
        if not IsMultiColumnSelection()
            alert("Can only conduct this calculation on one or more columns")
        end

        Array<Median> calculations
        integer i = 0
        repeat while i < selection:GetColumnSize()
            Median calculation
            GetColumn(selection:GetColumn(i)):Calculate(calculation)
            calculations:Add(calculation)
            i = i + 1
        end
        
        return calculations
    end

    /*
        This action calculates the skew of the selected column.

        Attribute: Returns the skew
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        output frame:Skew()
    */
    action Skew returns number
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        Skew calculation
        GetColumn(selection:GetColumn(0)):Calculate(calculation)
        return calculation:GetSkew()
    end

    /*
        This action calculates the skew of the selected columns. In this case, the full
        calculation objects are returned.

        Attribute: Returns an array of the calculations
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        Array<Skew> values = frame:SkewSelectedColumns()
    */
    action SkewSelectedColumns returns Array<Skew>
        if not IsMultiColumnSelection()
            alert("Can only conduct this calculation on one or more columns")
        end

        Array<Skew> calculations
        integer i = 0
        repeat while i < selection:GetColumnSize()
            Skew calculation
            GetColumn(selection:GetColumn(i)):Calculate(calculation)
            calculations:Add(calculation)
            i = i + 1
        end
        
        return calculations
    end

    /*
        This action calculates the kurtosis of the selected column.

        Attribute: Returns the kurtosis
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        output frame:Kurtosis()
    */
    action Kurtosis returns number
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        Kurtosis calculation
        GetColumn(selection:GetColumn(0)):Calculate(calculation)
        return calculation:GetKurtosis()
    end

    /*
        This action calculates the kurtosis of the selected columns. In this case, the full
        calculation objects are returned.

        Attribute: Returns an array of the calculations
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        Array<Kurtosis> values = frame:KurtosisSelectedColumns()
    */
    action KurtosisSelectedColumns returns Array<Kurtosis>
        if not IsMultiColumnSelection()
            alert("Can only conduct this calculation on one or more columns")
        end

        Array<Kurtosis> calculations
        integer i = 0
        repeat while i < selection:GetColumnSize()
            Kurtosis calculation
            GetColumn(selection:GetColumn(i)):Calculate(calculation)
            calculations:Add(calculation)
            i = i + 1
        end
        
        return calculations
    end

    /*
        This action calculates summary information for the column.

        Attribute: Returns the summary
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(0)
        Summarize summary = frame:Summarize()
    */
    action Summarize returns Summarize
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        Summarize calculation
        GetColumn(selection:GetColumn(0)):Calculate(calculation)
        return calculation
    end

    /*
        This action calculates summaries of the selected columns. In this case, the full
        calculation objects are returned.

        Attribute: Returns an array of the calculations
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
    
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumnRange(0,2)
        Array<Summarize> values = frame:SummarizeSelectedColumns()
    */
    action SummarizeSelectedColumns returns Array<Summarize>
        if not IsMultiColumnSelection()
            alert("Can only conduct this calculation on one or more columns")
        end

        Array<Summarize> calculations
        integer i = 0
        repeat while i < selection:GetColumnSize()
            Summarize calculation
            GetColumn(selection:GetColumn(i)):Calculate(calculation)
            calculations:Add(calculation)
            i = i + 1
        end
        
        return calculations
    end

    /*
        This action creates a Histogram from the current column selection in the DataFrame.
        By default, it uses the interquartile range to calculate its bin width. This
        can be overriden by calling Histogram(integer) and passing the bin width directly.

        Attribute: Returns a Histogram chart that can be displayed or placed into a user interface or game.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Interface.Controls.Charts.Histogram
        
        DataFrame frame
        frame:Load("Dogs.csv")
        frame:AddSelectedColumns("Maximum Life Span")
        Histogram chart = frame:Histogram()
        chart:SetTitle("Maximum Life Span of Dog Breeds")
        chart:SetXAxisTitle("Max Life Span")
        chart:Display()
    */
    action Histogram returns Histogram
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        HistogramCreator creator
        creator:SetColumn(selection:GetColumn(0))
        DataFrameColumn col = GetColumn(selection:GetColumn(0))
        
        Histogram histogram = cast(Histogram, CreateChart(creator))
        //set a default title.
        if col:GetHeader() not= undefined
            histogram:SetXAxisTitle(col:GetHeader())
        end
        return histogram
    end

    /*
        This action creates a Histogram from the current column selection in the DataFrame.
        By default, Histogram() uses the interquartile range to calculate its bin width, whereas in this version, 
        this is overriden and this bin width is used instead.

        Attribute: Returns a Histogram chart that can be displayed or placed into a user interface or game.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Interface.Controls.Charts.Histogram
        
        DataFrame frame
        frame:Load("Dogs.csv")
        frame:AddSelectedColumns("Maximum Life Span")
        Histogram chart = frame:Histogram(5)
        chart:SetTitle("Maximum Life Span of Dog Breeds")
        chart:SetXAxisTitle("Max Life Span")
        chart:Display()
    */
    action Histogram(integer binWidth) returns Histogram
        if not IsSingleColumnSelection()
            alert("Can only conduct this calculation on a single column")
        end

        HistogramCreator creator
        creator:SetColumn(selection:GetColumn(0))
        creator:SetBinWidth(binWidth)

        DataFrameColumn col = GetColumn(selection:GetColumn(0))
        
        Histogram histogram = cast(Histogram, CreateChart(creator))
        //set a default title.
        if col:GetHeader() not= undefined
            histogram:SetXAxisTitle(col:GetHeader())
        end
        return histogram
    end

    /*
        This action creates a BarChart from the current column selection in the DataFrame.
        By default, it uses the first column in the selection as the x-axis and the second 
        column as the y-axis. This can be reversed by changing the selection order.

        Attribute: Returns a BarChart chart that can be displayed or placed into a user interface or game.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Interface.Controls.Charts.BarChart
        
        DataFrame frame
        frame:Load("Data.csv")
        frame:SetSelectedColumnRange(0,1)
        BarChart chart = frame:BarChart()
        chart:SetTitle("My Awesome Title")
        chart:SetXAxisTitle("Time")
        chart:Display()
    */
    action BarChart returns BarChart
        BarChartCreator creator
        selection:CopyTo(cast(ColumnInput, creator))
        selection:CopyTo(cast(FactorInput, creator))

        return cast(BarChart, CreateChart(creator))
    end
    
    /*
        This action returns a text based representation of the data frame at a particular row.

        Attribute: Returns a text based representation, in comma separated format

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv")

        //output the frame to the console
        output frame:ToText(0)
    */
    action ToText(integer row) returns text
        DataFrame frame = me
        integer size = frame:GetSize() //number of columns

        text currentRow = ""
        text dq = currentRow:GetDoubleQuote()
        text lf = currentRow:GetLineFeed()
        
        i = 0
        repeat while i < frame:GetSize()
            DataFrameColumn column = frame:GetColumn(i)
            if row < column:GetSize()
                text value = column:GetAsText(row)
                if value not= undefined
                    if not value:IsEmpty() and value:Contains(",")
                        currentRow = currentRow + dq + value + dq
                    else
                        currentRow = currentRow + value
                    end
                end
                hadRow = true
            end
        
            
            if i not= frame:GetSize() - 1
                currentRow = currentRow + ","
            end
            i = i + 1
        end

        return currentRow
    end

    /*
        This action creates a LineChart from the current column selection in the DataFrame.
        By default, it uses all columns as separate lines in the selection as the chart area. 
        Only one factor is allowed and this factor controls the y-axis for all of the lines. The scale
        can be set manually by using the LineChart

        Attribute: Returns a BarChart chart that can be displayed or placed into a user interface or game.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Interface.Controls.Charts.LineChart
        
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(1)
        frame:AddSelectedFactor(0)
        LineChart chart = frame:BarChart()
        chart:SetTitle("My Awesome Title")
        chart:SetXAxisTitle("Time")
        chart:Display()
    */
    action LineChart returns LineChart
        LineChartCreator creator
        selection:CopyTo(cast(ColumnInput, creator))
        selection:CopyTo(cast(FactorInput, creator))

        return cast(LineChart, CreateChart(creator))
    end
    
    /*
        This action creates a ViolinChart from the current column selection in the DataFrame.
        By default, it uses all columns as separate values in the selection as the chart area. 
        Factors have no impact on this action.

        Attribute: Returns a chart that can be displayed or placed into a user interface or game.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Interface.Controls.Charts.ViolinChart
        
        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(1)
        ViolinChart chart = frame:ViolinChart()
        chart:SetTitle("My Awesome Title")
        chart:SetXAxisTitle("Time")
        chart:Display()
    */
    action ViolinChart returns ViolinChart
        ViolinChartCreator creator
        selection:CopyTo(cast(ColumnInput, creator))

        return cast(ViolinChart, CreateChart(creator))
    end

    /*
        This action creates a ScatterPlot from the current two column selection in the DataFrame.
        By default, it uses all columns as separate values in the selection as the chart area. 
        Factors have no impact on this action.

        Attribute: Returns a chart that can be displayed or placed into a user interface or game.
    
        Attribute: Example
        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Interface.Controls.Charts.ScatterPlot

        DataFrame frame
        frame:Load("Data.csv")
        frame:AddSelectedColumn(1)
        frame:AddSelectedColumn(2)
        ScatterPlot chart = frame:ScatterPlot()
        chart:SetTitle("My Awesome Title")
        chart:SetXAxisTitle("Time")
        chart:Display()
    */
    action ScatterPlot returns ScatterPlot
        ScatterPlotCreator creator
        selection:CopyTo(cast(ColumnInput, creator))

        return cast(ScatterPlot, CreateChart(creator))
    end

    /*
        The CrossTab class takes a DataFrame and its selection to decide how to transform it. Specifically, 
        CrossTab takes the first column in the selection and places it on the left-most column of the 
        new frame, then the second column and places it on top. In both cases, the columns are filtered 
        for unique items and sorted.
    
        Once these columns are placed, the CrossTab calculates how many of each unique item pair exist in 
        the original DataFrame. For example, if the first row of the original DataFrame has the value 'test'
        and the right-most value has '11.2,' then the position in the transformed CrossTab DataFrame will 
        increment this value in the table. Thus, if there were no other test, 11.2 pairs, this value in the 
        new DataFrame would be 1.
    
        Attribute: Author Andreas Stefik
    
        Attribute: Example
    
        use Libraries.Compute.Statistics.DataFrame
        
        //Create a DataFrame, load it, and set which column to focus on
        DataFrame frame
        frame:Load("Words.csv")
        frame:AddSelectedColumn(2)
        frame:AddSelectedColumn(3)
        
        DataFrame crossTab = frame:CrossTab()
        crossTab:Save("Cross.csv")
        output "File Saved."
    */
    action CrossTab returns DataFrame
        CrossTab tab
        selection:CopyTo(cast(ColumnInput, tab))
        return Transform(tab)
    end

    /*
        This action returns a text based representation of the data frame.

        Attribute: Returns a text based representation, in comma separated format

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame

        //Load a comma separated file
        DataFrame frame
        frame:Load("Data.csv")

        //output the frame to the console
        output frame:ToText()
    */
    action ToText returns text
        CommaSeparatedSaver saver
        return saver:ConvertToText(me)
    end
end