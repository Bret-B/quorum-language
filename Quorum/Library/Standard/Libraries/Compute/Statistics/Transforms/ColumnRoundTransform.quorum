package Libraries.Compute.Statistics.Transforms

use Libraries.Compute.Statistics.DataFrameTransform
use Libraries.Compute.Statistics.DataFrame
use Libraries.Compute.Statistics.DataFrameSelection
use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Compute.Math

/*
    This action takes the data from the current DataFrame then rounds the numbers in the selected columns.

    Attribute: Author Melissa Stefik 
    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Transforms.ColumnRoundTransform
    use Libraries.System.File

    //Load a comma separated file
    DataFrame frame
    frame:Load("Data.csv")

    //Get a copy of the data frame's first two columns, up to row 5.
    ColumnRoundTransform transform
    transform:SetDecimalPlace(2)
    frame:Transform(transform) 
*/
class ColumnRoundTransform is DataFrameTransform
    integer decimalPlaceValue = 1

    action Transform(DataFrame frame) returns DataFrame 

        DataFrameSelection selection = frame:GetSelection()
        integer size = frame:GetSelectedColumnSize()
        DataFrame newFrame = frame:Copy()
        integer decimalPlace = 3
        Math math
        i = 0
        repeat while i < size
            integer index = selection:GetColumn(i)
            DataFrameColumn column = newFrame:GetColumn(index)
            if column:IsNumberColumn()
                j = 0
                repeat while j < column:GetSize()
                    if not column:IsUndefined()
                        number value = column:GetAsNumber(j)
                        column:SetAsNumber(j, math:Round(value, decimalPlaceValue))
                    end
                    j = j + 1
                end

            else
                alert("All columns selected for rounding must contain numbers.")
            end

            i = i + 1
        end

        return newFrame
    end

    /* The decimal place to round to */
    action SetDecimalPlace(integer decimalPlace)
        if decimalPlace not= 0
            decimalPlaceValue = decimalPlace
        else
            alert("Cannot round the columns to the 0 place value. Try converting to an IntegerColumn instead.")
        end
    end
    
end