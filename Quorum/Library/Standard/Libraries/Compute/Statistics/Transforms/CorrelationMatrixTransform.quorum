package Libraries.Compute.Statistics.Transforms

use Libraries.Compute.Statistics.DataFrameTransform
use Libraries.Compute.Statistics.Inputs.ColumnInput
use Libraries.Compute.Statistics.DataFrame
use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Compute.Statistics.Columns.TextColumn
use Libraries.Compute.Statistics.Columns.NumberColumn
use Libraries.Compute.Statistics.Calculations.Correlation
use Libraries.Compute.Matrix

/*
    CorrelationMatrixTransform calculates the pearson correlation for every selected column against 
    the other selected columns in a DataFrame and returns a DataFrame of the correlations.
    For more information: https://en.wikipedia.org/wiki/Correlation

    Attribute: Author Hannah Williams

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Transforms.CorrelationMatrixTransform
    
    DataFrame frame
    frame:Load("Data.csv")
    
    CorrelationMatrixTransform cor
    cor:AddColumn(0)
    cor:AddColumn(1)
    cor:AddColumn(2)
    cor:AddColumn(3)
    DataFrame dataframeMatrix = frame:Transform(cor)
    dataframeMatrix:Save("Correlations.csv")
*/
class CorrelationMatrixTransform is DataFrameTransform, ColumnInput
    Matrix correlationMatrix = undefined

    action Transform(DataFrame frame) returns DataFrame 
        if GetColumnSize() < 1
            alert("At least one column must be selected to conduct a Correlation Matrix Transform.")
        end

        DataFrame newFrame
        TextColumn rows
        newFrame:AddColumn(rows)

        integer i = 0
        repeat while i < GetColumnSize()
            DataFrameColumn column1 = frame:GetColumn(GetColumn(i))
            if not column1:IsNumberColumn() and not column1:IsIntegerColumn()
                alert("Columns must be numerical. " + column1:GetHeader() + " is not a numerical column.")
            end
            rows:Add(column1:GetHeader())

            NumberColumn col
            col:SetHeader(column1:GetHeader())
            newFrame:AddColumn(col)

            integer j = 0
            repeat while j < GetColumnSize()
                repeat while j < i
                    col:Add(newFrame:GetColumn(j+1):GetAsNumber(i))
                    j = j + 1
                end
                DataFrameColumn column2 = frame:GetColumn(GetColumn(j))
                if not column2:IsNumberColumn() and not column2:IsIntegerColumn()
                    alert("Columns must be numerical. " + column2:GetHeader() + " is not a numerical column.")
                end

                if i = j
                    col:Add(1)
                else
                    Correlation cor
                    cor:Calculate(column1, column2)
                    col:Add(cor:GetCorrelation())
                end
                j = j + 1
            end
            i = i + 1
        end
        
        correlationMatrix = ConvertToMatrix(newFrame)
        return newFrame
    end

    /*
        This action returns the actual numerical matrix of this transform

        Attribute: Example

        use Libraries.Compute.Statistics.DataFrame
        use Libraries.Compute.Statistics.Transforms.CorrelationMatrixTransform
    
        DataFrame frame
        frame:Load("Data.csv")
        
        CorrelationMatrixTransform transform
        transform:AddColumn(0)
        transform:AddColumn(1)
        DataFrame dataframeMatrix = frame:Transform(transform)    
        Matrix actualMatrix = transform:GetMatrix()
    */
    action GetMatrix returns Matrix
        return correlationMatrix
    end

    private action ConvertToMatrix(DataFrame frame) returns Matrix
        Matrix matrix

        if frame not= undefined
            DataFrame copy = frame:Copy()
            // Remove first column (which is the row headers)
            copy:RemoveColumnAt(0)
            matrix = copy:ConvertToMatrix()
        end
        
        return matrix
    end
end