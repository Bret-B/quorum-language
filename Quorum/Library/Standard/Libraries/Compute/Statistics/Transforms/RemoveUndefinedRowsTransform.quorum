package Libraries.Compute.Statistics.Transforms

use Libraries.Compute.Statistics.DataFrameTransform
use Libraries.Compute.Statistics.DataFrame
use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Containers.Array

/*
    This action takes the data from the current DataFrame and then removes all undefined
    values from each row. In doing so, any row that contains at least one undefined value
    is removed and all other rows are pushed up. Maximum sizes are computed and the total
    set is truncated to the new column sizes.

    Attribute: Author Andreas Stefik 
    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Transforms.RemoveUndefinedRowsTransform

    //Load a comma separated file
    DataFrame frame
    frame:Load("Data.csv")

    //Get a copy of the data frame's first two columns, up to row 5.
    RemoveUndefinedRowsTransform transform
    frame:Transform(transform) 
    output frame:ToText()
*/
class RemoveUndefinedRowsTransform is DataFrameTransform
    boolean ignoreFirst = true

    action Transform(DataFrame frame) returns DataFrame 
        DataFrame newFrame = frame:Copy()

        //traverse every row and check each spot for an undefined value
        integer row = 0
        if ignoreFirst
            row = 1
        end

        Array<integer> sizes
        integer change = 0
        repeat while change < newFrame:GetSize()
            sizes:Add(row)
            change = change + 1
        end

        integer skip = 0
        Array<integer> rowsToRemove
        boolean done = false
        repeat while not done
            boolean markForRemoval = false
            if HasUndefined(newFrame, row)
                skip = skip + 1
                markForRemoval = true
            end

            MoveRow(newFrame, row, skip, sizes, markForRemoval)

            if not HasAnotherRow(frame, row)
                done = true
            end
            row = row + 1
        end

        i = 0
        repeat while i < sizes:GetSize()
            integer size = sizes:Get(i) + 1
            DataFrameColumn col = newFrame:GetColumn(i)
            col:SetSize(size)
            i = i + 1
        end
        
        return newFrame
    end

    private action HasAnotherRow(DataFrame frame, integer row) returns boolean
        integer column = 0
        //get the row for each column
        repeat while column < frame:GetSize()
            DataFrameColumn dc = frame:GetColumn(column)
            if row < dc:GetSize()
                return true
            end
            column = column + 1
        end
        return false
    end

    private action MoveRow(DataFrame frame, integer row, integer skip, Array<integer> sizes, boolean markForRemoval)
        if skip = 0
            i = 0
            repeat while i < sizes:GetSize()
                sizes:Set(i, sizes:Get(i) + 1)
                i = i + 1
            end
            return now
        end

        integer column = 0
        integer i = row
        repeat while column < frame:GetSize()
            DataFrameColumn col = frame:GetColumn(column)

            //make sure there are actually rows here
            if i + 1 < col:GetSize()
                col:Move(i+1, i + 1 - skip)
                if not markForRemoval
                    sizes:Set(column, sizes:Get(column) + 1)
                end
            end
            column = column + 1
        end
    end

    private action HasUndefined(DataFrame frame, integer row) returns boolean
        integer column = 0
        //get the row for each column
        repeat while column < frame:GetSize()
            DataFrameColumn dc = frame:GetColumn(column)
            if row < dc:GetSize()
                if dc:IsUndefined(row)
                    column = frame:GetSize()
                    return true
                end
            end
            column = column + 1
        end
        return false
    end

    /* Determines whether the first row should be ignored. */
    action IsIgnoreFirst returns boolean
        return ignoreFirst
    end

    /* Sets whether the first row should be ignored. */
    action SetIgnoreFirst(boolean ignoreFirst)
        me:ignoreFirst = ignoreFirst
    end
end