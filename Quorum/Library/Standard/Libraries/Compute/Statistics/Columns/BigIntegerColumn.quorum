package Libraries.Compute.Statistics.Columns

use Libraries.Containers.Array
use Libraries.Compute.Statistics.DataFrameColumnCalculation
use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Compute.Vector
use Libraries.Containers.HashTable
use Libraries.Containers.Iterator
use Libraries.Compute.BigInteger

/*
    BigIntegerColumn is a DataFrameColumn that contains BigInteger objects. These objects can be undefined
    or not. 

    Attribute: Author Andreas Stefik
*/
class BigIntegerColumn is DataFrameColumn
    Array<BigInteger> rows

    action Add(text value)
        if value = undefined
            rows:Add(undefined)
            parent:DataFrameColumn:undefinedSize = GetUndefinedSize() + 1
            return now
        end

        check 
            BigInteger num
            num:SetValue(value, 10)
            rows:Add(num)
        detect e
            rows:Add(undefined) //add a missing value
            parent:DataFrameColumn:undefinedSize = GetUndefinedSize() + 1
        end
    end

    /*
        By default, this replaces all undefined values with the BitInteger value of the text passed in.
        If the text passed in is not a BitInteger, then this call is ignored

        Attribute: Parameter value the value to be converted.        
    */
    action ReplaceUndefined(text value)
        if value = undefined
            return now
        end
        check 
            BigInteger num
            num:SetValue(value, 10) //should throw an error if it's wrong
            row = 0
            repeat while row < GetSize()
                if IsUndefined(row)
                    rows:Set(row, num)
                end
                row = row + 1
            end
        detect e
            return now
        end
    end

    action CanConvertToVector returns boolean
        return false
    end

    action IsUndefined(integer row) returns boolean
        return rows:Get(row) = undefined
    end

    action Get(integer row) returns BigInteger
        return rows:Get(row)
    end

    action GetAsText(integer index) returns text
        BigInteger value = rows:Get(index)
        if value = undefined
            return undefined
        else
            return "" + value:GetText()
        end
    end

    action SendValueTo(integer index, DataFrameColumnCalculation calculation)
        BigInteger num = rows:Get(index)
        calculation:Add(num)
    end

    action GetAsInteger(integer index) returns integer
        BigInteger num = rows:Get(index)
        if num = undefined
            alert("Cannot convert value at position " + index + " to integer.")
        end
        return num:ToInteger()
    end

    action IsNumberColumn returns boolean
        return false
    end

    action SetAsInteger(integer index, integer value)
        BigInteger num
        num:SetValue("" + value)
        rows:Set(index, num)
    end

    action SetSize(integer size)
        rows:SetSize(size)
    end

    action GetSize returns integer
        return rows:GetSize()
    end

    action Swap(integer left, integer right)
        BigInteger temp = undefined
        temp = rows:Get(left)
        rows:Set(left, rows:Get(right))
        rows:Set(right, temp)
    end

    action Move(integer left, integer right)
        rows:Set(right, rows:Get(left))
    end

    action Copy(integer rowStart, integer rowEnd) returns DataFrameColumn
        BigIntegerColumn column
        column:SetHeader(GetHeader())

        i = rowStart
        repeat while i < rowEnd
            BigInteger value = rows:Get(i)
            if value = undefined
                column:rows:Add(undefined)
            else
                BigInteger value2
                value2:SetValue(value:GetText())
                column:rows:Add(value2)
            end
            i = i + 1
        end

        return column
    end

    action Copy returns DataFrameColumn
        return Copy(0, rows:GetSize())
    end

    action Copy(integer rowStart, integer rowEnd, boolean sort, boolean unique) returns DataFrameColumn
        BigIntegerColumn column
        column:SetHeader(GetHeader())

        Array<BigInteger> newRows
        i = rowStart
        repeat while i < rowEnd
            BigInteger value = rows:Get(i)
            if value = undefined
                newRows:Add(undefined)
            else
                BigInteger value2
                value2:SetValue(value:GetText())
                newRows:Add(value2)
            end
            i = i + 1
        end

        //if it's unique or sortable, now do that
        if unique
            newRows = GetUnique(newRows)
        end

        if sort
            if GetSortComparison() not= undefined
                newRows:Sort(GetSortComparison())
            else
                newRows:Sort()
            end
        end
        column:rows = newRows

        return column
    end

    action Copy(boolean sort, boolean unique) returns DataFrameColumn
        return Copy(0, rows:GetSize(), sort, unique)
    end

    action CopyToRow(DataFrameColumn column, integer to, integer from)
        me:SetAsNumber(to,column:GetAsNumber(from))
    end

    private action CalculateValueCount(Array<BigInteger> values) returns HashTable<BigInteger,integer>
        HashTable<BigInteger, integer> hash
        i = 0
        repeat while i < rows:GetSize()
            if rows:Get(i) not= undefined
                if hash:HasKey(rows:Get(i))
                    hash:Add(rows:Get(i), hash:GetValue(rows:Get(i)) + 1)
                else
                    hash:Add(rows:Get(i), 1)
                end
            end
            i = i + 1
        end
        return hash
    end

    private action GetUnique(Array<BigInteger> array) returns Array<BigInteger>
        Array<BigInteger> set
        HashTable<BigInteger,integer> hash = CalculateValueCount(array)
        Iterator<BigInteger> iterator = hash:GetKeyIterator()
        repeat while iterator:HasNext()
            set:Add(iterator:Next())
        end
        return set
    end



    action CalculateValueCount returns HashTable<BigInteger,integer>
        HashTable<BigInteger, integer> hash
        i = 0
        repeat while i < rows:GetSize()
            if hash:HasKey(rows:Get(i))
                hash:Add(rows:Get(i), hash:GetValue(rows:Get(i)) + 1)
            else
                hash:Add(rows:Get(i), 1)
            end
            i = i + 1
        end
        return hash
    end

    action GetUnique returns Array<BigInteger>
        Array<BigInteger> set
        HashTable<BigInteger,integer> hash = CalculateValueCount()
        Iterator<BigInteger> iterator = hash:GetKeyIterator()
        repeat while iterator:HasNext()
            set:Add(iterator:Next())
        end
        return set
    end

    action ToText returns text
        text result = ""
        text lf = result:GetLineFeed()
        i = 0
        repeat while i < GetSize()
            BigInteger num = rows:Get(i)
            if num not= undefined
                text value = num:GetText()
                result = result + value + lf
            else
                result = result + GetUndefinedText() + lf
            end
            i = i + 1
        end
        return result
    end
end