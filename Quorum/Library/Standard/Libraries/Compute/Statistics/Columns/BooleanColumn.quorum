package Libraries.Compute.Statistics.Columns

use Libraries.Containers.Array
use Libraries.Compute.Statistics.DataFrameColumnCalculation
use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Containers.HashTable
use Libraries.Containers.Iterator
use Libraries.Language.Compile.Interpreter.Result
use Libraries.Language.Compile.Symbol.Type

/*
    BooleanColumn is a DataFrameColumn that contains Number objects. These objects can be undefined
    or not. 

    Attribute: Author Andreas Stefik
*/
class BooleanColumn is DataFrameColumn
    /* This is the new system, which is rows. */
    Array<Boolean> rows

    action Add(text value)
        if value = undefined or value:IsEmpty()
            rows:Add(undefined)
            parent:DataFrameColumn:undefinedSize = GetUndefinedSize() + 1
            return now
        end

        check 
            boolean boo = Convert(value)
            rows:Add(boo)
        detect e
            rows:Add(undefined) //add a missing value
            parent:DataFrameColumn:undefinedSize = GetUndefinedSize() + 1
        end
    end

    private action Convert(text value) returns boolean
        text val = value:ToLowerCase()
        if val = "true"
            return true
        elseif val = "t"
            return true
        elseif val = "y"
            return true
        elseif val = "yes"
            return true
        elseif val = "1"
            return true
        elseif val = "false"
            return false
        elseif val = "f"
            return false
        elseif val = "n"
            return false
        elseif val = "no"
            return false
        elseif val = "0"
            return false
        else //We don't recognize this value, so put in undefined
            alert("Could not identify text value as a boolean")
        end
    end

    /*
        By default, this replaces all undefined values with the number value of the text passed in.
        If the text passed in is not a number, then this call is ignored

        Attribute: Parameter value the value to be converted.        
    */
    action ReplaceUndefined(text value)
        check 
            boolean num = Convert(value)
            row = 0
            repeat while row < GetSize()
                if IsUndefined(row)
                    SetAsBoolean(row, num)
                end
                row = row + 1
            end
        detect e
            return now
        end
    end

    action AddAsResult(Result value)
        if not IsResultCheckable(value) rows:Add(undefined) return now end
        Type type = value:type
        if type:IsBoolean()
            rows:Add(value:booleanValue)
        else
            rows:Add(undefined)
        end
    end

    action SetAsResult(integer index, Result value)
        if not IsResultCheckable(value) rows:Set(index, undefined) return now end
        Type type = value:type
        if type:IsBoolean()
            rows:Set(index, value:booleanValue)
        else
            rows:Set(index, undefined)
        end
    end

    action Add(boolean value)
        rows:Add(value)
    end

    action SendValueTo(integer index, DataFrameColumnCalculation calculation)
        Boolean int = rows:Get(index)
        calculation:Add(int)
    end

    action IsUndefined(integer row) returns boolean
        return rows:Get(row) = undefined
    end

    action Get(integer row) returns Boolean
        return rows:Get(row)
    end

    action GetAsText(integer index) returns text
        Boolean value = rows:Get(index)
        if value = undefined
            return undefined
        else
            return "" + value:GetValue()
        end
    end

    action SetAsBoolean(integer index, boolean value)
        rows:Set(index, value)
    end

    action IsBooleanColumn returns boolean
        return false
    end

    action SetSize(integer size)
        rows:SetSize(size)
    end

    action GetSize returns integer
        return rows:GetSize()
    end

    action Swap(integer left, integer right)
        Boolean temp = undefined
        temp = rows:Get(left)
        rows:Set(left, rows:Get(right))
        rows:Set(right, temp)
    end

    action Move(integer left, integer right)
        rows:Set(right, rows:Get(left))
    end

    action Copy(integer rowStart, integer rowEnd) returns DataFrameColumn
        BooleanColumn column
        column:SetHeader(GetHeader())

        i = rowStart
        repeat while i < rowEnd
            Boolean value = rows:Get(i)
            if value = undefined
                column:rows:Add(undefined)
            else
                Boolean value2
                value2:SetValue(value:GetValue())
                column:rows:Add(value2)
            end
            i = i + 1
        end

        return column
    end

    action Copy returns DataFrameColumn
        return Copy(0, rows:GetSize())
    end

    action Copy(integer rowStart, integer rowEnd, boolean sort, boolean unique) returns DataFrameColumn
        BooleanColumn column
        column:SetHeader(GetHeader())

        Array<Boolean> newRows
        i = rowStart
        repeat while i < rowEnd
            Boolean value = rows:Get(i)
            if value = undefined
                newRows:Add(undefined)
            else
                Boolean value2
                value2:SetValue(value:GetValue())
                newRows:Add(value2)
            end
            i = i + 1
        end

        //if it's unique or sortable, now do that
        if unique
            newRows = GetUnique(newRows)
        end

        if sort
            if GetSortComparison() not= undefined
                newRows:Sort(GetSortComparison())
            else
                newRows:Sort()
            end
        end
        column:rows = newRows

        return column
    end

    action Copy(boolean sort, boolean unique) returns DataFrameColumn
        return Copy(0, rows:GetSize(), sort, unique)
    end

    action CopyToRow(DataFrameColumn column, integer to, integer from)
        me:SetAsBoolean(to,column:GetAsBoolean(from))
    end

    private action CalculateValueCount(Array<Boolean> values) returns HashTable<boolean,integer>
        HashTable<boolean, integer> hash
        i = 0
        repeat while i < rows:GetSize()
            if hash:HasKey(rows:Get(i))
                hash:Add(rows:Get(i), hash:GetValue(rows:Get(i)) + 1)
            else
                hash:Add(rows:Get(i), 1)
            end
            i = i + 1
        end
        return hash
    end

    private action GetUnique(Array<Boolean> array) returns Array<Boolean>
        Array<Boolean> set
        HashTable<boolean,integer> hash = CalculateValueCount(array)
        Iterator<boolean> iterator = hash:GetKeyIterator()
        repeat while iterator:HasNext()
            set:Add(iterator:Next())
        end
        return set
    end

    action ToText returns text
        text result = ""
        text lf = result:GetLineFeed()
        i = 0
        repeat while i < GetSize()
            Boolean value = rows:Get(i)
            if value not= undefined
                boolean b = value:GetValue()
                result = result + b + lf
            else
                result = result + GetUndefinedText() + lf
            end
            i = i + 1
        end
        return result
    end
end