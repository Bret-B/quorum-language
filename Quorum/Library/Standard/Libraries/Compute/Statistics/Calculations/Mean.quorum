package Libraries.Compute.Statistics.Calculations

use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Compute.Statistics.DataFrameColumnCalculation


/*
    This class calculates the mean of a particular DataFrameColumn. 

    Attribute: Author Andreas Stefik

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Calculations.Mean

    //Load a comma separated file
    DataFrame frame
    frame:Load("Data.csv") 
    Mean mean
    DataFrameColumn column = frame:GetColumn(0)
    column:Calculate(mean)
    number leftMean = mean:GetMean()
*/
class Mean is DataFrameColumnCalculation
    integer size = 0
    number mean = 0

    action Start(DataFrameColumn column) 
        size = column:GetSize() - column:GetUndefinedSize()
    end

    /*
        This action allows us to manually override the total size
        for the calculation.
    */
    action SetSize(integer size)
        me:size = size
    end
   
    action Add(Number value) 
        if value not= undefined
            number num = value:GetValue()
            mean = mean + (num / size)
        end
    end

    action Add(Integer value) 
        if value not= undefined
            number num = value:GetValue()
            mean = mean + (num / size)
        end
    end

    /*
        After a calculation is complete, this action will return the numeric mean.

        Attribute: Returns the mean
    */
    action GetMean returns number
        return mean
    end

    /*
        This Calculation can be re-used if the Empty action is called before
        passing it to another column. Otherwise, it retains its data.
    */
    action Empty
        size = 0
        mean = 0
    end
end