package Libraries.Compute.Statistics.Calculations

/*
    This is an implementation, ported from Apache Commons, of the incomplete beta function. 
    More information can be found here: https://en.wikipedia.org/wiki/Beta_function
*/
class RegularizedBetaContinuedFraction is ContinuedFraction
    number a = 0
    number b = 0

    action SetA(number a)
        me:a = a
    end

    action SetB(number b)
        me:b = b
    end

    action GetA(integer n, number x) returns number
        return 1.0
    end

    action GetB(integer n, number x) returns number
        number ret = 0
        number m = 0
        if n mod 2 = 0
            m = n / 2.0
            ret = (m * (b - m) * x) / ((a + (2 * m) - 1) * (a + (2 * m)))
        else
            m = (n - 1.0) / 2.0
            ret = -((a + m) * (a + b + m) * x) /
                ((a + (2 * m)) * (a + (2 * m) + 1.0))
        end

        return ret
    end
end