package Libraries.Compute.Statistics.Calculations

use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Compute.Statistics.DataFrameColumnCalculation
use Libraries.Compute.Statistics.Columns.NumberColumn
use Libraries.Compute.Math


/*
    This class calculates the Kurtosis. The equations used for it were pulled from Apache Commons:

    https://commons.apache.org/proper/commons-math/javadocs/api-3.6/org/apache/commons/math3/stat/descriptive/moment/Kurtosis.html

    Attribute: Author Andreas Stefik

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Calculations.Skew

    //Load a comma separated file
    DataFrame frame
    frame:Load("Data.csv") 
    Kurtosis kurtosis
    DataFrameColumn column = frame:GetColumn(0)
    output column:Calculate(kurtosis)
*/
class Kurtosis is DataFrameColumnCalculation
    number standardDeviation = 0
    number mean = 0
    number sum = 0
    number size = 0
    number kurtosis = 0

    action Start(DataFrameColumn column) 
        Mean meanCalc
        column:Calculate(meanCalc)
        mean = meanCalc:GetMean()
        Variance variance
        variance:SetMean(mean)
        column:Calculate(variance)
        number var = variance:GetVariance()
        Math math
        standardDeviation = math:SquareRoot(var)
    end

    action End(DataFrameColumn column) 
        Math math
        number numerator = size * (size + 1) * sum
        number denominator = (size - 1) * (size - 2) * (size - 3) * 
            math:RaiseToPower(standardDeviation, 4)
        number left = numerator / denominator

        number right = 3 * math:RaiseToPower(size - 1, 2)
        right = right / ((size - 2) * (size - 3))
        kurtosis = left - right
    end
   
    action Add(Number value) 
        if value not= undefined
            Math math 
            number num = value:GetValue()
            number middle = (num - mean)
            sum = sum + math:RaiseToPower(middle, 4)
            size = size + 1
        end
    end

    action Add(Integer value) 
        if value not= undefined
            Math math 
            number num = value:GetValue()
            number middle = (num - mean)
            sum = sum + math:RaiseToPower(middle, 4)
            size = size + 1
        end
    end


    /*
        This Calculation can be re-used if the Empty action is called before
        passing it to another column. Otherwise, it retains its data.
    */
    action Empty
        mean = 0
        standardDeviation = 0
        mean = 0
        sum = 0
        size = 0
        kurtosis = 0
    end

    /*
        This action returns the Kurtosis that was calculated.
    */
    action GetKurtosis returns number
        return kurtosis
    end
end