package Libraries.Compute.Statistics.Calculations

use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Compute.Statistics.DataFrameColumnCalculation
use Libraries.Compute.Vector

/*
    This class calculates the median of a particular DataFrameColumn. 

    Attribute: Author Cody Molisee

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Calculations.Median

    //Load a comma separated file
    DataFrame frame
    frame:Load("Data.csv") 
    Median median
    DataFrameColumn column = frame:GetColumn(0)
    column:Calculate(median)
    output median:GetMedian()
*/
class Median is DataFrameColumnCalculation
    integer size = 0
    number median = 0
    
    action Start(DataFrameColumn column)
    end

    action IsIterable returns boolean
        return false
    end
    
    action Calculate(DataFrameColumn column)
        if column:CanConvertToVector()
            Vector v = column:ConvertToVector()
            v:Sort()
            size = v:GetSize()
            if size = 1 or size = 2
                median = v:Get(0)
                return now
            end
            if size mod 2 = 1
                median = v:Get(size / 2)
            else
                median = 0.5 * (v:Get((size / 2) + 1) + v:Get(size / 2))
            end
        else
            return now
        end
    end

    /*
        This Calculation can be re-used if the Empty action is called before
        passing it to another column. Otherwise, it retains its data.
    */
    action Empty
        size = 0
        median = 0
    end
    
    /*
        Returns the median of the column.

        Attribute: Returns the median of the column
    */
    action GetMedian returns number
        return median
    end
end