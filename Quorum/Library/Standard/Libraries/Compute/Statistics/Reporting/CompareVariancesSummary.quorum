package Libraries.Compute.Statistics.Reporting

use Libraries.Containers.HashTable
use Libraries.Containers.Iterator
use Libraries.Compute.Math
use Libraries.Containers.Array
use Libraries.Compute.Statistics.DataFrame
use Libraries.Compute.Statistics.Columns.TextColumn
use Libraries.Compute.Statistics.Columns.NumberColumn

class CompareVariancesSummary
    CompareVariancesResult result = undefined
    StatisticsFormatting format = undefined
    integer numberOfSamples = 0
    boolean multivariate = false
    boolean fatcorial = false
    Math math
    
    action SetResult(CompareVariancesResult result)
        if result not= undefined
            me:result = result
            me:format = result:GetFormat()
            numberOfSamples = result:GetGroupsFrame():GetSize()
            if result:GetFactors():GetSize() > 0 and result:GetColumns():GetSize() > 1
                multivariate = true
                numberOfSamples = result:GetGroupsTable():GetSize()
            end
            if result:GetFactors():GetSize() > 1 
                factorial = true
            end
        end
    end

    action GetSummary returns text
        if result not= undefined
            text summary = ""
            text lf = summary:GetLineFeed()
            text tab = "    "
            integer digits = format:GetSignificantDigits()

            text factors = result:GetFactorList()
            text columns = result:GetColumnList()
            text dependence = ""
            if result:IsRepeated()
                dependence = "dependent"
            else
                dependence = "independent"
            end
            summary = summary + "A " + result:GetFormalTestName() + " was conducted across " + numberOfSamples + " " + dependence + " samples. "
            if factors not= "None"
                summary = summary + lf + "Factor: " + columns + " by " + factors
            end

            if result:IsRepeated()
                Array<text> sources = result:GetSources()
                i = 0
                repeat while i < sources:GetSize()
                    text source = result:GetSources():Get(i)
                    summary = summary + lf + "Source: " + source
                    if result:GetInformation():GetValue(source):HasKey("W")
                        number w = result:GetInformation():GetValue(source):GetValue("W")
                        number x2 = result:GetInformation():GetValue(source):GetValue("X2")
                        number df = result:GetInformation():GetValue(source):GetValue("df")
                        number p = result:GetInformation():GetValue(source):GetValue("p")
                        number gg = result:GetInformation():GetValue(source):GetValue("gg")
                        number hf = result:GetInformation():GetValue(source):GetValue("hf")
                        number lb = result:GetInformation():GetValue(source):GetValue("lb")
            
                        summary = summary + lf + tab + "Statistic (W): " +  math:Round(w, digits)
                        summary = summary + lf + tab + "Statistic (Approximate χ2): " + math:Round(x2, digits)
                        summary = summary + lf + tab + "Degrees Of Freedom: " + math:Round(df, digits)
                        summary = summary + lf + tab + "Probability: " + math:Round(p, digits)
                        summary = summary + lf + tab + "Greenhouse-Geisser Correction (ε): " + math:Round(gg, digits)
                        summary = summary + lf + tab + "Huynh-Feldt Correction (ε): " + math:Round(hf, digits)
                        summary = summary + lf + tab + "Lower-Bound Correction (ε): " + math:Round(lb, digits)
                    elseif result:GetInformation():GetValue(source):HasKey("F")
                        number f = result:GetInformation():GetValue(source):GetValue("F")
                        number dfn = result:GetInformation():GetValue(source):GetValue("dfn")
                        number dfd = result:GetInformation():GetValue(source):GetValue("dfd")
                        number p = result:GetInformation():GetValue(source):GetValue("p")

                        summary = summary + lf + tab + "Statistic (F): " + math:Round(f, digits)
                        summary = summary + lf + tab + "Degrees Of Freedom Numerator: " + math:Round(dfn, digits)        
                        summary = summary + lf + tab + "Degrees Of Freedom Denominator: " + math:Round(dfd, digits)
                        summary = summary + lf + tab + "Probability: " + math:Round(p, digits)
                    end
                    i = i + 1
                end
            else
                if multivariate
                    text source = result:GetSources():Get(0)
                    number m = result:GetTestStatisticsTable():GetValue(source):GetValue("M")
                    number x2 = result:GetTestStatisticsTable():GetValue(source):GetValue("χ2")
                    number f = result:GetTestStatisticsTable():GetValue(source):GetValue("F")
                    number dfn = result:GetDegreesOfFreedomTable():GetValue(source+"F"):GetValue("numerator")
                    number dfd = result:GetDegreesOfFreedomTable():GetValue(source+"F"):GetValue("denominator")
                    number fpval = result:GetProbabilityValuesTable():GetValue(source+"F"):GetValue(source+"F")
                    number x2pval = result:GetProbabilityValuesTable():GetValue(source+"χ2"):GetValue(source+"χ2")
                    summary = summary + lf + "Statistic (M): " + math:Round(m, digits)
                    summary = summary + lf + "Statistic (Approximate F): " + math:Round(f, digits)
                    summary = summary + lf + "Degrees Of Freedom Numerator: " + math:Round(dfn, digits)        
                    summary = summary + lf + "Degrees Of Freedom Denominator: " + math:Round(dfd, digits)
                    summary = summary + lf + "Probability (F): " + math:Round(fpval, digits)
                    summary = summary + lf + "Statistic (Approximate χ2): " + math:Round(x2, digits)
                    summary = summary + lf + "Degrees Of Freedom: " + math:Round(dfn, digits)
                    summary = summary + lf + "Probability (χ2): " + math:Round(x2pval, digits)
                else
                    text source = result:GetSources():Get(0)
                    text statName = result:GetTestStatisticsTable():GetValue(source):GetKeyIterator():Next()
                    number stat = result:GetTestStatisticsTable():GetValue(source):GetValue(statName)
                    number dfb = result:GetDegreesOfFreedomTable():GetValue(source):GetValue("numerator")
                    number dfw = result:GetDegreesOfFreedomTable():GetValue(source):GetValue("denominator")
                    number pval = result:GetProbabilityValuesTable():GetValue(source):GetValue(source)
                    summary = summary + lf + "Degrees Of Freedom Numerator: " + math:Round(dfb, digits)        
                    summary = summary + lf + "Degrees Of Freedom Denominator: " + math:Round(dfw, digits)
                    summary = summary + lf + "Statistic (F): " + math:Round(stat, digits)
                    summary = summary + lf + "Probability: " + math:Round(pval, digits)
                end
            end
            return summary
        end
        alert("There is no CompareVariancesResult defined to summarize. Try using SetResult()")
    end

    action GetFormalSummary returns text
        if result not= undefined
            text summary = ""
            text lf = summary:GetLineFeed()

            text factors = result:GetFactorList()
            text columns = result:GetColumnList()
            text dependence = ""
            if result:IsRepeated()
                dependence = " dependent measurements. "
            else
                dependence = " independent samples. "
            end
    
            if factors = "None"
                summary = summary + "A " + result:GetFormalTestName() + " was conducted across " + numberOfSamples + dependence
            else
                summary = summary + "A " + result:GetFormalTestName() + " was conducted on " + factors + " across " + numberOfSamples + dependence
            end
    
            if multivariate
                boolean significant = false
                if result:UseFApproximation()
                    text statSource = result:GetSources():Get(0)+"F"
                    significant = result:GetProbabilityValuesTable():GetValue(statSource):GetValue(statSource) < 0.001
                elseif result:UseX2Approximation()
                    text statSource = result:GetSources():Get(0)+"χ2"
                    significant = result:GetProbabilityValuesTable():GetValue(statSource):GetValue(statSource) < 0.001
                end
                if significant
                    //P-value ≤ α: The differences between some of the means are statistically significant (Reject H0)
                    summary = summary + lf + "There was a statistically significant difference in the covariance matrices"
                else
                    //P-value > α: The differences between the means are not statistically significant (Fail to reject H0)
                    summary = summary + lf + "There was not a statistically significant difference in the covariance matrices"
                end
            else
                if result:IsSignificant()
                    //P-value ≤ α: The differences between some of the means are statistically significant (Reject H0)
                    summary = summary + lf + "There was a statistically significant difference in the variances"
                else
                    //P-value > α: The differences between the means are not statistically significant (Fail to reject H0)
                    summary = summary + lf + "There was not a statistically significant difference in the variances"
                end
            end
   
            if result:GetSources():GetSize() > 1
                summary = summary + ": " + lf + GetFormalNumericalResult()
            else
                summary = summary + ", " + GetFormalNumericalResult() + "."
            end

            return summary
        end
        alert("There is no CompareVariancesResult defined to summarize. Try using SetResult()")
    end

    action GetFormalNumericalResult returns text
        if result not= undefined
            text summary = ""
            text lf = summary:GetLineFeed()
            if result:IsRepeated()
                Array<text> sources = result:GetSources()
                text list = ""
                i = 0
                repeat while i < sources:GetSize()
                    text source = sources:Get(i)
                    text equation = GetFormalNumericalResultSeveral(source)
                    if equation not= ""
                        if list not= ""
                            list = list + lf
                        end
                        if sources:GetSize() = 1
                            list = equation
                        else
                            list = list + source + ": "+ equation
                        end
                    end
                    i = i + 1
                end
                return list
            else
                if multivariate
                    if result:UseFApproximation()
                        text source = result:GetSources():Get(0)
                        text statName = "F"
                        number stat = result:GetTestStatisticsTable():GetValue(source):GetValue(statName)
                        number dfb = result:GetDegreesOfFreedomTable():GetValue(source+statName):GetValue("numerator")
                        number dfw = result:GetDegreesOfFreedomTable():GetValue(source+statName):GetValue("denominator")
                        number pval = result:GetProbabilityValuesTable():GetValue(source+statName):GetValue(source+statName)
        
                        summary = summary + "Box's M = " + format:FormatTestStatistic(result:GetTestStatisticsTable():GetValue(source):GetValue("M"))
                        summary = summary + ", " + statName + "(" + format:FormatDegreesOfFreedom(dfb) + ", " + format:FormatDegreesOfFreedom(dfw)+") = " + format:FormatTestStatistic(stat)
                        if pval < format:PRECISION_LIMIT
                            summary = summary + ", p < " + format:FormatProbabilityValue(pval)
                        else
                            summary = summary + ", p = " + format:FormatProbabilityValue(pval)
                        end  
                    elseif result:UseX2Approximation()
                        text source = result:GetSources():Get(0)
                        text statName = "χ2"
                        number stat = result:GetTestStatisticsTable():GetValue(source):GetValue(statName)
                        number df = result:GetDegreesOfFreedomTable():GetValue(source+statName):GetValue(source+statName)
                        number pval = result:GetProbabilityValuesTable():GetValue(source+statName):GetValue(source+statName)
        
                        summary = summary + "Box's M = " + format:FormatTestStatistic(result:GetTestStatisticsTable():GetValue(source):GetValue("M"))
                        summary = summary + ", " + statName + "(" + format:FormatDegreesOfFreedom(df) + ") = " + format:FormatTestStatistic(stat)
                        if pval < format:PRECISION_LIMIT
                            summary = summary + ", p < " + format:FormatProbabilityValue(pval)
                        else
                            summary = summary + ", p = " + format:FormatProbabilityValue(pval)
                        end
                    end                
                else
                    text source = result:GetSources():Get(0)
                    text statName = result:GetTestStatisticsTable():GetValue(source):GetKeyIterator():Next()
                    number stat = result:GetTestStatisticsTable():GetValue(source):GetValue(statName)
                    number dfb = result:GetDegreesOfFreedomTable():GetValue(source):GetValue("numerator")
                    number dfw = result:GetDegreesOfFreedomTable():GetValue(source):GetValue("denominator")
                    number pval = result:GetProbabilityValuesTable():GetValue(source):GetValue(source)
    
                    summary = summary + statName+"("+format:FormatDegreesOfFreedom(dfb)+", "+format:FormatDegreesOfFreedom(dfw)+") = "+format:FormatTestStatistic(stat)
                    if pval < format:PRECISION_LIMIT
                        summary = summary + ", p < " + format:FormatProbabilityValue(pval)
                    else
                        summary = summary + ", p = " + format:FormatProbabilityValue(pval)
                    end
                end
            end
            return summary
        end
        alert("There is no CompareVariancesResult defined to summarize. Try using SetResult()")
    end

    private action GetFormalNumericalResultSeveral(text source) returns text
        text summary = ""
        HashTable<text, number> info = result:GetInformation():GetValue(source)
        if result:IsRepeated() and info:HasKey("p")
            number w = info:GetValue("W")
            number x2 = info:GetValue("X2")
            number df = info:GetValue("df")
            number pval = info:GetValue("p")
    
            summary = summary + "Mauchly's W = " + format:FormatTestStatistic(w)
            summary = summary + ", χ2("+format:FormatDegreesOfFreedom(df)+") = "+format:FormatTestStatistic(x2)
            if pval < format:PRECISION_LIMIT
                summary = summary + ", p < " + format:FormatProbabilityValue(pval)
            else
                summary = summary + ", p = " + format:FormatProbabilityValue(pval)
            end
        end
        return summary
    end

    action GetSummaryDataFrame returns DataFrame
        if result not= undefined
            if result:IsRepeated()
                return DependentSummaryDataFrame()
            else
                if multivariate
                    return MultivariateSummaryDataFrame()
                else
                    return IndependentSummaryDataFrame()
                end
            end
        end
        alert("There is no CompareVariancesResult defined to summarize. Try using SetResult()")
    end

    private action IndependentSummaryDataFrame returns DataFrame
        number nan = 0
        integer digits = format:GetSignificantDigits()
        text source = result:GetSources():Get(0)
        text statName = result:GetTestStatisticsTable():GetValue(source):GetKeyIterator():Next()
        number stat = result:GetTestStatisticsTable():GetValue(source):GetValue(statName)
        number dfb = result:GetDegreesOfFreedomTable():GetValue(source):GetValue("numerator")
        number dfw = result:GetDegreesOfFreedomTable():GetValue(source):GetValue("denominator")
        number pval = result:GetProbabilityValuesTable():GetValue(source):GetValue(source)

        TextColumn name
        name:SetHeader("Levene's Test Of Homogeneity")
        name:Add(source)

        NumberColumn f
        f:SetHeader("Statistic (F)")
        f:Add(math:Round(stat, digits))

        NumberColumn df1
        df1:SetHeader("Degrees Of Freedom (DF1)")
        df1:Add(math:Round(dfb, digits))

        NumberColumn df2
        df2:SetHeader("Degrees Of Freedom (DF2)")
        df2:Add(math:Round(dfw, digits))

        NumberColumn p
        p:SetHeader("Probability (P)")
        p:Add(math:Round(pval, digits))

        DataFrame table
        table:AddColumn(name)
        table:AddColumn(f)
        table:AddColumn(df1)
        table:AddColumn(df2)
        table:AddColumn(p)
        return table
    end

    private action MultivariateSummaryDataFrame returns DataFrame
        number nan = 0
        integer digits = format:GetSignificantDigits()
        text source = result:GetSources():Get(0)
        text statName = "M"
        number mStat = result:GetTestStatisticsTable():GetValue(source):GetValue("M")
        number x2Stat = result:GetTestStatisticsTable():GetValue(source):GetValue("χ2")
        number fStat = result:GetTestStatisticsTable():GetValue(source):GetValue("F")
        number dfn = result:GetDegreesOfFreedomTable():GetValue(source+"F"):GetValue("numerator")
        number dfd = result:GetDegreesOfFreedomTable():GetValue(source+"F"):GetValue("denominator")
        number x2p = result:GetProbabilityValuesTable():GetValue(source+"χ2"):GetValue(source+"χ2")
        number fp = result:GetProbabilityValuesTable():GetValue(source+"F"):GetValue(source+"F")
        

        TextColumn name
        name:SetHeader("Box's M-Test Of Homogeneity")
        name:Add(source)

        NumberColumn m
        m:SetHeader("Statistic (M)")
        m:Add(math:Round(mStat, digits))

        NumberColumn f
        f:SetHeader("Approximate F")
        f:Add(math:Round(fStat, digits))

        NumberColumn df1
        df1:SetHeader("Degrees Of Freedom (DF1)")
        df1:Add(math:Round(dfn, digits))

        NumberColumn df2
        df2:SetHeader("Degrees Of Freedom (DF2)")
        df2:Add(math:Round(dfd, digits))

        NumberColumn p1
        p1:SetHeader("F Probability (P)")
        p1:Add(math:Round(fp, digits))

        NumberColumn x2
        x2:SetHeader("Approximate χ2")
        x2:Add(math:Round(x2Stat, digits))

        NumberColumn df
        df:SetHeader("Degrees Of Freedom (DF)")
        df:Add(math:Round(dfn, digits))

        NumberColumn p2
        p2:SetHeader("χ2 Probability (P)")
        p2:Add(math:Round(x2p, digits))

        DataFrame table
        table:AddColumn(name)
        table:AddColumn(m)
        table:AddColumn(f)
        table:AddColumn(df1)
        table:AddColumn(df2)
        table:AddColumn(p1)
        table:AddColumn(x2)
        table:AddColumn(df)
        table:AddColumn(p2)
        return table
    end

    private action DependentSummaryDataFrame returns DataFrame
        number nan = 0
        integer digits = format:GetSignificantDigits()

        TextColumn name
        name:SetHeader(result:GetFormalTestName())
        NumberColumn w
        w:SetHeader("Statistic (W)")
        NumberColumn df
        df:SetHeader("Degrees Of Freedom")
        NumberColumn x2
        x2:SetHeader("Approximate (χ2)")
        NumberColumn p
        p:SetHeader("Probability (P)")
        NumberColumn gg
        gg:SetHeader("Greenhouse-Geisser (ε)")
        NumberColumn hf
        hf:SetHeader("Huynh-Feldt (ε)")
        NumberColumn lb
        lb:SetHeader("Lower-Bound (ε)")

        Array<text> sources = result:GetSources()
        i = 0
        repeat while i < sources:GetSize()
            text source = result:GetSources():Get(i)

            number wValue = result:GetInformation():GetValue(source):GetValue("W")
            number x2Value = result:GetInformation():GetValue(source):GetValue("X2")
            number dfValue = result:GetInformation():GetValue(source):GetValue("df")
            number pValue = result:GetInformation():GetValue(source):GetValue("p")
            number ggValue = result:GetInformation():GetValue(source):GetValue("gg")
            number hfValue = result:GetInformation():GetValue(source):GetValue("hf")
            number lbValue = result:GetInformation():GetValue(source):GetValue("lb")

            name:Add(source)
            w:Add(math:Round(wValue, digits))
            x2:Add(math:Round(x2Value, digits))
            df:Add(math:Round(dfValue, digits))
            p:Add(math:Round(pValue, digits))
            gg:Add(math:Round(ggValue, digits))
            hf:Add(math:Round(hfValue, digits))
            lb:Add(math:Round(lbValue, digits))
            i = i + 1
        end

        DataFrame table
        table:AddColumn(name)
        table:AddColumn(w)
        table:AddColumn(x2)
        table:AddColumn(df)
        table:AddColumn(p)
        table:AddColumn(gg)
        table:AddColumn(hf)
        table:AddColumn(lb)

        return table
    end
end