package Libraries.Compute.Statistics.Reporting
use Libraries.Compute.Statistics.Inputs.ColumnInput

/*
    This class represents data that comes back from a CorrelateGroups or 
    CorrelateRanks calculation (Pearson or Spearman correlation).

    Attribute: Author Andreas Stefik, Hannah Williams
*/
class CorrelateGroupsResult is StatisticalTestResult, ColumnInput
    number correlation = 0
    boolean spearman = false
    boolean rankTieWarning = false 
    text variable1 = ""
    text variable2 = ""
    text factor = ""

    /*
        Obtains the correlation calculated by the test. This may be of any kind.

        Attribute: Returns the correlation
    */
    action GetCorrelation returns number
        return correlation
    end

    /*
        Sets the correlation calculated by the test. This may be of any kind.

        Attribute: Parameter correlation the correlation
    */
    action SetCorrelation(number correlation)
        me:correlation = correlation
    end

    /*
        Sets the first column used in this calculation.

        Attribute: Parameter variable is column header.
    */
    action SetVariable1(text variable)
        me:variable1 = variable
    end

    /*
        Sets the second column used in this calculation.

        Attribute: Parameter variable is column header.
    */
    action SetVariable2(text variable)
        me:variable2 = variable
    end

    /*
        Sets the factor (if used) in this calculation.

        Attribute: Parameter factor is factor header and unique factor.
    */
    action SetFactor(text factor)
        me:factor = factor
    end

    /*
        Sets the spearman flag for the test result.

        Attribute: Parameter spearman is true if the result comes from a spearman test.
    */
    action SetSpearman(boolean spearman)
        me:spearman = spearman
    end

    /*
        This action summarizes the result and lists it informally.
    */
    action GetSummary returns text
        text summary = ""
        text lf = summary:GetLineFeed()

        summary = summary + "A "+ GetFormalTestName() + " was performed."
        if factor not= ""
            summary = summary + lf + "Factor: " + factor
        end
       
        summary = summary + lf + "Columns: " + variable1 + " and " + variable2

        summary = summary + lf + "Statistic (t): " + GetTestStatistic()

        summary = summary + lf + "Degrees Of Freedom: " + GetDegreesOfFreedom()

        summary = summary + lf + "Probability: " + GetProbabilityValue()

        summary = summary + lf + "Correlation: " + GetCorrelation()

        if GetEffectSizeName() not= ""
            summary = summary + lf + "Effect Size: " + GetEffectSizeName() + " = " + GetEffectSize()
        end

        return summary
    end

    action GetFormalSummary returns text
        text summary = ""
        text lf = summary:GetLineFeed()

        if factor = ""
            summary = summary + "A "+ GetFormalTestName() + " was performed to assess the linear relationship between " 
                                + variable1 + " and " + variable2 + "."
        else
            summary = summary + "A "+ GetFormalTestName() + " was performed on " + factor + " to assess the linear relationship between " 
                                + variable1 + " and " + variable2 + "."
        end

        if correlation = -1 
            summary = summary + lf + "There was a perfect negative correlation between the two variables"
        elseif correlation = 1
            summary = summary + lf + "There was a perfect positive correlation between the two variables"
        elseif correlation = 0
            summary = summary + lf + "There was no correlation between the two variables"
        elseif correlation < 0 
            text level = ""
            if correlation <= -0.5
                level = "strong"
            elseif correlation <= -0.3 and correlation > -0.5
                level = "moderate"
            else
                level = "weak"
            end
            summary = summary + lf + "There was a " + level + " negative correlation between the two variables"
        else
            text level = ""
            if correlation >= 0.5
                level = "strong"
            elseif correlation >= 0.3 and correlation < 0.5
                level = "moderate"
            else
                level = "weak"
            end
            summary = summary + lf + "There was a " + level + " positive correlation between the two variables"
        end   

        if spearman
            summary = summary + ", rs("
        else
            summary = summary + ", r("
        end
        summary = summary + GetFormat():FormatDegreesOfFreedom(GetDegreesOfFreedom()) + ") = " 
                          + GetFormat():FormatCorrelation(GetCorrelation())

        number p = GetProbabilityValue()
        if p < GetFormat():PRECISION_LIMIT
            summary = summary + ", p < " + GetFormat():FormatProbabilityValue(p)
        else
            summary = summary + ", p = " + GetFormat():FormatProbabilityValue(p)
        end
        summary = summary + "."

        return summary
    end
end