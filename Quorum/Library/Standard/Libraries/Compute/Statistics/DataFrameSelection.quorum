package Libraries.Compute.Statistics
use Libraries.Containers.Array
use Libraries.Compute.Statistics.Inputs.ColumnInput
use Libraries.Compute.Statistics.Inputs.FactorInput
use Libraries.Containers.Iterator
use Libraries.Compute.Statistics.Inputs.RowInput
use Libraries.Compute.Statistics.Inputs.CellInput
use Libraries.Containers.Support.Pair

/*
    This encompasses the overall selection of columns and factors
    in the DataFrame. DataFrame, by default, passes through all of its
    actions into this system. The selection itself is more rigid than the
    DataFrame, on purpose, so that the potential points of failure exist
    only in one place.
    
    Attribute: Author Andreas Stefik
*/
class DataFrameSelection is ColumnInput, FactorInput, RowInput, CellInput
    Array<DataFrameSelectionListener> listeners
    DataFrame frame = undefined

    action Add(DataFrameSelectionListener listener)
        listeners:Add(listener)
    end

    action Remove(DataFrameSelectionListener listener)
        listeners:Remove(listener)
    end

    action GetListeners returns Iterator<DataFrameSelectionListener>
        return listeners:GetIterator()
    end

    private action FireSelectionChanged
        Iterator<DataFrameSelectionListener> iterator = GetListeners()
        repeat while iterator:HasNext()
            DataFrameSelectionListener listener = iterator:Next()
            listener:SelectionChanged(frame)
        end
    end

    action CopyTo(RowInput col)
        col:EmptyRows()
        integer i = 0
        repeat while i < GetColumnSize()
            integer value = GetColumn(i)
            col:AddRow(value)
            i = i + 1
        end
        FireSelectionChanged()
    end

    action CopyTo(CellInput col)
        col:EmptyCells()
        integer i = 0
        repeat while i < GetColumnSize()
            Pair<integer> value = GetCell(i)
            Pair<integer> copy
            integer x = value:GetFirstValue()
            integer y = value:GetSecondValue()
            copy:Set(x,y)
            col:AddCell(copy)
            i = i + 1
        end
        FireSelectionChanged()
    end

    action CopyTo(ColumnInput col)
        col:EmptyColumns()
        integer i = 0
        repeat while i < GetColumnSize()
            integer value = GetColumn(i)
            col:AddColumn(value)
            i = i + 1
        end
        FireSelectionChanged()
    end

    action CopyTo(FactorInput col)
        col:EmptyFactors()
        integer i = 0
        repeat while i < GetFactorSize()
            integer value = GetFactor(i)
            col:AddFactor(value)
            i = i + 1
        end
        FireSelectionChanged()
    end

    action AddColumn(integer column)
        parent:Libraries.Compute.Statistics.Inputs.ColumnInput:AddColumn(column)
        FireSelectionChanged()
    end

    action AddFactor(integer column)
        parent:Libraries.Compute.Statistics.Inputs.FactorInput:AddFactor(column)
        FireSelectionChanged()
    end

    action AddRow(integer row)
        parent:Libraries.Compute.Statistics.Inputs.RowInput:AddRow(row)
        FireSelectionChanged()
    end

    action AddCell(integer x, integer y)
        parent:Libraries.Compute.Statistics.Inputs.CellInput:AddCell(x, y)
        FireSelectionChanged()
    end

    action AddCell(Pair<integer> cell)
        parent:Libraries.Compute.Statistics.Inputs.CellInput:AddCell(cell)
        FireSelectionChanged()
    end

    action RemoveRowAt(integer index)
        parent:Libraries.Compute.Statistics.Inputs.RowInput:RemoveRowAt(index)
        FireSelectionChanged()
    end

    action RemoveRow(integer index) returns boolean
        boolean value = parent:Libraries.Compute.Statistics.Inputs.RowInput:RemoveRow(index)
        if value
            FireSelectionChanged()
        end
        return value
    end

    action EmptyRows
        boolean empty = GetRowSize() = 0
        parent:Libraries.Compute.Statistics.Inputs.RowInput:EmptyRows()
        if not empty
            FireSelectionChanged()
        end
    end

    action RemoveFactorAt(integer index)
        parent:Libraries.Compute.Statistics.Inputs.FactorInput:RemoveFactorAt(index)
        FireSelectionChanged()
    end

    action RemoveFactor(integer column) returns boolean
        boolean value = parent:Libraries.Compute.Statistics.Inputs.FactorInput:RemoveFactor(column)
        if value
            FireSelectionChanged()
        end
        return value
    end

    action EmptyFactors
        boolean empty = GetFactorSize() = 0
        parent:Libraries.Compute.Statistics.Inputs.FactorInput:EmptyFactors()
        if not empty
            FireSelectionChanged()
        end
    end

    action RemoveCellAt(integer index)
        parent:Libraries.Compute.Statistics.Inputs.CellInput:RemoveCellAt(index)
        FireSelectionChanged()
    end

    action RemoveRow(Pair<integer> cell) returns boolean
        boolean value = parent:Libraries.Compute.Statistics.Inputs.CellInput:RemoveCell(cell)
        if value
            FireSelectionChanged()
        end
        return value
    end

    action EmptyCells
        boolean empty = GetCellSize() = 0
        parent:Libraries.Compute.Statistics.Inputs.CellInput:EmptyCells()
        if not empty
            FireSelectionChanged()
        end
    end

    action RemoveColumnAt(integer index)
        parent:Libraries.Compute.Statistics.Inputs.ColumnInput:RemoveColumnAt(index)
        FireSelectionChanged()
    end
    
    action RemoveColumn(integer column) returns boolean
        boolean value = parent:Libraries.Compute.Statistics.Inputs.ColumnInput:RemoveColumn(column)
        if value
            FireSelectionChanged()
        end
        return value
    end

    action EmptyColumns
        boolean empty = GetColumnSize() = 0
        parent:Libraries.Compute.Statistics.Inputs.ColumnInput:EmptyColumns()
        if not empty
            FireSelectionChanged()
        end
    end

    action GetFrame returns DataFrame
        return frame
    end

    action SetFrame(DataFrame frame)
        me:frame = frame
    end
end