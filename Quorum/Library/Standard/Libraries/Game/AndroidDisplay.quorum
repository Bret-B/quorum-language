package Libraries.Game
use Libraries.Game.Graphics.Shaders.ShaderManager

class AndroidDisplay is GameDisplay

    private AndroidConfiguration config = undefined

    /* 
    This action sets the AndroidConfiguration being used by the game,
    which includes data for the screen. If the given ApplicationConfiguration is
    not an AndroidConfiguration, an error will be thrown.
    */
    action SetConfiguration(ApplicationConfiguration conf)
        if not (conf is AndroidConfiguration)
            alert("Attempted to configure a DesktopDisplay with an application configuration that is not used for desktops!")
        end
        config = cast(AndroidConfiguration, conf)
    end

    /*
    This action returns the AndroidConfiguration being used by this
    AndroidDisplay.
    */
    action GetConfiguration returns ApplicationConfiguration
        return config
    end

    /*
    This action normally sets the width and height of the AndroidDisplay, but
    games on Android devices always fill the screen, so this does nothing. The
    action will return false to indicate that no action was taken.
    */
    action SetDisplayMode(integer width, integer height, boolean fullscreen) returns boolean
        return false
    end

    /*
    This action reloads resources that were lost due to context loss (most commonly due to an app being minimized).
    This is called automatically by the engine when needed.
    */
    action ReloadResources
        GameStateManager gameState
        ShaderManager manager = gameState:GetShaderManager()
        manager:ReloadMeshes()
        manager:ReloadShaders()
    end

    /*
    This action returns the width of the AndroidDisplay.
    */
    system action GetWidth returns integer

    /*
    This action returns the height of the AndroidDisplay.
    */
    system action GetHeight returns integer

    /*
    This action sets vSync on or off for the AndroidDisplay.
    */
    system action SetVSync(boolean vsync)

    /*
    This action returns whether the AndroidDisplay is resizable or not. This
    will always return false.
    */
    action GetResize returns boolean
        return false
    end

    /*
    This action returns whether or not the AndroidDisplay has been resized.
    */
    system action WasResized returns boolean

    /*
    This action tells the AndroidDisplay to render to the screen.
    */
    system action RequestRendering

    /*
    This action closes the window.
    */
    system action Destroy

    /*
    This action tests how many seconds have passed since the last time that the
    time was updated.
    */
    system action GetSecondsBetweenFrames returns number

    /*
    This action returns the number of seconds that have passed since the game
    was started.

    Attribute: Example

    use Libraries.Game.Game
    use Libraries.Game.GameDisplay
    use Libraries.Game.GameStateManager
    
    class Main is Game

        GameDisplay display = undefined
        integer frameCount = 0

        action Main
            StartGame()
        end

        action CreateGame
            GameStateManager manager
            display = manager:GetGameDisplay()
        end

        action Update(number seconds)
            frameCount = frameCount + 1
            if frameCount = 30
                number time = display:GetSecondsSinceStart()
                output "The game has been running for " + time + " seconds."
            end
        end
    end
    */
    system action GetSecondsSinceStart returns number

    /*
    This action returns the ratio between screen coordinates and pixels.
    */
    action GetPixelScaleFactor returns number
        return 1
    end

    /*
    The IsAvailable action returns whether or not a display is available
    to draw on. If no display is available, actions that try to create or draw
    graphics (such as loading a Drawable) will create an error. This will return
    false if called before StartGame() is called in the Game class, and will
    typically return true after the game has been started.
    */
    action IsAvailable returns boolean
        // TO-DO: Make this accurately reflect the state of the OpenGL ES context.
        return true
    end
end