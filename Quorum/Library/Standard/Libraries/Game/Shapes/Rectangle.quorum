package Libraries.Game.Shapes

/*
    The Rectangle class stores the location of an unrotated rectangle. This is
    managed as a location on the screen in (x,y) coordinates coupled with a
    given height and width.
*/
class Rectangle
    
    number x = 0
    number y = 0
    number width = 0
    number height = 0

    /*
    This action sets the x and y coordinates of the Rectangle, along with the
    width and height.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetRectangle(20, 20, 200, 80)
            end
        end
    */
    action SetRectangle(number newX, number newY, number newWidth, number newHeight)
        x = newX
        y = newY
        width = newWidth
        height = newHeight
    end

    /*
    This action sets the coordinates, width, and height of this rectangle to be
    the same as the given rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle1
            Rectangle rectangle2

            action Main
                StartGame()
            end

            action CreateGame
                rectangle1:SetRectangle(20, 20, 200, 80)
                rectangle2:CopyRectangle(rectangle1)
            end
        end
    */
    action CopyRectangle(Rectangle rectangle)
        x = rectangle:x
        y = rectangle:y
        width = rectangle:width
        height = rectangle:height
    end

    /*
    This action returns the current x coordinate of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetRectangle(20, 20, 200, 80)
                number x = rectangle:GetX()
            end
        end
    */
    action GetX returns number
        return x
    end

    /*
    This action sets the current x coordinate of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetX(40)
            end
        end
    */
    action SetX(number newX)
        x = newX
    end

    /*
    This action returns the current y coordinate of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetRectangle(20, 20, 200, 80)
                number y = rectangle:GetY()
            end
        end
    */
    action GetY returns number
        return y
    end

    /*
    This action sets the current y coordinate of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetY(75)
            end
        end
    */
    action SetY(number newY)
        y = newY
    end

    /*
    This action returns the current width of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetWidth(100)
                number width = rectangle:GetWidth()
            end
        end
    */
    action GetWidth returns number
        return width
    end

    /*
    This action sets the current width of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetWidth(100)
            end
        end
    */
    action SetWidth(number newWidth)
        width = newWidth
    end
    
    /*
    This action returns the current height of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetHeight(100)
                number height = rectangle:GetHeight()
            end
        end
    */
    action GetHeight returns number
        return height
    end

    /*
    This action sets the current height of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetHeight(100)
            end
        end
    */
    action SetHeight(number newHeight)
        height = newHeight
    end

    /*
    This action sets the x and y coordinates of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetPosition(80, 100)
            end
        end
    */
    action SetPosition(number newX, number newY)
        x = newX
        y = newY
    end

    /*
    This action sets the width and height of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetSize(40, 70)
            end
        end
    */
    action SetSize(number newWidth, number newHeight)
        width = newWidth
        height = newHeight
    end

    /*
    This action sets the width and height of the rectangle to be the given value,
    making it into a square.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetSquare(60)
            end
        end
    */
    action SetSquare(number scale)
        width = scale
        height = scale
    end

    /*
    This action tests if the given point is contained within the Rectangle. If
    it is, this action will return true. Otherwise, it will return false.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetRectangle(50, 50, 100, 100)
                boolean hasPoint = rectangle:Contains(125, 75)
            end
        end
    */
    action Contains(number testX, number testY) returns boolean
        return x <= testX and x + width >= testX and y <= testY and y + height >= testY
    end

    /*
    This action tests if the entire given Rectangle is contained within this
    Rectangle. If it is, this action will return true. Otherwise, it will return
    false.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle1
            Rectangle rectangle2

            action Main
                StartGame()
            end

            action CreateGame
                rectangle1:SetRectangle(50, 50, 100, 100)
                rectangle2:SetRectangle(75, 75, 50, 50)
                boolean contains = rectangle1:Contains(rectangle2)
            end
        end
    */
    action Contains(Rectangle rectangle) returns boolean
        number xMin = rectangle:x
        number xMax = xMin + rectangle:width

        number yMin = rectangle:y
        number yMax = yMin + rectangle:height

        return (xMin > x and xMin < x + width) and (xMax > x and xMax < x + width) and (yMin > y and yMin < y + height) and (yMax > y and yMax < y + height)

    end

    /*
    This action tests if the given Rectangle overlaps with this Rectangle. If it
    is, this action will return true. Otherwise, it will return false.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle1
            Rectangle rectangle2

            action Main
                StartGame()
            end

            action CreateGame
                rectangle1:SetRectangle(50, 50, 100, 100)
                rectangle2:SetRectangle(100, 100, 100, 100)
                boolean overlap = rectangle1:Overlaps(rectangle2)
            end
        end
    */
    action Overlaps(Rectangle rectangle) returns boolean
        return (x < rectangle:x + rectangle:width and x + width > rectangle:x) and (y < rectangle:y + rectangle:height and y + height > rectangle:y)
    end

    /*
    This action will make this Rectangle expand to contain the given Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle1
            Rectangle rectangle2

            action Main
                StartGame()
            end

            action CreateGame
                rectangle1:SetRectangle(50, 50, 100, 100)
                rectangle2:SetRectangle(100, 100, 100, 100)
                rectangle1:Merge(rectangle2)
            end
        end
    */
    action Merge (Rectangle rectangle)
        number minX = 0.0
        number maxX = 0.0
        number minY = 0.0
        number maxY = 0.0

        if x < rectangle:x
            minX = x
        else
            minX = rectangle:x
        end

        if x > rectangle:x
            maxX = x
        else
            maxX = rectangle:x
        end

        if y < rectangle:y
            minY = y
        else
            minY = rectangle:y
        end

        if y > rectangle:y
            maxY = y
        else
            maxY = rectangle:y
        end

        x = minX
        width = maxX - minX
        
        y = minY
        height = maxY - minY

    end

    /*
    This action will make this Rectangle expand to contain the given point.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetRectangle(50, 50, 100, 100)
                rectangle:Merge(75, 250)
            end
        end
    */
    action Merge (number pointX, number pointY)
        number minX = 0.0
        number maxX = 0.0
        number minY = 0.0
        number maxY = 0.0

        if x < pointX
            minX = x
        else
            minX = pointX
        end

        if x > pointX
            maxX = x
        else
            maxX = pointX
        end

        if y < pointY
            minY = y
        else
            minY = pointY
        end

        if y > pointY
            maxY = y
        else
            maxY = pointY
        end

        x = minX
        width = maxX - minX
        
        y = minY
        height = maxY - minY

    end

    /*
    This action will return the aspect ratio of the Rectangle, which is the
    ratio of the width over the height of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetSize(200, 100)
                number ratio = rectangle:GetAspectRatio()
            end
        end
    */
    action GetAspectRatio returns number
        if height not= 0
            return width / height
        end
        return 0
    end

    /*
    This action will set the x, y coordinates of the Rectangle so that the
    center of the Rectangle is at the given x, y coordinates.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetSize(200, 100)
                rectangle:SetCenter(200, 150)
            end
        end
    */
    action SetCenter(number centerX, number centerY)
        SetPosition(centerX - width/2, centerY - height/2)
    end

    /*
    This action scales this Rectangle so it maintains the same aspect ratio and
    fits around the given Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle1
            Rectangle rectangle2

            action Main
                StartGame()
            end

            action CreateGame
                rectangle1:SetRectangle(0, 0, 200, 100)
                rectangle2:SetRectangle(100, 100, 150, 200)
                rectangle1:FitOutside(rectangle2)
            end
        end
    */
    action FitOutside(Rectangle rectangle)
        number ratio = GetAspectRatio()
        
        if ratio < rectangle:GetAspectRatio()
            SetSize(rectangle:height * ratio, rectangle:height)
        else
            SetSize(rectangle:width, rectangle:width / ratio)
        end

        SetPosition((rectangle:x + rectangle:width/2) - width/2, (rectangle:y + rectangle:height/2) - height/2)

    end

    /*
    This action scales this Rectangle so it maintains the same aspect ratio and
    fits inside the given Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle1
            Rectangle rectangle2

            action Main
                StartGame()
            end

            action CreateGame
                rectangle1:SetRectangle(0, 0, 200, 100)
                rectangle2:SetRectangle(100, 100, 150, 200)
                rectangle1:FitInside(rectangle2)
            end
        end
    */
    action FitInside(Rectangle rectangle)
        number ratio = GetAspectRatio()
        
        if ratio > rectangle:GetAspectRatio()
            SetSize(rectangle:height * ratio, rectangle:height)
        else
            SetSize(rectangle:width, rectangle:width / ratio)
        end

        SetPosition((rectangle:x + rectangle:width/2) - width/2, (rectangle:y + rectangle:height/2) - height/2)
    end

    /*
    This action returns the area of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetRectangle(0, 0, 200, 100)
                number area = rectangle:Area()
            end
        end
    */
    action Area returns number
        return width * height
    end

    /*
    This action returns the perimeter of the Rectangle.

    Attribute: Example

        use Libraries.Game.Shapes.Rectangle
        use Libraries.Game.Game

        class Main is Game

            Rectangle rectangle

            action Main
                StartGame()
            end

            action CreateGame
                rectangle:SetRectangle(0, 0, 200, 100)
                number perimeter = rectangle:Perimeter()
            end
        end
    */
    action Perimeter returns number
        return 2 * (width + height)
    end
end