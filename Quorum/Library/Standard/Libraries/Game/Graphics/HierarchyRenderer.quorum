package Libraries.Game.Graphics

use Libraries.Containers.Array
use Libraries.Interface.Item2D

/*
The HierarchyRenderer is an Item2D which custom renders all of its children
elements when the root is drawn. This is used internally by special components,
such as View2Ds or some controls. Most users will never need to use this class.
*/
class HierarchyRenderer is Item2D
    on create
        SetCustomDrawing(true)
    end

    action CustomDraw(Painter2D painter)
        DrawChildren(me, painter)
    end

    private action DrawChildren(Item2D item, Painter2D painter)
        if item:IsShowing() = false
            return now
        end

        if item:GetView2D() not= undefined
            item:GetView2D():Draw(painter)
        end

        if item is Drawable
            painter:Draw(cast(Drawable, item))
        end

        Array<Item2D> children = item:GetChildren()
        integer counter = 0
        repeat while counter < children:GetSize()
            DrawChildren(children:Get(counter), painter)
            counter = counter + 1
        end
    end
end