package Libraries.Game.Graphics

use Libraries.Containers.Number32BitArray
use Libraries.Containers.Integer32BitArray
use Libraries.Compute.Matrix3
use Libraries.Compute.Matrix4
use Libraries.Compute.Vector2
use Libraries.Compute.Vector3
use Libraries.Game.BoundingBox
use Libraries.Containers.Array
use Libraries.Game.Disposable
use Libraries.Game.Graphics.OpenGL.IndexBufferObject
use Libraries.Game.Graphics.OpenGL.VertexBufferObject
use Libraries.Game.Graphics.OpenGL.OpenGLManager
use Libraries.Game.Graphics.Shaders.ShaderProgram
use Libraries.Game.GameStateManager
use Libraries.Game.Graphics.Shaders.ShaderManager
use Libraries.Game.Graphics.OpenGL.OpenGLConstants

class Mesh is Disposable

    VertexData vertices = undefined
    IndexData indices = undefined
    boolean autoBind = true

    GameStateManager manager

    action Load(VertexData vertices, IndexData indices)
        me:vertices = vertices
        me:indices = indices

        AddReloadableMesh()
    end

    action Load(boolean isStatic, integer maxVertices, integer maxIndices, VertexAttributes attributes)
        vertices = MakeVertexBuffer(isStatic, maxVertices, attributes)
        
        if manager:GetGameGraphics() is OpenGLManager
IndexBufferObject index
        index:Load(isStatic, maxIndices)
        indices = index
        else
            alert("Vulkan support NYI")
        end
        
        isVertexArray = false

        AddReloadableMesh()
    end

    private action MakeVertexBuffer(boolean isStatic, integer maxVertices, VertexAttributes attributes) returns VertexData
if manager:GetGameGraphics() is OpenGLManager
        VertexBufferObject temp
        temp:Load(isStatic, maxVertices, attributes)
        return temp
        else
            alert("Vulkan support NYI")
            return undefined
end
    end

    action SetVertices(Number32BitArray vertices)
       me:vertices:SetVertices(vertices)
    end

    action UpdateVertices(integer targetOffset, Number32BitArray source)
        UpdateVertices(targetOffset, source, 0, source:GetSize())
    end

    action UpdateVertices(integer targetOffset, Number32BitArray source, integer sourceOffset, integer count)
        vertices:UpdateVertices(targetOffset, source, sourceOffset, count)
    end

    action SetIndices(Integer32BitArray indices)
        me:indices:SetIndices(indices, 0, indices:GetSize())
    end

    action SetIndices(Integer32BitArray indices, integer offset, integer count)
        me:indices:SetIndices(indices, offset, count)
    end

    action GetIndicesCount returns integer
        return indices:GetSize()
    end

    action GetVerticesCount returns integer
        return vertices:GetSize()
    end

    action GetMaxIndices returns integer
        return indices:GetMaxSize()
    end

    action GetMaxVertices returns integer
        return vertices:GetMaxSize()
    end

    action Dispose
        vertices:Dispose()
        indices:Dispose()
        RemoveReloadableMesh()
    end

    action GetVertexData returns VertexData
        return vertices
    end

    action GetIndexData returns IndexData
        return indices
    end

    action GetVertexAttributes returns VertexAttributes
        return vertices:GetAttributes()
    end

    /*
    This action will create a new BoundingBox object that will contain all of
    the vertices stored in this Mesh. If no vertices have been defined yet, this
    will alert an error.
    */
    action CalculateBoundingBox returns BoundingBox
        BoundingBox box
        return CalculateBoundingBox(box)
    end

    /*
    This action will calculate the bounds of this Mesh's vertices, and store it
    inside the given BoundingBox. This will overwrite any data in the provided
    BoundingBox. If no vertices have been defined yet for this Mesh, the action
    will alert an error.
    */
    action CalculateBoundingBox(BoundingBox box) returns BoundingBox
        box:Invalidate()

        if GetVerticesCount() = 0
            alert("There were no vertices defines for this Mesh!")
        end

        Number32BitBuffer buffer = GetVertexData():GetBuffer()

        VertexAttribute positionAttribute = GetVertexAttributes():FindByUsage(GetVertexAttributes():POSITION)
        integer offset = positionAttribute:offset / 4
        integer vertexSize = vertices:GetAttributes():vertexSize
        integer index = offset
        integer componentCount = positionAttribute:componentCount

        integer i = 0
        
        if componentCount = 1
            repeat while i < GetVerticesCount()
                box:Extend(buffer:Get(index), 0, 0)
                index = index + vertexSize
                i = i + 1
            end
        elseif componentCount = 2
            repeat while i < GetVerticesCount()
                box:Extend(buffer:Get(index), buffer:Get(index + 1), 0)
                index = index + vertexSize
                i = i + 1
            end
        elseif componentCount = 3
            repeat while i < GetVerticesCount()
                box:Extend(buffer:Get(index), buffer:Get(index + 1), buffer:Get(index + 2))
                index = index + vertexSize
                i = i + 1
            end
            
        end

        return box
    end

    /*
    This action will extend the given BoundingBox to include the vertices of
    this Mesh. If this Mesh has no vertices defined, this action will alert an
    error.
    */
    action ExtendBoundingBox(BoundingBox box, integer offset, integer count, Matrix4 transform) returns BoundingBox
        integer indicesCount = GetIndicesCount()

        if offset < 0
            alert("Attempted to extend bounding box with offset = " + offset + ", but the offset must be greater than 0.")
        elseif count < 1
            alert("I can't extend the bounding box using " + count + " indices -- the count must be >= 1")
        elseif (offset + count > indicesCount)
            alert("Out of bounds: attempted to access the indices between " + offset + " and " + (offset + count) + ", but there are only " + indicesCount + " total indices")
        end

        Number32BitBuffer vertexBuffer = vertices:GetBuffer()
        Integer32BitBuffer indexBuffer = indices:GetBuffer()

        VertexAttribute positionAttribute = GetVertexAttributes():FindByUsage(GetVertexAttributes():POSITION)

        integer positionOffset = positionAttribute:offset / 4
        integer vertexSize = vertices:GetAttributes():vertexSize / 4
        integer stop = offset + count
        integer componentCount = positionAttribute:componentCount

        Vector3 calcVector

        if componentCount = 1
            integer i = offset
            repeat while i < stop
                integer index = indexBuffer:Get(i) * vertexSize + positionOffset
                calcVector:Set(vertexBuffer:Get(index), 0, 0)
                if transform not= undefined
                    calcVector:Multiply(transform)
                end
                
                box:Extend(calcVector)

                i = i + 1
            end
        elseif componentCount = 2
            integer i = offset
            repeat while i < stop
                integer index = indexBuffer:Get(i) * vertexSize + positionOffset
                calcVector:Set(vertexBuffer:Get(index), vertexBuffer:Get(index + 1), 0)
                if transform not= undefined
                    calcVector:Multiply(transform)
                end
                
                box:Extend(calcVector)

                i = i + 1
            end
        elseif componentCount = 3
            integer i = offset
            repeat while i < stop
                integer index = indexBuffer:Get(i) * vertexSize + positionOffset
                calcVector:Set(vertexBuffer:Get(index), vertexBuffer:Get(index + 1), vertexBuffer:Get(index + 2))
                if transform not= undefined
                    calcVector:Multiply(transform)
                end
                
                box:Extend(calcVector)

                i = i + 1
            end
        end

        return box
    end

    action ExtendBoundingBox(BoundingBox box, integer offset, integer count)
        ExtendBoundingBox(box, offset, count, undefined)
    end

    /*
    This action forcibly reloads the Mesh's IndexData and VertexData. This is
    necessary after the OpenGL context is lost. This is automatically performed
    by the engine, so it shouldn't be necessary for users to call this directly.
    */
    action Reload
        indices:Reload()
        vertices:Reload()
    end

    /*
    Adds this mesh to the static collection of meshes in the plugins, which are
    maintained so that they can be reloaded in case of context loss.
    */
    private action AddReloadableMesh
        ShaderManager shaderManager = manager:GetShaderManager()
        shaderManager:AddReloadableMesh(me)
    end

    /*
    Removes this mesh from the static collection of meshes in the plugins, which
    are maintained so that they can be reloaded in case of context loss.
    */
    private action RemoveReloadableMesh
        ShaderManager shaderManager = manager:GetShaderManager()
        shaderManager:RemoveReloadableMesh(me)
    end

    action Render(ShaderProgram program, integer type, integer offset, integer count)
        Render(program, type, offset, count, autoBind)
    end

    action Render(ShaderProgram program, integer type, integer offset, integer count, boolean autoBind)
        if count = 0
            return now
        end

        if autoBind
            Bind(program)
        end

        GraphicsManager graphics = manager:GetGameGraphics()
        if graphics is OpenGLManager
            OpenGLManager openGL = cast(OpenGLManager, graphics)
            
if indices:GetSize() > 0
            OpenGLConstants constants
            openGL:DrawIndexedBuffer(type, count, constants:GL_UNSIGNED_INT, offset * 4)
        else
            openGL:DrawBuffer(type, offset, count)
        end
        else
            alert("Vulkan rendering NYI")
        end


        if autoBind
            Unbind(program)
        end
    end

    action Bind(ShaderProgram program)
        Bind(program, undefined)
    end

    action Bind(ShaderProgram program, Array<integer> locations)
        vertices:Bind(program, locations)
        if indices:GetSize() > 0
            indices:Bind()
        end
    end

    action Unbind(ShaderProgram program)
        Unbind(program, undefined)
    end

    action Unbind(ShaderProgram program, Array<integer> locations)
        vertices:Unbind(program, locations)
        if indices:GetSize() > 0
            indices:Unbind()
        end
    end

end