package Libraries.Game.Graphics

use Libraries.Game.Graphics.Attribute
use Libraries.Game.Graphics.DirectionalLight
use Libraries.Containers.Array

class DirectionalLightsAttribute is Attribute
    
    AttributeManager manager

    public constant text DIRECTIONAL_LIGHTS_ALIAS = "directionalLights"
    public constant integer DIRECTIONAL_LIGHTS = manager:Register(DIRECTIONAL_LIGHTS_ALIAS)

    /*
    An array containing all lights that are stored in this attribute.
    */
    public Array<DirectionalLight> lights

    on create
        SetAttribute(GetDirectionalLightsValue())
    end
    
    /*
    This action will copy the data from the given DirectionalLightsAttribute 
    into this object. Any data previously stored in this object will be replaced.
    */
    action CopyData(DirectionalLightsAttribute copyFrom)
        lights:Empty()
        integer index = 0
        repeat copyFrom:lights:GetSize() times
            lights:Add(copyFrom:lights:Get(index))
            index = index + 1
        end
    end

    /*
    This action will return a new DirectionalLightsAttribute object which is a 
    copy of this object.
    */
    action Copy returns Attribute
        DirectionalLightsAttribute new
        new:CopyData(me)
        return new
    end

    /*
    Returns the integer value representing a DirectionalLightsAttribute.
    */
    action GetDirectionalLightsValue returns integer
        return DIRECTIONAL_LIGHTS
    end

end