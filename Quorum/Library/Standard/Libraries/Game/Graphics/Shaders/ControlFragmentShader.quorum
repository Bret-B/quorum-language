package Libraries.Game.Graphics.Shaders

class ControlFragmentShader is Shader

    constant text SHADER_NAME = "Control Fragment Shader"

    on create
        SetName(SHADER_NAME)
        SetType(parent:Shader:FRAGMENT_SHADER)
        SetCode(GetOpenGLVersion() +
"
" +
"#ifdef GL_ES
    #define LOWP lowp
    precision mediump float;
#else
    #define LOWP 
#endif

in LOWP vec4 v_color;
in LOWP vec4 v_borderColor;
in vec2 v_texCoords;
flat in int v_arrayIndex;

uniform sampler2D u_texture;
uniform vec4 u_rounding[200];
uniform vec4 u_borderThickness[200];
uniform float u_aspectRatio[200];
uniform float u_antiAliasDistance[200];

layout (location = 0) out vec4 outputColor;

// Calculate how far a point is from the outer shell of a rounded rectangle.
// The rounding vector contains edge roundness values for:
// x: top-right
// y: bottom-right
// z: top-left
// w: bottom-left
float signedDistanceFunction(in vec2 point, in vec2 dimensions, in vec4 rounding)
{
    // We calculate as though the point is in the top-right quadrant.
    // If this isn't true, we need adjust which of the edge rounding values we use.
    rounding.xy = (point.x > 0.0) ? rounding.xy : rounding.zw;
    rounding.x = (point.y > 0.0) ? rounding.x : rounding.y;
    
    // We take the absolute value of the point to ensure we're calculating from the top-right quadrant
    vec2 calc = abs(point) - dimensions + rounding.x;
    return min(max(calc.x, calc.y), 0.0) + length(max(calc, 0.0)) - rounding.x;
}

void main()
{
    vec4 rounding = u_rounding[v_arrayIndex];
    vec4 borderThickness = u_borderThickness[v_arrayIndex];
    float aspectRatio = u_aspectRatio[v_arrayIndex];
    float antiAliasDistance = u_antiAliasDistance[v_arrayIndex];

    bool renderBorders = (borderThickness.x + borderThickness.y + borderThickness.z + borderThickness.w) > 0;

    vec2 point = (v_texCoords.xy - 0.5) * 2.0;
    point.x *= aspectRatio;
    vec2 dimensions = vec2(aspectRatio, 1.0);

    float distance = signedDistanceFunction(point, dimensions, rounding);
    vec4 resultColor;

    if (renderBorders)
    {
        // The enabled borders vector stores values for the sides in the following order:
        // x: right
        // y: top (y-positive: technically the bottom due to how GLSL handles texture coordinates)
        // z: left
        // w: bottom (y-negative)
        vec2 fillPoint = point - vec2(borderThickness.x - borderThickness.z, borderThickness.y - borderThickness.w);
        vec2 fillDimensions = vec2(dimensions.x - borderThickness.x - borderThickness.z, dimensions.y - borderThickness.y - borderThickness.w);
        float fillDistance = signedDistanceFunction(fillPoint, fillDimensions, rounding);
    
        resultColor = mix(v_borderColor * texture(u_texture, v_texCoords), vec4(v_borderColor.rgb, 0.0), smoothstep(0.0, antiAliasDistance, distance));
        resultColor = mix(v_color * texture(u_texture, v_texCoords), resultColor, smoothstep(0.0, antiAliasDistance, fillDistance));
    }
    else
    {
        resultColor = v_color * texture(u_texture, v_texCoords);
        resultColor = mix(resultColor, vec4(resultColor.rgb, 0.0), smoothstep(0.0, antiAliasDistance, distance));
    }

    outputColor = resultColor;
}")
    end

end