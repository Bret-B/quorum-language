package Libraries.Interface.Controls

use Libraries.Containers.Array
use Libraries.Game.Graphics.Painter2D
use Libraries.Interface.Layouts.LayoutProperties

class ScrollableMenuPanel is MenuPanelBlueprint

    number itemsInView = 10
    number scrollIndex = 0
    
    Array<MenuItem> menuItems

    Control scrollAnchor
    Control scrollBar
    Control scrollControl

    on create
        SetClipping(true)
        SetCustomDrawing(true)

        scrollAnchor:SetName("Scroll Anchor")
        scrollBar:SetName("Scroll Bar")
        scrollControl:SetName("Scroll Control")
    end

    action Draw(Painter2D painter)

    end

    action SetItemsInView(number itemCount)
        if itemCount = itemsInView
            return now
        end

        itemsInView = itemCount
        RequestLayout()
    end

    action GetItemsInView returns number
        return itemsInView
    end

    action Add(MenuItem item)
        menuItems:Add(item)
        parent:Control:Add(item)
    end

    action Add(integer index, MenuItem item)
        menuItems:Add(index, item)
        parent:Control:Add(index, item)
    end

    action Remove(MenuItem item)
        menuItems:Remove(item)
        parent:Control:Remove(item)
    end

    action Remove(integer index) returns Item2D
        menuItems:RemoveAt(index)
        return parent:Control:Remove(index)
    end

    action GetSize returns integer
        return menuItems:GetSize()
    end

    /*
    This action returns the index of the first MenuItem visible in the scroll
    pane. This can return non-integer values to indicate the first item is
    partially offscreen -- for example, a returned value of 8.4 would indicate
    that the first visible item in the scroll pane is the 8th indexed item, and
    it is 40% of the way off of the top of the pane.

    Attribute: Returns The index of the first item visible in the scroll pane, with a decimal component describing how much of it is off the top of the panel (if any).
    */
    action GetScrollIndex returns number
        return scrollIndex
    end

    action Resize
        /*
        We manually override the Resize call here to do special work.
        This is more convenient than using a Layout because we're doing
        custom rendering on our components anyway and we want to limit the
        amount of layout work we're doing to only the relevant MenuItems.
        */

        // If we're already resizing, return immediately from a recursive call.
        if parent:Item2D:isResizing
            return now
        end

        parent:Item2D:isResizing = true

        LayoutProperties containerProperties = GetDefaultLayoutProperties()

        number leftBorder = 0
        number rightBorder = 0
        number topBorder = 0
        number bottomBorder = 0

        number thickness = containerProperties:GetBorderThickness()

        if containerProperties:HasLeftBorder()
             leftBorder = thickness
        end
        if containerProperties:HasRightBorder()
            rightBorder = thickness
        end
        if containerProperties:HasTopBorder()
            topBorder = thickness
        end
        if containerProperties:HasBottomBorder()
            bottomBorder = thickness
        end

        number longestWidth = 0
        integer i = 0

        repeat while i < menuItems:GetSize()
            MenuItem menuItem = menuItems:Get(i)
            LayoutProperties properties = menuItem:GetDefaultLayoutProperties()

            if properties:NeedsRendering()
                menuItem:LoadGraphics(properties)
            end

            if menuItem:GetIconWidth() > GetIconRegionWidth()
                SetIconRegionWidth(menuItem:GetIconWidth())
            end
            if menuItem:GetNameWidth() > GetLabelRegionWidth()
                SetLabelRegionWidth(menuItem:GetNameWidth())
            end
            if menuItem:GetShortcutWidth() > GetShortcutRegionWidth()
                SetShortcutRegionWidth(menuItem:GetShortcutWidth())
            end
            if menuItem:GetSubmenuIconWidth() > GetArrowRegionWidth()
                SetArrowRegionWidth(menuItem:GetSubmenuIconWidth())
            end

            i = i + 1
        end
 
        integer intIndex = cast(integer, scrollIndex)
        integer itemsToView = cast(integer, itemsInView)

        // If there's a fractional component, we need to consider an additional item which will be at least partially off the screen.
        if itemsToView not= itemsInView
            itemsToView = itemsToView + 1
        end

        integer last = intIndex + itemsToView
        number offset = scrollIndex - intIndex

        MenuItem lastItem = menuItems:Get(last)
        lastItem:Resize()

        if longestWidth < lastItem:GetWidth()
            longestWidth = lastItem:GetWidth()
        end

        number y = -(1 - offset) * lastItem:GetHeight()
        lastItem:SetPosition(leftBorder, y)
        y = y + lastItem:GetHeight()

        number largestHeight = 0

        i = last
        repeat while i > intIndex
            i = i - 1
            MenuItem kid = menuItems:Get(i)
            kid:Resize()

            if longestWidth < kid:GetWidth()
                longestWidth = kid:GetWidth()
            end

            kid:SetPosition(leftBorder, y)

            y = y + kid:GetHeight()

            if kid:GetHeight() > largestHeight
                largestHeight = kid:GetHeight()
            end
        end

        // Set the size of the menu panel to fit the items in view, and adjust the position so the top remains in the same place.
        number oldHeight = GetHeight()
        SetSize(longestWidth + leftBorder + rightBorder, bottomBorder + topBorder + largestHeight * itemsInView)
        MoveY(oldHeight - GetHeight())

        repeat while i < menuItems:GetSize()
            Item2D kid = menuItems:Get(i)
            kid:SetWidth(longestWidth)
            i = i + 1
        end

        /*
        We need to reset our layout flag to indicate we don't need resizing
        until more changes occur, and reset our resizing flag to allow future calls.
        */
        ResetLayoutFlag()
        parent:Item2D:isResizing = false
    end
end