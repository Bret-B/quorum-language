package Libraries.Interface.Controls

use Libraries.Compute.Vector2
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.Painter2D
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Views.DrawableView

class ScrollableMenuPanel is MenuPanelBlueprint

    number itemsInView = 10
    number scrollIndex = 0
    number barThickness = 20

    Control scrollBar
    Control scrollControl

    // Whether or not the scroll bar is currently being dragged with the mouse.
    boolean dragging = false

    // Whether the scroll bars have had their graphics loaded yet.
    boolean scrollGraphicsLoaded = false

    Vector2 scrollClickPoint

    on create
        SetClipping(true)
        SetCustomDrawing(true)
        SetChildrenProcessingMouseEvents(true)

        SetName("Scroll Menu Panel")
        SetInputGroup("ScrollableMenuPanel")

        scrollBar:SetName("Scroll Bar")
        scrollControl:SetName("Scroll Control")

        Add(scrollBar)
        scrollBar:Add(scrollControl)
    end

    action IsScrollable returns boolean
        return true
    end

    action Draw(Painter2D painter)
        if not IsShowing()
            return now
        end

        if GetView2D() not= undefined
            GetView2D():Draw(painter)
        end

        integer intIndex = cast(integer, scrollIndex)
        number last = scrollIndex + itemsInView

        if last >= GetSize()
            last = GetSize() - 1
        end

        integer counter = intIndex
        repeat while counter <= last
            MenuItem item = Get(counter)
            item:Draw(painter)
            counter = counter + 1
        end

        scrollBar:Draw(painter)
    end

    action SetItemsInView(number itemCount)
        if itemCount = itemsInView
            return now
        end

        itemsInView = itemCount
        // Setting the scroll index again triggers a resize, possibly updating
        // the index to a new value if the old value is no longer valid.
        SetScrollIndex(scrollIndex)
    end

    action GetItemsInView returns number
        return itemsInView
    end

    /*
    This action returns the index of the first MenuItem visible in the scroll
    pane. This can return non-integer values to indicate the first item is
    partially offscreen -- for example, a returned value of 8.4 would indicate
    that the first visible item in the scroll pane is the 8th indexed item, and
    it is 40% of the way off of the top of the pane.

    Attribute: Returns The index of the first item visible in the scroll pane, with a decimal component describing how much of it is off the top of the panel (if any).
    */
    action GetScrollIndex returns number
        return scrollIndex
    end

    action SetScrollIndex(number index)
        if index < 0
            index = 0
        end
        scrollIndex = index
        Resize()
    end

    action Resize
        /*
        We manually override the Resize call here to do special work.
        This is more convenient than using a Layout because we're doing
        custom rendering on our components anyway and we want to limit the
        amount of layout work we're doing to only the relevant MenuItems.
        */

        // If we're already resizing, return immediately from a recursive call.
        if parent:Item2D:isResizing
            return now
        end

        if IsEmpty()
            return now
        end

        if scrollIndex + itemsInView > GetSize()
            scrollIndex = GetSize() - itemsInView
        end

        if scrollIndex < 0
            scrollIndex = 0
        end

        parent:Item2D:isResizing = true

        LayoutProperties containerProperties = GetDefaultLayoutProperties()

        number leftBorder = 0
        number rightBorder = 0
        number topBorder = 0
        number bottomBorder = 0

        number thickness = containerProperties:GetBorderThickness()

        if containerProperties:HasLeftBorder()
             leftBorder = thickness
        end
        if containerProperties:HasRightBorder()
            rightBorder = thickness
        end
        if containerProperties:HasTopBorder()
            topBorder = thickness
        end
        if containerProperties:HasBottomBorder()
            bottomBorder = thickness
        end

        number longestWidth = 0
        integer i = 0

        integer intIndex = cast(integer, scrollIndex)
        integer itemsToView = cast(integer, itemsInView)

        repeat while i < GetSize()
            MenuItem menuItem = Get(i)
            LayoutProperties properties = menuItem:GetDefaultLayoutProperties()

            if i < intIndex or i > scrollIndex + itemsInView
                menuItem:Hide()
            else
                menuItem:Show()
            end

            if properties:NeedsRendering()
                menuItem:LoadGraphics(properties)
            end

            if menuItem:GetIconWidth() > GetIconRegionWidth()
                SetIconRegionWidth(menuItem:GetIconWidth())
            end
            if menuItem:GetNameWidth() > GetLabelRegionWidth()
                SetLabelRegionWidth(menuItem:GetNameWidth())
            end
            if menuItem:GetShortcutWidth() > GetShortcutRegionWidth()
                SetShortcutRegionWidth(menuItem:GetShortcutWidth())
            end
            if menuItem:GetSubmenuIconWidth() > GetArrowRegionWidth()
                SetArrowRegionWidth(menuItem:GetSubmenuIconWidth())
            end

            i = i + 1
        end
 
        integer last = cast(integer, scrollIndex + itemsInView)

        if last >= GetSize()
            last = GetSize() - 1
        end

        number offset = (scrollIndex + itemsInView) - last
        if itemsInView > GetSize()
            offset = 1
        end

        MenuItem lastItem = Get(last)
        lastItem:Resize()

        if longestWidth < lastItem:GetWidth()
            longestWidth = lastItem:GetWidth()
        end

        number y = -(1 - offset) * lastItem:GetHeight()
        lastItem:SetPosition(leftBorder, y)
        y = y + lastItem:GetHeight()

        number largestHeight = 0

        i = last
        repeat while i > intIndex
            i = i - 1
            MenuItem kid = Get(i)
            kid:Resize()

            if longestWidth < kid:GetWidth()
                longestWidth = kid:GetWidth()
            end

            kid:SetPosition(leftBorder, y)

            y = y + kid:GetHeight()

            if kid:GetHeight() > largestHeight
                largestHeight = kid:GetHeight()
            end
        end

        // Set the size of the menu panel to fit the items in view, and adjust the position so the top remains in the same place.
        number oldHeight = GetHeight()

        repeat while i < GetSize()
            Item2D kid = Get(i)
            kid:SetWidth(longestWidth)
            i = i + 1
        end

        scrollBar:SetPosition(longestWidth, 0)
        scrollBar:SetSize(barThickness, GetHeight())

        number controlRatio = itemsInView / GetSize()
        if controlRatio >= 1.0
            SetSize(longestWidth + leftBorder + rightBorder, largestHeight * GetSize())
            MoveY(oldHeight - GetHeight())

            scrollControl:Hide()
            scrollBar:Hide()
        else
            SetSize(longestWidth + leftBorder + rightBorder + barThickness, bottomBorder + topBorder + largestHeight * itemsInView)
            MoveY(oldHeight - GetHeight())

            scrollControl:Show()
            scrollBar:Show()

            scrollControl:SetSize(barThickness, GetHeight() * controlRatio)
            scrollControl:SetPosition(0, GetHeight() * (GetSize() - (scrollIndex + itemsInView)) / GetSize())
        end

        /*
        We need to reset our layout flag to indicate we don't need resizing
        until more changes occur, and reset our resizing flag to allow future calls.
        */
        ResetLayoutFlag()
        parent:Item2D:isResizing = false
    end

    action LoadGraphics(LayoutProperties properties)
        if scrollGraphicsLoaded = false
            Color color
            DrawableView scrollBarView
            DrawableView scrollControlView

            scrollBarView:LoadFilledRectangle(1, 1, color:CustomColor(0.9, 0.9, 0.9, 1))
            scrollControlView:LoadFilledRectangle(1, 1, color:CustomColor(0.75, 0.75, 0.75, 1))

            scrollBar:SetView2D(scrollBarView)
            scrollControl:SetView2D(scrollControlView)
        end
        scrollGraphicsLoaded = true
        parent:Control:LoadGraphics(properties)
    end

    /*
    This action returns true if the user is currently dragging the scroll bar
    with the mouse, or false otherwise.

    Attribute: Returns Whether or not the scroll bar is being dragged with the mouse.
    */
    action IsDragging returns boolean
        return dragging
    end

    action SetDragging(boolean drag)
        dragging = drag
    end

    action SetScrollClickPoint(number x, number y)
        scrollClickPoint:Set(x, y)
    end

    private action GetScrollClickPoint returns Vector2
        return scrollClickPoint
    end

    action DragScrollControlTo(number x, number y)
        if not IsDragging()
            return now
        end

        number offset = y - scrollClickPoint:GetY()
        if scrollControl:GetY() + scrollControl:GetHeight() + offset > scrollBar:GetHeight()
            SetScrollIndex(0)
        elseif scrollControl:GetY() + offset <= 0
            SetScrollIndex(GetSize() - itemsInView)
        else
            number value = (1.0 - ((scrollControl:GetY() + offset) / (GetHeight() - scrollControl:GetHeight()))) * (GetSize() - itemsInView)
            SetScrollIndex(value)
        end

        scrollClickPoint:SetY(y)
    end

    action ScrollToView(MenuItem item)
        integer index = parent:MenuPanelBlueprint:menuItems:GetFirstLocation(item)
        if index < 0
            return now
        end

        if index < scrollIndex
            SetScrollIndex(index)
        elseif index + 1 > scrollIndex + itemsInView
            SetScrollIndex(index + 1 - itemsInView)
        end
    end
end