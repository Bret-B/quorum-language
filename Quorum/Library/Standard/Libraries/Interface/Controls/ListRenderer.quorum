package Libraries.Interface.Controls

use Libraries.Game.Graphics.Painter2D
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Views.ControlShaderView
use Libraries.Containers.Array
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Game.Graphics.ColorGroup

class ListRenderer is Control

    Array<ListItem> drawnItems

    on create
        ManualLayout manual
        SetLayout(manual)

        SetName("List Background")

        // We want custom drawing to occur, but we also want to make sure ListItems can still receive mouse input.
        SetCustomDrawing(true)
        SetChildrenProcessingMouseEvents(true)
    end

    action GetList returns List
        return cast(List, GetParent())
    end

    action GetListItem(integer index) returns ListItem
        return cast(ListItem, GetChildren():Get(index))
    end

    action Empty
        parent:Item2D:Empty()
        drawnItems:Empty()
    end

    action LoadGraphics(LayoutProperties properties)
        if GetView2D() = undefined
            ControlShaderView newView
            newView:Initialize()
            SetView2D(newView)
        end

        parent:Control:LoadGraphics(properties)
    end

    action RefreshContents
        drawnItems:Empty()

        List list = GetList()
        if list = undefined
            return now
        end

        integer firstLine = list:GetFirstVisibleLine()

        boolean lastItem = false
        number x = 0
        number width = GetWidth()
        integer i = 0

        repeat while i < list:GetLineCount()
            ListItem item = GetListItem(i)

            if i < firstLine or lastItem = true
                item:Hide()
            else
                item:Show()

                if item:GetDefaultLayoutProperties():NeedsRendering()
                    item:LoadGraphics(item:GetDefaultLayoutProperties())
                end
    
                if item:NeedsLayout()
                    item:Resize()
                end
    
                number y = list:GetLineY(i)
                if y < 0
                    lastItem = true
                end
    
                drawnItems:Add(item)
                item:SetPosition(x, y)
                item:SetWidth(width)
            end

            i = i + 1
        end
    end

    action Draw(Painter2D painter)
        if GetView2D() not= undefined
            GetView2D():Draw(painter)
        end

        integer counter = 0
        repeat while counter < drawnItems:GetSize()
            ListItem item = drawnItems:Get(counter)
            item:Draw(painter)
            counter = counter + 1
        end
    end

    action GetBackgroundColor returns ColorGroup
        if HasColorPropertyOverride("backgroundColor")
            return parent:Control:GetBackgroundColor()
        end

        List list = GetList()
        if list not= undefined
            return list:GetBackgroundColor()
        else
            return parent:Control:GetBackgroundColor()
        end
    end

end