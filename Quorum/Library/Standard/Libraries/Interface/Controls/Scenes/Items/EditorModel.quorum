package Libraries.Interface.Controls.Scenes.Items

use Libraries.Game.Graphics.Model
use Libraries.Game.Graphics.Color
use Libraries.Containers.Array
use Libraries.Game.Graphics.Material
use Libraries.Game.Graphics.ColorAttribute
use Libraries.Data.Formats.JavaScriptObjectNotation
use Libraries.Interface.Events.FocusEvent
use Libraries.Compute.Vector3
use Libraries.Game.Scenes.SceneProperties


/*
The EditorModel class represents a 3D item that is being edited in the SceneEditor. Other 
than representing a 3D model to be drawn on the screen, this class contains helper functions
that allow for setting the rotation of the 3D object from a set of 3 angles. As well as setting and 
resetting the scale of the object. 
*/
class EditorModel is Model

    number yaw = 0
    number pitch = 0
    number roll = 0

    /*
        This action sets the orientation of the 3D object using yaw, pitch, and roll
        angles set in a Vector3.
    
        The yaw describes how far the camera is rotated along the X/Z axis. It
        effectively controls how the camera turns left and right. A positive value
        will rotate the camera clockwise (typically to the right).
    
        The pitch describes how far the camera is angled up or down. The pitch value
        should typically be between -90 and 90 degrees. Larger values will cause the
        camera to look in the opposite direction specified by the yaw value. For
        pitch values between -90 and 90, positive values angle the camera upwards,
        and negative values angle it downwards.
    
        The roll describes how the top of the camera is oriented. A roll value of 0
        will angle the camera so the top of the camera points upward, towards the
        positive Y axis. A positive roll value "twists" the camera clockwise, and a
        negative value will twist it counter-clockwise.
    
        Attribute: Parameter rotation Vector containing the 3 angles to set the rotation to
    */
    action SetRotation(Vector3 rotation)
        SetRotation(rotation:GetX(), rotation:GetY(), rotation:GetZ())
    end

    /*
        This action sets the orientation of the 3D object using yaw, pitch, and roll
        angles in degrees.
    
        The yaw describes how far the camera is rotated along the X/Z axis. It
        effectively controls how the camera turns left and right. A positive value
        will rotate the camera clockwise (typically to the right).
    
        The pitch describes how far the camera is angled up or down. The pitch value
        should typically be between -90 and 90 degrees. Larger values will cause the
        camera to look in the opposite direction specified by the yaw value. For
        pitch values between -90 and 90, positive values angle the camera upwards,
        and negative values angle it downwards.
    
        The roll describes how the top of the camera is oriented. A roll value of 0
        will angle the camera so the top of the camera points upward, towards the
        positive Y axis. A positive roll value "twists" the camera clockwise, and a
        negative value will twist it counter-clockwise.
    
        Attribute: Parameter newYaw The yaw angle, measured in degrees.
        Attribute: Parameter newPitch The pitch angle, measured in degrees.
        Attribute: Parameter newRoll The roll angle, measured in degrees.
    */
    action SetRotation(number newYaw, number newPitch, number newRoll)

        if roll not= 0
            Rotate(0, 0, 1, -roll)
        end
        if pitch not= 0
            Rotate(1, 0, 0, -pitch)
        end
        if yaw not= 0
            Rotate(0, 1, 0, -yaw)
        end

        yaw = newYaw
        pitch = newPitch
        roll = newRoll

        if yaw not= 0
            Rotate(0, 1, 0, yaw)
        end
        if pitch not= 0
            Rotate(1, 0, 0, pitch)
        end
        if roll not= 0
            Rotate(0, 0, 1, roll)
        end
    end


    /*
        This action returns the yaw of the model in degrees.
        
        Attribute: Returns The yaw angle, measured in degrees.
    */
    action GetYaw returns number
        return yaw
    end

    /*
        This action returns the pitch of the model in degrees.
        
        Attribute: Returns The pitch angle, measured in degrees.
    */
    action GetPitch returns number
        return pitch
    end

    /*
        This action returns the roll of the model in degrees.
        
        Attribute: Returns The roll angle, measured in degrees.
    */
    action GetRoll returns number
        return roll
    end

    /*
        ResetScale sets the scale of the object along each axis to the default which is usually 1. 
        The scale of the object affects how large it is in the scene. So a scale of 2 on the x-axis
        means the object is twice as big on the x-axis than when normally sized. 
    */
    action ResetScale
        SetScale(1, 1, 1)
    end

    /*
        GetScale returns the scale of the 3D object along all 3 axes. 
        Attribute: Returns A Vector3 containing the scale along the X, Y, and Z axis 
    */
    action GetScale returns Vector3
        Vector3 vector
        vector:Set(GetScaleX(), GetScaleY(), GetScaleZ())
        return vector
    end

end