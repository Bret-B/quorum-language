package Libraries.Interface.Controls

use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Game.Graphics.ColorGroup
use Libraries.Interface.Views.ControlShaderView
use Libraries.Interface.Layouts.ManualLayout

class ScrollBarTrack is Control

    boolean vertical = true

    on create
        SetName("Scroll Bar Track")

        ManualLayout layout
        SetLayout(layout)
    end

    action LoadGraphics(LayoutProperties properties)
        if GetView2D() = undefined
            ControlShaderView view
            view:SetName("Scroll Track View")
            view:Initialize()
            SetView2D(view)
        end

        parent:Control:LoadGraphics(properties)
    end

    action GetBackgroundColor returns ColorGroup
        if HasColorPropertyOverride("backgroundColor")
            return parent:Control:GetBackgroundColor()
        end

        ScrollableControl control = GetScrollableControl()
        if control not= undefined
            return control:GetForegroundColor()
        end

        return parent:Control:GetBackgroundColor()
    end

    action GetScrollableControl returns ScrollableControl
        Item2D result = GetParent()
        repeat until result is ScrollableControl or result = undefined
            result = result:GetParent()
        end
        return cast(ScrollableControl, result)
    end

    action IsVertical returns boolean
        return vertical
    end

    action SetVertical(boolean vertical)
        me:vertical = vertical
    end

end