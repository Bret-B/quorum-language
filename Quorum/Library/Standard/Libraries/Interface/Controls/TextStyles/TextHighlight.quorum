package Libraries.Interface.Controls.TextStyles

use Libraries.Game.Graphics.Drawable

class TextHighlight 

    boolean isLineHighlight = false
    boolean visibleWhileSelected = true
    boolean isError = false


    /*
    This action returns a Drawable representing the visual content of a text
    highlight of a given width and height.
    */
    blueprint action GetDrawable(number width, number height) returns Drawable

    /*
    This action sets whether or not this highlight should be applied to an
    entire line of text. The highlight will be applied to whatever line contains
    the start index of the TextStyle that is using this highlight.

    Attribute: Parameter isLineHighlight True to make this highlight appear on an entire line, false to make it appear only for the TextStyle's specific text.
    */
    action SetLineHighlight(boolean isLineHighlight)
        me:isLineHighlight = isLineHighlight
    end

    /*
    This action returns whether or not this highlight is applied to an entire
    line of text.
    */
    action IsLineHighlight returns boolean
        return isLineHighlight
    end

    /*
    This action sets whether or not this highlight will be rendered on top of 
    the selection highlight for selected text. If the provided value is false, 
    then the highlight will appear normally on unselected text, but will be 
    hidden on selected text. If the text's selection color uses transparency,
    this highlight will be partially visible underneath the selection highlight.
    */
    action SetVisibleWhileSelected(boolean visible)
        visibleWhileSelected = visible
    end

    /*
    This action returns whether or not this highlight will be visible on 
    selected text. If this value is false and text is selected, the selection
    highlight will be rendered on top of this highlight. Note that if the
    selection highlight uses transparency, this highlight will be partially
    visible underneath the selection highlight.
    */
    action IsVisibleWhileSelected returns boolean
        return visibleWhileSelected
    end

    /*
    The SetErrorFlag action is used to designate this highlight as representing
    an error in the text, most commonly a spelling error. This is used by the
    accessibility platform to inform screen readers if they interact with a
    spelling error.
    */
    action SetErrorFlag(boolean error)
        isError = error
    end

    /*
    This action returns a value indicating whether or not this highlight
    represents an error in the text, most commonly a spelling error. This is
    used by the accessibility platform to inform screen readers if they interact
    with a spelling error. By default, this value returns false.
    */
    action IsError returns boolean
        return isError
    end
end