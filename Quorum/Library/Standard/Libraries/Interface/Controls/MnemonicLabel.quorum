package Libraries.Interface.Controls

use Libraries.Game.Graphics.Label
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Font
use Libraries.Game.GameStateManager
use Libraries.Interface.Item2D
use Libraries.Game.Graphics.Glyph

class MnemonicLabel is Label

    text mnemonic = undefined
    Drawable underline = undefined
    boolean visible = true
    GameStateManager manager

    action SetMnemonic(text mnemonic)
        parent:Label:textChanged = true
        me:mnemonic = mnemonic
    end

    action GetMnemonic returns text
        return mnemonic
    end

    action ShowUnderline
        visible = true
        if underline not= undefined
            underline:Show()
        end
    end

    action HideUnderline
        visible = false
        if underline not= undefined
            underline:Hide()
        end
    end

    private action LoadDrawables
        if not manager:IsDisplayAvailable()
            return now
        end

        parent:Label:LoadDrawables()

        underline = undefined

        Font font = GetFont()
        if font:IsLoaded() = false or mnemonic = undefined
            return now
        end

        text labelText = GetText()
        text targetText = mnemonic:ToUpperCase()
        integer index = labelText:IndexOf(targetText)

        if index < 0
            targetText = mnemonic:ToLowerCase()
            index = labelText:IndexOf(targetText)
        end

        if index >= 0
            Item2D child = GetChildren():Get(index)
            Glyph glyph = font:GetGlyph(targetText)
            number x = child:GetX() - glyph:lengthToGlyph
            number y = child:GetY() - glyph:heightFromBaseLine + child:GetHeight() + font:GetUnderlinePosition()

            Drawable underline = GetDrawable()
            me:underline = underline
            if visible = false
                underline:Hide()
            end
            underline:LoadFilledRectangle(glyph:horizontalAdvance, font:GetUnderlineThickness(), GetFontColor())
            underline:SetPosition(x, y)
            Add(underline)
        end
    end

end