package Libraries.Interface.Controls

use Libraries.Game.Graphics.Label
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Color
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Interface.Behaviors.Controls.OptionDialogCancelBehavior
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Views.ControlShaderView

class OptionDialog is Dialog
    Label lineLabel
    boolean initialized = false
    boolean selectedOk = false
    Button ok
    Button cancel

    OptionDialogCancelBehavior defaultCancelBehavior
    Behavior cancelBehavior = defaultCancelBehavior

    on create
        SetModal(true)
        lineLabel:SetText("Are you sure?")

        SetInterfaceOptionsKey("Dialog")
        SetBorderThickness(1)

        Setup()
    end

    action GetLabelTitle returns text
        return lineLabel:GetText()
    end

    action SetLabelTitle(text title)
        SetName(title + " Dialog")
        lineLabel:SetText(title)
    end

    action GetCancelBehavior returns Behavior
        return cancelBehavior
    end

    action SetCancelBehavior(Behavior behavior)
        cancelBehavior = behavior
        cancel:SetBehavior(behavior)
    end

    action SetBehavior(Behavior behavior)
        parent:Dialog:SetBehavior(behavior)
        ok:SetBehavior(behavior)
    end

    private action Setup
        initialized = true
        SetPixelWidth(400)
        SetVerticalLayoutMode(parent:Libraries.Interface.Controls.Control:FIT_CONTENTS)
        defaultCancelBehavior:SetDialog(me)

        SetNextFocus(lineLabel)
        SetPreviousFocus(ok)

        ControlShaderView view
        view:Initialize()

        LayoutProperties props = lineLabel:GetDefaultLayoutProperties()
        props:SetPercentageOriginX(0.5)
        props:SetPercentageOriginY(0.5)
        props:SetPercentageX(0.2)
        props:SetPercentageY(0.525)
        lineLabel:SetNextFocus(cancel)
        lineLabel:SetPreviousFocus(ok)

        //setup the ok button
        props = ok:GetDefaultLayoutProperties()
        props:SetPercentageOriginX(0.5)
        props:SetPercentageOriginY(0.5)
        props:SetPercentageX(0.825)
        props:SetPercentageY(0.5)
        ok:SetInterfaceOptionsKey("DialogAcceptButton")
        ok:SetName("Yes")
        ok:SetFontSize(14)
        ok:SetHorizontalLayoutMode(ok:parent:Libraries.Interface.Controls.Control:STANDARD)
        ok:SetPercentageWidth(0.25)
        ok:SetFocusable(true)
        ok:SetNextFocus(cancel)
        ok:SetPreviousFocus(cancel)

        props = cancel:GetDefaultLayoutProperties()
        props:SetPercentageOriginX(0.5)
        props:SetPercentageOriginY(0.5)
        props:SetPercentageX(0.55)
        props:SetPercentageY(0.5)
        cancel:SetName("No")
        cancel:SetFontSize(14)
        cancel:SetHorizontalLayoutMode(ok:parent:Libraries.Interface.Controls.Control:STANDARD)
        cancel:SetPercentageWidth(0.25)
        cancel:SetFocusable(true)
        cancel:SetNextFocus(ok)
        cancel:SetBehavior(defaultCancelBehavior)
        cancel:SetPreviousFocus(ok)

        Control panel
        Color color
        ManualLayout findLayout
        panel:SetLayout(findLayout)
        panel:SetPercentageWidth(1.0)
        panel:SetPixelHeight(60)
        panel:SetBorderColor(color:Black())
        panel:SetBorderThickness(1)

        panel:Add(lineLabel)
        panel:Add(cancel)
        panel:Add(ok)
        panel:SetView2D(view)
        Add(panel)
    end

    action OnShow
        ok:Focus()
    end
end