package Libraries.Interface.Controls.Charts

use Libraries.Interface.Controls.Control
use Libraries.Game.Graphics.Color
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Interface.Views.ControlShaderView
use Libraries.Game.Graphics.Font
use Libraries.Containers.Array
use Libraries.Game.Graphics.ColorGroup
use Libraries.Interface.Controls.Charts.Graphics.RegressionLine

/*
    This class represents the chart area sub panels within splitbyfactor charts.
    This class is not meant to be accessed by the user directly.
    All of its actions and components should only be controlled by the user
    through the Chart class.


    Attribute: Hannah Williams
*/

class ChartAreaPanel is ChartItem
    private LayoutProperties properties = undefined
    private Series series = undefined
    private ChartLabel label = undefined
    private Array<ChartItem> chartAreaItems
    private boolean isShowing = false

    ColorGroup backgroundColor = undefined
    ColorGroup borderColor = undefined
    ColorGroup selectionColor = undefined
    ColorGroup selectionBorderColor = undefined

    /*
    A series containing the movable regression line unique to this chart area.
    Undefined by default, but will be set if the chart has "ShowMovableRegressionLine" 
    set to true during creation.
    */
    MovableRegressionSeries movableRegressionSeries = undefined

    on create
        ManualLayout manualLayout
        SetLayout(manualLayout)
        
        
        Color transparent
        transparent:SetColor(0,0,0,0)
        SetBackgroundColor(transparent)
        SetSelectionColor(transparent)
        SetBorderColor(transparent)
        SetBorderThickness(4)
        properties = GetDefaultLayoutProperties()
        Font font = GetDefaultFont()
        properties:SetFont(font)
        properties:SetFontSize(GetDefaultFontSize())

        SetFocusable(true)
        SetAccessibilityCode(parent:Item:CHART_SECTION)
    end

    action IsAccessibleParent returns boolean
        return true
    end

    action LoadChartAreaView 
        if properties:GetView2D() = undefined
            ControlShaderView view 
            view:Initialize()
            SetView2D(view)
        else
            SetView2D(GetDefaultLayoutProperties():GetView2D())
        end
    end

    action GainedSelection
        //if GetParentChart():GetChartArea() = me
        //    parent:Control:GainedSelection()
        //else
            parent:ChartItem:GainedSelection()
        //end
    end

    action LostSelection
        //if GetParentChart():GetChartArea() = me
        //    parent:Control:LostSelection()
        //else
            parent:ChartItem:LostSelection()
        //end
    end

    action GetChartAreaItems returns Array<ChartItem>
        return chartAreaItems
    end

    action AddChartAreaItem(ChartItem item)
        chartAreaItems:Add(item)
    end

    action GetBorderColor returns ColorGroup
        if borderColor not= undefined
            return borderColor
        end

        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetBorderColor()
        end
        return undefined
    end

    action SetBorderColor(ColorGroup color)
        borderColor = color
    end

    action GetBackgroundColor returns ColorGroup
        if backgroundColor not= undefined
            return backgroundColor
        end

        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetForegroundColor()
        end

        return undefined
    end

    action SetBackgroundColor(ColorGroup color)
        backgroundColor = color
    end

    action GetSelectionBorderColor returns ColorGroup
        if HasColorPropertyOverride("selectionBorderColor")
            return parent:Control:GetSelectionBorderColor()
        end

        if selectionBorderColor not= undefined
            return selectionBorderColor
        end

        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetSelectionColor()
        end
        return undefined
    end

    action SetSelectionBorderColor(ColorGroup color)
        selectionBorderColor = color
    end

    action GetSelectionColor returns ColorGroup
        if selectionColor not= undefined
            return selectionColor
        end

        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetForegroundSelectionColor()
        end

        return undefined
    end

    action SetSelectionColor(ColorGroup color)
        selectionColor = color
    end

    action GetLabel returns ChartLabel
        return label
    end

    action SetLabel(ChartLabel label)
        me:label = label
    end

    action GetSeries returns Series
        return series
    end

    action SetSeries(Series series)
        me:series = series
    end

    // Try to get the chart this is attached to via the parent hierarchy.
    private action GetParentChart returns Chart
        Item2D parentItem = GetParent()
        repeat until parentItem is Chart or parentItem = undefined
            parentItem = parentItem:GetParent()
        end
        return cast(Chart, parentItem)
    end

    /*
    Returns a series containing a movable regression line unique to this area panel.
    The series is representative of all elements in this panel. This is undefined by
    default, and is only defined if "ShowMovableRegressionLine" was set to true in
    the Chart before creation.

    Attribute: Returns A series containing the movable regression line, if they are enabled for this chart.
    */
    action GetMovableRegressionSeries returns MovableRegressionSeries
        return movableRegressionSeries
    end

    action SetMovableRegressionSeries(MovableRegressionSeries movableRegressionSeries)
        me:movableRegressionSeries = movableRegressionSeries
    end

end