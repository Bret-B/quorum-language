package Libraries.Interface.Controls.Charts

use Libraries.Interface.Controls.Control
use Libraries.Game.Graphics.Color
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Interface.Views.ControlShaderView
use Libraries.Game.Graphics.Font
use Libraries.Containers.Array
use Libraries.Game.Graphics.ColorGroup

/*
    This class represents the chart area sub panels within splitbyfactor charts.
    This class is not meant to be accessed by the user directly.
    All of its actions and components should only be controlled by the user
    through the Chart class.


    Attribute: Hannah Williams
*/

class ChartAreaPanel is Control
    private LayoutProperties properties = undefined
    private Series series = undefined
    private ChartLabel label = undefined
    private Array <Item> chartAreaItems
    private boolean isShowing = false

    ColorGroup backgroundColor = undefined
    ColorGroup borderColor = undefined
    ColorGroup selectionColor = undefined
    ColorGroup selectionBorderColor = undefined

    on create
        ManualLayout manualLayout
        SetLayout(manualLayout)
        
        properties = GetDefaultLayoutProperties()
        Font font = GetDefaultFont()
        properties:SetFont(font)
        properties:SetFontSize(GetDefaultFontSize())

        SetFocusable(true)
        SetAccessibilityCode(parent:Item:CUSTOM)
    end

    action IsAccessibleParent returns boolean
        return true
    end

    action LoadChartAreaView 
        if properties:GetView2D() = undefined
            ControlShaderView view 
            view:Initialize()
            SetView2D(view)
        else
            SetView2D(GetDefaultLayoutProperties():GetView2D())
        end
    end

    action GetChartAreaItems returns Array <Item>
        return chartAreaItems
    end

    action AddChartAreaItem(Item item)
        chartAreaItems:Add(item)
    end

    action GetBorderColor returns ColorGroup
        if borderColor not= undefined
            return borderColor
        end

        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetBorderColor()
        end
        return undefined
    end

    action SetBorderColor(ColorGroup color)
        borderColor = color
    end

    action GetBackgroundColor returns ColorGroup
        if backgroundColor not= undefined
            return backgroundColor
        end

        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetForegroundColor()
        end

        return undefined
    end

    action SetBackgroundColor(ColorGroup color)
        backgroundColor = color
    end

    action GetSelectionBorderColor returns ColorGroup
        if selectionBorderColor not= undefined
            return selectionBorderColor
        end

        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetSelectionBorderColor()
        end
        return undefined
    end

    action SetSelectionBorderColor(ColorGroup color)
        selectionBorderColor = color
    end

    action GetSelectionColor returns ColorGroup
        if selectionColor not= undefined
            return selectionColor
        end

        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetForegroundSelectionColor()
        end

        return undefined
    end

    action SetSelectionColor(ColorGroup color)
        selectionColor = color
    end

    action GetLabel returns ChartLabel
        return label
    end

    action SetLabel(ChartLabel label)
        me:label = label
    end

    // Try to get the chart this is attached to via the parent hierarchy.
    private action GetParentChart returns Chart
        Item2D parentItem = GetParent()
        repeat until parentItem is Chart or parentItem = undefined
            parentItem = parentItem:GetParent()
        end
        return cast(Chart, parentItem)
    end
end