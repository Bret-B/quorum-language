package Libraries.Interface.Controls.Charts
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.Charts.ChartOptions
use Libraries.Game.Graphics.Color
use Libraries.Containers.Array
use Libraries.Interface.Controls.Icon
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Controls.ControlLabel
use Libraries.Compute.Math
use Libraries.Interface.Selections.ChartSelection
use Libraries.Interface.Item
use Libraries.Interface.Controls.Control
use Libraries.Game.Graphics.ColorGroup
use Libraries.Interface.Views.LabelBoxView

class TwoVariableChart is Chart

    // The root ChartItem.
    ChartItem summary
    Array<Series> seriesList
    Array<ChartLine> lines
    Array<ChartPoint> points
    private boolean showPoints = true
    Color defaultColor
    Math math
    ChartOptions options

    private integer PointRadius = 2
    private integer LineDensity = 1

    // The minimum radius of a single chart point.
    private integer minimumPointRadius = 2

    // A value used to scale the size of points as a percentage of chart size.
    private number pointScaleFactor = 0.008
    private integer pointThreshold = 21
    private integer maxChunks = 16

    private integer tickLength = 10
    private integer tickWidth = 3
    ChartPointComparison comparison

    Color highlightColor = undefined
    ColorGroup previousColorGroup = undefined
    Color previousColor = undefined

    // If there are fewer than this many points in a quadrant, we don't subdivide the quadrant further.
    integer quadrantThreshold = 10

    // The maximum number of times that regions can be subdivided into quadrants.
    integer maxSubdivisions = 4

    // The default color of each point.
    Color pointColor
    Color pointSelectionColor

    // How many ticks are set along the x and y axes.
    integer xTicks = 0
    integer yTicks = 0
    number xTickInterval = -1
    number yTickInterval = -1
    // Default uses best interval by range to graph, if tick count is set, it overrides this.
    boolean xIntervalBasedOnTickCount = false
    boolean yIntervalBasedOnTickCount = false

    // The minimum and maximum values portrayed in the chart area.
    number xAxisMinimum = 0
    number xAxisMaximum = 1
    number yAxisMinimum = 0
    number yAxisMaximum = 1
    boolean overrideXLabels = false
    boolean overrideYLabels = false
    Array<number> xTickValues = undefined
    Array<number> yTickValues = undefined
    Array<text> xLabelsOverride = undefined
    Array<text> yLabelsOverride = undefined

    // The default orientation for line charts and scatter plots is horizontal
    text defaultOrientation = "horizontal"

    action OverrideXLabelsWithText(Array<text> labels)
        if labels not= undefined
            xTicks = labels:GetSize()
        end
        xLabelsOverride = labels
    end

    action OverrideYLabelsWithText(Array<text> labels)
        if labels not= undefined
            yTicks = labels:GetSize()
        end
        yLabelsOverride = labels
    end

    action LostSelection(ChartItem ci)
        if ci = undefined
            return now
        end
        Item target = ci:GetFocusTarget()

        if target not= undefined
            if target is ControlLabel
                ControlLabel temp = cast(ControlLabel, target)
                temp:LostSelection()
            elseif target is ChartPoint
                if GetPointsFlag()
                    ChartPoint temp = cast(ChartPoint, target)
                    temp:GetIcon():Hide()
                end
            elseif target is LineSegment
                LineSegment temp = cast(LineSegment, target)
                if GetPointsFlag()
                    temp:GetPoint1():GetIcon():Hide()
                    temp:GetPoint2():GetIcon():Hide()
                end
                temp:GetIcon():Hide()
            elseif target is ChartLine
                ChartLine temp = cast(ChartLine, target)
                integer i = 0
                if GetPointsFlag()
                    repeat while i < temp:GetPoints():GetSize()
                        temp:GetPoints():Get(i):GetIcon():Hide()
                        i = i + 1
                    end
                end
                i = 0
                repeat while i < temp:GetLines():GetSize()
                    temp:GetLines():Get(i):GetIcon():Hide()
                    i = i + 1
                end
            elseif target is Icon
                target:Hide()
            elseif target is Control
                Control temp = cast(Control, target)
                LayoutProperties properties = temp:GetDefaultLayoutProperties()
                if properties not= undefined
                    LabelBoxView view
                    view:SetBorderThickness(cast(integer, properties:GetBorderThickness()))
                    view:Initialize(properties:GetBackgroundColor(), previousColorGroup)
                    temp:SetView2D(view)
                end
            end
        end
    end

    action GainedSelection(ChartItem ci)
        if ci = undefined
            return now
        end
        Item target = ci:GetFocusTarget()

        if target not= undefined
            target:Focus()
            if target is ControlLabel
                ControlLabel temp = cast(ControlLabel, target)
                temp:GainedSelection()
            elseif target is ChartPoint
                if GetPointsFlag()
                    ChartPoint temp = cast(ChartPoint, target)
                    temp:GetIcon():Show()
                end
            elseif target is LineSegment
                LineSegment temp = cast(LineSegment, target)
                if GetPointsFlag()
                    temp:GetPoint1():GetIcon():Show()
                    temp:GetPoint2():GetIcon():Show()
                end
                temp:GetIcon():Show()
            elseif target is ChartLine
                ChartLine temp = cast(ChartLine, target)
                integer i = 0
                if GetPointsFlag()
                    repeat while i < temp:GetPoints():GetSize()
                        temp:GetPoints():Get(i):GetIcon():Show()
                        i = i + 1
                    end
                end
                i = 0
                repeat while i < temp:GetLines():GetSize()
                    temp:GetLines():Get(i):GetIcon():Show()
                    i = i + 1
                end
            elseif target is Icon
                target:Show()
            elseif target is Control
                Control temp = cast(Control, target)
                LayoutProperties properties = temp:GetDefaultLayoutProperties()
                if properties not= undefined
                    previousColorGroup = properties:GetBorderColor()
                    LabelBoxView view
                    view:SetBorderThickness(cast(integer, properties:GetBorderThickness())+4)
                    view:Initialize(properties:GetBackgroundColor(), GetHighlightColor())
                    temp:SetView2D(view)
                end
            end            
        end
    end
    action DisposeDrawables()
        // For line charts
        if lines:GetSize() > 0
            integer i = 0
            repeat while i < lines:GetSize()
                integer j = 0
                repeat while j < lines:Get(i):GetPoints():GetSize()
                    ChartPoint point = lines:Get(i):GetPoints():Get(j)
                    if point:GetTexture() not= undefined
                        point:Dispose()
                    end
                    j = j + 1
                end
                j = 0
                repeat while j < lines:Get(i):GetLines():GetSize()
                    LineSegment line = lines:Get(i):GetLines():Get(j)
                    if line:GetTexture() not= undefined
                        line:Dispose()
                    end
                    j = j + 1
                end
                i = i + 1
            end
        end
        // For scatter plots
        if points:GetSize() > 0
            integer i = 0
            repeat while i < points:GetSize()
                ChartPoint point = points:Get(i)
                if point:GetTexture() not= undefined
                    point:Dispose()
                end
                i = i + 1
            end
        end

        GetLegend():Empty()
    end
    /*
    This action is used to load the graphical components of the Control. This is
    handled automatically by the Game engine as needed, and most users shouldn't
    need to use this action directly.
    */
    action LoadGraphics(LayoutProperties properties)
        DisposeDrawables()
        parent:Chart:LoadGraphics(properties)
        if properties = undefined
            return now
        end
        
        Array<Drawable> xTickDrawables = GetXTicks()
        Array<Drawable> yTickDrawables = GetYTicks()
        Array<Drawable> yGridlines = GetMajorYGridlines()
        Array<Drawable> xGridlines = GetMajorXGridlines()
        Array<Drawable> yMinorGridlines = GetMinorYGridlines()
        Array<Drawable> xMinorGridlines = GetMinorXGridlines()
        Array<ControlLabel> xLabels = GetXLabels()
        Array<ControlLabel> yLabels = GetYLabels()
        Control horizontalPanel = GetHorizontalPanel()
        Control verticalPanel = GetVerticalPanel()
        Control chartArea = GetChartArea()

        // Check for orientation change
        text orientation = GetOrientation()
        if defaultOrientation not= orientation
            flipAxes()
        end        

        // If text x-axis is being used then this will stop the manually interval changing.
        if xLabelsOverride not= undefined
            xAxisMinimum = 0
            xAxisMaximum = xLabelsOverride:GetSize()-1
        end

        number scaleNum = xAxisMinimum
        number scaleWidth = xAxisCalculateScaleWidth()

        if xTickDrawables:GetSize() not= xTicks
            if xTickDrawables:GetSize() < xTicks
                integer i = 0
                repeat while i < xTicks
                    Drawable tick
                    tick:LoadFilledRectangle(tickWidth, tickLength)
                    horizontalPanel:Add(tick)
                    xTickDrawables:Add(tick)

                    if IsShowingMajorXGridLines()
                        Drawable gridline
                        xGridlines:Add(gridline)
                        chartArea:Add(gridline)
                    end
                    if IsShowingMinorXGridLines()
                        integer j = 0
                        repeat while j < 5
                            Drawable gridline
                            xMinorGridlines:Add(gridline)
                            chartArea:Add(gridline)
                            j = j + 1
                        end
                    end

                    ControlLabel label
                    label:SetFontSize(GetFontSize() - 5)
                    label:SetFocusable(true)
                    label:SetAccessibilityCode(parent:Item:ITEM)
                    horizontalPanel:Add(label)
                    xLabels:Add(label)
                    i = i + 1
                end
            else
                repeat until xTickDrawables:GetSize() = xTicks - 1
                    Drawable tick = xTickDrawables:RemoveFromEnd()
                    horizontalPanel:Remove(tick)

                    ControlLabel label = xLabels:RemoveFromEnd()
                    horizontalPanel:Remove(label)
                end
            end

            if xTicks > 0
                i = 0
                repeat while i < xTicks
                    Drawable tick = xTickDrawables:Get(i)
                    tick:SetName("X Tick " + (i + 1))
    
                    //make the labels for each scale tick (auto based on max)
                    text scaleText = "" + math:Round(scaleNum, options:GetTickDigits())
                    ControlLabel label = xLabels:Get(i)
                    if xLabelsOverride = undefined
                        label:SetText(scaleText)
                    else
                        number percent = (i * 1.0) / ((xTicks) * 1.0)
                        integer index = cast(integer, math:Round(percent * xLabelsOverride:GetSize()))
                        label:SetText(xLabelsOverride:Get(index))
                    end
                    label:SetName(scaleText + " " + (i + 1) + " of " + xTicks)
                    scaleNum = scaleNum + scaleWidth
                    i = i + 1
                end
            end
        end

        scaleNum = yAxisMinimum
        scaleWidth = yAxisCalculateScaleWidth()

        if yTickDrawables:GetSize() not= yTicks
            if yTickDrawables:GetSize() < yTicks
                repeat until yTickDrawables:GetSize() = yTicks
                    Drawable tick
                    tick:LoadFilledRectangle(tickLength, tickWidth)
                    verticalPanel:Add(tick)
                    yTickDrawables:Add(tick)

                    if IsShowingMajorYGridLines()
                        Drawable gridline
                        yGridlines:Add(gridline)
                        chartArea:Add(gridline)
                    end
                    if IsShowingMinorYGridLines()
                        integer j = 0
                        repeat while j < 5
                            Drawable gridline
                            yMinorGridlines:Add(gridline)
                            chartArea:Add(gridline)
                            j = j + 1
                        end
                    end

                    ControlLabel label
                    label:SetFontSize(GetFontSize() - 5)
                    label:SetFocusable(true)
                    label:SetAccessibilityCode(parent:Item:ITEM)
                    verticalPanel:Add(label)
                    yLabels:Add(label)
                end
            else
                repeat until yTickDrawables:GetSize() = yTicks
                    Drawable tick = yTickDrawables:RemoveFromEnd()
                    verticalPanel:Remove(tick)

                    ControlLabel label = yLabels:RemoveFromEnd()
                    verticalPanel:Remove(label)
                end
            end

            i = 0
            repeat while i < yTicks
                Drawable tick = yTickDrawables:Get(i)
                tick:SetName("Y Tick " + (i + 1))

                //make the labels for each scale tick (auto based on max)
                text scaleText = "" + math:Round(scaleNum, options:GetTickDigits())
                ControlLabel label = yLabels:Get(i)
                if yLabelsOverride = undefined
                    label:SetText(scaleText)
                else
                    //now get the location of the text value to put in
                    if i = yTicks
                        label:SetText(yLabelsOverride:Get(yLabelsOverride:GetSize())) 
                    else
                        number percent = (i * 1.0) / ((yTicks) * 1.0)
                        integer index = cast(integer, math:Round(percent * yLabelsOverride:GetSize()))
                        label:SetText(yLabelsOverride:Get(index))
                    end
                end
                label:SetName(scaleText + " " + (i + 1) + " of " + yTicks)
                scaleNum = scaleNum + scaleWidth
                i = i + 1
            end
        end

        if GetHighlightColor() = undefined
            SetHighlightColor(options:GetSelectionColor())
        end
        
        // Line Chart
        if lines:GetSize() > 0
            integer i = 0
            repeat while i < lines:GetSize()
                integer j = 0
                repeat while j < lines:Get(i):GetLines():GetSize()
                    LineSegment line = lines:Get(i):GetLines():Get(j)
                    chartArea:Add(line)
                    Icon lineIcon = lines:Get(i):GetLines():Get(j):GetIcon()
                    chartArea:Add(lineIcon)
                    j = j + 1
                end
                if lines:Get(i):GetPointsFlag()
                    j = 0
                    repeat while j < lines:Get(i):GetPoints():GetSize()
                        ChartPoint point = lines:Get(i):GetPoints():Get(j)
                        chartArea:Add(point)
                        Icon pointIcon = lines:Get(i):GetPoints():Get(j):GetIcon()
                        chartArea:Add(pointIcon)
                        j = j + 1
                    end
                end

                chartArea:Add(lines:Get(i))
                i = i + 1
            end
        end

        // Scatter Plot
        if points:GetSize() > 0
            integer i = 0
            repeat while i < points:GetSize()
                ChartPoint point = points:Get(i)
                chartArea:Add(point)
                Icon pointIcon = points:Get(i):GetIcon()
                GetChartArea():Add(pointIcon)
                i = i + 1
            end
        end

        i = 0
        repeat while i < seriesList:GetSize()
            Series series = seriesList:Get(i)
            series:SetColor(options:GetColorFromPalette(i))
            series:GetLabel():SetFocusable(true)
            series:GetLabel():SetAccessibilityCode(parent:Item:ITEM)
            integer j = 0
            repeat while j < series:GetSize()
                if lines:GetSize() > 0
                    ChartLine cline = cast(ChartLine, series:GetItem(j))
                    cline:SetColor(series:GetColor())
                else
                    ChartPoint cpoint = cast(ChartPoint, series:GetItem(j))
                    cpoint:SetColor(series:GetColor())
                end
                j = j + 1
            end
            i = i + 1
        end

        if IsShowingLegend()
            Add(GetLegend())
        end
    end

    action flipAxes()
        // Line Chart
        if lines:GetSize() > 0
            integer i = 0
            repeat while i < lines:GetSize()
                j = 0
                repeat while j < lines:Get(i):GetPoints():GetSize()
                    ChartPoint point = lines:Get(i):GetPoints():Get(j)
                    number temp = point:GetValueX()
                    point:SetValueX(point:GetValueY())
                    point:SetValueY(temp)
                    j = j + 1
                end
                i = i + 1
            end
        end

        // Scatter Plot
        if points:GetSize() > 0
            integer i = 0
            repeat while i < points:GetSize()
                ChartPoint point = points:Get(i)
                number temp = point:GetValueX()
                point:SetValueX(point:GetValueY())
                point:SetValueY(temp)
                i = i + 1
            end
        end
        number temp = xAxisMaximum
        xAxisMaximum = yAxisMaximum
        yAxisMaximum = temp

        temp = xAxisMinimum
        xAxisMinimum = yAxisMinimum
        yAxisMinimum = temp

        if xLabelsOverride not= undefined
            if yLabelsOverride not= undefined
                Array<text> temps = xLabelsOverride
                xLabelsOverride = yLabelsOverride
                yLabelsOverride = temps
                
                xTicks = xLabelsOverride:GetSize()
                yTicks = yLabelsOverride:GetSize()
            else
                OverrideYLabelsWithText(xLabelsOverride)
                xLabelsOverride = undefined
            end
        else
            if yLabelsOverride not= undefined
                OverrideXLabelsWithText(yLabelsOverride)
                yLabelsOverride = undefined
            end
        end

        text tempTitle = GetXLabel():GetText()
        GetXLabel():SetText(GetYLabel():GetText())
        GetXLabel():SetName(GetYLabel():GetText())
        GetYLabel():SetText(tempTitle)
        GetYLabel():SetName(tempTitle)
    end

    /*
        This action takes a range of data and returns a decent interval, this is only the default.
        The user can choose any interval they wish and set it manually.  
    */
    action CalculateBestTickInterval(number range) returns number
        number tickSkips = 0
        if range <= 2.5
            if range > 1.5
                tickSkips = 0.5
            else
                tickSkips = 0.1
            end
        else
            if range > 5000
                tickSkips = 1000
            elseif range > 2500
                tickSkips = 500
            elseif range > 1000
                tickSkips = 250
            elseif range > 500
                tickSkips = 100
            elseif range > 200
                tickSkips = 50
            elseif range > 100
                tickSkips = 20
            elseif range > 50
                tickSkips = 10
            elseif range > 20
                tickSkips = 5
            elseif range > 10
                tickSkips = 2
            else
                tickSkips = 1
            end
        end
        return tickSkips
    end

    action xAxisCalculateScaleWidth() returns number
        number xRange = xAxisMaximum-xAxisMinimum
        number scaleWidth = 0.0
        // Calculating intervals and/or number of ticks
        if xIntervalBasedOnTickCount
            if xTicks > 1
                SetXTickInterval(math:Round(xRange / cast(number, xTicks-1), 2))
                xAxisMaximum = cast(number, xTicks-1) * GetXTickInterval() + xAxisMinimum
            end
        end
        // Numerical x-scale (number intervals)
        if xLabelsOverride = undefined
            number tickSkips = GetXTickInterval()
            if tickSkips = -1
                tickSkips = CalculateBestTickInterval(xRange)
            end
            if tickSkips not= 1.0
                number i = xAxisMinimum
                repeat while i < xAxisMaximum
                    i = i + tickSkips
                end
                xAxisMaximum = math:Round(i, 2)
                xTicks = cast(integer, (xAxisMaximum-xAxisMinimum) / tickSkips) + 1
                scaleWidth = tickSkips
            else
                xTicks = cast(integer, xRange) + 1
                scaleWidth = xRange / (xTicks - 1)
            end 
        // Text x-scale (integer intervals only)
        else
            number tickSkips = cast(number, cast(integer, GetXTickInterval()))
            if tickSkips = -1
                tickSkips = CalculateBestTickInterval(xRange)
            end
            if tickSkips > 1 and tickSkips < xLabelsOverride:GetSize()
                Array <text> labels
                number i = 0
                repeat while i < xLabelsOverride:GetSize()
                    labels:Add(xLabelsOverride:Get(cast(integer, i)))
                    i = i + tickSkips
                end
                xAxisMaximum = i
                labels:Add(cast(text, cast(integer, xAxisMaximum)))
                xTicks = labels:GetSize()
                xLabelsOverride = labels
            end
            scaleWidth = xRange / (xTicks - 1)
        end
        return scaleWidth
    end

    action yAxisCalculateScaleWidth() returns number
        number yRange = yAxisMaximum-yAxisMinimum
        number scaleWidth = 0.0
        // Calculating intervals and/or number of ticks
        if yIntervalBasedOnTickCount
            if yTicks > 1
                SetYTickInterval(math:Round(yRange / cast(number, yTicks-1), 2))
                yAxisMaximum = cast(number, yTicks-1) * GetYTickInterval() + yAxisMinimum
            end
        end
        // Numerical y-scale (number intervals)
        if yLabelsOverride = undefined
            number tickSkips = GetYTickInterval()
            if tickSkips = -1
                tickSkips = CalculateBestTickInterval(yRange)
            end
            if tickSkips not= 1.0
                number i = yAxisMinimum
                repeat while i < yAxisMaximum
                    i = i + tickSkips
                end
                yAxisMaximum = math:Round(i, 2) 
                yTicks = cast(integer, (yAxisMaximum-yAxisMinimum) / tickSkips) + 1
                scaleWidth = tickSkips
            else
                yTicks = cast(integer, yRange) + 1
                scaleWidth = yRange / (yTicks - 1)
            end 
        // Text y-scale (integer intervals only)
        else
            number tickSkips = cast(number, cast(integer, GetYTickInterval()))
            if tickSkips = -1
                tickSkips = CalculateBestTickInterval(yRange)
            end
            if tickSkips > 1 and tickSkips < yLabelsOverride:GetSize()
                Array <text> labels
                number i = 0
                repeat while i < yLabelsOverride:GetSize()
                    labels:Add(yLabelsOverride:Get(cast(integer, i)))
                    i = i + tickSkips
                end
                yAxisMaximum = i
                labels:Add(cast(text, cast(integer, yAxisMaximum)))
                yTicks = labels:GetSize()
                yLabelsOverride = labels
            end
            scaleWidth = yRange / (yTicks - 1)
        end
        return scaleWidth
    end

    /*
        Gets the default selection color (highlighting)
    */
    action GetHighlightColor returns Color
        return highlightColor
    end

    /*
        Sets the default selection color (highlighting)
    */
    action SetHighlightColor(Color color)
        me:highlightColor = color
    end

    /*
        Gets the previous color
    */
    action GetPreviousColor returns Color
        return previousColor
    end

    /*
        Sets the previous color 
    */
    action SetPreviousColor(Color color)
        me:previousColor = color
    end

    /*
    Used to create a new ChartLine object for the chart and add it to the lines array.
    */
    action AddChartLine(ChartLine newLine)
        lines:Add(newLine)
    end

    action AddPoint(ChartPoint point)
        points:Add(point)
    end

    action GetChartPoints returns Array<ChartPoint> 
        return points
    end

    /*
    Return a specific line from the chart.
    */
    action GetLine(integer i) returns ChartLine
        if lines:Get(i) = undefined
            alert("The specified line does not exist.")
        else
            return lines:Get(i)
        end
    end

    /*
    Returns the entire list of lines from the chart.
    */
    action GetAllLines() returns Array<ChartLine>
        return lines
    end

    /*
    Returns the number of full lines from the chart.
    */
    action GetNumberOfLines() returns integer
        return lines:GetSize()
    end

    /*
    Returns the number of points from the chart. (scatter)
    */
    action GetNumberOfPoints() returns integer
        return points:GetSize()
    end

    /*
    Returns the default color for the chart.
    */
    action GetDefaultColor() returns Color
        return defaultColor
    end

    /*
    Set and get the point radius.
    */
    action SetPointRadius(integer radius)
        PointRadius = radius
    end
    action GetPointRadius() returns integer
        return PointRadius
    end

    /*
    Set and get the line density.
    */
    action SetLineDensity(integer density)
        LineDensity = density
    end
    action GetLineDensity() returns integer
        return LineDensity
    end

    /*
    Helper used to add the points and lines of a ChartLine to the chart area.
    */
    action Populate()
        //Line chart
        integer i = 0
        repeat while i < lines:GetSize()
            integer j = 0
            repeat while j < lines:Get(i):GetLines():GetSize()
                GetChartArea():Add(lines:Get(i):GetLines():Get(j))
                j = j + 1
            end
            j = 0
            repeat while j < lines:Get(i):GetPoints():GetSize()
                GetChartArea():Add(lines:Get(i):GetPoints():Get(j))
                j = j + 1
            end
            i = i + 1
        end

        //Scatter Plot
        i = 0
        repeat while i < points:GetSize()
            GetChartArea():Add(points:Get(i))
            i = i + 1
        end
    end
    /*
        Returns an array of all the series sets 
    */
    action GetSeriesList returns Array<Series>
        return seriesList
    end

    action AddSeries(Series series)
        seriesList:Add(series)
    end

    action SetXTickCount(integer ticks)
        xTicks = ticks
        xIntervalBasedOnTickCount = true
    end

    action GetXTickCount returns integer
        return xTicks
    end

    action SetYTickCount(integer ticks)
        yTicks = ticks
        yIntervalBasedOnTickCount = true
    end

    action GetYTickCount returns integer
        return yTicks
    end

    action SetXTickInterval(number set)
        xTickInterval = set
    end

    action SetYTickInterval(number set)
        yTickInterval = set
    end
    
    action SetXTickInterval(integer set)
        xTickInterval = cast(number, set)
    end

    action SetYTickInterval(integer set)
        yTickInterval = cast(number, set)
    end
    
    action GetXTickInterval() returns number
        return xTickInterval
    end

    action GetYTickInterval() returns number
        return yTickInterval
    end

    action GetXAxisMinimum returns number
        return xAxisMinimum
    end

    action SetXAxisMinimum(number xAxisMinimum)
        me:xAxisMinimum = xAxisMinimum
    end

    action GetXAxisMaximum returns number
        return xAxisMaximum
    end

    action SetXAxisMaximum(number xAxisMaximum)
        me:xAxisMaximum = xAxisMaximum
    end

    action GetYAxisMinimum returns number
        return yAxisMinimum
    end

    action SetYAxisMinimum(number yAxisMinimum)
        me:yAxisMinimum = yAxisMinimum
    end

    action GetYAxisMaximum returns number
        return yAxisMaximum
    end

    action SetYAxisMaximum(number yAxisMaximum)
        me:yAxisMaximum = yAxisMaximum
    end

    /*
    Returns the value of the showPoints flag.
    */
    action GetPointsFlag() returns boolean
        return showPoints
    end

    //blueprint action GenerateChartAreaInfo(ChartItem chartArea, Chart chart)

    /*
        Generates the tree of ChartItems that define how the chart will be 
        navigated and what extra information might be sent to the screen reader.
    */
    action GenerateInfoTree 
        if GetDefaultLayoutProperties():NeedsRendering()
            return now //we haven't loaded graphics yet, so bail.
        end
        
        //NOTE: These nodes implement a cheap form of ordinality might need changing
        ChartItem xAxis
        ChartItem yAxis
        ChartItem chartArea//not to be confused with panel
        //Level 1 top level
        summary:SetDisplayName(GenerateSummary())
        summary:SetNext(yAxis)
        summary:SetChild(yAxis)
        summary:SetContainer(me)
        summary:SetFocusTarget(me)
    
        //Level 2 three main areas of bar chart (maybe 4 if legend is added)
        xAxis:SetDisplayName("Horizontal Axis")
        xAxis:SetFocusTarget(GetXLabel())
        yAxis:SetDisplayName("Vertical Axis")
        yAxis:SetFocusTarget(GetYLabel())

        text lineText = "lines"
        text pointText = "points"
        if GetNumberOfLines() = 1
            linetext = "line"
        end
        if points:GetSize() = 1
            linetext = "point"
        end
        
        if lines:GetSize() > 0
            chartArea:SetDisplayName("This is the chart area with " + lines:GetSize() + " " + lineText + ".")
            me:GetChartArea():SetDescription("with" + lines:GetSize() + " " + lineText + ".")
        elseif points:GetSize() > 0
            chartArea:SetDisplayName("This is the chart area with " + points:GetSize() + " " + pointText + ".")
            me:GetChartArea():SetDescription("with" + points:GetSize() + " " + pointText + ".")
        end
        chartArea:SetFocusTarget(me:GetChartArea())
        xAxis:SetContainer(me)        
        yAxis:SetContainer(me)
        chartArea:SetContainer(me)

        yAxis:SetNext(chartArea)
        chartArea:SetNext(xAxis)
        xAxis:SetPrevious(chartArea)
        yAxis:SetPrevious(xAxis)

        chartArea:SetPrevious(yAxis)
        xAxis:SetParent(summary)
        yAxis:SetParent(summary)
        yAxis:SetPrevious(summary)
        chartArea:SetParent(summary)

        // Legend Area
        if IsShowingLegend()
            GetLegend():GenerateInfoTree(me, summary, xAxis, yAxis)
        end

        // Generate Line Chart Info
        if lines:GetSize() > 0
            ChartItem firstChartLineItem
            firstChartLineItem:SetContainer(me)
            chartArea:SetChild(firstChartLineItem)
            firstChartLineItem:SetDisplayName("This is the line for " + lines:Get(0):GetName() + ".")
            firstChartLineItem:SetParent(chartArea)

            ChartLine firstChartLine = lines:Get(0)
            number lineMax = firstChartLine:GetLineMax():GetPercentY()*GetYAxisMaximum()
            number lineMin = firstChartLine:GetLineMin():GetPercentY()*GetYAxisMaximum()
            number lineStart = firstChartLine:GetLineStart():GetPercentY()*GetYAxisMaximum()
            number lineEnd = firstChartLine:GetLineEnd():GetPercentY()*GetYAxisMaximum()
            integer numOfPoints = firstChartLine:GetPoints():GetSize()
            integer numOfSegs = firstChartLine:GetLines():GetSize()
            text lineChange = " "
            if lineStart > lineEnd
                lineChange = ", This line is decreasing overall."
            elseif lineStart < lineEnd
                lineChange = ", This line is increasing overall."
            else 
                lineChange = ", This line shows no overall change from start to end."
            end
            firstChartLine:SetDescription(" , 1 of " + lines:GetSize() + " lines. 
                This line has " + numOfPoints + " points and " + numOfSegs + " line segments. 
                The maximum value for this line is " + lineMax + " , 
                the minimum value for this line is " + lineMin + " , 
                This line starts at a value of " + lineStart + " and ends at a value of "+ lineEnd + lineChange)
            firstChartLineItem:SetFocusTarget(firstChartLine)

            Icon region = NewRegionIcon(0,0,1,1)
            CreateChunks(firstChartLineItem, region, lines:Get(0):GetPoints(), lines:Get(0):GetLines(), 0)

            integer i = 1
            ChartItem previousChartLineItem = firstChartLineItem
            repeat while i < lines:GetSize()
        
                ChartItem chartLineItem
                chartLineItem:SetContainer(me)
                chartLineItem:SetDisplayName("This is the line for " + lines:Get(i):GetName() + ".")
                chartLineItem:SetParent(chartArea)
                chartLineItem:SetPrevious(previousChartLineItem)

                ChartLine chartLine = lines:Get(i)
                lineMax = chartLine:GetLineMax():GetPercentY()*GetYAxisMaximum()
                lineMin = chartLine:GetLineMin():GetPercentY()*GetYAxisMaximum()
                lineStart = chartLine:GetLineStart():GetPercentY()*GetYAxisMaximum()
                lineEnd = chartLine:GetLineEnd():GetPercentY()*GetYAxisMaximum()
                numOfPoints = chartLine:GetPoints():GetSize()
                numOfSegs = chartLine:GetLines():GetSize()
                lineChange = " "
                if lineStart > lineEnd
                    lineChange = ", This line is decreasing overall."
                elseif lineStart < lineEnd
                    lineChange = ", This line is increasing overall."
                else 
                    lineChange = ", This line shows no overall change from start to end."
                end
                chartLine:SetDescription(" , "+(i+1)+" of " + lines:GetSize() + " lines. 
                    This line has " + numOfPoints + " points and " + numOfSegs + " line segments. 
                    The maximum value for this line is " + lineMax + " , 
                    the minimum value for this line is " + lineMin + " , 
                    This line starts at a value of " + lineStart + " and ends at a value of "+ lineEnd + lineChange)
                chartLineItem:SetFocusTarget(chartLine)

                region = NewRegionIcon(0,0,1,1)
                CreateChunks(chartLineItem, region, lines:Get(i):GetPoints(), lines:Get(i):GetLines(), 0)
    
                previousChartLineItem:SetNext(chartLineItem)
                previousChartLineItem = chartLineItem
                i = i + 1
            end
            previousChartLineItem:SetNext(firstChartLineItem)
        
        end

        // Generate Scatter Plot Info
        if points:GetSize() > 0
            Icon newIcon = NewRegionIcon(0,0,1,1)
            SubdivideQuadrant(chartArea, newIcon, points, 0)
        end

        // y-axis labels
        ChartItem yAxisNode
        yAxisNode:SetContainer(me)
        yAxisNode:SetDisplayName("The y-axis has " + yTicks + " tick marks and goes from " + GetYAxisMinimum() + " to " + GetYAxisMaximum())
        yAxisNode:SetParent(yAxis)
        yAxis:GetFocusTarget():SetDescription("Scale with " + yTicks + " ticks, from " + GetYAxisMinimum() + " to " + GetYAxisMaximum())
        ChartItem firstScale
        firstScale:SetContainer(me)
        Array<ControlLabel> scaleLabels = GetYLabels()
        if yTicks > 0
            firstScale:SetDisplayName(scaleLabels:Get(0):GetText())
            firstScale:SetParent(yAxis)
            firstScale:SetFocusTarget(scaleLabels:Get(0))
            yAxis:SetChild(firstScale)
        end

        i = 1
        ChartItem previous = firstScale
        repeat while i < scaleLabels:GetSize()
            ChartItem scaleNode
            scaleNode:SetContainer(me)
            scaleNode:SetDisplayName(scaleLabels:Get(i):GetText())
            scaleNode:SetParent(yAxis)
            scaleNode:SetPrevious(previous)
            scaleNode:SetFocusTarget(scaleLabels:Get(i))
            previous:SetNext(scaleNode)
            previous = scaleNode
            i = i + 1
        end

        // x-axis labels
        ChartItem xAxisNode
        xAxisNode:SetContainer(me)
        xAxisNode:SetDisplayName("The x-axis has " + xTicks + " tick marks and goes from " + GetXAxisMinimum() + " to " + GetXAxisMaximum())
        xAxisNode:SetParent(xAxis)
        xAxis:GetFocusTarget():SetDescription("Scale with " + xTicks + " ticks, from " + GetXAxisMinimum() + " to " + GetXAxisMaximum())
        ChartItem firstXTick
        firstXTick:SetContainer(me)
        Array<ControlLabel> xLabels = GetXLabels()
        if xTicks > 0
            firstXTick:SetDisplayName(xLabels:Get(0):GetText())
            firstXTick:SetParent(xAxis)
            firstXTick:SetFocusTarget(xLabels:Get(0))
            xAxis:SetChild(firstXTick)
        end

        i = 1
        previous = firstXTick
        repeat while i < xLabels:GetSize()
            ChartItem xNode
            xNode:SetContainer(me)
            xNode:SetDisplayName(xLabels:Get(i):GetText())
            xNode:SetParent(xAxis)
            xNode:SetPrevious(previous)
            xNode:SetFocusTarget(xLabels:Get(i))
            previous:SetNext(xNode)
            previous = xNode
            i = i + 1
        end

        ChartSelection selection = GetSelection()
        selection:Set(summary)
    end

    /*
    Create a new region on the chart for accessbility.
    */
    private action NewRegionIcon (number percentX, number percentY, number percentWidth, number percentHeight) returns Icon
        Icon region
        Color transparent
        transparent:SetColor(0, 0, 1, 0.3)
        region:LoadFilledRectangle(1, 1, transparent)
        region:SetPercentageX(percentX)
        region:SetPercentageY(percentY)
        region:SetPercentageWidth(percentWidth)
        region:SetPercentageHeight(percentHeight)
        region:SetFocusable(true)
        region:SetAccessibilityCode(region:parent:Item:ITEM)
        LayoutProperties properties = region:GetDefaultLayoutProperties()
        region:Hide()
        return region
    end
    /*
    Split the chart area into regions until a minimum number of points is contained in a region or until a maximum number of division.
    */
    private action CreateChunks(ChartItem item, Icon region, Array<ChartPoint> ps,Array<LineSegment> ls, integer currentLevel)

        number x1 = math:Round(region:GetPercentageX() * GetXAxisMinimum(), options:GetTickDigits())
        number x2 = math:Round((region:GetPercentageX() + region:GetPercentageWidth()) * GetXAxisMaximum(), options:GetTickDigits())

        text pointDescription = "No points"
        text lineDescription = "No lines"
        if ps:GetSize() = 1
            pointDescription = "1 point"
        else
            pointDescription = ps:GetSize() + " points"
        end
        if ls:GetSize() = 1
            lineDescription = "1 line"
        else
            lineDescription = ls:GetSize() + " lines"
        end
        text chunkDescription = pointDescription + " and " + lineDescription + "."

        if ps:GetSize() <= pointThreshold or currentLevel >= maxChunks
            if ps:IsEmpty() = false
                region:SetDescription(chunkDescription + " " + "Use the arrow keys to navigate lines, points, or chunks.")
                
                //First point
                ChartItem firstPointItem
                ChartItem prevPoint
                if GetPointsFlag()
                    firstPointItem:SetContainer(me)
                    firstPointItem:SetDisplayName(" , 1 of " + ps:GetSize() + " points in this chunk, value of " + math:Round(ps:Get(0):GetPercentY() * GetYAxisMaximum(), 1))
                    firstPointItem:SetParent(item)
                    item:SetChild(firstPointItem)
    
                    ChartPoint firstPoint = ps:Get(0)
                    firstPoint:SetDescription(firstPointItem:GetDisplayName())
                    firstPointItem:SetFocusTarget(firstPoint)
                    prevPoint = firstPointItem
                end

                //First Line
                ChartItem firstLineItem
                ChartItem prevLine
                if ls:IsEmpty() = false
                    firstLineItem:SetContainer(me)
                    
                    if GetPointsFlag()
                        firstLineItem:SetParent(firstPointItem)
                        firstPointItem:SetChild(firstLineItem)
                    else
                        firstLineItem:SetParent(item)
                        item:SetChild(firstLineItem)
                        prevLine = firstLineItem
                    end

                    LineSegment firstLine = ls:Get(0)
                    text lineChange = " "
                    number start = math:Round(firstLine:GetPoint1():GetPercentY() * GetYAxisMaximum(), 1)
                    number stop = math:Round(firstLine:GetPoint2():GetPercentY() * GetYAxisMaximum(), 1)
                    if start > stop
                        lineChange = ", This line segment is decreasing value from " + start + " to " + stop
                    elseif start < stop
                        lineChange = ", This line segment is increasing value from " + start + " to " + stop
                    else 
                        lineChange = ", This line segment shows no change with the value remaining at " + start
                    end
                    firstLine:SetDescription(" segment between " + firstLine:GetPoint1():GetName() + " and " + firstLine:GetPoint2():GetName() + lineChange)
                    firstLineItem:SetDisplayName("Line segment between " + firstLine:GetPoint1():GetName() + " and " + firstLine:GetPoint2():GetName() + lineChange)
                    firstLineItem:SetFocusTarget(firstLine)
                end

                integer i = 1
                
                repeat while i < ps:GetSize()
                    //points
                    ChartItem pointItem
                    if GetPointsFlag()
                        pointItem:SetContainer(me)
                        pointItem:SetDisplayName(" , "+(i+1)+" of " + ps:GetSize() + " points in this chunk, value of " + math:Round(ps:Get(i):GetPercentY() * GetYAxisMaximum(), 1))
                        pointItem:SetParent(item)
                        pointItem:SetPrevious(prevPoint)
        
                        ChartPoint point = ps:Get(i)
                        point:SetDescription(pointItem:GetDisplayName())
                        pointItem:SetFocusTarget(point)
                    end

                    //lines
                    if i < ls:GetSize()
                        ChartItem lineItem
                        lineItem:SetContainer(me)

                        if GetPointsFlag()
                            lineItem:SetParent(pointItem)
                            pointItem:SetChild(lineItem)
                        else
                            lineItem:SetParent(item)
                            lineItem:SetPrevious(prevLine)
                            prevLine:SetNext(lineItem)
                            prevLine = lineItem
                        end
    
                        LineSegment line = ls:Get(i)
                        text lineChange = " "
                        number start = math:Round(line:GetPoint1():GetPercentY() * GetYAxisMaximum(), 1)
                        number stop = math:Round(line:GetPoint2():GetPercentY() * GetYAxisMaximum(), 1)
                        if start > stop
                            lineChange = ", This line segment is decreasing value from " + start + " to " + stop
                        elseif start < stop
                            lineChange = ", This line segment is increasing value from " + start + " to " + stop
                        else 
                            lineChange = ", This line segment shows no change with the value remaining at " + start
                        end
                        line:SetDescription(" segment between " + line:GetPoint1():GetName() + " and " + line:GetPoint2():GetName() + lineChange)
                        lineItem:SetDisplayName("Line segment between " + line:GetPoint1():GetName() + " and " + line:GetPoint2():GetName() + lineChange)
                        lineItem:SetFocusTarget(line)
                    end

                    prevPoint:SetNext(pointItem)
                    prevPoint = pointItem
                    i = i + 1
                end
                prevPoint:SetNext(firstPointItem)  
                prevLine:SetNext(firstLineItem)            
            else
                region:SetDescription(chunkDescription)
            end
        else
            region:SetDescription(chunkDescription + " " + "Use the arrow keys to navigate points or chunks.")
            number xDivider = region:GetPercentageX() + (region:GetPercentageWidth() / 2.0)
            Array<ChartPoint> leftChunkPoints
            Array<Drawable> leftChunkLines
            Array<ChartPoint> rightChunkPoints
            Array<Drawable> rightChunkLines

            integer i = 0
            repeat while i < ps:GetSize()
                ChartPoint point = ps:Get(i)
                if point:GetPercentX() < xDivider
                    leftChunkPoints:Add(point)
                    if i < ls:GetSize()
                        leftChunkLines:Add(ls:Get(i))
                    end
                else
                    rightChunkPoints:Add(point)
                    if i < ls:GetSize()
                        rightChunkLines:Add(ls:Get(i))
                    end
                end
                i = i + 1
            end

            //left chunk
            Icon leftChunkRegion = NewRegionIcon(region:GetPercentageX(), 0, region:GetPercentageWidth() / 2.0, 1.0)
            leftChunkRegion:SetName("Left Chunk")
            GetChartArea():Add(leftChunkRegion)
            
            ChartItem leftChunkItem
            leftChunkItem:SetContainer(me)
            leftChunkItem:SetParent(item)
            leftChunkItem:SetFocusTarget(leftChunkRegion)

            //right chunk
            Icon rightChunkRegion = NewRegionIcon(region:GetPercentageX() + region:GetPercentageWidth() / 2.0, 0, region:GetPercentageWidth() / 2.0, 1.0)
            rightChunkRegion:SetName("Right Chunk")
            GetChartArea():Add(rightChunkRegion)
            
            ChartItem rightChunkItem
            rightChunkItem:SetContainer(me)
            rightChunkItem:SetParent(item)
            rightChunkItem:SetFocusTarget(rightChunkRegion)

            item:SetChild(leftChunkItem)
            leftChunkItem:SetNext(rightChunkItem)
            leftChunkItem:SetPrevious(rightChunkItem)
            rightChunkItem:SetNext(leftChunkItem)
            rightChunkItem:SetPrevious(leftChunkItem)
            
            CreateChunks(leftChunkItem, leftChunkRegion, leftChunkPoints, leftChunkLines, currentLevel + 1)
            CreateChunks(rightChunkItem, rightChunkRegion, rightChunkPoints, rightChunkLines, currentLevel + 1)
        end
    end

    /*
    If the quadrant represented by the ChartItem has more points than the quadrant
    threshold, up to four new quadrants will be added as children to the ChartItem.
    This is then called recursively on the new quadrants, if any.
    */
    private action SubdivideQuadrant(ChartItem item, Icon region, Array<ChartPoint> points, integer currentLevel)
        number x1 = math:Round(region:GetPercentageX() * GetXAxisMaximum(), options:GetTickDigits())
        number x2 = math:Round((region:GetPercentageX() + region:GetPercentageWidth()) * GetXAxisMaximum(), options:GetTickDigits())
        number y1 = math:Round(region:GetPercentageY() * GetYAxisMaximum(), options:GetTickDigits())
        number y2 = math:Round((region:GetPercentageY() + region:GetPercentageHeight()) * GetYAxisMaximum(), options:GetTickDigits())

        text pointsText = ""
        if points:GetSize() = 1
            pointsText = "1 point. "
        else
            pointsText = points:GetSize() + " points. "
        end

        text rangeText = pointsText + GetXAxisTitle() + " " + x1 + " to " + x2 + ", " + GetYAxisTitle() + " " + y1 + " to " + y2

        if points:GetSize() <= quadrantThreshold or currentLevel >= maxSubdivisions
            if points:IsEmpty() = false
                points:Sort(comparison)

                region:SetDescription(rangeText + ", Use the arrow keys to navigate the points.")

                ChartItem firstPointItem
                ChartPoint firstPoint = points:Get(0)
                firstPointItem:SetContainer(me)
                firstPointItem:SetDisplayName(GetXAxisTitle() + " " + math:Round(firstPoint:GetPercentX() * GetXAxisMaximum(), options:GetTickDigits()) + ", " + GetYAxisTitle() + " " + math:Round(firstPoint:GetPercentY() * GetYAxisMaximum(), options:GetTickDigits())
                    + ", 1 of " + points:GetSize())
                firstPoint:SetName(firstPointItem:GetDisplayName())
                firstPoint:SetParentRegion(region)
                firstPointItem:SetParent(item)
                firstPointItem:SetFocusTarget(firstPoint)
                item:SetChild(firstPointItem)
        
                integer i = 1
                ChartItem previous = firstPointItem
                repeat while i < points:GetSize()
                    ChartItem pointItem
                    ChartPoint point = points:Get(i)

                    pointItem:SetContainer(me)
                    pointItem:SetDisplayName(GetXAxisTitle() + " " + math:Round(point:GetPercentX() * GetXAxisMaximum(), options:GetTickDigits()) + ", " + GetYAxisTitle() + " " + math:Round(point:GetPercentY() * GetYAxisMaximum(), options:GetTickDigits())
                        + ", " + (i + 1) + " of " + points:GetSize())
                    point:SetName(pointItem:GetDisplayName())
                    point:SetParentRegion(region)
                    pointItem:SetParent(item)
                    pointItem:SetPrevious(previous)
                    pointItem:SetFocusTarget(point)
                    previous:SetNext(pointItem)
                    previous = pointItem
                    i = i + 1
                end
            else
                region:SetDescription(rangeText)
            end
        else
            region:SetDescription(rangeText + ", Use the arrows to inspect the sub-regions.")

            number xDivider = region:GetPercentageX() + (region:GetPercentageWidth() / 2.0)
            number yDivider = region:GetPercentageY() + (region:GetPercentageHeight() / 2.0)
            Array<ChartPoint> topLeft
            Array<ChartPoint> topRight
            Array<ChartPoint> bottomLeft
            Array<ChartPoint> bottomRight

            integer i = 0
            repeat while i < points:GetSize()
                ChartPoint point = points:Get(i)
                if point:GetPercentX() < xDivider
                    if point:GetPercentY() < yDivider
                        bottomLeft:Add(point)
                    else
                        topLeft:Add(point)
                    end
                else
                    if point:GetPercentY() < yDivider
                        bottomRight:Add(point)
                    else
                        topRight:Add(point)
                    end
                end

                i = i + 1
            end

            text quadrantName = "Quadrant"
            if currentLevel >= 1
                quadrantName = "Subregion"
            end

            ChartItem topLeftItem
            topLeftItem:SetContainer(me)
            topLeftItem:SetParent(item)
            Icon topLeftRegion = NewRegionIcon(region:GetPercentageX(), region:GetPercentageY() + region:GetPercentageHeight() / 2.0,
                    region:GetPercentageWidth() / 2.0, region:GetPercentageHeight() / 2.0)
            topLeftRegion:SetName("Top-Left " + quadrantName)
            GetChartArea():Add(topLeftRegion)
            topLeftItem:SetFocusTarget(topLeftRegion)

            ChartItem topRightItem
            topRightItem:SetContainer(me)
            topRightItem:SetParent(item)
            Icon topRightRegion = NewRegionIcon(region:GetPercentageX() + region:GetPercentageWidth() / 2.0, region:GetPercentageY() + region:GetPercentageHeight() / 2.0,
                    region:GetPercentageWidth() / 2.0, region:GetPercentageHeight() / 2.0)
            topRightRegion:SetName("Top-Right " + quadrantName)
            GetChartArea():Add(topRightRegion)
            topRightItem:SetFocusTarget(topRightRegion)

            ChartItem bottomRightItem
            bottomRightItem:SetContainer(me)
            bottomRightItem:SetParent(item)
            Icon bottomRightRegion = NewRegionIcon(region:GetPercentageX() + region:GetPercentageWidth() / 2.0, region:GetPercentageY(),
                    region:GetPercentageWidth() / 2.0, region:GetPercentageHeight() / 2.0)
            bottomRightRegion:SetName("Bottom-Right " + quadrantName)
            GetChartArea():Add(bottomRightRegion)
            bottomRightItem:SetFocusTarget(bottomRightRegion)

            ChartItem bottomLeftItem
            bottomLeftItem:SetContainer(me)
            bottomLeftItem:SetParent(item)
            Icon bottomLeftRegion = NewRegionIcon(region:GetPercentageX(), region:GetPercentageY(),
                    region:GetPercentageWidth() / 2.0, region:GetPercentageHeight() / 2.0)
            bottomLeftRegion:SetName("Bottom-Left " + quadrantName)
            GetChartArea():Add(bottomLeftRegion)
            bottomLeftItem:SetFocusTarget(bottomLeftRegion)

            item:SetChild(topLeftItem)
            topLeftItem:SetNext(topRightItem)
            topLeftItem:SetPrevious(bottomLeftItem)
            topRightItem:SetNext(bottomRightItem)
            topRightItem:SetPrevious(topLeftItem)
            bottomRightItem:SetNext(bottomLeftItem)
            bottomRightItem:SetPrevious(topRightItem)
            bottomLeftItem:SetNext(topLeftItem)
            bottomLeftItem:SetPrevious(bottomRightItem)

            SubdivideQuadrant(topLeftItem, topLeftRegion, topLeft, currentLevel + 1)
            SubdivideQuadrant(topRightItem, topRightRegion, topRight, currentLevel + 1)
            SubdivideQuadrant(bottomRightItem, bottomRightRegion, bottomRight, currentLevel + 1)
            SubdivideQuadrant(bottomLeftItem, bottomLeftRegion, bottomLeft, currentLevel + 1)
        end
    end
    action SetQuadrantThreshold(integer threshold)
        quadrantThreshold = threshold
    end
    
    action GetQuadrantThreshold returns integer
        return quadrantThreshold
    end

    action SetMaximumSubdivisons(integer subdivisions)
        maxSubdivisions = subdivisions
    end

    action GetMaximumSubdivisions returns integer
        return maxSubdivisions
    end

    /*
        This action is called whenever the window is resized.
    */
    action Resize()
        parent:Chart:Resize()
    end
    /*
    Flag used to display the points of all lines.
    */
    action ShowAllPoints()
        showPoints = true
        integer i = 0
        repeat while i < lines:GetSize()
            lines:Get(i):ShowPoints()
            i = i + 1
        end
    end

    /*
    Flag used to hide all the points of all lines.
    */
    action HideAllPoints()
        showPoints = false
        integer i = 0
        repeat while i < lines:GetSize()
            lines:Get(i):HidePoints()
            i = i + 1
        end
    end
end