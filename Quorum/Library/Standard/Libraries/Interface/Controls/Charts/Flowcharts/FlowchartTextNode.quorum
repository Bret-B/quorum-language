package Libraries.Interface.Controls.Charts.Flowcharts

use Libraries.Game.Graphics.Label
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Views.ControlShaderView
/*
    This flowchart node represents a visible flowchart node. This node can display 
    text within the node with all the default capabilities of a flowchart node.

    Attribute: Author William Allee

    Attribute: Example
    use Libraries.Game.Graphics.Drawable
    use Libraries.Interface.Controls.Charts.Flowcharts.Flowchart
    use Libraries.Interface.Controls.Charts.Flowcharts.FlowchartTextNode
    
    Flowchart chart

    chart:SetName("RockNotRockTreeFlowchart2")
    chart:SetPercentageX(0.0)
    chart:SetPercentageY(0.0)
    chart:SetFontSize(15)

    // Add basic visable text nodes for the chart
    FlowchartTextNode node1
    node1:Show()
    node1:SetName("Start of the Rock Not Rock Tree Diagram")
    node1:SetPercentageWidth(0.10)
    node1:SetPercentageHeight(0.20)
    node1:SetPercentageX(0.05)
    node1:SetPercentageY(0.45)
    chart:Add(node1)
    chart:Display()
*/
class FlowchartTextNode is FlowchartNode

    Label label

    on create
        SetName("Text Node")
        SetGridWidth(1)
        SetGridHeight(1)

        ManualLayout layout
        SetLayout(layout)

        label:SetPercentageX(0.5)
        label:SetPercentageY(0.5)
        label:SetPercentageOriginX(0.5)
        label:SetPercentageOriginY(0.5)
        Add(label)

        SetPercentageX(0.5)
        SetPercentageY(0.5)
        SetPercentageOriginX(0.5)
        SetPercentageOriginY(0.5)
        SetPercentageWidth(0.9)
        SetPercentageHeight(0.9)
    end

    action ConvertToScalableVectorGraphics returns text
        return ""
    end
    /*
        This action Sets the name (identifier) for the text node.
        
        Attribute: Parameter name the identifier for this flowchart text node.
    */
    action SetName(text name)
        label:SetText(name)
        parent:Control:SetName(name)
    end

    action LoadGraphics(LayoutProperties properties)
        if GetView2D() = undefined
            ControlShaderView view
            view:Initialize()
            SetView2D(view)
        end

        Flowchart chart = GetFlowchart()
        if chart not= undefined
            if label:GetFontSize() not= chart:GetFontSize()
                label:SetFontSize(chart:GetFontSize())
            end

            if HasColorPropertyOverride("fontColor")
                label:SetFontColor(GetFontColor())
            else
                label:SetFontColor(GetFontColor())
            end
        end
    end

    action Resize
        label:SetMaximumWidth(GetWidth())
        parent:Control:Resize()
    end

end