package Libraries.Interface.Controls.Charts

use Libraries.Game.Graphics.Drawable
use Libraries.Containers.Array
use Libraries.Interface.Controls.Charts.ChartPoint
use Libraries.Interface.Controls.Charts.LineSegment
use Libraries.Game.Graphics.Color
use Libraries.Interface.Controls.Icon
use Libraries.Game.Graphics.Label

/*
    This object class is used to contain the points and lines for each line object in the line chart. It should
    not be used without Line Chart.

    Attribute: Author Cody Molisee
*/
class ChartLine is Drawable
    Label name
    Array<ChartPoint> points
    Array<LineSegment> lines
    private boolean showPoints = true
    private Color lineColor
    number maxYPercent = 0.0
    number minYPercent = 1.0
    number maxXPercent = 0.0
    number minXPercent = 1.0

    on create
        lineColor:SetColor(0, 0, 1, 1)
    end

    /*
    Returns the name of this line.
    */
    action GetName() returns text
        return name:GetName()
    end

    /*
    Sets/Changes the name of this line.
    */
    action SetName(text name)
        me:name:SetName(name)
        me:name:SetText(name)
    end

    /*
    Returns max y percent of all points
    */
    action GetMaxYPercent() returns number
        return maxYPercent
    end
    /*
    Returns min y percent of all points
    */
    action GetMinYPercent() returns number
        return minYPercent
    end
    /*
    Returns max x percent of all points
    */
    action GetMaxXPercent() returns number
        return maxXPercent
    end
    /*
    Returns min x percent of all points
    */
    action GetMinXPercent() returns number
        return minXPercent
    end


    /*
    Returns an array of ChartPoints for this line.
    */
    action GetPoints() returns Array<ChartPoint>
        return points
    end

    /*
    Returns an array of Drawables for this line.
    */
    action GetLines() returns Array<LineSegment>
        return lines
    end

    /*
    Sets the showPoints flag to false.
    flag is true by default.
    */
    action HidePoints()
        showPoints = false
    end

    /*
    Sets the showPoints flag to true.
    flag is true by default.
    */
    action ShowPoints()
        showPoints = true
    end

    /*
    Returns the value of the showPoints flag.
    */
    action GetPointsFlag() returns boolean
        return showPoints
    end

    /*
    Returns the color for this line.
    */
    action GetColor() returns Color
        return lineColor
    end

    /*
    Sets/changes the color for this line.
    */
    action SetColor(Color lineColor)
        me:lineColor = lineColor
    end
    
    /*
    Adds a ChartPoint to this lines array of ChartPoints.
    If points:GetSize() > 1, add a line Drawable to this lines line array.
    */
    action Add(ChartPoint point)
        points:Add(point)
        if point:GetPercentY() > maxYPercent
            maxYPercent = point:GetPercentY()
        end
        if point:GetPercentY() < minYPercent
            minYPercent = point:GetPercentY()
        end
        if point:GetPercentX() > maxXPercent
            maxXPercent = point:GetPercentX()
        end
        if point:GetPercentX() < minXPercent
            minXPercent = point:GetPercentX()
        end
        if points:GetSize() > 1
            LineSegment line
            line:SetFocusable(true)
            line:SetAccessibilityCode(parent:Item:CUSTOM)
            lines:Add(line)
        end
    end
    
    /*
    Sorts all the chart points within the line by lowest x value first
    Very basic bubble sort, can be improved. Used in numerical factor line charts
    */
    action SortPoints()
        integer i = 0
        repeat while i < points:GetSize()-1
            integer j = 0
            repeat while j < points:GetSize()-i-1
                if points:Get(j):GetPercentX() > points:Get(j+1):GetPercentX()
                    ChartPoint temp
                    temp:SetPercentX(points:Get(j):GetPercentX())
                    temp:SetPercentY(points:Get(j):GetPercentY())

                    points:Get(j):SetPercentX(points:Get(j+1):GetPercentX())
                    points:Get(j):SetPercentY(points:Get(j+1):GetPercentY())

                    points:Get(j+1):SetPercentX(temp:GetPercentX())
                    points:Get(j+1):SetPercentY(temp:GetPercentY())
                end
                j = j + 1 
            end
            i = i + 1
        end
    end
end