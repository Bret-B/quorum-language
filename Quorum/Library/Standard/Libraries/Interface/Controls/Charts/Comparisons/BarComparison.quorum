package Libraries.Interface.Controls.Charts

use Libraries.Containers.Support.Comparison
use Libraries.Interface.Controls.Charts.Graphics.Bar

/*
    This class provides a way to compare two Bar objects. The returned values from 
    its sole action is an integer with the value of -1 (the left item is smaller), 
    0 (the items are equal) or 1 (the left item is larger). It can be used to 
    override the standard comparison in Object for sorting single bars by bar size. 
    
    Atrribute: Author Hannah Williams, Tim Kluthe
*/
class BarComparison is Comparison
    boolean ascending = false

    /*  
        This currently sorts single-bars in descending bar size order.
        Whenever sort is used with this, be sure to update axis labels.
    */
    action Compare(Object left, Object right) returns integer
        Bar bar1 = cast(Bar, left)
        Bar bar2 = cast(Bar, right)

        if ascending
            if bar1:GetValue() > bar2:GetValue()
                return parent:Comparison:LARGER
            elseif bar1:GetValue() < bar2:GetValue()
                return parent:Comparison:SMALLER
            else
                return parent:Comparison:EQUAL
            end
        else
            if bar1:GetValue() > bar2:GetValue()
                return parent:Comparison:SMALLER
            elseif bar1:GetValue() < bar2:GetValue()
                return parent:Comparison:LARGER
            else
                return parent:Comparison:EQUAL
            end
        end
    end
    /*  
        This sets the comparison so bar groups are sorted in ascending (true) or descending (false) order.
    */
    action SetAscending(boolean ascending)
        me:ascending = ascending
    end
end