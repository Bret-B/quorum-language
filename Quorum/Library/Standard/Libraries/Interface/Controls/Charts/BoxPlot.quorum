package Libraries.Interface.Controls.Charts

use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Layouts.BoxPlotLayout
use Libraries.Interface.Selections.ChartSelection
use Libraries.Interface.Item
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Color
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Controls.ControlLabel
use Libraries.Containers.Array
use Libraries.Compute.Math
use Libraries.Compute.Vector
use Libraries.Compute.Statistics.Analysis.UnivariateAction
use Libraries.Compute.Statistics.WindowingActions.NormalDistributionAction
use Libraries.Interface.Controls.Charts.BoxPlotWriter 
use Libraries.Compute.Statistics.Charts.BoxPlotCreator
use Libraries.Game.Graphics.ColorGroup
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Gradient
use Libraries.Interface.Controls.Charts.PlotGroup
use Libraries.Interface.Controls.Charts.Plot
use Libraries.Interface.Controls.Charts.Series


/*
    Attribute: Author Christian Castillo

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Charts.BoxPlotCreator
    use Libraries.Interface.Controls.Charts.BoxPlot
    use Libraries.Game.Game
    
    class Main is Game
        action Main
            StartGame()
        end
    
        action CreateGame
            DataFrame frame
            frame:Load("Data/Data.csv")
            BoxPlotCreator creator
            creator:AddColumn(2)
        
            BoxPlot chart = cast(BoxPlot,frame:CreateChart(creator))
            chart:SetPercentageWidth(1)
            chart:SetPercentageHeight(1)
            chart:SetTitle("Sample Title")
            chart:SetXAxisTitle("X-axis Variable")
            chart:SetYAxisTitle("Y-axis Variable")
            Add(chart)
        end
    end
*/

class BoxPlot is Chart
    // The root ChartItem.
    private ChartItem summary

    // The series of the plot. Only used when a factor is used
    Array<Series> seriesList
    Array<PlotGroup> plotGroups

    Color highlightColor = undefined
    ColorGroup previousColorGroup = undefined
    Color previousColor = undefined

    ChartOptions options
    Math math

    boolean showOutliers = true
    boolean hasGroups = true

    // How many ticks are set along the y axis and max value on y axis.
    private integer yIncrements = 0 //increments in the y axis
    private number yAxisMaximum = 0 // the maximum value in y axis
    private number yAxisMinimum = 0 // the maximum value in y axis
    number yTickInterval = -1
    // Default uses best interval by range to graph, if tick count is set, it overrides this.
    boolean yIntervalBasedOnTickCount = false
    
    // Initial size of x and y axis
    private integer IntervalLength = 10
    private integer IntervalWidth = 30

    // Labels that is shown on the x axis
    private Array<text> xAxisLabels

   // SetYAxisTitle("Speed of Cars miles per hour")
    public text xTitle = ""
    public text yTitle = ""

    // Panels
    Control TitlePanel
    Control AreaOfChart
    Control VerticalTitlePanel
    Control HorizontalTitlePanel
    
    //Gets the TitlePanel
    action GetTitlePanel returns Control
        return TitlePanel
    end

    // Gets the AreaOfChart
    action GetChartA returns Control
        return AreaOfChart
    end

    // Gets the HorizontalTitlePanel
     action GetHorizontalTitlePanel returns Control
        return HorizontalTitlePanel
    end

    // Gets the VerticalTitlePanel
    action GetVerticalTitlePanel returns Control
        return VerticalTitlePanel
    end

    // Sets the labels along the x axis
    action SetXAxisLabels(Array<text> label)
        xAxisLabels = label
    end
  
    // Returns the labels
    action GetXAxisLabels returns Array<text>
        return xAxisLabels
    end
    
    // Sets how many increments in y-axis
    action SetYIncrementCount(integer increments)
        yIncrements = increments
    end
   
    // Returns number of increments on the y axis
    action GetYIncrementCount returns integer
        return yIncrements
    end

    // Sets value between y-ticks
    action SetYTickInterval(number set)
        yTickInterval = set
    end

    action SetYTickInterval(integer set)
        yTickInterval = cast(number, set)
    end

    action GetYTickInterval() returns number
        return yTickInterval
    end

    // y Axis min and max
    action SetYAxisMaximum(number max)
        yAxisMaximum = max
     end
    action GetYAxisMaximum returns number
        return yAxisMaximum
    end
    action SetYAxisMinimum(number min)
        yAxisMinimum = min
    end
    action GetYAxisMinimum returns number
        return yAxisMinimum
    end

    /*
    Flag used to hide all the outliers.
    */
    action HideOutliers()
        showOutliers = false
    end

    /*
    Returns the value of the showOutlier flag.
    */
    action GetOutliersFlag() returns boolean
        return showOutliers
    end

     /*
    Sets a flag that's used by GenerateInfoTree to determine if it should include accessibility navigation through plot groups
    */
    action HasGroups(boolean flag)
        hasGroups = flag
    end

    /*
    Appends a plotgroup.
    */
    action AddPlotGroup(PlotGroup group)
        plotGroups:Add(group)
    end
   
    /*
        Returns an array of all the plot groups
    */
    action GetPlotGroups returns Array<PlotGroup>
        return plotGroups
    end

     /*
    Returns an array of all the series sets 
    */
    action GetSeriesList returns Array<Series>
        return seriesList
    end

    action AddSeries(Series series)
        seriesList:Add(series)
    end

    /*
        Gets the default selection color (highlighting)
    */
    action GetHighlightColor returns Color
        return highlightColor
    end

    /*
        Sets the default selection color (highlighting)
    */
    action SetHighlightColor(Color color)
        me:highlightColor = color
    end

    on create
        BoxPlotLayout layout
        SetLayout(layout)

        LayoutProperties properties = GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:MAINTAIN_ASPECT_RATIO)
        properties:SetVerticalLayoutMode(properties:STANDARD)

        Font font 
        font:LoadFont("Arial")
        properties:SetFont(font)
        properties:SetFontSize(16)

        Color color
        Gradient gradient
        Color gray = color:LightGray()
        Color lightGray = color:CustomColor(0.9, 0.9, 0.9, 1)
        gradient:Set(gray, gray, lightGray, lightGray)

        properties:SetBackgroundColor(gradient)
        properties:SetBorderColor(color:Black())
        properties:SetBorderThickness(2)
        SetName("Box Plot")

        SetInputGroup("Chart")
        SetFocusable(true)
        SetAccessibilityCode(parent:Item:ITEM)

    end

    /*
    This is called by GenerateInfoTree to generate the summary that is heard when
    you first focus on the chart. Also the highest level of the information tree
    of  the chart.
    */
    action GenerateSummary returns text
        text lineText = "Groups"
        text plotText = "box plots"
        if xAxisLabels:GetSize() = 1
            linetext = "Group"
            plotText = "box plot"
        end
        if hasGroups
            SetDescription(" with " + xAxisLabels:GetSize() + " " + lineText 
            + ". " + "Use the arrow keys to navigate the chart.")
        else
            SetDescription(" with " + xAxisLabels:GetSize() + " " + plotText 
            + ". " + "Use the arrow keys to navigate the chart.")
        end
        return GetDescription()
    end // end of GenerateSummary

    action DisposeDrawables
        parent:Chart:DisposeDrawables()
        Control chartArea = me:GetChartArea()
        integer i = 0
        repeat while i < plotGroups:GetSize()
            integer j = 0
            repeat while j < plotGroups:Get(i):GetSize()
                Array<Drawable> items = plotGroups:Get(i):Get(j):GetItems()
                integer k = 0
                repeat while k < items:GetSize()
                    if items:Get(k) not= undefined
                        Drawable target = items:Get(k)
                        target:Dispose()
                        chartArea:Remove(target)
                    end
                    k = k + 1
                end
                items:Empty()
                j = j + 1
            end
            if plotGroups:Get(i):GetItem() not= undefined
                Drawable target = plotGroups:Get(i):GetItem()
                target:Dispose()
                chartArea:Remove(target)
            end
            i = i + 1
        end
    end


    /*
        This action loads the graphics to the game, and associates those graphics to their verbal descriptions.
    */
    action LoadGraphics(LayoutProperties properties)

        DisposeDrawables()
        parent:Chart:LoadGraphics(properties)
        if properties = undefined
            return now
        end
        
        //Sets the base format of boxplot chart
        Control vertical = GetVerticalPanel()
        Control horizontal = GetHorizontalPanel()
        Control chartArea = GetChartArea()

        // Gets the increments of the axes of the chart
        Array<Drawable> drawable_Xincrements = GetXTicks()
        Array<Drawable> drawable_Yincrements = GetYTicks()
        Array<Drawable> yGridlines = GetMajorYGridlines()
        Array<Drawable> xGridlines = GetMajorXGridlines()
        Array<Drawable> yMinorGridlines = GetMinorYGridlines()
        Array<Drawable> xMinorGridlines = GetMinorXGridlines()
        
        //Gets the labels of the axes
        Array<ControlLabel> x_Labels = GetXLabels()
        Array<ControlLabel> y_Labels = GetYLabels()

        if GetHighlightColor() = undefined
            SetHighlightColor(options:GetSelectionColor())
        end

        number scaleNum = yAxisMinimum
        number scaleWidth = yAxisCalculateScaleWidth() // Get scale width and tick increments

        //Adding Tick and labels on Y axis
        if yIncrements not= 0 and drawable_Yincrements:IsEmpty()
            repeat until drawable_Yincrements:GetSize() = yIncrements
                Drawable Increment
                Increment:LoadFilledRectangle(IntervalLength, IntervalWidth)
                drawable_Yincrements:Add(Increment)
                vertical:Add(Increment)

                if IsShowingMajorYGridLines()
                    Drawable gridline
                    yGridlines:Add(gridline)
                    chartArea:Add(gridline)
                end
                if IsShowingMinorYGridLines()
                    integer j = 0
                    repeat while j < 5
                        Drawable gridline
                        yMinorGridlines:Add(gridline)
                        chartArea:Add(gridline)
                        j = j + 1
                    end
                end

                ChartOptions chartOptions
                Math math
                text scaleText = "" + math:Round(scaleNum, chartOptions:GetTickDigits())
                ControlLabel label
                label:SetFontSize(GetFontSize())
                label:SetFocusable(true)
                label:SetAccessibilityCode(parent:Item:ITEM)
                label:SetText(scaleText)
                label:SetName(scaleText)
                vertical:Add(label)
                y_Labels:Add(label)
                scaleNum = scaleNum + scaleWidth
            end //end of repeat statement
        end // end of if statement

        //Add Labels on X axis
        integer count = 0
        repeat while count < xAxisLabels:GetSize()
            ControlLabel label
            label:SetFocusable(true)
            label:SetFontSize(GetFontSize())
            label:SetAccessibilityCode(parent:Item:ITEM)
            label:SetName(xAxisLabels:Get(count))
            label:SetText(xAxisLabels:Get(count))
            horizontal:Add(label)
            x_Labels:Add(label)
            count = count + 1
        end

        // add drawables per each boxplot to the chart
        integer i = 0
        repeat while i < plotGroups:GetSize()
            PlotGroup group = plotGroups:Get(i)
            integer k = 0
            repeat while k < group:GetSize()
                Plot plot = group:Get(k)
                number median = plot:GetInfoAt(0)
                number firstQ = plot:GetInfoAt(1)
                number thirdQ = plot:GetInfoAt(2)
                number minimum = plot:GetInfoAt(3)
                number maximum = plot:GetInfoAt(4)
                number iqr = thirdQ - firstQ

                // Minimum Line 
                Drawable minimumLine
                minimumLine:SetName("The minimum value is " + minimum + " ")
                minimumLine:SetDescription("Minimum")
                minimumLine:SetFocusable(true)
                minimumLine:SetAccessibilityCode(0)
                // Lower Whisker Line
                Drawable lowerWhisker
                // Interquartile Range Region Area
                Drawable interQuartileRange
                interQuartileRange:SetName("The Interquartile range of is " + iqr + " ")
                interQuartileRange:SetDescription("Interquartile range")
                interQuartileRange:SetFocusable(true)
                interQuartileRange:SetAccessibilityCode(0)
                // Right Side of IQR
                Drawable rightBorder
                // Left Side of IQR
                Drawable leftBorder
                // Lower Quartile Line Q1
                Drawable firstQuartileLine
                firstQuartileLine:SetName("The lower quartile value is " + firstQ + " ")
                firstQuartileLine:SetDescription("1st Quartile")
                firstQuartileLine:SetFocusable(true)
                firstQuartileLine:SetAccessibilityCode(0)
                // Median Line (denser)
                Drawable medianLine
                medianLine:SetName("The Median value is " + median + "  ")
                medianLine:SetDescription("Median")
                medianLine:SetFocusable(true)
                medianLine:SetAccessibilityCode(0)
                // Upper Quartile Line Q3
                Drawable thirdQuartileLine
                thirdQuartileLine:SetName("The upper quartile value is " + thirdQ + " ")
                thirdQuartileLine:SetDescription("3rd Quartile")
                thirdQuartileLine:SetFocusable(true)
                thirdQuartileLine:SetAccessibilityCode(0)
                // Upper Whisker Line
                Drawable upperWhisker
                // Maximum Line
                Drawable maximumLine
                maximumLine:SetName("The Maximum value is " + maximum + " ")
                maximumLine:SetDescription("Maximum")
                maximumLine:SetFocusable(true)
                maximumLine:SetAccessibilityCode(0)
                // Plot Area Outline Panel
                Icon plotArea
                plotArea:SetName(group:Get(k):GetName() + " box plot")
                plotArea:SetFocusable(true)
                plotArea:SetAccessibilityCode(0)
    
    
                chartArea:Add(rightBorder)
                chartArea:Add(leftBorder)
                chartArea:Add(lowerWhisker)
                chartArea:Add(upperWhisker)

                chartArea:Add(minimumLine)
                chartArea:Add(firstQuartileLine)
                chartArea:Add(medianLine)
                chartArea:Add(thirdQuartileLine)
                chartArea:Add(maximumLine)
                chartArea:Add(interQuartileRange)  //Place on top of others
                chartArea:Add(plotArea)
                //Add the outliers
    
                /* Indices for items of individual box plot
                    0: PLOT AREA (ENTIRE REGION)
                    1: MINIMUM LINE
                    2: LOWER WHISKER
                    3: INTERQUARTILE RANGE
                    4: RIGHT BORDER OF IQR
                    5: LEFT BORDER OF IQR
                    6: Q1 LINE
                    7: MEDIAN LINE
                    8: Q3 LINE
                    9: UPPER WHISKER    
                    10: MAXIMUM LINE 
                    11+: OUTLIER POINTS
                */
                plot:AddItem(plotArea)
                plot:AddItem(minimumLine)      
                plot:AddItem(lowerWhisker)
                plot:AddItem(interQuartileRange)
                plot:AddItem(rightBorder)
                plot:AddItem(leftBorder)
                plot:AddItem(firstQuartileLine)
                plot:AddItem(medianLine)
                plot:AddItem(thirdQuartileLine)
                plot:AddItem(upperWhisker)
                plot:AddItem(maximumLine)
                //Add the outliers
    
    
                if GetOutliersFlag()
                    // Outliers ++
                    Array <number> outliers = plot:GetOutliers()
                    integer j = 0
                    repeat while j < outliers:GetSize()
                        Drawable outlier
                        outlier:SetName(""+outliers:Get(j))
                        outlier:SetDescription("Outlier")
                        outlier:SetFocusable(true)
                        outlier:SetAccessibilityCode(0)
                        chartArea:Add(outlier)
                        plot:AddItem(outlier)
                        j = j + 1
                    end
                end
                k = k + 1
            end

            // Area covering entire group panel
            Icon panel
            panel:SetName(xAxisLabels:Get(i) + " group has " + group:GetSize() + " box plots.")
            panel:SetFocusable(true)
            panel:SetAccessibilityCode(0)
            chartArea:Add(panel)
            group:SetItem(panel)
            i = i + 1
        end//end of loop

        if seriesList:GetSize() > 0
            i = 0
            repeat while i < seriesList:GetSize()
                Series series = seriesList:Get(i)
                series:SetColor(options:GetColorFromPalette(i))
                series:GetLabel():SetFocusable(true)
                series:GetLabel():SetAccessibilityCode(parent:Item:ITEM)
                integer j = 0
                repeat while j < series:GetSize()
                    series:GetItem(j):SetColor(series:GetColor())
                    j = j + 1
                end
                i = i + 1
            end
        end


        if IsShowingLegend()
            Add(GetLegend())
        end

    end // end of loadGraphics
    
    /*
        This action takes a range of data and returns a decent interval, this is only the default.
        The user can choose any interval they wish and set it manually.  
    */
    action CalculateBestTickInterval(number range) returns number
        number tickSkips = 0
        if range <= 2.5
            if range > 1.5
                tickSkips = 0.5
            else
                tickSkips = 0.1
            end
        else
            if range > 5000
                tickSkips = 1000
            elseif range > 2500
                tickSkips = 500
            elseif range > 1000
                tickSkips = 250
            elseif range > 500
                tickSkips = 100
            elseif range > 200
                tickSkips = 50
            elseif range > 100
                tickSkips = 20
            elseif range > 50
                tickSkips = 10
            elseif range > 20
                tickSkips = 5
            elseif range > 10
                tickSkips = 2
            else
                tickSkips = 1
            end
        end
        return tickSkips
    end

    action yAxisCalculateScaleWidth() returns number
        number yRange = yAxisMaximum-yAxisMinimum
        number scaleWidth = 0.0
        // Calculating intervals and/or number of ticks
        if yIntervalBasedOnTickCount
            if yIncrements > 1
                SetYTickInterval(math:Round(yRange / cast(number, yIncrements-1), 2))
                yAxisMaximum = cast(number, yIncrements-1) * GetYTickInterval() + yAxisMinimum
            end
        end
        // Numerical y-scale (number intervals)
        number tickSkips = GetYTickInterval()
        if tickSkips = -1
            tickSkips = CalculateBestTickInterval(yRange)
        end
        if tickSkips not= 1.0
            number i = yAxisMinimum
            repeat while i < yAxisMaximum
                i = i + tickSkips
            end
            yAxisMaximum = math:Round(i, 2) 
            yIncrements = cast(integer, (yAxisMaximum-yAxisMinimum) / tickSkips) + 1
            scaleWidth = tickSkips
        else
            yIncrements = cast(integer, yRange) + 1
            scaleWidth = yRange / (yIncrements - 1)
        end 
        return scaleWidth
    end

    /* 
    Creates the accessibility information for the chart area.
    */
    action GenerateInfoTree
        if GetDefaultLayoutProperties():NeedsRendering()
            return now //we haven't loaded graphics yet, so bail.
        end
        
                //NOTE: These nodes implement a cheap form of ordinality might need changing
        ChartItem xAxis
        ChartItem yAxis
        ChartItem chartArea//not to be confused with panel
        //Level 1 top level
        summary:SetDisplayName(GenerateSummary())
        summary:SetNext(yAxis)
        summary:SetChild(yAxis)
        summary:SetContainer(me)
        summary:SetFocusTarget(me)
    
        //Level 2 three main areas of bar chart (maybe 4 if legend is added)
        xAxis:SetDisplayName("Horizontal Axis")
        xAxis:SetFocusTarget(GetXLabel())
        yAxis:SetDisplayName("Vertical Axis")
        yAxis:SetFocusTarget(GetYLabel())

        chartArea:SetDisplayName("Box Plot")
        if hasGroups
            me:GetChartArea():SetDescription(" with " + plotGroups:GetSize() + " groups")
        else
            me:GetChartArea():SetDescription(" with " + plotGroups:GetSize() + " box plots")
        end
        chartArea:SetFocusTarget(me:GetChartArea())
        xAxis:SetContainer(me)        
        yAxis:SetContainer(me)
        chartArea:SetContainer(me)

        yAxis:SetNext(chartArea)
        chartArea:SetNext(xAxis)
        xAxis:SetPrevious(chartArea)
        yAxis:SetPrevious(xAxis)

        chartArea:SetPrevious(yAxis)
        xAxis:SetParent(summary)
        yAxis:SetParent(summary)
        yAxis:SetPrevious(summary)
        chartArea:SetParent(summary)

        // Legend Area
        if IsShowingLegend()
            GetLegend():GenerateInfoTree(me, summary, xAxis, yAxis)
        end

       // Makes control labels for y axis
        Array<ControlLabel> y_Labels = GetYLabels()
        ChartItem y_Prev
    
        integer i = 0
    
        repeat while i < y_Labels:GetSize()
            ChartItem next
            if i = 0
                yAxis:SetChild(next)
            else
                next:SetPrevious(y_Prev)
                y_Prev:SetNext(next)
            end
        
            next:SetDisplayName(y_Labels:Get(i):GetText())
            next:SetFocusTarget(y_Labels:Get(i))
            next:SetContainer(me)
            next:SetParent(yAxis)
            
            y_Prev = next
        
            i = i + 1 
        end //end of repeat loop


        //Makes control labels for x axis      
        Array<ControlLabel> x_Labels = GetXLabels()
        ChartItem x_Prev
        integer j = 0
        repeat while j < x_Labels:GetSize()
            ChartItem next
            if j =0
                xAxis:SetChild(next)
            else
                next:SetPrevious(x_Prev)
                x_Prev:SetNext(next)
            end
            
            next:SetDisplayName(x_Labels:Get(j):GetText())
            next:SetFocusTarget(x_Labels:Get(j))
            next:SetContainer(me)
            next:SetParent(xAxis)
           
            x_Prev = next

            j = j + 1
        end // end of loop
        
        if hasGroups
            //Plot Groups
            ChartItem firstGroup
            firstGroup:SetContainer(me)
            if plotGroups:GetSize() > 0
                chartArea:SetChild(firstGroup)
                firstGroup:SetParent(chartArea)
                firstGroup:SetFocusTarget(plotGroups:Get(0):GetItem())
    
                //Plots within Plot Groups
                if plotGroups:Get(0):GetSize() > 0 
                    ChartItem prev_target
                    integer k = 0
                    repeat while k < plotGroups:Get(0):GetSize()
                        ChartItem next_target
                        Array<Drawable> items = plotGroups:Get(0):Get(k):GetItems() //First plot
                        if k = 0
                            firstGroup:SetChild(next_target)
                        else
                            next_target:SetPrevious(prev_target)
                            prev_target:SetNext(next_target)
                        end
                        
                        next_target:SetContainer(me)
                        next_target:SetDisplayName(items:Get(0):GetName()) 
                        next_target:SetFocusTarget(items:Get(0))  
                        next_target:SetParent(firstGroup)
                        prev_target = next_target    
                        ChartItem previous
                        integer target_index = 0
                        integer index = 0
                        repeat while index < items:GetSize()
                            Drawable item = items:Get(index)
                            Drawable lastItem = items:Get(items:GetSize() - 1)
                            text Description = item:GetDescription()
                  
                            if Description not= ""
            
                                ChartItem next
                                if target_index = 0
                                    next_target:SetChild(next)
                                else
                                    next:SetPrevious(previous)
                                    previous:SetNext(next)
                                end
                                next:SetContainer(me)
                                next:SetDisplayName(item:GetName()) 
                                next:SetFocusTarget(item)
                                next:SetParent(next_target)
                                
                                previous = next
                                target_index = target_index+1 
                            end
                            index = index + 1
                        end
                        k = k + 1
                    end
                end
            end
    
            i = 1
            ChartItem previousGroup = firstGroup
            repeat while i < plotGroups:GetSize()
                ChartItem group
                group:SetContainer(me)
                group:SetParent(chartArea)
                group:SetFocusTarget(plotGroups:Get(i):GetItem())
                previousGroup:SetNext(group)
                group:SetPrevious(previousGroup)
                previousGroup = group

                //Plots within Plot Groups
                if plotGroups:Get(i):GetSize() > 0 
                    ChartItem prev_target
                    integer k = 0
                    repeat while k < plotGroups:Get(i):GetSize()
                        ChartItem next_target
                        Array<Drawable> items = plotGroups:Get(i):Get(k):GetItems() //First plot
                        if k = 0
                            group:SetChild(next_target)
                        else
                            next_target:SetPrevious(prev_target)
                            prev_target:SetNext(next_target)
                        end

                        next_target:SetContainer(me)
                        next_target:SetDisplayName(items:Get(0):GetName()) 
                        next_target:SetFocusTarget(items:Get(0))  
                        next_target:SetParent(group)
                        prev_target = next_target      
                        ChartItem previous
                        integer target_index = 0
                        integer index = 0
                        repeat while index < items:GetSize()
                            Drawable item = items:Get(index)
                            Drawable lastItem = items:Get(items:GetSize() - 1)
                            text Description = item:GetDescription()
                  
                            if Description not= ""
            
                                ChartItem next
                                if target_index = 0
                                    next_target:SetChild(next)
                                else
                                    next:SetPrevious(previous)
                                    previous:SetNext(next)
                                end
                                next:SetContainer(me)
                                next:SetDisplayName(item:GetName()) 
                                next:SetFocusTarget(item)
                                next:SetParent(next_target)
                                
                                previous = next
                                target_index = target_index+1 
                            end
                            index = index + 1
                        end
                        k = k + 1
                    end
                end
                i = i + 1
            end
        else
            ChartItem prev_target
            integer k = 0
            repeat while k < plotGroups:GetSize()
                ChartItem next_target
                Array<Drawable> items = plotGroups:Get(k):Get(0):GetItems() //First plot
                if k = 0
                    chartArea:SetChild(next_target)
                else
                    next_target:SetPrevious(prev_target)
                    prev_target:SetNext(next_target)
                end

                next_target:SetContainer(me)
                next_target:SetDisplayName(items:Get(0):GetName()) 
                next_target:SetFocusTarget(items:Get(0))  
                next_target:SetParent(chartArea)
                prev_target = next_target      
                ChartItem previous
                integer target_index = 0
                integer index = 0
                repeat while index < items:GetSize()
                    Drawable item = items:Get(index)
                    Drawable lastItem = items:Get(items:GetSize() - 1)
                    text Description = item:GetDescription()
            
                    if Description not= ""
    
                        ChartItem next
                        if target_index = 0
                            next_target:SetChild(next)
                        else
                            next:SetPrevious(previous)
                            previous:SetNext(next)
                        end
                        next:SetContainer(me)
                        next:SetDisplayName(item:GetName()) 
                        next:SetFocusTarget(item)
                        next:SetParent(next_target)
                        
                        previous = next
                        target_index = target_index+1 
                    end
                    index = index + 1
                end
                k = k + 1
            end
        end
        
        ChartSelection selection = GetSelection()
        selection:Set(summary)
            
    end //end of Genarate infotree
   

    action LostSelection(ChartItem ci)
        if ci = undefined
            return now
        end
        Item target = ci:GetFocusTarget()

        if target not= undefined
            if target is ControlLabel
                ControlLabel temp = cast(ControlLabel, target)
                temp:LostSelection()
            elseif target is Drawable
                Drawable temp = cast(Drawable, target)
                temp:SetZ(0)
                temp:SetColor(previousColor)
            elseif target is Control
                Control temp = cast(Control, target)
                LayoutProperties properties = temp:GetDefaultLayoutProperties()
                if properties not= undefined
                    LabelBoxView view
                    view:SetBorderThickness(cast(integer, properties:GetBorderThickness())+4)
                    view:Initialize(properties:GetBackgroundColor(), previousColorGroup)
                    temp:SetView2D(view)
                end
            end
        end
    end

    action GainedSelection(ChartItem ci)
        if ci = undefined
            return now
        end
        Item target = ci:GetFocusTarget()

        if target not= undefined
            target:Focus()
            if target is ControlLabel
                ControlLabel temp = cast(ControlLabel, target)
                temp:GainedSelection()
            elseif target is Drawable
                Drawable temp = cast(Drawable, target)
                temp:SetZ(-1)
                previousColor = temp:GetColor()
                temp:SetColor(GetHighlightColor())
            elseif target is Control
                Control temp = cast(Control, target)
                LayoutProperties properties = temp:GetDefaultLayoutProperties()
                if properties not= undefined
                    previousColorGroup = properties:GetBorderColor()
                    LabelBoxView view
                    view:SetBorderThickness(cast(integer, properties:GetBorderThickness())+4)
                    view:Initialize(properties:GetBackgroundColor(), GetHighlightColor())
                    temp:SetView2D(view)
                end
            end            
        end
    end
    /* 
        This action converts this chart to a text value that contains information 
        for a scalable vector graphics file. This is useful for saving charts to disk.
        Each sub-class of chart must implement this action separately.

        Attribute: Returns the Scalable Vector Graphics (SVG) text.
    */
    action ConvertToScalableVectorGraphics returns text
        BoxPlotWriter writer
        return writer:WriteOutChart(me)
    end

end
