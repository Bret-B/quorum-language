package Libraries.Interface.Controls.Charts

use Libraries.Interface.Layouts.BoxPlotLayout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.ColorGroup
use Libraries.Game.Graphics.Drawable
use Libraries.Containers.Array
use Libraries.Compute.Math
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Views.LabelBoxView
use Libraries.Interface.Selections.ChartSelection
use Libraries.System.File
use Libraries.Interface.Controls.Charts.Graphics.Plot
use Libraries.Interface.Controls.Charts.Graphics.PlotGroup
use Libraries.Interface.Controls.Charts.Graphics.ChartDrawable
use Libraries.Language.Errors.CastError

/*
    The BoxPlot class is Chart object that inherits from Control and like other
    UI elements it is added to the Game class. 
    The Box Plot is used as an indication of how the values in the data are spread out. 
    Box Plots can also be used to compare datasets for more than one group. 
    The title label, axis labels, and scales can be modified. Any number of plots can be added.    

    Attribute: Author Christian Castillo, Hannah Williams

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Charts.BoxPlotCreator
    use Libraries.Interface.Controls.Charts.BoxPlot
    use Libraries.Game.Game
    
    class Main is Game
        action Main
            StartGame()
        end
    
        action CreateGame
            DataFrame frame
            frame:Load("Data/Data.csv")
            BoxPlotCreator creator
            creator:AddColumn(2)
        
            BoxPlot chart = cast(BoxPlot,frame:CreateChart(creator))
            chart:SetPercentageWidth(1)
            chart:SetPercentageHeight(1)
            chart:SetTitle("Sample Title")
            chart:SetXAxisTitle("X-axis Variable")
            chart:SetYAxisTitle("Y-axis Variable")
            Add(chart)
        end
    end
*/

class BoxPlot is Chart
    private integer numberOfPlots = 0
    Array<PlotGroup> plotGroups

    ColorGroup previousColorGroup = undefined
    Color previousColor = undefined
    number previousZ = 0

    ChartOptions options
    Math math

    private boolean showOutliers = true

    private number maxValue = 0
    private number minValue = 0
    private number maxOutlierValue = 0
    private number minOutlierValue = 0

    // Terminology for navigation instructions
    private text chartItemText = "plot"
    private text chartItemsText = "plots"
    private text groupText = "plot group"
    private text groupsText = "plot groups"

    on create
        BoxPlotLayout layout
        SetLayout(layout)

        LayoutProperties properties = GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:MAINTAIN_ASPECT_RATIO)
        properties:SetVerticalLayoutMode(properties:STANDARD)
        Font font = GetDefaultFont()
        properties:SetFont(font)
        properties:SetFontSize(GetDefaultFontSize())

        properties:SetBorderThickness(2)
        SetName("Box Plot")

        SetInputGroup("Chart")
        SetFocusable(true)
        SetAccessibilityCode(parent:Item:ITEM)

        SeparationCapable(true)
        SetDefaultShowBrackets(true)
        SetDefaultOrientationToVertical()
        GetVerticalPanel():BlockPaddingOffset(true)
        GetHorizontalPanel():BlockPaddingOffset(true)
    end

    /*
        LoadGraphics
        This action is used to load the graphical components of the Control. This is
        handled automatically by the Game engine as needed, and most users shouldn't
        need to use this action directly.
    */
    action LoadGraphics(LayoutProperties properties)
        DisposeDrawables()
        if properties = undefined
            return now
        end
        parent:Chart:LoadGraphics(properties)

        LoadChartAreaItems()
    end

    action LoadChartAreaItems()   
        ChartAreaPanel chartArea
        // Adding drawables for each boxplot to the chart
        integer i = 0
        repeat while i < plotGroups:GetSize()
            PlotGroup group = plotGroups:Get(i)
            text groupname = plotGroups:Get(i):GetName()
            text chartAreaName = ""
            integer k = 0
            repeat while k < group:GetSize()
                Plot plot = group:Get(k)

                // Plot Area Outline Panel
                Icon plotArea
                plot:SetPlotArea(plotArea)
    
                if IsSeparated()
                    integer j = 0
                    repeat while j < GetNumberOfChartAreas()
                        if IsSeparatedBySeries()
                            if plot:GetSeries():GetName() = GetSubChartAreas():Get(j):GetName()
                                chartArea = GetSubChartAreas():Get(j)
                                chartArea:AddChartAreaItem(plot)
                            end
                        elseif IsSeparatedByFactor()
                            if group:GetName() = GetSubChartAreas():Get(j):GetName()
                                chartArea = GetSubChartAreas():Get(j)
                                chartArea:AddChartAreaItem(plot)
                            end
                        end
                        j = j + 1
                    end
                    chartAreaName = chartArea:GetName()
                else
                    chartArea = GetChartArea()
                end

                ChartDrawable minimumLine
                ChartDrawable lowerWhisker 
                ChartDrawable interQuartileRange
                ChartDrawable rightBorder // Right side of IQR
                ChartDrawable leftBorder  // Left side of IQR
                ChartDrawable firstQuartileLine
                ChartDrawable medianLine
                ChartDrawable thirdQuartileLine
                ChartDrawable upperWhisker 
                ChartDrawable maximumLine
                ChartDrawable filler

                if not plot:IsUndefined()
                    number median = plot:GetInfoAt(0)
                    number firstQ = plot:GetInfoAt(1)
                    number thirdQ = plot:GetInfoAt(2)
                    number minimum = plot:GetInfoAt(3)
                    number maximum = plot:GetInfoAt(4)
                    number iqr = thirdQ - firstQ
    
                    // Minimum Line 
                    minimumLine:SetFocusable(true)
                    minimumLine:SetAccessibilityCode(0)
                    minimumLine:SetNextFocus(GetNextFocus())
                    minimumLine:SetPreviousFocus(GetPreviousFocus())
                    minimumLine:SetName("Minimum")
                    minimumLine:SetDescription(" " + minimum)
                    if CheckIfInteger(minimum)
                        minimumLine:SetDescription(" " + cast(integer, minimum))
                    end
                    chartArea:Add(minimumLine:GetSelectionArrow())

                    // Interquartile Range Region Area
                    interQuartileRange:SetFocusable(true)
                    interQuartileRange:SetAccessibilityCode(0)
                    interQuartileRange:SetNextFocus(GetNextFocus())
                    interQuartileRange:SetPreviousFocus(GetPreviousFocus())
                    interQuartileRange:SetName("Interquartile range")
                    interQuartileRange:SetDescription(" " + iqr)
                    if CheckIfInteger(iqr)
                        interQuartileRange:SetDescription(" " + cast(integer, iqr))
                    end
                    chartArea:Add(interQuartileRange:GetSelectionArrow())
                    Drawable topBrack
                    interQuartileRange:GetSelectionBracket():Add(topBrack)
                    chartArea:Add(topBrack)
                    Drawable sideBrack
                    interQuartileRange:GetSelectionBracket():Add(sideBrack) 
                    chartArea:Add(sideBrack)
                    Drawable bottomBrack
                    interQuartileRange:GetSelectionBracket():Add(bottomBrack) 
                    chartArea:Add(bottomBrack)  
    
                    // Lower Quartile Line Q1
                    firstQuartileLine:SetFocusable(true)
                    firstQuartileLine:SetAccessibilityCode(0)
                    firstQuartileLine:SetNextFocus(GetNextFocus())
                    firstQuartileLine:SetPreviousFocus(GetPreviousFocus())
                    firstQuartileLine:SetName("1st Quartile")
                    firstQuartileLine:SetDescription(" " + firstQ)
                    if CheckIfInteger(firstQ)
                        firstQuartileLine:SetDescription(" " + cast(integer, firstQ))
                    end
                    chartArea:Add(firstQuartileLine:GetSelectionArrow())
    
                    // Median Line (denser) 
                    medianLine:SetFocusable(true)
                    medianLine:SetAccessibilityCode(0)
                    medianLine:SetNextFocus(GetNextFocus())
                    medianLine:SetPreviousFocus(GetPreviousFocus())
                    medianLine:SetName("Median")
                    medianLine:SetDescription(" " + median)
                    if CheckIfInteger(median)
                        medianLine:SetDescription(" " + cast(integer, median))
                    end
                    chartArea:Add(medianLine:GetSelectionArrow())
    
                    // Upper Quartile Line Q3
                    thirdQuartileLine:SetFocusable(true)
                    thirdQuartileLine:SetAccessibilityCode(0)
                    thirdQuartileLine:SetNextFocus(GetNextFocus())
                    thirdQuartileLine:SetPreviousFocus(GetPreviousFocus())
                    thirdQuartileLine:SetName("3rd Quartile")
                    thirdQuartileLine:SetDescription(" " + thirdQ)
                    if CheckIfInteger(thirdQ)
                        thirdQuartileLine:SetDescription(" " + cast(integer, thirdQ))
                    end
                    chartArea:Add(thirdQuartileLine:GetSelectionArrow())
    
                    // Maximum Line
                    maximumLine:SetFocusable(true)
                    maximumLine:SetAccessibilityCode(0)
                    maximumLine:SetNextFocus(GetNextFocus())
                    maximumLine:SetPreviousFocus(GetPreviousFocus())
                    maximumLine:SetName("Maximum")
                    maximumLine:SetDescription(" " + maximum)
                    if CheckIfInteger(maximum)
                        maximumLine:SetDescription(" " + cast(integer, maximum))
                    end
                    chartArea:Add(maximumLine:GetSelectionArrow())
                end

                // Add to chart
                chartArea:Add(rightBorder)
                chartArea:Add(leftBorder)
                chartArea:Add(lowerWhisker)
                chartArea:Add(upperWhisker)
                chartArea:Add(minimumLine)
                chartArea:Add(firstQuartileLine)
                chartArea:Add(medianLine)
                chartArea:Add(thirdQuartileLine)
                chartArea:Add(maximumLine)
                chartArea:Add(interQuartileRange)  //Place on top of others
                chartArea:Add(plotArea)
                chartArea:Add(plot)
                //Add the outliers
    
                /* Indices for items of individual box plot
                    0: PLOT AREA (ENTIRE REGION)
                    1: MINIMUM LINE
                    2: LOWER WHISKER
                    3: INTERQUARTILE RANGE
                    4: RIGHT BORDER OF IQR
                    5: LEFT BORDER OF IQR
                    6: Q1 LINE
                    7: MEDIAN LINE
                    8: Q3 LINE
                    9: UPPER WHISKER    
                    10: MAXIMUM LINE 
                    11+: OUTLIER POINTS
                */
                plot:AddItem(filler)
                plot:AddItem(minimumLine)      
                plot:AddItem(lowerWhisker)
                plot:AddItem(interQuartileRange)
                plot:AddItem(rightBorder)
                plot:AddItem(leftBorder)
                plot:AddItem(firstQuartileLine)
                plot:AddItem(medianLine)
                plot:AddItem(thirdQuartileLine)
                plot:AddItem(upperWhisker)
                plot:AddItem(maximumLine)
                //Add the outliers

                if GetOutliersFlag()
                    // Outliers ++
                    Array <number> outliers = plot:GetOutliers()
                    integer j = 0
                    repeat while j < outliers:GetSize()
                        ChartPoint outlier
                        outlier:SetName("Outlier")
                        if CheckIfInteger(outliers:Get(j))
                            outlier:SetDescription(" " + cast(integer, outliers:Get(j)))
                        else
                            outlier:SetDescription(" " + outliers:Get(j))
                        end
                        outlier:SetFocusable(true)
                        outlier:SetAccessibilityCode(0)
                        outlier:SetNextFocus(GetNextFocus())
                        outlier:SetPreviousFocus(GetPreviousFocus())
                        chartArea:Add(outlier)
                        chartArea:Add(outlier:GetSelectionArrow())
                        plot:AddItem(outlier)
                        j = j + 1
                    end
                end

                // Set the plot description
                plot:SetFocusable(true)
                plot:SetAccessibilityCode(parent:Item:ITEM)
                plot:SetNextFocus(GetNextFocus())
                plot:SetPreviousFocus(GetPreviousFocus())
                plot:SetName(group:GetName() + " " + plot:GetSeries():GetName() + " plot, ")
                text plotDescription = ""
                plotDescription = plotDescription + medianLine:GetDescription() + ","
                plotDescription = plotDescription + " box from " + firstQuartileLine:GetDescription() + " to " + thirdQuartileLine:GetDescription() + ","
                plotDescription = plotDescription + " whiskers from " + minimumLine:GetDescription() + " to " + maximumLine:GetDescription()
                if GetOutliersFlag() and plot:GetOutliers():GetSize() > 0
                    if plot:GetOutliers():GetSize() = 1
                        plotDescription = plotDescription + ", " + plot:GetOutliers():GetSize() + " outlier, "
                    else
                        plotDescription = plotDescription + ", " + plot:GetOutliers():GetSize() + " outliers, "
                    end
                else
                    plotDescription = plotDescription + ", "
                end
                if IsSeparated() 
                    if IsSeparatedBySeries()
                        plotDescription = plotDescription + (i+1) + " of " + plotGroups:GetSize() + " plots inside " + chartAreaName + " chart area."
                    elseif IsSeparatedByFactor()
                        plotDescription = plotDescription + (k+1) + " of " + group:GetSize() + " plots inside " + chartAreaName + " chart area."
                    end
                else
                    if HasGroups()
                        plotDescription = plotDescription + (k+1) + " of " + group:GetSize() + " plots inside " + groupname + " group."
                    else
                        plotDescription = plotDescription + (i+1) + " of " + plotGroups:GetSize() + " plots."
                    end
                end
                plot:SetDescription(plotDescription)
                if not IsSeparated()
                    if HasGroups() 
                        if k = 0
                            AddChartItemInstructions(plot,plot:GetDescription())
                        end
                    else
                        if i = 0 and k = 0
                            AddChartItemInstructions(plot,plot:GetDescription())
                        end
                    end
                else
                    if IsSeparatedByFactor()
                        if k = 0
                            AddChartItemInstructions(plot,plot:GetDescription())
                        end
                    elseif IsSeparatedBySeries()
                        if i = 0
                            AddChartItemInstructions(plot,plot:GetDescription())
                        end
                    end
                end

                // Set selection arrow for plot
                chartArea:Add(plot:GetSelectionArrow())

                // Set Navigation Instruction for first plot item (minimum)
                AddPlotItemInstructions(minimumLine, minimumLine:GetDescription())
                k = k + 1
            end

            // Area covering entire group panel
            group:SetName(group:GetName() + " group, ")
            group:SetDescription((i+1) + " of " + plotGroups:GetSize() + " groups, has " + group:GetSize() + " box plots.")
            group:SetFocusable(true)
            group:SetAccessibilityCode(parent:Item:ITEM)
            group:SetNextFocus(GetNextFocus())
            group:SetPreviousFocus(GetPreviousFocus())
            chartArea:Add(group)
            if i = 0
                AddGroupPanelInstructions(group, group:GetDescription())
            end
            chartArea:Add(group:GetSelectionArrow())

            Drawable top
            group:GetSelectionBracket():Add(top)
            chartArea:Add(top)

            Drawable side
            group:GetSelectionBracket():Add(side) 
            chartArea:Add(side)

            Drawable bottom
            group:GetSelectionBracket():Add(bottom) 
            chartArea:Add(bottom)  

            GetGroupPanels():Add(group)
            i = i + 1
        end

        // Set chart area(s) description
        if IsSeparated()
            GetChartArea():SetDescription(" with " + GetNumberOfChartAreas() + " sub charts.")
            AddChartAreaInstructions(GetChartArea():GetDescription())
            integer subAreaCount = GetSubChartAreas():GetSize()
            i = 0
            repeat while i < subAreaCount
                integer itemCount = GetSubChartAreas():Get(i):GetChartAreaItems():GetSize()
                GetSubChartAreas():Get(i):SetDescription(" chart area with " + itemCount + " box plots, " + (i+1) + " of " + subAreaCount + " sub charts.")
                if i = 0
                    AddSubChartAreaInstructions(GetSubChartAreas():Get(i), GetSubChartAreas():Get(i):GetDescription())
                end
                i = i + 1
            end
        else
            if HasGroups()
                me:GetChartArea():SetDescription(" with " + plotGroups:GetSize() + " plot groups.")
            else
                me:GetChartArea():SetDescription(" with " + plotGroups:GetSize() + " box plots.")
            end
            AddChartAreaInstructions(GetChartArea():GetDescription())
        end
    end

    private action CheckIfInteger(number value) returns boolean
        return (cast(integer, value) = value)
    end

    /*
        DisposeDrawables
        
        Empties drawables from the chart area.
    */
    action DisposeDrawables
        parent:Chart:DisposeDrawables()
        GetChartArea():Empty()
    end

    /*
        LostSelection is a supplemental action to the parent Chart's LostSelection.
        Any items specific to only this type of chart or if an item needs different highlighting
        instructions than that of the parent class, it will have their LostSelection defined here.

        Unlike many kinds of user interface controls, there is no universal way of interacting with a chart and, as such, this 
        may be defined by any chart to be custom. As such, charts must be able to take messages suggesting an item in the chart
        has either lost or gained the focus. Broadly speaking, this is done automatically and while charts need to implement
        this action, they do not need to call this action directly.

        Attribute: Parameter ci the ChartItem representing the structure for this particular kind of chart. 
    */
    action LostSelection(ChartItem ci)
        if ci = undefined
            return now
        end
        Item target = ci

        if target not= undefined
            if target is Series
                Series temp = cast(Series, target)
                integer i = 0
                repeat while i < temp:GetSize()
                    Plot plot = cast(Plot, temp:GetItemAt(i))
                    integer j = 1
                    repeat while j < plot:GetPlotItems():GetSize()
                        ChartDrawable item = plot:GetPlotItems():Get(j)
                        if j = 3
                            item:SetColor(previousColor)
                        else
                            item:SetColor(GetBorderColor()) 
                        end
                        j = j + 1
                    end
                    i = i + 1
                end
                temp:GetEntryIcon():SetColor(previousColor)
                temp:GetEntryBox():SetColor(0,0,0,0)
            else
                parent:Chart:LostSelection(ci)
            end
        end
    end

    /* 
        OnSelectionChange is a supplemental action to the parent Chart's OnSelectionChange.
        Any items specific to only this type of chart or if an item needs different highlighting
        instructions than that of the parent class, it will have their GainedSelection defined here.

        Unlike many kinds of user interface controls, there is no universal way of interacting with a chart and, as such, this 
        may be defined by any chart to be custom. As such, charts must be able to take messages suggesting an item in the chart
        has either lost or gained the focus. Broadly speaking, this is done automatically and while charts need to implement
        this action, they do not need to call this action directly.

        Attribute: Parameter ci the ChartItem representing the structure for this particular kind of chart. 
    */
    action OnSelectionChange(ChartItem ci)
        if ci = undefined
            return now
        end
        Item target = ci

        if target not= undefined
            target:Focus()
            if target is Series
                Series temp = cast(Series, target)
                previousColor = temp:GetColor()
                integer i = 0
                repeat while i < temp:GetSize()
                    Plot plot = cast(Plot, temp:GetItemAt(i))
                    integer j = 1
                    repeat while j < plot:GetPlotItems():GetSize()
                        ChartDrawable item = plot:GetPlotItems():Get(j)
                        item:SetColor(GetSelectionColor())
                        j = j + 1
                    end
                    i = i + 1
                end
                temp:GetEntryIcon():SetColor(GetSelectionColor())
                temp:GetEntryBox():SetColor(GetSelectionColor())
            else
                parent:Chart:OnSelectionChange(ci)
            end            
        end
    end

    /*
        Flag used to hide all the outliers.
    */
    action HideOutliers()
        showOutliers = false
    end

    /*
        Enables outliers in the chart. Outliers are visible by default, but can
        be disabled via the HideOutliers action.
    */
    action ShowOutliers
        showOutliers = true
    end

    /*
        Returns the value of the showOutlier flag.
    */
    action GetOutliersFlag() returns boolean
        return showOutliers
    end

    /*
        Appends a plotgroup.
    */
    action AddPlotGroup(PlotGroup group)
        if plotGroups:GetSize() = 0
            maxValue = group:GetMaxValue()
            minValue = group:GetMinValue()
            maxOutlierValue = group:GetMaxOutlierValue()
            minOutlierValue = group:GetMinOutlierValue()
        else
            if maxValue < group:GetMaxValue()
                maxValue = group:GetMaxValue()
            end
            if minValue > group:GetMinValue()
                minValue = group:GetMinValue()
            end
            if maxOutlierValue < group:GetMaxOutlierValue()
                maxOutlierValue = group:GetMaxOutlierValue()
            end
            if minOutlierValue > group:GetMinOutlierValue()
                minOutlierValue = group:GetMinOutlierValue()
            end
        end
        plotGroups:Add(group)
        if group:GetSize() > 1
            HasGroups(true)
        end
        numberOfPlots = numberOfPlots + group:GetSize()
    end
   
    /*
        Returns an array of all the plot groups
    */
    action GetPlotGroups returns Array<PlotGroup>
        return plotGroups
    end

    /*
        Returns the number of boxes currently added to the Box Plot
    */
    action GetNumberOfPlots returns integer
        return numberOfPlots
    end

    /* 
        Max and min value will track the highest and lowest values and limit the axis controls to not pass that value.
    */ 
    action SetXAxisMaximum(number max)
        if GetVerticalFlag()
            parent:Chart:SetXAxisMaximum(max)
        else
            number currXmax = maxValue
            if showOutliers
                currXmax = maxOutlierValue
            end
            if max >= currXmax
                parent:Chart:SetXAxisMaximum(max)
            else
                output "X-axis maximum cannot be below the highest plot value: " + currXmax
            end
        end
    end

    action SetXAxisMinimum(number min)
        if GetVerticalFlag()
            parent:Chart:SetXAxisMinimum(min)
        else
            number currXmin = minValue
            if showOutliers
                currXmin = minOutlierValue
            end
            if min <= currXmin
                parent:Chart:SetXAxisMinimum(min)
            else
                output "X-axis minimum cannot be above the lowest plot value: " + currXmin
            end
        end
    end

    action SetYAxisMaximum(number max)
        if GetHorizontalFlag()
            parent:Chart:SetYAxisMaximum(max)
        else
            number currYmax = maxValue
            if showOutliers
                currYmax = maxOutlierValue
            end
            if max >= currYmax
                parent:Chart:SetYAxisMaximum(max)
            else
                output "Y-axis maximum cannot be below highest plot maximum value: " + currYmax
            end
        end
    end

    action SetYAxisMinimum(number min)
        if GetHorizontalFlag()
            parent:Chart:SetYAxisMinimum(min)
        else
            number currYmin = minValue
            if showOutliers
                currYmin = minOutlierValue
            end
            if min <= currYmin
                parent:Chart:SetYAxisMinimum(min)
            else
                output "Y-axis minimum cannot be above the lowest plot value: " + currYmin
            end
        end
    end
    /*
        This is called by GenerateInfoTree to generate the summary that is heard when
        you first focus on the chart. Also the highest level of the information tree
        of  the chart.
    */
    action GenerateSummary returns text
        text grouptext = ""
        if HasGroups()
            text groups = " groups and "
            if GetPlotGroups():GetSize() = 1
                groups = " group and "
            end
            grouptext = GetPlotGroups():GetSize() + groups
        end
        text plottext = numberOfPlots + " plots"
        if numberOfPlots = 1
            plottext = numberOfPlots + " plot"
        end
        if GetName() = "Box Plot"
            SetDescription(" with " + grouptext + plottext + ". Use the arrow keys to navigate chart information and Tab to access the chart content.")
        else
            SetDescription(", Box Plot with " + grouptext + plottext + ". Use the arrow keys to navigate chart information and Tab to access the chart content.")
        end
        return GetDescription()
    end


    /*
        GenerateInfoTree
        Generates the tree of ChartItems that define how the chart will be 
        navigated and what extra information might be sent to the screen reader.
    */
    action GenerateInfoTree
        if GetDefaultLayoutProperties():NeedsRendering()
            return now //we haven't loaded graphics yet, so bail.
        end
        
        //NOTE: These nodes implement a cheap form of ordinality might need changing
        ChartItem xAxis = GetHorizontalPanel()
        ChartItem yAxis = GetVerticalPanel()
        ChartItem chartArea = GetChartArea()
        ChartItem summary = me
        // The chart area is the child unless y-axis is showing.
        summary:SetDisplayName(GenerateSummary())
        summary:SetNextFocus(chartArea)
        summary:SetContainer(me)
        chartArea:SetPreviousFocus(summary)
        chartArea:SetContainer(me)

        chartArea:SetDisplayName("Box Plot")
    
        // If any of these are not showing the tree will skip them in the navigation
        if IsShowingXAxis()
            GetHorizontalPanel():GenerateInfoTree(me, summary, chartArea, yAxis, xAxis)
        end        
        if IsShowingYAxis()
            GetVerticalPanel():GenerateInfoTree(me, summary, chartArea, yAxis, xAxis)
        end
        if IsShowingLegend()
            GetLegend():GenerateInfoTree(me, summary, chartArea, yAxis, xAxis)
        end
        
        if IsSeparated()
            //Chart Areas
            integer subAreaCount = GetLegend():GetSeriesList():GetSize()
            if GetNumberOfChartAreas() > 0
                ChartItem firstArea = GetSubChartAreas():Get(0)
                firstArea:SetContainer(me)
                firstArea:SetPreviousFocus(summary)
//                GetChartArea():SetDescription(" with " + subAreaCount + " sub charts")
                chartArea:SetEnterItem(firstArea)
                firstArea:SetEscapeItem(chartArea)
                firstArea:SetDisplayName(GetSubChartAreas():Get(0):GetName())
                Array<ChartItem> chartAreaItems = GetSubChartAreas():Get(0):GetChartAreaItems()
//                GetSubChartAreas():Get(0):SetDescription(" chart area with " + chartAreaItems:GetSize() + " plots, 1 of " + subAreaCount + " sub charts.")

                //Plots within Chart Areas
                if chartAreaItems:GetSize() > 0 
                    ChartItem prev_target = undefined
                    integer k = 0
                    repeat while k < chartAreaItems:GetSize()
                        Plot plot = undefined
                        check
                            plot = cast(Plot, chartAreaItems:Get(k))
                        detect e is CastError
                            // If a casting error occurs, then this wasn't a plot, so bail.
                            return now
                        end

                        ChartItem next_target = plot
                        
                        Array<ChartDrawable> items = plot:GetPlotItems() //First plot
                        if k = 0
                            firstArea:SetEnterItem(next_target)
                        else
                            next_target:SetLeftItem(prev_target)
                            prev_target:SetRightItem(next_target)
                        end
                        
                        next_target:SetContainer(me)
                        next_target:SetPreviousFocus(summary)
                        next_target:SetDisplayName(plot:GetName())
                        next_target:SetEscapeItem(firstArea)
                        prev_target = next_target    
                        ChartItem previous = undefined
                        integer target_index = 0
                        integer index = 1
                        repeat while index < items:GetSize()
                            ChartDrawable item = items:Get(index)
                            ChartDrawable lastItem = items:Get(items:GetSize() - 1)
                            text description = item:GetDescription()
                    
                            if description not= ""
                                ChartItem next = item
                                if target_index = 0
                                    next_target:SetEnterItem(next)
                                else
                                    next:SetLeftItem(previous)
                                    previous:SetRightItem(next)
                                end
                                next:SetContainer(me)
                                next:SetPreviousFocus(summary)
                                next:SetDisplayName(item:GetName())
                                next:SetEscapeItem(next_target)
                                
                                previous = next
                                target_index = target_index+1 
                            end
                            index = index + 1
                        end
                        k = k + 1
                    end
                end

                i = 1
                ChartItem previousArea = firstArea
                repeat while i < GetNumberOfChartAreas()
                    ChartAreaPanel chartSubArea = GetSubChartAreas():Get(i)
                    if chartSubArea:IsShowing()
                        ChartItem nextArea = chartSubArea
                        nextArea:SetContainer(me)
                        nextArea:SetPreviousFocus(summary)
                        nextArea:SetEscapeItem(chartArea)
                        previousArea:SetRightItem(nextArea)
                        nextArea:SetLeftItem(previousArea)
                        previousArea = nextArea
                        nextArea:SetDisplayName(chartSubArea:GetName())
                        Array<ChartItem> chartSubAreaItems = chartSubArea:GetChartAreaItems()
    //                    chartSubArea:SetDescription(" chart area with " + chartSubAreaItems:GetSize() + " plots, " + (i+1) + " of " + subAreaCount + " sub charts.")
    
                        //Plots within Plot Groups
                        if chartSubAreaItems:GetSize() > 0 
                            ChartItem prev_target = undefined
                            integer k = 0
                            repeat while k < chartSubAreaItems:GetSize()
                                Plot plot = undefined
                                check
                                    plot = cast(Plot, chartSubAreaItems:Get(k))
                                detect e is CastError
                                    // If a casting error occurs, then this wasn't a plot, so bail.
                                    return now
                                end
                                ChartItem next_target = plot
                                Array<ChartDrawable> items = plot:GetPlotItems() //First plot
                                if k = 0
                                    nextArea:SetEnterItem(next_target)
                                else
                                    next_target:SetLeftItem(prev_target)
                                    prev_target:SetRightItem(next_target)
                                end
        
                                next_target:SetContainer(me) 
                                next_target:SetPreviousFocus(summary)
                                next_target:SetDisplayName(plot:GetName())
                                next_target:SetEscapeItem(nextArea)
                                prev_target = next_target      
                                ChartItem previous = undefined
                                integer target_index = 0
                                integer index = 1
                                repeat while index < items:GetSize()
                                    ChartDrawable item = items:Get(index)
                                    ChartDrawable lastItem = items:Get(items:GetSize() - 1)
                                    text description = item:GetDescription()
                            
                                    if description not= ""
                                        ChartItem next = item
                                        if target_index = 0
                                            next_target:SetEnterItem(next)
                                        else
                                            next:SetLeftItem(previous)
                                            previous:SetRightItem(next)
                                        end
                                        next:SetContainer(me)
                                        next:SetPreviousFocus(summary)
                                        next:SetDisplayName(item:GetName())
                                        next:SetEscapeItem(next_target)
                                        
                                        previous = next
                                        target_index = target_index+1 
                                    end
                                    index = index + 1
                                end
                                k = k + 1
                            end
                        end
                    end
                    i = i + 1
                end
            end
        else
            if HasGroups()
                //Plot Groups
                if plotGroups:GetSize() > 0
                    ChartItem firstGroup = plotGroups:Get(0)
                    firstGroup:SetContainer(me)
                    firstGroup:SetPreviousFocus(summary)

                    chartArea:SetEnterItem(firstGroup)
                    firstGroup:SetEscapeItem(chartArea)
                    firstGroup:SetDisplayName(plotGroups:Get(0):GetName())
        
                    //Plots within Plot Groups
                    if plotGroups:Get(0):GetSize() > 0 
                        ChartItem prev_target = undefined
                        integer k = 0
                        repeat while k < plotGroups:Get(0):GetSize()
                            ChartItem next_target = plotGroups:Get(0):Get(k)
                            Array<ChartDrawable> items = plotGroups:Get(0):Get(k):GetPlotItems() //First plot
                            if k = 0
                                firstGroup:SetEnterItem(next_target)
                            else
                                next_target:SetLeftItem(prev_target)
                                prev_target:SetRightItem(next_target)
                            end
                            
                            next_target:SetContainer(me)
                            next_target:SetPreviousFocus(summary)
                            next_target:SetDisplayName(plotGroups:Get(0):Get(k):GetName()) 
                            next_target:SetEscapeItem(firstGroup)
                            prev_target = next_target    
                            ChartItem previous = undefined
                            integer target_index = 0
                            integer index = 1
                            repeat while index < items:GetSize()
                                ChartDrawable item = items:Get(index)
                                ChartDrawable lastItem = items:Get(items:GetSize() - 1)
                                text description = item:GetDescription()
                      
                                if description not= ""
                                    ChartItem next = item
                                    if target_index = 0
                                        next_target:SetEnterItem(next)
                                    else
                                        next:SetLeftItem(previous)
                                        previous:SetRightItem(next)
                                    end
                                    next:SetContainer(me)
                                    next:SetPreviousFocus(summary)
                                    next:SetDisplayName(item:GetName())
                                    next:SetEscapeItem(next_target)
                                    
                                    previous = next
                                    target_index = target_index+1 
                                end
                                index = index + 1
                            end
                            k = k + 1
                        end
                    end

                    i = 1
                    ChartItem previousGroup = firstGroup
                    repeat while i < plotGroups:GetSize()
                        ChartItem group = plotGroups:Get(i)
                        group:SetContainer(me)
                        group:SetPreviousFocus(summary)
                        group:SetEscapeItem(chartArea)
                        group:SetDisplayName(plotGroups:Get(i):GetName())
                        previousGroup:SetRightItem(group)
                        group:SetLeftItem(previousGroup)
                        previousGroup = group
        
                        //Plots within Plot Groups
                        if plotGroups:Get(i):GetSize() > 0 
                            ChartItem prev_target = undefined
                            integer k = 0
                            repeat while k < plotGroups:Get(i):GetSize()
                                ChartItem next_target = plotGroups:Get(i):Get(k)
                                Array<ChartDrawable> items = plotGroups:Get(i):Get(k):GetPlotItems() //First plot
                                if k = 0
                                    group:SetEnterItem(next_target)
                                else
                                    next_target:SetLeftItem(prev_target)
                                    prev_target:SetRightItem(next_target)
                                end
        
                                next_target:SetContainer(me)
                                next_target:SetPreviousFocus(summary)
                                next_target:SetDisplayName(plotGroups:Get(i):Get(k):GetName()) 
                                next_target:SetEscapeItem(group)
                                prev_target = next_target      
                                ChartItem previous = undefined
                                integer target_index = 0
                                integer index = 1
                                repeat while index < items:GetSize()
                                    ChartDrawable item = items:Get(index)
                                    ChartDrawable lastItem = items:Get(items:GetSize() - 1)
                                    text description = item:GetDescription()
                          
                                    if description not= ""
                                        ChartItem next = item
                                        if target_index = 0
                                            next_target:SetEnterItem(next)
                                        else
                                            next:SetLeftItem(previous)
                                            previous:SetRightItem(next)
                                        end
                                        next:SetContainer(me)
                                        next:SetPreviousFocus(summary)
                                        next:SetDisplayName(item:GetName()) 
                                        next:SetEscapeItem(next_target)
                                        
                                        previous = next
                                        target_index = target_index+1 
                                    end
                                    index = index + 1
                                end
                                k = k + 1
                            end
                        end
                        i = i + 1
                    end
                end
            else
                ChartItem prev_target = undefined
                integer k = 0
                repeat while k < plotGroups:GetSize()
                    ChartItem next_target = plotGroups:Get(k):Get(0)
                    Array<ChartDrawable> items = plotGroups:Get(k):Get(0):GetPlotItems() //First plot
                    if k = 0
                        chartArea:SetEnterItem(next_target)
                    else
                        next_target:SetLeftItem(prev_target)
                        prev_target:SetRightItem(next_target)
                    end
    
                    next_target:SetContainer(me)
                    next_target:SetPreviousFocus(summary)
                    next_target:SetDisplayName(plotGroups:Get(k):Get(0):GetName()) 
                    next_target:SetEscapeItem(chartArea)
                    prev_target = next_target      
                    ChartItem previous = undefined
                    integer target_index = 0
                    integer index = 1
                    repeat while index < items:GetSize()
                        ChartDrawable item = items:Get(index)
                        ChartDrawable lastItem = items:Get(items:GetSize() - 1)
                        text description = item:GetDescription()
                
                        if description not= ""
                            ChartItem next = item
                            if target_index = 0
                                next_target:SetEnterItem(next)
                            else
                                next:SetLeftItem(previous)
                                previous:SetRightItem(next)
                            end
                            next:SetContainer(me)
                            next:SetPreviousFocus(summary)
                            next:SetDisplayName(item:GetName())
                            next:SetEscapeItem(next_target)
                            
                            previous = next
                            target_index = target_index+1 
                        end
                        index = index + 1
                    end
                    k = k + 1
                end
            end
        end
        
        ChartSelection selection = GetSelection()
        selection:Set(summary)
    end
   
    /* 
        This action converts this chart to a text value that contains information 
        for a scalable vector graphics file. This is useful for saving charts to disk.
        Each sub-class of chart must implement this action separately.

        Attribute: Returns the Scalable Vector Graphics (SVG) text.
    */
    action ConvertToScalableVectorGraphics returns text
        BoxPlotWriter writer
        return writer:WriteOutChart(me)
    end

    /*
        This action saves this chart to disk at the position of the current file. To conduct the 
        conversion, the file extension is used. Only Scalable Vector Graphics (SVG) is currently 
        supported.

        Attribute: Parameter file the location of where to save the file.
    */
    action Save(File file)
        if file:GetFileExtension() = "svg"
            BoxPlotWriter chartWriter
            chartWriter:WriteOutChart(me,file)
        end
    end

    action GetGroupText returns text
        return groupText
    end

    action GetGroupsText returns text
        return groupsText
    end

    action GetChartItemText returns text
        return chartItemText
    end

    action GetChartItemsText returns text
        return chartItemsText
    end

    /*
    This action generates the instruction text for a chart item description.

    Attribute: Parameter chartItem The chart item that will have the instructions
    Attribute: Parameter description The base portion of the description that will appear before the instructions
    */
    action AddChartItemInstructions(Item chartItem, text description)

        description = description + " To navigate the list of " + GetChartItemsText() + ", use the arrow keys to move between " + GetChartItemsText() + " in list order," 
        description = description + " use Page Up Down or Control + arrow keys to move between " + GetChartItemsText() + " by median value order,"
        if not IsSeparatedBySeries() and IsSeparatedByFactor()
            description = description + " use Escape to return to the " + GetGroupText() + " list."
        elseif IsSeparatedBySeries() and not IsSeparatedByFactor()
            description = description + " use Escape to return to the " + GetSeriesText() + " list."
        elseif not IsSeparated() and HasGroups()
            description = description + " use Escape to return to the chart area."
        else
            description = description + " use Escape to return to the " + GetGroupText() + " list."
        end
        chartItem:SetDescription(description)
    end

    /*
    This action generates the instruction text for a plot item description.

    Attribute: Parameter chartItem The chart item that will have the instructions
    Attribute: Parameter description The base portion of the description that will appear before the instructions
    */
    action AddPlotItemInstructions(Item chartItem, text description)
        description = description + " To navigate the list of plot summary items, use the arrow keys to move between plot summary items in list order," 
        description = description + " use Escape to return to the " + GetChartItemsText() + " list."
        chartItem:SetDescription(description)
    end
end
