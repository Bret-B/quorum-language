package Libraries.Interface.Controls.Charts

use Libraries.System.File
use Libraries.Interface.Controls.Charts.ViolinPlot
use Libraries.Data.Formats.ScalableVectorGraphics.ScalableVectorGraphics
use Libraries.Data.Formats.ScalableVectorGraphics.Line
use Libraries.Containers.Array
use Libraries.Data.Formats.ScalableVectorGraphics.Rectangle
use Libraries.Data.Formats.ScalableVectorGraphics.Label
use Libraries.Data.Formats.ScalableVectorGraphics.Circle
use Libraries.System.FileWriter
use Libraries.Compute.Math
use Libraries.Interface.Controls.Charts.Chart
use Libraries.Interface.Controls.Charts.ChartPoint
use Libraries.Compute.Vector
use Libraries.Interface.Layouts.ViolinPlotLayout

/*
    The ViolinPlotWriter class is part of the set of writers that allow for Charts
    to save their representation in the form ScalableVectorGraphics. This class will write out
    all of the necessary text needed to write out an SVG file that will contain the chart.

    Attribute: Author Gabriel Contreras

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Interface.Controls.Charts.ViolinPlot
    use Libraries.Interface.Controls.Charts.ViolinPlotWriter

    DataFrame frame
    frame:Load("Data.csv")
    frame:SetSelectedColumnRange(0,1)
    ViolinPlot chart = frame:ViolinPlot()
    chart:SetTitle("My Awesome Title")
    chart:SetXAxisTitle("Time")
    chart:Display()

    ViolinPlotWriter writer
    output writer:WriteOutChart(chart)
*/
class ViolinPlotWriter
    private number axisPointX = 15
    private number axisPointY = 85
    private number axisLength = 75

/*
    This action writes out the ViolinPlot by grabbing necessary information from the chart and it will then return
    all of the necessary text needed to write out an SVG file that will contain the chart.

    Attribute: Parameter chart The chart that will be used to create an SVG

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Interface.Controls.Charts.ViolinPlot
    use Libraries.Interface.Controls.Charts.ViolinPlotWriter
    
    DataFrame frame
    frame:Load("Data.csv")
    frame:SetSelectedColumnRange(0,1)
    ViolinPlot chart = frame:ViolinPlot()
    chart:Display()

    ViolinPlotWriter writer
    output writer:WriteOutChart(chart)
*/
    action WriteOutChart(ViolinPlot chart) returns text

        ScalableVectorGraphics chartCanvas
        chartCanvas:SetSize(700,600)

        Label chartTitle
        Label xAxisLabel
        Label yAxisLabel

        //start setting stuff
        AddBackground(chartCanvas)
        AddAxes(chartCanvas)
        chartTitle:SetText(chart:GetTitle())
        chartTitle:SetTextAnchor("middle")
        chartTitle:SetPosition(50, 5, "%")
        chartTitle:SetFontSize("18pt")
        chartCanvas:Add(chartTitle)

        xAxisLabel:SetText(chart:GetXAxisTitle())
        xAxisLabel:SetTextAnchor("middle")
        xAxisLabel:SetPosition(50, 95, "%")
        xAxisLabel:SetFontSize("18pt")
        chartCanvas:Add(xAxisLabel)

        yAxisLabel:SetText(chart:GetYAxisTitle())
        yAxisLabel:SetTextAnchor("middle")
        yAxisLabel:SetPosition(5, 50, "%")
        yAxisLabel:SetTransform("rotate(270, 40,300)")
        yAxisLabel:SetFontSize("18pt")
        chartCanvas:Add(yAxisLabel)


        AddViolins(chartCanvas, chart)
        AddXLabels(chartCanvas, chart)
        AddYLabels(chartCanvas, chart)

        return chartCanvas:ToText()
    end

    /*
        This action adds a white background to the canvas
    */
    private action AddBackground(ScalableVectorGraphics canvas)
        Rectangle background
        background:SetFill("white")
        background:SetSize(100,100, "%")
        canvas:Add(background)
    end

    /*
        This action adds the two lines for the x and y axis
    */
    private action AddAxes(ScalableVectorGraphics canvas)
        Line xAxis
        Line yAxis
        xAxis:SetPoints(axisPointX, axisPointY, axisPointX + axisLength, axisPointY, "%")
        xAxis:SetStroke("black")
        xAxis:SetTabIndex(-1)
        xAxis:SetStrokeWidth(0.4,"%")
        yAxis:SetPoints(axisPointX, axisPointY, axisPointX, axisPointY - axisLength, "%")
        yAxis:SetStroke("black")
        yAxis:SetTabIndex(-1)
        yAxis:SetStrokeWidth(0.4,"%")

        canvas:Add(xAxis)
        canvas:Add(yAxis)
    end

    /*
        This action adds the content of the BarChart
    */
    private action AddViolins(ScalableVectorGraphics canvas, ViolinPlot chart)
        //this is needed for calculations so this will probably change if stuff is moved around 
        ViolinPlotLayout layout

        Array<PlotGroup> groups = chart:GetPlotGroups()
        if groups:GetSize() > 0
            number total = groups:GetSize()
            number panelwidth = axisLength/total
            number height = axisLength
            number dataScale = chart:GetYMaximum()/height

            integer i = 0
            repeat while i < groups:GetSize()
                PlotGroup group = groups:Get(i)

                //Give each group an alloted section  
                integer startpanelw = cast(integer, panelwidth*i)

                if group:GetSize() > 0
                    integer width = cast(integer, panelwidth/group:GetSize())
                    integer center = cast(integer, width/2) 
                    integer size = cast(integer, width/4)
                    integer lineH = cast(integer, width * (0.005)*group:GetSize()*groups:GetSize())

                    integer k = 0
                    repeat while k < group:GetSize()
                        Plot plot = group:Get(k)
                        number median = plot:GetInfoAt(0)
                        number lowerQuartile = plot:GetInfoAt(1)
                        number upperQuartile = plot:GetInfoAt(2)
                        number min = plot:GetInfoAt(3)
                        number max = plot:GetInfoAt(4)
            
                        //Give each chart an alloted section  
                        integer startW = width*k + startpanelw
    
                        //even though lines could be used here rectangles are used for translation purposes
                        Rectangle first
                        first:SetSize(lineH, (upperQuartile-lowerQuartile)/dataScale, "%")
                        first:SetPosition(axisPointX + startW+(width-lineH)/2,axisPointY - upperQuartile/dataScale, "%")
                        Rectangle second
                        second:SetSize(lineH, (max-min)/dataScale, "%")
                        second:SetPosition(axisPointX + startW+(width-lineH)/2,axisPointY - max/dataScale, "%")
                        Rectangle third
                        third:SetSize(lineH*4, lineH, "%")
                        third:SetPosition(axisPointX + startW+(width-lineH*4)/2,axisPointY - max/dataScale, "%")
                        Rectangle fourth
                        fourth:SetSize(lineH, lineH, "%")
                        fourth:SetPosition(axisPointX + startW+(width-lineH)/2,axisPointY - upperQuartile/dataScale, "%")
                        Circle dot
                        dot:SetRadius(lineH/2, "%")
                        dot:SetFill("white")
                        dot:SetPosition(axisPointX + startW+(width-lineH)/2 + lineH/2,axisPointY - median/dataScale, "%")
                        Rectangle fifth
                        fifth:SetSize(lineH, lineH, "%")
                        fifth:SetPosition(axisPointX + startW+(width-lineH)/2,axisPointY - lowerQuartile/dataScale - lineH, "%")
                        Rectangle sixth
                        sixth:SetSize(lineH*4, lineH, "%")
                        sixth:SetPosition(axisPointX + startW+(width-lineH*4)/2,axisPointY - min/dataScale - lineH, "%")
                        
                        //bandWidth will be auto set to a value according the height of the chart
                        number bandWidth = chart:GetYMaximum()*0.015
                        Array<number> estimates = layout:EstimateDensity(bandWidth,dataScale,plot:GetData(),chart:GetWindowingAction())
                        integer nextPixel = cast(integer, min/dataScale)+1
                        number maxEstimate = estimates:Get(estimates:GetSize()-1)
                        number estimateScale = maxEstimate/(width/2)
                        integer index = 0
                        repeat while index < estimates:GetSize()-1
                            number estimate = estimates:Get(index)
                            number pixW = (estimate/estimateScale)*0.75
                            number pixH = height - nextPixel
                            Line tempLine
                            tempLine:SetPoints(axisPointX + startW + width/2-pixW,axisPointY - axisLength + pixH,axisPointX + startW + width/2+pixW, axisPointY - axisLength + pixH, "%")
                            tempLine:SetStroke("blue")
                            tempLine:SetStrokeWidth(6)
                            canvas:Add(tempLine)
                            nextPixel = nextPixel + 1
                            index = index + 1
                        end
                        canvas:Add(first)
                        canvas:Add(second)
                        canvas:Add(third)
                        canvas:Add(fourth)
                        canvas:Add(dot)
                        canvas:Add(fifth)
                        canvas:Add(sixth)
                        k = k + 1
                    end 
                end  
                i = i + 1
            end
        end
    end

    /*
        This action adds the labels on the x axis
    */
    private action AddXLabels(ScalableVectorGraphics canvas, ViolinPlot chart)
        integer i = 0
        Array<text> xLabelText = chart:GetXAxisLabels()
        integer total = chart:GetPlotGroups():GetSize()
        number space = axisLength/(total)
        repeat while i < total
            Label tempLabel

            tempLabel:SetText(chart:GetXAxisLabels():Get(i))
            tempLabel:SetTextAnchor("middle")
            tempLabel:SetPosition(axisPointX + (i) * space + space/2, axisPointY + 4, "%")
            canvas:Add(tempLabel)
            i = i + 1
        end
    end

    /*
        This action the labels on the y axis
    */
    private action AddYLabels(ScalableVectorGraphics canvas, ViolinPlot chart)
        number scaleNum = 0//chart:GetYAxisMinimum()
        
        number scaleWidth = (chart:GetYMaximum()) / (chart:GetYTickCount()- 1)
        number scaleDivWidth = axisLength / (chart:GetYTickCount() - 1)
        // scale
        Math math
        i = 0
        //output chart:GetYMax()
        repeat while i < chart:GetYTickCount()
            Label scaleLabel
            Line scaleTick
            
            scaleLabel:SetTextAnchor("end")
            scaleLabel:SetText("" + math:Round(scaleNum, 1))
            scaleLabel:SetFontSize("8pt")
            scaleNum = scaleNum + scaleWidth

            scaleTick:SetTabIndex(-1)
            scaleTick:SetFirstPoint(axisPointX, axisPointY - scaleDivWidth*i, "%")
            scaleTick:SetSecondPoint(axisPointX - 2, axisPointY - scaleDivWidth*i, "%")
            scaleTick:SetStroke("black")
            scaleTick:SetStrokeWidth(0.4,"%")
            
            scaleLabel:SetPosition(axisPointX - 3, axisPointY - scaleDivWidth*i + 0.5, "%")

            canvas:Add(scaleLabel)
            canvas:Add(scaleTick)
        i = i+1
        end
    end

end