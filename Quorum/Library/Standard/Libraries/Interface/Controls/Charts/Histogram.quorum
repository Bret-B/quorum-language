package Libraries.Interface.Controls.Charts

use Libraries.Interface.Layouts.HistogramLayout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.Gradient
use Libraries.Containers.Array
use Libraries.Compute.Math
use Libraries.Containers.HashTable
use Libraries.Compute.Statistics.Calculations.InterQuartileRange
use Libraries.Containers.Support.Comparison
use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Interface.Layouts.ManualLayout
use Libraries.System.File

/*
    The Histogram class is Chart object that inherits from SharedBarChartParent and like other
    UI elements it is added to the Game class. The Histogram is used to represent
    categorical data with rectangular bars that have a height proportional to the 
    data they represent. By default, the chart has no added bars and has a scale 
    that goes from 0 to 1. The title label, axis labels, and scale can be modified, 
    and any number of bars can be added. 

    Attribute: Author Gabriel Contreras, Hannah Williams, Tim Kluthe

    Attribute: Example

    use Libraries.Interface.Controls.Charts
    use Libraries.Interface.Controls.Charts.Histogram
    use Libraries.Game.Game

    class Main is Game
        action Main
            StartGame()
        end

        action CreateGame
            Histogram chart
            Add(chart)
        end
    end
*/
class Histogram is SharedBarChartParent
    Math math
    Array <DataFrameColumn> columns 
    Array<number> binFloor
    Comparison comparison = undefined
    number binWidth = -1
    boolean binWidthOverride = false
    integer binCount = 0
    boolean binCountOverride = false

    boolean decimalScale = false
    number dataMin = 0
    number dataMax = 0

    on create
        HistogramLayout layout
        SetLayout(layout)


        LayoutProperties properties = GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:MAINTAIN_ASPECT_RATIO)
        properties:SetVerticalLayoutMode(properties:STANDARD)
        Font font = GetDefaultFont()
        properties:SetFont(font)
        properties:SetFontSize(GetDefaultFontSize())

        Color color
        Gradient gradient
        Color gray = color:LightGray()
        Color lightGray = color:CustomColor(0.9, 0.9, 0.9, 1)
        gradient:Set(gray, gray, lightGray, lightGray)

        properties:SetBackgroundColor(gradient)
        properties:SetBorderColor(color:Black())
        properties:SetBorderThickness(2)
        SetName("Histogram")

        SetInputGroup("Chart")
        SetFocusable(true)
        SetAccessibilityCode(parent:Item:ITEM)

        // Default settings specific to histogram
        RegressionCapable(false)
        ShowAllTicks()
        SetDefaultOrientationToVertical()
        SetDefaultSeparation(true)
        GetVerticalPanel():BlockPaddingOffset(true)
        GetHorizontalPanel():BlockPaddingOffset(true)
    end

    /*
        This is called by GenerateInfoTree to generate the summary that is heard when
        you first focus on the chart. Also the highest level of the information tree
        of  the chart.
    */
    private action GenerateSummary returns text
        text grouptext = ""
        if HasGroups()
            text groups = " groups and "
            if GetBarGroups():GetSize() = 1
                groups = " group and "
            end
            grouptext = GetBarGroups():GetSize() + groups
        end
        text bartext = GetNumberOfBars() + " bars."
        if GetNumberOfBars() = 1
            bartext = GetNumberOfBars() + " bar."
        end
        if GetName() = "Histogram"
            SetDescription(" with " + grouptext + bartext + ". Use the arrow keys to navigate the chart.")
        else
            SetDescription("Histogram with " + grouptext + bartext + ". Use the arrow keys to navigate the chart.")
        end
        return GetDescription()
    end

    action MakeBins()
        integer numberOfColumns = columns:GetSize()
        if numberOfColumns < 1
            alert("Cannot create a Histogram without any columns.")
        end

        if numberOfColumns = 1
            HasGroups(false) //remove the bargroup level of accessible navigation since every bar group should have 1 bar
        end

        GetBarGroups():Empty()
        HashTable <text, Series> seriesFactor
        HashTable <text, BarGroup> groupFactor
        number maxFrequency = 0.0
        number min = dataMin
        number max = dataMax

        if GetHorizontalFlag() // Default is vertical
            min = GetVerticalPanel():GetMinimum()
            max = GetVerticalPanel():GetMaximum()
        else
            min = GetHorizontalPanel():GetMinimum()
            max = GetHorizontalPanel():GetMaximum()
        end
        number range = max - min

        // Take range and divide it amongst bins and round up to get the default bin width
        if binCountOverride //Bin count override supercedes bin width override
            if max < 2.5
                decimalScale = true
                binWidth = math:Round(range / cast(number, binCount),3)
                if GetHorizontalFlag()
                    GetVerticalPanel():SetTickDigits(3)
                else
                    GetHorizontalPanel():SetTickDigits(3)
                end
                if math:Round(max mod binWidth, 3) = min
                    binWidth = math:Round(range / cast(number, binCount-1),3)
                end
                max = math:Round(binWidth * binCount + min, 3)
            else
                min = math:Floor(min)
                max = math:Ceiling(max)
                range = max - min
                binWidth = math:Ceiling(range / cast(number, binCount))
                if math:Round(max mod binWidth,3) = min
                    binWidth = math:Ceiling(range / cast(number, binCount-1))
                end
                max = math:Round(binWidth * binCount + min, 3)
            end
        else
            if binWidthOverride
                if max < 2.5
                    decimalScale = true
                    binCount = cast(integer, math:Ceiling(range/binWidth))+1
                    if GetHorizontalFlag()
                        GetVerticalPanel():SetTickDigits(3)
                    else
                        GetHorizontalPanel():SetTickDigits(3)
                    end
                    max = math:Round(binWidth * binCount + min, 3)
                else
                    if decimalScale 
                        if GetHorizontalFlag()
                            GetVerticalPanel():SetTickDigits(3)
                        else
                            GetHorizontalPanel():SetTickDigits(3)
                        end
                    else
                        min = math:Floor(min)
                        max = math:Ceiling(max)
                        range = max - min
                    end
                    binCount = cast(integer, math:Ceiling(range/binWidth))+1
                end
            else
                //Default binwidth and bincount
                binCount = cast(integer, math:Ceiling(math:SquareRoot(columns:Get(0):GetSize())))
                if max < 2.5
                    decimalScale = true
                    binWidth = math:Round(range / cast(number, binCount),1)
                    if GetHorizontalFlag()
                        GetVerticalPanel():SetTickDigits(3)
                    else
                        GetHorizontalPanel():SetTickDigits(3)
                    end
                    binCount = cast(integer, math:Ceiling(range / binWidth))
                else
                    min = math:Floor(min)
                    max = math:Ceiling(max)
                    range = max - min
                    binWidth = math:Ceiling(range  / cast(number, binCount))
                    binCount = cast(integer, math:Ceiling(range / binWidth))+1
                    if max = dataMax and (max mod binWidth = 0)
                        max = max + binWidth
                    end
                end
            end
        end

        if binCount > 100
            alert("This results in over 100 bins, please change bin width.")
        end

        Array<integer> count
        binFloor:Empty()

        n = 0
        number floor = min // Starts at minimum value
        repeat while n < binCount
            count:Add(0)
            binFloor:Add(floor)
            text valueDisplay = "[" + cast(integer, floor) + "-" + cast(integer, floor + binWidth) + "]"
            if(decimalScale)
                valueDisplay = "[" + (math:Round(floor, 2)) + "-" + (math:Round(floor+binWidth, 2)) + "]"
            end
            if(not groupFactor:HasKey(valueDisplay))
                BarGroup group
                group:SetName(valueDisplay)
                groupFactor:Add(valueDisplay, group)
            end
            floor = math:Round(floor + binWidth, 3)
            n = n + 1
        end  

        n = 0
        repeat while n < numberOfColumns 
            DataFrameColumn column = columns:Get(n)   

            // Reset counts to zero for each series
            i = 0
            repeat while i < binCount
                count:Set(i, 0)
                i = i + 1
            end     

            i = 0
            repeat while i < column:GetSize()
                if not column:IsUndefined(i)
                    number value = column:GetAsNumber(i)
                    if value >= min
                        integer bin = cast(integer, math:Floor((value - min) / binWidth))
                        if bin < binFloor:GetSize()
                            number binRoof = binFloor:Get(bin) + binWidth
                            if value < binRoof and value >= binFloor:Get(bin)
                                count:Set(bin, count:Get(bin) + 1)
                            elseif value >= binRoof
                                count:Set(bin+1, count:Get(bin+1) + 1)
                            end
                        end
                    end
                end
                i = i + 1
            end
    
            // Find max frequency of all counts across all series
            i = 0
            repeat while i < count:GetSize()
                integer amount = count:Get(i)
                if amount > maxFrequency
                    maxFrequency = amount
                end
                i = i + 1
            end

            // If a new series (ie new column)
            if(not seriesFactor:HasKey(column:GetHeader()))
                Series series
                series:SetName(column:GetHeader())
                seriesFactor:Add(column:GetHeader(), series)
            end
    
            i = 0
            floor = 0.0
            repeat while i < count:GetSize() and floor < max
                integer amount = count:Get(i)
                floor = binFloor:Get(i)
                text valueDisplay = "[" + cast(integer, floor) + "-" + cast(integer, floor+binWidth) + "]"
                if(decimalScale)
                    valueDisplay = "[" + (math:Round(floor, 2)) + "-" + (math:Round(floor+binWidth, 2)) + "]"
                end
                Bar bar
                bar:SetValue(amount)
                bar:SetName(column:GetHeader() + " " + valueDisplay)
                // If bar is part of a group
                if(groupFactor:HasKey(valueDisplay))
                    groupFactor:GetValue(valueDisplay):AddBar(bar)
                end
                // If bar is part of a series
                if(seriesFactor:HasKey(column:GetHeader()))
                    seriesFactor:GetValue(column:GetHeader()):AddItem(bar:GetIcon())
                    bar:SetSeries(seriesFactor:GetValue(column:GetHeader()))
                end
                i = i + 1
            end
            n = n + 1
        end

        Array <text> groupNames
        // Bar Groups
        i = 0
        repeat while i < groupFactor:GetSize()
            floor = binFloor:Get(i)
            text valueDisplay = "[" + cast(integer, floor) + "-" + cast(integer, floor+binWidth) + "]"
            if(decimalScale)
                valueDisplay = "[" + (math:Round(floor, 2)) + "-" + (math:Round(floor+binWidth, 2)) + "]"
                maxValue = (math:Round(floor+binWidth, 2))
                interval = maxValue - (math:Round(floor, 2))
            end
            if(groupFactor:HasKey(valueDisplay) and floor < max) 
                AddBarGroup(groupFactor:GetValue(valueDisplay))
                groupNames:Add(valueDisplay)
            end
            i = i + 1
        end    
        
        // Series
        Array <Series> seriesList
        Array<text> seriesKeyArray = seriesFactor:CopyToKeyArray()
        if comparison = undefined
            seriesKeyArray:Sort()
        else
            seriesKeyArray:Sort(comparison)
        end
        i = 0
        repeat while i < seriesKeyArray:GetSize()
            seriesList:Add(seriesFactor:GetValue(seriesKeyArray:Get(i)))
            i = i + 1
        end

        if GetHorizontalFlag() // Default is vertical
            if GetHorizontalPanel():OverrideDefaultMax()
                if maxFrequency < GetHorizontalPanel():GetMaximum()
                    maxFrequency = GetHorizontalPanel():GetMaximum()
                end
            end
            SetScale(min, max, 0, maxFrequency)
            SetYTickInterval(binWidth)
        else
            if GetVerticalPanel():OverrideDefaultMax()
                if maxFrequency < GetVerticalPanel():GetMaximum()
                    maxFrequency = GetVerticalPanel():GetMaximum()
                end
            end
            SetScale(0, maxFrequency, min, max)
            SetXTickInterval(binWidth)
        end
        SetSeriesList(seriesList)
    end

    private action SetScale(number ymin, number ymax, number xmin, number xmax)
        if GetHorizontalFlag()
            parent:SharedBarChartParent:SetYAxisMaximum(ymax)
            parent:SharedBarChartParent:SetYAxisMinimum(ymin)
            GetHorizontalPanel():SetMinimum(xmin)
            GetHorizontalPanel():SetMaximum(xmax)
        else
            GetVerticalPanel():SetMinimum(ymin)
            GetVerticalPanel():SetMaximum(ymax)
            parent:SharedBarChartParent:SetXAxisMaximum(xmax)
            parent:SharedBarChartParent:SetXAxisMinimum(xmin)
        end
    end

    action SetXAxisMaximum(number max)
        parent:SharedBarChartParent:SetXAxisMaximum(max)
        if GetVerticalFlag()
            MakeBins()
        end
    end

    action SetXAxisMinimum(number min)
        parent:SharedBarChartParent:SetXAxisMinimum(min)
        if GetVerticalFlag()
            MakeBins()
        end
    end

    action SetYAxisMaximum(number max)
        parent:SharedBarChartParent:SetYAxisMaximum(max)
        if GetHorizontalFlag()
            MakeBins()
        end
    end

    action SetYAxisMinimum(number min)
        parent:SharedBarChartParent:SetYAxisMinimum(min)
        if GetHorizontalFlag()
            MakeBins()
        end
    end

    action SetXTickInterval(number set)
        parent:Chart:SetXTickInterval(set)
        if GetVerticalFlag()
            SetBinWidth(set)
        end
    end

    action SetXTickInterval(integer set)
        parent:Chart:SetXTickInterval(set)
        if GetVerticalFlag()
            SetBinWidth(set)
        end
    end

    action SetYTickInterval(number set)
        parent:Chart:SetYTickInterval(set)
        if GetHorizontalFlag()
            SetBinWidth(set)
        end
    end

    action SetYTickInterval(integer set)
        parent:Chart:SetYTickInterval(set)
        if GetHorizontalFlag()
            SetBinWidth(set)
        end
    end

    action SetBinWidth(integer width) 
        if width > 0 and binWidth not= width
            binWidth = cast(number, width)
            binWidthOverride = true
            MakeBins()
        end
    end

    action SetBinWidth(number width) 
        if width > 0 and binWidth not= width
            binWidth = width
            binWidthOverride = true
            decimalScale = true
            MakeBins()
        end
    end

    action SetXTickCount(integer count)
        parent:Chart:SetXTickCount(count)
        if GetVerticalFlag()
            binCountOverride = true
            binCount = count
            MakeBins()
        end
    end

    action SetYTickCount(integer count)
        parent:Chart:SetYTickCount(count)
        if GetHorizontalFlag()
            binCountOverride = true
            binCount = count
            MakeBins()
        end
    end

    action SetBinCount(integer count)
        binCountOverride = true
        binCount = count
        if GetVerticalFlag()
            parent:Chart:SetXTickCount(count+1)
        else
            parent:Chart:SetYTickCount(count+1)
        end
        MakeBins()
    end

    action SetDecimalScale(boolean decimalScale)
        me:decimalScale = decimalScale
    end

    action GetColumns() returns Array<DataFrameColumn>
        return columns
    end

    action SetDataMinimum(number dataMin)
        me:dataMin = dataMin
    end

    action SetDataMaximum(number dataMax)
        me:dataMax = dataMax
    end

    /* 
        This action converts this chart to a text value that contains information 
        for a scalable vector graphics file. This is useful for saving charts to disk.
        Each sub-class of chart must implement this action separately.

        Attribute: Returns the Scalable Vector Graphics (SVG) text.
    */
    action ConvertToScalableVectorGraphics returns text
        HistogramWriter writer
        return writer:WriteOutChart(me)
    end

    /*
        This action saves this chart to disk at the position of the current file. To conduct the 
        conversion, the file extension is used. Only Scalable Vector Graphics (SVG) is currently 
        supported.

        Attribute: Parameter file the location of where to save the file.
    */
    action Save(File file)
        if file:GetFileExtension() = "svg"
            HistogramWriter chartWriter
            chartWriter:WriteOutChart(me,file)
        end
    end
end

