  package Libraries.Interface.Controls.Charts

use Libraries.Interface.Selections.ChartSelection
use Libraries.Interface.Controls.ControlLabel
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Views.LabelBoxView
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.ColorGroup
use Libraries.Game.Graphics.Drawable
use Libraries.Containers.Array
use Libraries.Compute.Math
use Libraries.Interface.Layouts.ManualLayout

/*
    The SharedBarChartParent class is Chart object that inherits from Control and like other
    UI elements it is added to the Game class. SharedBarChartParent is a parent to the BarChart 
    and the Histogram class and is not directly used itself.

    Attribute: Authors: Gabriel Contreras, Hannah Williams, Tim Kluthe

    Attribute: Example

    use Libraries.Interface.Controls.Charts
    use Libraries.Interface.Controls.Charts.BarChart
    use Libraries.Game.Game

    class Main is Game
        action Main
            StartGame()
        end

        action CreateGame
            BarChart chart
            Add(chart)
        end
    end
*/

class SharedBarChartParent is Chart
    private ChartItem summary // Root of the info tree
    private integer numberOfBars = 0
    private number maxBarValue = 0

    Array<BarGroup> barGroups
    boolean isStacked = false

    Color barColor = undefined

    ColorGroup previousColorGroup = undefined
    Color previousColor = undefined
    integer previousZ = 0
    ChartOptions options
    Math math
    
    /*
        LoadGraphics
        This action is used to load the graphical components of the Control. This is
        handled automatically by the Game engine as needed, and most users shouldn't
        need to use this action directly.

        Attribute: Parameter takes the layout properties of the chart.
    */
    action LoadGraphics(LayoutProperties properties)
        DisposeDrawables()
        if properties = undefined
            return now
        end

        // If stacked, we need to adjust the maximum value to the full stack
        if GetStackedFlag() and not IsSeparatedByFactor()
            AdjustStacked()
        end

        parent:Chart:LoadGraphics(properties)

        if GetHorizontalFlag()
            HideMajorYGridLines()
        else
            HideMajorXGridLines()
        end

        LoadChartAreaItems()
    end

    action LoadChartAreaItems()
        integer i = 0
        repeat while i < barGroups:GetSize()
            text groupname = barGroups:Get(i):GetName()
            integer j = 0                 
            if HasGroups() and not IsSeparatedByFactor()
                Icon barGroupIcon = barGroups:Get(i):GetIcon() 
                barGroupIcon:SetName(groupname + " group, ")
                barGroupIcon:SetDescription((i+1) + " of " + barGroups:GetSize() + " groups, this group has " + barGroups:Get(i):GetSize() + " bars.")
                barGroupIcon:SetFocusable(true)
                barGroupIcon:SetAccessibilityCode(parent:Item:ITEM)
                barGroupIcon:SetNextFocus(GetNextFocus())
                barGroupIcon:SetPreviousFocus(GetPreviousFocus())
                GetChartArea():Add(barGroupIcon)
            end
            GetGroupPanels():Add(barGroups:Get(i):GetIcon())
    
            repeat while j < barGroups:Get(i):GetSize()
                //make bars
                Bar bar = barGroups:Get(i):Get(j)
                Icon temp = bar:GetIcon()
                text barname = bar:GetName()
                number value = math:Round(bar:GetValue(), 2)
                text placement = (j + 1) + " of " + barGroups:Get(i):GetSize() + " bars inside " + groupname + "group."
                if HasGroups()
                    temp:SetName(barname + " bar. ")
                    temp:SetDescription(" value of " + value + " " + placement)
                else
                    temp:SetName(barname + " bar. ")
                    temp:SetDescription(" value of " + value + " " + (i + 1) + " of " + barGroups:GetSize() + " bars.")
                end
                temp:SetFocusable(true)
                temp:SetAccessibilityCode(parent:Item:ITEM)
                temp:SetNextFocus(GetNextFocus())
                temp:SetPreviousFocus(GetPreviousFocus())
                if IsSeparatedByFactor()
                    integer k = 0
                    repeat while k < GetNumberOfChartAreas()
                        if bar:GetSeries() = GetSubChartAreas():Get(k):GetSeries()
                            GetSubChartAreas():Get(k):Add(temp)
                            GetSubChartAreas():Get(k):AddChartAreaItem(temp)
                        end
                        k = k + 1
                    end
                else
                    GetChartArea():Add(temp)
                end
                j = j + 1
            end
            i = i + 1
        end
    end

    action AdjustStacked() 
        integer i = 0
        repeat while i < barGroups:GetSize()
            number sum = 0
            integer j = 0
            repeat while j < barGroups:Get(i):GetSize()
                sum = sum + barGroups:Get(i):Get(j):GetValue()
                j = j + 1
            end
            if sum > maxBarValue
                maxBarValue = sum
            end
            if GetHorizontalFlag()
                if sum > GetXAxisMaximum()
                    GetHorizontalPanel():SetMaximum(sum)
                end
            else
                if sum > GetYAxisMaximum()
                    GetVerticalPanel():SetMaximum(sum)
                end
            end
            i = i + 1
        end
    end

    /*
        DisposeDrawables
        
        Empties drawables from the chart area.
    */
    action DisposeDrawables()
        parent:Chart:DisposeDrawables()

        GetChartArea():Empty()
    end
    
    /*
        LostSelection
        This an inherited action from a blueprint in parent Chart class

        Unlike many kinds of user interface controls, there is no universal way of interacting with a chart and, as such, this 
        may be defined by any chart to be custom. As such, charts must be able to take messages suggesting an item in the chart
        has either lost or gained the focus. Broadly speaking, this is done automatically and while charts need to implement
        this action, they do not need to call this action directly.

        Attribute: Parameter ci the ChartItem representing the structure for this particular kind of chart. 
    */
    action LostSelection(ChartItem ci)
        if ci = undefined
            return now
        end
        Item target = ci:GetFocusTarget()
        if target not= undefined
            if target is ControlLabel
                ControlLabel temp = cast(ControlLabel, target)
                temp:LostSelection()
            elseif target is Series
                Series temp = cast(Series, target)
                integer i = 0
                repeat while i < temp:GetSize()
                    Drawable item = temp:GetItemAt(i)
                    item:SetColor(previousColor)
                    item:SetZ(previousZ)
                    i = i + 1
                end
                temp:GetLabel():LostSelection()
                temp:GetIcon():SetColor(previousColor)
            elseif target is Icon
                Icon temp = cast(Icon, target)
                temp:SetColor(previousColor)
                temp:SetZ(previousZ)
            elseif target is Control
                Control temp = cast(Control, target)
                LayoutProperties properties = temp:GetDefaultLayoutProperties()
                if properties not= undefined
                    LabelBoxView view
                    view:SetBorderThickness(cast(integer, properties:GetBorderThickness()))
                    view:Initialize(properties:GetBackgroundColor(), previousColorGroup)
                    temp:SetView2D(view)
                end
            end
        end
    end

    /* 
        GainedSelection
        This an inherited action from a blueprint in parent Chart class

        Unlike many kinds of user interface controls, there is no universal way of interacting with a chart and, as such, this 
        may be defined by any chart to be custom. As such, charts must be able to take messages suggesting an item in the chart
        has either lost or gained the focus. Broadly speaking, this is done automatically and while charts need to implement
        this action, they do not need to call this action directly.

        Attribute: Parameter ci the ChartItem representing the structure for this particular kind of chart. 
    */
    action GainedSelection(ChartItem ci)
        if ci = undefined
            return now
        end
        Item target = ci:GetFocusTarget()
        if target not= undefined
            target:Focus()
            if target is ControlLabel
                ControlLabel temp = cast(ControlLabel, target)
                temp:GainedSelection()
            elseif target is Series
                Series temp = cast(Series, target)
                previousColor = temp:GetColor()
                integer i = 0
                repeat while i < temp:GetSize()
                    Drawable item = temp:GetItemAt(i)
                    item:SetColor(GetHighlightColor())
                    item:SetZ(-1)
                    i = i + 1
                end
                temp:GetLabel():GainedSelection()
                temp:GetIcon():SetColor(GetHighlightColor())
            elseif target is Icon
                Color c
                Icon temp = cast(Icon, target)
                previousColor = temp:GetColor()
                temp:SetColor(GetHighlightColor())
                temp:SetZ(-1)
            elseif target is Control
                Control temp = cast(Control, target)
                LayoutProperties properties = temp:GetDefaultLayoutProperties()
                if properties not= undefined
                    previousColorGroup = properties:GetBorderColor()
                    LabelBoxView view
                    view:SetBorderThickness(cast(integer, properties:GetBorderThickness())+4)
                    view:Initialize(properties:GetBackgroundColor(), GetHighlightColor())
                    temp:SetView2D(view)
                end
            end            
        end
    end

    /*
        Resize

        This action is called whenever the window is resized.
    */
    action Resize
        parent:Chart:Resize()
    end

    /*
        Sorts the bars by size in descending order (only if there are no groups)
        Updates the axis labels to the correct position after sorting.
    */
    action SortByBarSize()
        if not HasGroups()
            BarGroupComparison comparison
            barGroups:Sort(comparison)
            Array <text> labels = GetUpdatedLabels()
            if GetHorizontalFlag()
                GetVerticalPanel():SetOverrideLabels(labels)
            else
                GetHorizontalPanel():SetOverrideLabels(labels)
            end
        end
    end
    action GetUpdatedLabels() returns Array<text>
        Array <text> grouplabels
        integer i = 0
        repeat while i < barGroups:GetSize()
            grouplabels:Add(barGroups:Get(i):GetName())
            i = i + 1
        end
        return grouplabels
    end

    /*
        Appends a bargroup to end of the Bar Chart. 
        The number of bars in the group are added to the total number of bars.
    */
    action AddBarGroup(BarGroup group)
        if maxBarValue < group:GetMaxBarValue()
            maxBarValue = group:GetMaxBarValue()
        end
        barGroups:Add(group)
        numberOfBars = numberOfBars + group:GetSize()
        if group:GetSize() > 1
            HasGroups(true)
        end
    end

    /*
        Returns the number of bars currently added to the Bar Chart
    */
    action GetNumberOfBars returns integer
        return numberOfBars
    end

    /*
        Returns an array of all the bar groups
    */
    action GetBarGroups returns Array<BarGroup>
        return barGroups
    end

    /*
        Sets a flag that will stack bars inside each group if set to true.
    */
    action IsStacked(boolean flag)
        isStacked = flag
    end

    /*
        Returns stacked flag 

        Attribute: True if stacked
    */
    action GetStackedFlag() returns boolean
        return isStacked
    end

    /*
        GenerateInfoTree

        Generates the tree of ChartItems that define how the chart will be 
        navigated and what extra information might be sent to the screen reader.
        GenerateSummary is done in the child class: BarChart or Histogram
    */
    action GenerateInfoTree
        if GetDefaultLayoutProperties():NeedsRendering()
            return now //we haven't loaded graphics yet, so bail.
        end
        Math math
        //NOTE: These nodes implement a cheap form of ordinality might need changing
        ChartItem xAxis
        ChartItem yAxis
        ChartItem chartArea //not to be confused with panels
        
        text bartext = " bars"
        if numberOfBars = 1
            bartext = " bar"    
        end

        // The chart area is the child unless y-axis is showing.
        summary:SetDisplayName(GenerateSummary())
        summary:SetNext(chartArea)
        summary:SetChild(chartArea)
        summary:SetContainer(me)
        summary:SetFocusTarget(me)
        chartArea:SetDisplayName("Chart Area with " + numberOfBars + bartext)
        chartArea:SetFocusTarget(me:GetChartArea())
        chartArea:SetParent(summary)
        chartArea:SetContainer(me)
        me:GetChartArea():SetDescription(" with " + numberOfBars + bartext)
    
        // If any of these are not showing the tree will skip them in the navigation        
        if IsShowingYAxis()
            GetVerticalPanel():GenerateInfoTree(me, summary, chartArea, yAxis, xAxis)
        end
        if IsShowingXAxis()
            GetHorizontalPanel():GenerateInfoTree(me, summary, chartArea, yAxis, xAxis)
        end
        if IsShowingLegend()
            GetLegend():GenerateInfoTree(me, summary, chartArea, yAxis, xAxis)
        end

        if IsSeparatedByFactor()
            ChartItem firstArea
            firstArea:SetContainer(me)
            if GetNumberOfChartAreas() > 0
                chartArea:SetChild(firstArea)
                firstArea:SetParent(chartArea)
                firstArea:SetFocusTarget(GetSubChartAreas():Get(0))
                firstArea:SetDisplayName(GetSubChartAreas():Get(0):GetTitleName())
                Array <Item> chartAreaItems = GetSubChartAreas():Get(0):GetChartAreaItems()
                GetSubChartAreas():Get(0):SetName(GetSubChartAreas():Get(0):GetSeries():GetName())
                GetSubChartAreas():Get(0):SetDescription(" chart area with " + chartAreaItems:GetSize() + " bars")

                //Bars within SubChartArea
                ChartItem firstBar
                firstBar:SetContainer(me)
                if chartAreaItems:GetSize() > 0
                    firstArea:SetChild(firstBar)
                    firstBar:SetParent(firstArea)
                    firstBar:SetFocusTarget(chartAreaItems:Get(0))
                end

                integer j = 1
                ChartItem previousBar = firstBar
                repeat while j < chartAreaItems:GetSize()
                    ChartItem bar
                    bar:SetContainer(me)
                    bar:SetParent(firstArea)
                    bar:SetFocusTarget(chartAreaItems:Get(j))
                    previousBar:SetNext(bar)
                    bar:SetPrevious(previousBar)
                    previousBar = bar
                    j = j + 1
                end
            end

            integer i = 1
            ChartItem previousArea = firstArea
            repeat while i < GetNumberOfChartAreas()
                ChartAreaPanel chartSubArea = GetSubChartAreas():Get(i)
                if chartSubArea:GetIsShowing()
                    ChartItem nextArea
                    nextArea:SetContainer(me)
                    nextArea:SetParent(chartArea)
                    nextArea:SetFocusTarget(chartSubArea)
                    previousArea:SetNext(nextArea)
                    nextArea:SetPrevious(previousArea)
                    previousArea = nextArea
                    nextArea:SetDisplayName(chartSubArea:GetTitleName())
                    Array <Item> chartAreaItems = chartSubArea:GetChartAreaItems()
                    chartSubArea:SetName(chartSubArea:GetSeries():GetName())
                    chartSubArea:SetDescription(" chart area with " + chartAreaItems:GetSize() + " bars")

                    //Bars within SubChartArea
                    ChartItem firstBar
                    firstBar:SetContainer(me)
                    if chartAreaItems:GetSize() > 0
                        nextArea:SetChild(firstBar)
                        firstBar:SetParent(nextArea)
                        firstBar:SetFocusTarget(chartAreaItems:Get(0))
                    end
    
                    j = 1
                    ChartItem previousBar = firstBar
                    repeat while j < chartAreaItems:GetSize()
                        ChartItem bar
                        bar:SetContainer(me)
                        bar:SetParent(nextArea)
                        bar:SetFocusTarget(chartAreaItems:Get(j))
                        previousBar:SetNext(bar)
                        bar:SetPrevious(previousBar)
                        previousBar = bar
                        j = j + 1
                    end
                end
                i = i + 1
            end
        else
            if HasGroups()
                //Bar Groups
                ChartItem firstGroup
                firstGroup:SetContainer(me)
                if barGroups:GetSize() > 0
                    chartArea:SetChild(firstGroup)
                    firstGroup:SetParent(chartArea)
                    firstGroup:SetFocusTarget(barGroups:Get(0):GetIcon())
        
                    //Bars within Bar Groups
                    ChartItem firstBar
                    firstBar:SetContainer(me)
                    if barGroups:Get(0):GetSize() > 0
                        firstGroup:SetChild(firstBar)
                        firstBar:SetParent(firstGroup)
                        firstBar:SetFocusTarget(barGroups:Get(0):Get(0):GetIcon())
                    end
                    integer j = 1
                    ChartItem previousBar = firstBar
                    repeat while j < barGroups:Get(0):GetSize()
                        ChartItem bar
                        bar:SetContainer(me)
                        bar:SetParent(firstGroup)
                        bar:SetFocusTarget(barGroups:Get(0):Get(j):GetIcon())
                        previousBar:SetNext(bar)
                        bar:SetPrevious(previousBar)
                        previousBar = bar
                        j = j + 1
                    end
                end
        
                integer i = 1
                ChartItem previousGroup = firstGroup
                repeat while i < barGroups:GetSize()
                    ChartItem group
                    group:SetContainer(me)
                    group:SetParent(chartArea)
                    group:SetFocusTarget(barGroups:Get(i):GetIcon())
                    previousGroup:SetNext(group)
                    group:SetPrevious(previousGroup)
                    previousGroup = group
        
                    //Bars within Bar Groups
                    ChartItem firstBar
                    firstBar:SetContainer(me)
                    if barGroups:Get(i):GetSize() > 0
                        group:SetChild(firstBar)
                        firstBar:SetParent(group)
                        firstBar:SetFocusTarget(barGroups:Get(i):Get(0):GetIcon())
                    end
                    integer j = 1
                    ChartItem previousBar = firstBar
                    repeat while j < barGroups:Get(i):GetSize()
                        ChartItem bar
                        bar:SetContainer(me)
                        bar:SetParent(group)
                        bar:SetFocusTarget(barGroups:Get(i):Get(j):GetIcon())
                        previousBar:SetNext(bar)
                        bar:SetPrevious(previousBar)
                        previousBar = bar
                        j = j + 1
                    end
                    i = i + 1
                end
            else
                if barGroups:GetSize() > 0
                    ChartItem firstBar
                    firstBar:SetContainer(me)
                    if barGroups:Get(0):GetSize() > 0
                        chartArea:SetChild(firstBar)
                        firstBar:SetParent(chartArea)
                        firstBar:SetFocusTarget(barGroups:Get(0):Get(0):GetIcon())
                    end
                    integer i = 1
                    ChartItem previousBar = firstBar
                    repeat while i < barGroups:GetSize()
                        ChartItem bar
                        bar:SetContainer(me)
                        bar:SetParent(chartArea)
                        bar:SetFocusTarget(barGroups:Get(i):Get(0):GetIcon())
                        previousBar:SetNext(bar)
                        bar:SetPrevious(previousBar)
                        previousBar = bar
                        i = i + 1
                    end
                end
            end
        end
        ChartSelection selection = GetSelection()
        selection:Set(summary)
    end

    /* 
        Max bar value will track the largest bar and limit the axis controls to not drop below that value.
    */ 

    action SetXAxisMaximum(number max)
        if GetVerticalFlag()
            parent:Chart:SetXAxisMaximum(max)
        else
            if max >= maxBarValue
                parent:Chart:SetXAxisMaximum(max)
            else
                output "X-axis maximum cannot be below highest bar value: " + maxBarValue
            end
        end
    end

    action SetYAxisMaximum(number max)
        if GetHorizontalFlag()
            parent:Chart:SetYAxisMaximum(max)
        else
            if max >= maxBarValue
                parent:Chart:SetYAxisMaximum(max)
            else
                output "Y-axis maximum cannot be below highest bar value: " + maxBarValue
            end
        end
    end
end