package Libraries.Interface.Controls.Charts

use Libraries.Interface.Layouts.ScatterPlotLayout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.Color
use Libraries.System.File
use Libraries.Containers.Array
use Libraries.Interface.Controls.Charts.Graphics.ResidualSquare
use Libraries.Interface.Controls.Charts.Graphics.ChartDrawable

/*
    The ScatterPlot class is Chart object that inherits from TwoVariableChart and like other
    UI elements it is added to the Game class. The Scatter Plot is used to represent a a relationship between
    pairs of numerical data, with one variable on each axis. The first two columns will determine the axes.
    Any additional column will be plotted against the first column.
    The title label, axis labels, and scales can be modified. Any number of points can be added.

    Attribute: Author Andreas Stefik, Hannah Williams

    Attribute: Example

    use Libraries.Interface.Controls.Charts.ScatterPlot
    use Libraries.Game.Game

    class Main is Game
        action Main
            StartGame()
        end

        action CreateGame
            ScatterPlot chart
            Add(chart)
        end
    end
*/
class ScatterPlot is TwoVariableChart
    // Terminology for navigation instructions
    private text groupText = "series"
    private text groupsText = "series"
    private text chartItemText = "point"
    private text chartItemsText = "points"

    Array<ResidualSquare> residualSquares
    boolean showSquaresOfResiduals = false
    number residualSquareThickness = 1.2

    on create
        ScatterPlotLayout layout
        SetLayout(layout)

        LayoutProperties properties = GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:MAINTAIN_ASPECT_RATIO)
        properties:SetVerticalLayoutMode(properties:STANDARD)
        Font font = GetDefaultFont()
        properties:SetFont(font)
        properties:SetFontSize(GetDefaultFontSize())

        properties:SetBorderThickness(2)
        SetName("Scatter Plot")

        SetInputGroup("Chart")
        SetFocusable(true)
        SetAccessibilityCode(parent:Item:ITEM)

        // Default settings specific to scatter plot
        RegressionCapable(true)
        ShowAllTicks()
        SetDefaultOrientationToHorizontal()
    end

    /*
        This is called by GenerateInfoTree to generate the summary that is heard when
        you first focus on the chart. Also the highest level of the information tree
        of  the chart.
    */
    private action GenerateSummary returns text
        integer points = parent:TwoVariableChart:GetNumberOfPoints()
        text pointText = "points"
        if points = 1
            pointText = "point"
        end
        if GetName() = "Scatter Plot"
            SetDescription(" with " + points + " " + pointText + ". Use the arrow keys to navigate chart information and Tab to access the chart content.")
        else
            SetDescription(", Scatter plot with " + points + " " + pointText + ". Use the arrow keys to navigate chart information and Tab to access the chart content.")
        end
        return GetDescription()
    end

    action Add(ChartPoint point)
        parent:TwoVariableChart:AddPoint(point)
    end

    /*
    Supplemental actions to Chart class's SeparateByFactor and SeparateBySeries.
    This ensures the separate pie charts are created and filled in properly.
    */
    action SeparateByFactor(integer requestColumns)
        if GetFactorList() not= undefined
            parent:Chart:SeparateBySeries(requestColumns)
        end
    end

    /* 
        This action converts this chart to a text value that contains information 
        for a scalable vector graphics file. This is useful for saving charts to disk.
        Each sub-class of chart must implement this action separately.

        Attribute: Returns the Scalable Vector Graphics (SVG) text.
    */
    action ConvertToScalableVectorGraphics returns text
        ScatterPlotWriter writer
        writer:SetAccessibilityFlag(HasAccessibility())
        return writer:WriteOutChart(me)
    end

    /*
        This action saves this chart to disk at the position of the current file. To conduct the 
        conversion, the file extension is used. Only Scalable Vector Graphics (SVG) is currently 
        supported.

        Attribute: Parameter file the location of where to save the file.
    */
    action Save(File file)
        if file:GetFileExtension() = "svg"
            ScatterPlotWriter chartWriter
            chartWriter:SetAccessibilityFlag(HasAccessibility())
            chartWriter:WriteOutChart(me,file)
        else
           alert("Save currently only supports Scalable Vector Graphics (SVG), please use a .svg file extension.")
        end
    end

    /*
        This action saves this chart to disk at the position of the current file. To conduct the 
        conversion, the file extension is used. Only Hypertext Markup Language (HTML) is currently
        supported, and Scalable Vector Graphics (SVG) conversion is used. This makes a file that can
        be opened in the browser that imports the Javascript and Cascading Stylesheets necessary
        to navigate the charts accessibly on the web.

        Attribute: Parameter file the location of where to save the file.
    */
    action Share(File file)
        if file:GetFileExtension() = "html"
            ScatterPlotWriter chartWriter
            chartWriter:SetAccessibilityFlag(HasAccessibility())
            chartWriter:SetScriptFilepath(GetScriptFilepath())
            chartWriter:SetStyleFilepath(GetStyleFilepath())
            chartWriter:WriteOutHTML(me,file)
        else
            alert("Share currently only supports Hypertext Markup Language (HTML), please use a .html file extension.")
        end
    end


    /*
    This action generates the instruction text for the Chart Area description.

    Attribute: Parameter description The base portion of the description that will appear before the instructions
    */
    action AddChartAreaInstructions(text description)
        
        if not IsSeparated()
            //Not Separated
                description = description + " To navigate the chart, press Enter to move into the list of " + GetGroupsText() + " or Shift Tab to return to chart information."
        else
            //Separated, each Series is also separated by the Factor levels
            if IsSeparatedBySeries() and IsSeparatedByFactor()  
                description = description + " To navigate the chart, press Enter to move into the list of " + GetChartItemsText() + " or Shift Tab to return to chart information."
            //Separated, by the Factor levels with multiple Series per subplot
            elseif not IsSeparatedBySeries() and IsSeparatedByFactor()
                description = description + " To navigate the chart, press Enter to move into the list of " + GetGroupsText() + " or Shift Tab to return to chart information."
            //Separated, one Series per subplot should go from Chart Area then group list info for sub chart areas
            else
                description = description + " To navigate the chart, press Enter to move into the list of " + GetSeriesText() + " or Shift Tab to return to chart information."
            end
        end
        me:GetChartArea():SetDescription(description)
    end

    /*
    This action generates the instruction text for a sub chart area description.

    Attribute: Parameter subChartArea The sub chart area that will have the instructions
    Attribute: Parameter description The base portion of the description that will appear before the instructions
    */
    action AddSubChartAreaInstructions(ChartAreaPanel subChartArea, text description)
        //Separated, by the Factor levels with multiple Series per subplot
        if not IsSeparatedBySeries() and IsSeparatedByFactor()
            description = description + " To navigate the list of " + GetGroupsText() + ", use the arrow keys to move between " + GetGroupsText() + " in list order," 
            description = description + " use Enter to move into the list of " + GetChartItemsText() + " for that " + GetGroupText() + ", use Shift + Enter to return to the chart area."
        //Separated, one Series per subplot should go from Chart Area then group list info for sub chart areas
        elseif IsSeparatedBySeries() and not IsSeparatedByFactor()
            description = description + " To navigate the list of " + GetSeriesText() + ", use the arrow keys to move between " + GetSeriesText() + " in list order,"
            description = description + " use Enter to move into the list of " + GetChartItemsText() + " for that " + GetSeriesText() + ", use Shift + Enter to return to the chart area."
        end
        subChartArea:SetDescription(description)
    end

    /*
    This action generates the instruction text for a group panel description.

    Attribute: Parameter groupPanel The group panel that will have the instructions
    Attribute: Parameter description The base portion of the description that will appear before the instructions
    */
    action AddGroupPanelInstructions(ChartDrawable groupPanel, text description)

        if GetSeriesList():GetSize() > 1
            description = description + " To navigate the list of " + GetGroupsText() + ", use the arrow keys to move between " + GetGroupsText() + " in list order," 
            description = description + " use Enter to move into the list of " + GetChartItemsText() + " for that " + GetGroupText() + ", use Shift + Enter to return to the chart area."
        else
            description = description + " To navigate the " + GetGroupText() + ", " 
            description = description + " use Enter to move into the list of " + GetChartItemsText() + " for that " + GetGroupText() + ", use Shift + Enter to return to the chart area."
        end
        
        groupPanel:SetDescription(description)
    end

    /*
    This action generates the instruction text for a chart item description.

    Attribute: Parameter chartItem The chart item that will have the instructions
    Attribute: Parameter description The base portion of the description that will appear before the instructions
    */
    action AddChartItemInstructions(Item chartItem, text description)

        description = description + " To navigate the list of " + GetChartItemsText() + ", use the arrow keys to move between " + GetChartItemsText() + " in value order on the X axis," 
        description = description + " use Page Up Down to move between " + GetChartItemsText() + " by value order on the Y axis,"

        if IsShowingLinearRegression()
            description = description + " use Enter to view the regression,"
        end

        if not IsSeparatedBySeries() and IsSeparatedByFactor()
            description = description + " use Shift + Enter to return to the " + GetGroupText() + " list."
        elseif IsSeparatedBySeries() and not IsSeparatedByFactor()
            description = description + " use Shift + Enter to return to the " + GetSeriesText() + " list."
        elseif not IsSeparated() and HasGroups()
            description = description + " use Shift + Enter to return to the chart area."
        else
            description = description + " use Shift + Enter to return to the " + GetGroupText() + " list."
        end
        chartItem:SetDescription(description)
    end

    action GetGroupText returns text
        return groupText
    end

    action GetGroupsText returns text
        return groupsText
    end

    action GetChartItemText returns text
        return chartItemText
    end

    action GetChartItemsText returns text
        return chartItemsText
    end

    /*
    This action returns true if squares of residuals are being displayed in the chart,
    or false if they are not. 
    
    Attribute: Returns True if residual squares are showing, or false otherwise.
    */
    action IsShowingSquaresOfResiduals returns boolean
        return showSquaresOfResiduals
    end

    /*
    This action enables or disables the visual squares of residuals within the interactive chart.
    These appear as squares between points and the regression line, if the line is visible.

    Attribute: Parameter showSquaresOfResiduals True to show residual squares, or false to hide them.
    Attribute: Example
    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Interface.Controls.Charts.ScatterPlot
    
    DataFrame frame
    frame:Load("Data/ChartTestData.csv")
    frame:AddSelectedColumns("Column1")
    frame:AddSelectedColumns("Column2")
    frame:AddSelectedFactors("Factor1")
    
    ScatterPlot chart = frame:ScatterPlot()
    chart:SetTitle("ScatterPlot Feature Example")
    chart:SetSubtitle("Two Columns - One Factor")
    chart:ShowLinearRegression(true)
    chart:ShowSquaresOfResiduals(true)
    chart:SetMovableRegressionLines(true)
    chart:SetResidualSquareThickness(2)
    chart:LockRegressionYIntercept(0)
    chart:Display(1200, 1200)
    */
    action ShowSquaresOfResiduals(boolean showSquaresOfResiduals)
        integer i = 0
        repeat while i < residualSquares:GetSize()
            if showSquaresOfResiduals
                residualSquares:Get(i):Show()
            else
                residualSquares:Get(i):Hide()
            end
            i = i + 1
        end

        me:showSquaresOfResiduals = showSquaresOfResiduals
    end

    action GetResidualSquares returns Array<ResidualSquare>
        return residualSquares
    end

    /*
    This action returns how thick the borders of the residual squares should be, in pixels.

    Attribute: Parameter Returns How thick, in pixels, each side of the squares should be.
    */
    action GetResidualSquareThickness returns number
        return residualSquareThickness
    end

    /*
    This action sets how thick the borders of the residual squares should be, in pixels.

    Attribute: Parameter residualSquareThickness How thick, in pixels, each side of the squares should be.
    Attribute: Example
    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Interface.Controls.Charts.ScatterPlot
    
    DataFrame frame
    frame:Load("Data/ChartTestData.csv")
    frame:AddSelectedColumns("Column1")
    frame:AddSelectedColumns("Column2")
    frame:AddSelectedFactors("Factor1")
    
    ScatterPlot chart = frame:ScatterPlot()
    chart:SetTitle("ScatterPlot Feature Example")
    chart:SetSubtitle("Two Columns - One Factor")
    chart:ShowLinearRegression(true)
    chart:ShowSquaresOfResiduals(true)
    chart:SetMovableRegressionLines(true)
    chart:SetResidualSquareThickness(2)
    chart:LockRegressionYIntercept(0)
    chart:Display(1200, 1200)
    */
    action SetResidualSquareThickness(number residualSquareThickness)
        me:residualSquareThickness = residualSquareThickness
    end

end

