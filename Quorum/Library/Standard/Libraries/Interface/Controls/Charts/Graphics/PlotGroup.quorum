package Libraries.Interface.Controls.Charts.Graphics

use Libraries.Containers.Array
use Libraries.Interface.Controls.Charts.GroupPanel
use Libraries.Interface.Layouts.ManualLayout

/*
The PlotGroup class is a data structure that stores information for each 
group (array) of plots at a certain tick or by a certain factor marked 
by its label. This class also contains an Icon that is used for accessibility.

Attribute: Example

use Libraries.Interface.Controls.Charts.PlotGroup
class Main
   action Main
      //make group
      PlotGroup group

      //add a plot
      Plot plot
      group:AddPlot(plot)

      //get a plot set its data
      group:Get(0):SetData(Vector v)
   end
end
*/

class PlotGroup is GroupPanel
    private number maxValue = 0
    private number minValue = 0
    private number maxOutlierValue = 0
    private number minOutlierValue = 0

    on create
        ManualLayout manualLayout
        SetLayout(manualLayout)

        SetFocusable(true)
        SetAccessibilityCode(parent:Item:CUSTOM)
    end

    action IsAccessibleParent returns boolean
        return true
    end

    action AddPlot(Plot plot)
        AddItem(plot)
        RecalculateGroupValues()
    end

    action Get(integer index) returns Plot
        return cast(Plot, GetItemAt(index))
    end

    action GetPlots returns Array<Plot>
        return cast(Array<Plot>, GetGroupItems())
    end

    action EmptyItems()    
        maxValue = 0
        minValue = 0
        maxOutlierValue = 0
        minOutlierValue = 0  
        parent:GroupPanel:EmptyItems()
    end

    action GetMaxValue returns number
        return maxValue
    end

    action GetMinValue returns number
        return minValue
    end

    action GetMaxOutlierValue returns number
        return maxOutlierValue
    end

    action GetMinOutlierValue returns number
        return minOutlierValue
    end

    /*
        Since the user can alter the value of the plot manually, 
        Anytime a plot is added the totals for this group will be recalculated. 
    */
    action RecalculateGroupValues
        maxValue = 0
        minValue = 0
        maxOutlierValue = 0
        minOutlierValue = 0  
        Array<Plot> plots = GetPlots()
        integer i = 0
        repeat while i < plots:GetSize()
            Plot plot = plots:Get(i)
            if not plot:IsUndefined()
                if i = 0
                    maxValue = plot:GetInfoAt(4)
                    minValue = plot:GetInfoAt(3)
                    maxOutlierValue = plot:GetMaxOutlierValue()
                    minOutlierValue = plot:GetMinOutlierValue()
                else   
                    if plot:GetInfoAt(4) > maxValue
                        maxValue = plot:GetInfoAt(4)
                    end
                    if plot:GetInfoAt(3) < minValue
                        minValue = plot:GetInfoAt(3)
                    end
                    if plot:GetMaxOutlierValue() > maxOutlierValue
                        maxOutlierValue = plot:GetMaxOutlierValue()
                    end
                    if plot:GetMinOutlierValue() < minOutlierValue
                        minOutlierValue = plot:GetMinOutlierValue()
                    end
                end
            end
            i = i + 1
        end
    end
end