package Libraries.Interface.Controls.Charts

use Libraries.Interface.Controls.Control

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.ColorGroup
use Libraries.Game.Graphics.Gradient
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Texture
use Libraries.Game.Graphics.TextureRegion
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.Label
use Libraries.System.File
use Libraries.Interface.Views.ImageControlView
use Libraries.Interface.Views.LabelBoxView
use Libraries.Interface.Views.ControlView
use Libraries.Interface.Views.View2D
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Controls.Icon
use Libraries.Containers.Array


class Legend is Control

    Label title
    Array<Series> seriesList
    Array<text> itemNames

    on create
        FlowLayout flowLayout
        SetLayout(flowLayout)

        LayoutProperties properties = GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:STANDARD)
        properties:SetVerticalLayoutMode(properties:FIT_CONTENTS)
        Font font = GetDefaultFont()
        properties:SetFont(font)
        properties:SetFontSize(GetDefaultFontSize())

        Color color
        Gradient gradient
        Color gray = color:LightGray()
        Color lightGray = color:CustomColor(0.9, 0.9, 0.9, 1)
        gradient:Set(gray, gray, lightGray, lightGray)

        properties:SetBackgroundColor(color:White())
        properties:SetBorderColor(color:Black())
        properties:SetBorderThickness(2)
        SetName("Legend")

//        SetInputGroup("Button")
        SetFocusable(true)
        SetAccessibilityCode(parent:Item:ITEM)
    end

    action Add(text entry)
        itemNames:AddToEnd(entry)
    end

    action Empty()
        parent:Control:Empty()
        itemNames:Empty()
    end

    action SetSeriesList(Array<Series> seriesList)
        me:seriesList = seriesList
    end

    action GetSeriesList() returns Array<Series>
        return seriesList 
    end

    action SetTitle(text title)
        me:title:SetName(title)
        me:title:SetText(title)
    end
    
    action GetTitle() returns text
        return title:GetName()
    end

    action SetName(text name)
        parent:Item2D:SetName(name)
        
        LayoutProperties defaultProperties = GetDefaultLayoutProperties()

        if defaultProperties not= undefined
            defaultProperties:SetLabelText(name)
        end
        if GetView2D() is ControlView
            ControlView content = cast(ControlView, GetView2D())
            content:SetText(name)
        end
    end
    
    /*
        Generates the tree of ChartItems that define how the chart will be 
        navigated and what extra information might be sent to the screen reader.
    */
    action GenerateInfoTree(Chart chart, ChartItem summary, ChartItem xAxis, ChartItem yAxis)
        ChartItem legendArea
        legendArea:SetDisplayName("Legend")
        legendArea:SetFocusTarget(me)
        legendArea:SetParent(summary)

        me:SetDescription(me:GetTitle() + seriesList:GetSize() + " series")
        legendArea:SetFocusTarget(me)
        legendArea:SetContainer(chart)

        xAxis:SetNext(legendArea)
        legendArea:SetPrevious(xAxis)
        yAxis:SetPrevious(legendArea)

        // Legend Entries
        ChartItem firstEntry
        firstEntry:SetContainer(chart)
        if seriesList:GetSize() > 0
            legendArea:SetChild(firstEntry)
            seriesList:Get(0):GetIcon():SetDescription(", 1 of " + seriesList:GetSize() + " series, " + seriesList:Get(0):GetName())
            firstEntry:SetParent(legendArea)
            firstEntry:SetFocusTarget(seriesList:Get(0):GetIcon())
        end
        integer i = 1
        ChartItem previous = firstEntry
        repeat while i < seriesList:GetSize()
            ChartItem entry
            entry:SetContainer(chart)
            seriesList:Get(i):GetIcon():SetDescription(", " + (i+1) + " of " + seriesList:GetSize() + " series, " + seriesList:Get(i):GetName())
            entry:SetParent(legendArea)
            entry:SetFocusTarget(seriesList:Get(i):GetIcon())
            previous:SetNext(entry)
            entry:SetPrevious(previous)
            previous = entry
            i = i + 1
        end
    end
    /*
    This action is used to load the graphical components of the Control. This is
    handled automatically by the Game engine as needed, and most users shouldn't
    need to use this action directly.
    */
    action LoadGraphics(LayoutProperties properties)

//        Empty()
        if properties = undefined
            return now
        end
        ColorGroup background = properties:GetBackgroundColor()
        ColorGroup border = properties:GetBorderColor()
        number borderThickness = properties:GetBorderThickness()
        text labelText = properties:GetLabelText()

        Color color
        Font font = properties:GetFont()

        View2D view = properties:GetView2D()

        if font not= undefined and labelText not= ""
            if view = undefined
                LabelBoxView labelBoxView
                if background = undefined
                    background = color:CustomColor(0, 0, 0, 0)
                end
                if border = undefined
                    border = color:CustomColor(0, 0, 0, 0)
                end

                labelBoxView:SetBorderThickness(cast(integer, borderThickness))
                labelBoxView:Initialize(background, border)
                view = labelBoxView
                SetView2D(view)
            end
            integer i = 0
            Control titleLabel = MakeTitleEntry(labelText)
            titleLabel:Resize()
            number maxWidth = titleLabel:GetWidth()
            number maxLabel = maxWidth - 21 // 21 for extra padding hardcoded in MakeEntry
            parent:Item2D:Add(titleLabel)

            // This gets the longest label, so we can calculate how much of a padding is needed to wrap legend
            repeat while i < seriesList:GetSize()
                if(seriesList:Get(i):GetLabel():GetWidth() > maxLabel)
                    maxLabel = seriesList:Get(i):GetLabel():GetWidth()
                end
                i = i + 1
            end

            i = 0
            integer rightPadding = 0
            repeat while i < seriesList:GetSize()
                rightPadding = cast(integer, maxLabel - seriesList:Get(i):GetLabel():GetWidth())
                Icon newEntry = MakeEntry(seriesList:Get(i), seriesList:Get(i):GetColor(), rightPadding)
                newEntry:Resize()
                if(newEntry:GetWidth() > maxWidth)
                    maxWidth = newEntry:GetWidth()
                end
                seriesList:Get(i):SetIcon(newEntry)
                parent:Item2D:Add(newEntry)
                i = i + 1
            end

            SetWidth(maxWidth + 10)
            SetHeight(600) //it should fill in automatically

        end

        if view not= undefined
            SetView2D(view)
        end

        parent:Control:LoadGraphics(properties)
    end

    
    private action MakeEntry(Series series, Color color, integer rightPadding) returns Icon
        Icon newItemStructure
        newItemStructure:SetFocusable(true)
        newItemStructure:SetAccessibilityCode(parent:Item:ITEM)
        FlowLayout layout


        Label newLabel = series:GetLabel()
        LayoutProperties properties = GetDefaultLayoutProperties()
        LayoutProperties labelProperties = newLabel:GetDefaultLayoutProperties()
        labelProperties:SetHorizontalLayoutMode(properties:FIT_CONTENTS)
        labelProperties:SetVerticalLayoutMode(properties:FIT_CONTENTS)
        labelProperties:SetLeftPadding(7)
        labelProperties:SetRightPadding(7+rightPadding)
        labelProperties:SetTopPadding(4)
        labelProperties:SetBottomPadding(7)
        newLabel:SetSize(properties:GetFontSize())

        Icon newIcon
        newIcon:SetName("Icon")
        newIcon:LoadFilledRectangle(20,20,color)

        LayoutProperties iconProperties = newIcon:GetDefaultLayoutProperties()
        iconProperties:SetHorizontalLayoutMode(iconProperties:MAINTAIN_ASPECT_RATIO)
        iconProperties:SetPercentageWidth(cast(number, newIcon:GetWidth()) / newIcon:GetHeight())
        iconProperties:SetPercentageHeight(1.0)
        iconProperties:SetLeftPadding(7)
        iconProperties:SetTopPadding(4)
        iconProperties:SetBottomPadding(4)

        LayoutProperties itemStructureProperties = newItemStructure:GetDefaultLayoutProperties()
        itemStructureProperties:SetHorizontalLayoutMode(itemStructureProperties:FIT_CONTENTS)
        itemStructureProperties:SetVerticalLayoutMode(itemStructureProperties:FIT_FONT)
        itemStructureProperties:SetFont(newLabel:GetFont())
        itemStructureProperties:SetFontSize(properties:GetFontSize())
        itemStructureProperties:SetInterfaceScale(properties:GetInterfaceScale())
        newItemStructure:SetLayout(layout)

        newItemStructure:Add(newIcon)
        newItemStructure:Add(newLabel)

        return newItemStructure
    end
    
    private action MakeEntry(text name) returns Control
        Control newItemStructure
        newItemStructure:SetFocusable(true)
        newItemStructure:SetAccessibilityCode(parent:Item:ITEM)
        FlowLayout layout

        Color color

        Label newLabel
        LayoutProperties properties = GetDefaultLayoutProperties()
        LayoutProperties labelProperties = newLabel:GetDefaultLayoutProperties()
        labelProperties:SetHorizontalLayoutMode(properties:FIT_CONTENTS)
        labelProperties:SetVerticalLayoutMode(properties:FIT_CONTENTS)
        labelProperties:SetLeftPadding(7)
        labelProperties:SetRightPadding(7)
        labelProperties:SetTopPadding(4)
        labelProperties:SetBottomPadding(7)
        newLabel:SetSize(properties:GetFontSize())
        newLabel:SetText(name)

        Icon newIcon
        newIcon:SetName("Icon")
        newIcon:LoadFilledRectangle(20,20,color:Navy())

        LayoutProperties iconProperties = newIcon:GetDefaultLayoutProperties()
        iconProperties:SetHorizontalLayoutMode(iconProperties:MAINTAIN_ASPECT_RATIO)
        iconProperties:SetPercentageWidth(cast(number, newIcon:GetWidth()) / newIcon:GetHeight())
        iconProperties:SetPercentageHeight(1.0)
        iconProperties:SetLeftPadding(7)
        iconProperties:SetTopPadding(4)
        iconProperties:SetBottomPadding(4)

        LayoutProperties itemStructureProperties = newItemStructure:GetDefaultLayoutProperties()
        itemStructureProperties:SetHorizontalLayoutMode(itemStructureProperties:FIT_CONTENTS)
        itemStructureProperties:SetVerticalLayoutMode(itemStructureProperties:FIT_FONT)
        itemStructureProperties:SetFont(newLabel:GetFont())
        itemStructureProperties:SetFontSize(properties:GetFontSize())
        itemStructureProperties:SetInterfaceScale(properties:GetInterfaceScale())
        newItemStructure:SetLayout(layout)

        newItemStructure:Add(newIcon)
        newItemStructure:Add(newLabel)

        return newItemStructure
    end

    action MakeTitleEntry(text title) returns Control
        Control newItemStructure
        newItemStructure:SetFocusable(true)
        newItemStructure:SetAccessibilityCode(parent:Item:ITEM)
        FlowLayout layout

        Label newLabel = me:title
        LayoutProperties properties = GetDefaultLayoutProperties()
        LayoutProperties labelProperties = newLabel:GetDefaultLayoutProperties()
        labelProperties:SetHorizontalLayoutMode(properties:FIT_CONTENTS)
        labelProperties:SetVerticalLayoutMode(properties:FIT_CONTENTS)
        labelProperties:SetLeftPadding(7)
        labelProperties:SetRightPadding(7)
        labelProperties:SetTopPadding(4)
        labelProperties:SetBottomPadding(7)
        newLabel:SetSize(properties:GetFontSize())
        newLabel:SetText(title)

        LayoutProperties itemStructureProperties = newItemStructure:GetDefaultLayoutProperties()
        itemStructureProperties:SetHorizontalLayoutMode(itemStructureProperties:FIT_CONTENTS)
        itemStructureProperties:SetVerticalLayoutMode(itemStructureProperties:FIT_FONT)
        itemStructureProperties:SetFont(newLabel:GetFont())
        itemStructureProperties:SetFontSize(properties:GetFontSize())
        itemStructureProperties:SetInterfaceScale(properties:GetInterfaceScale())
        newItemStructure:SetLayout(layout)

        newItemStructure:Add(newLabel)

        return newItemStructure
    end
end