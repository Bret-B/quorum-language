package Libraries.Interface.Controls.Charts

use Libraries.Game.Game
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Interface.Events.TextInputListener
use Libraries.Interface.Events.TextInputEvent
use Libraries.Interface.Events.KeyboardEvent
use Libraries.Game.InputMonitor
use Libraries.Game.InputTable
use Libraries.Interface.Behaviors.Controls.MapNavigateBehavior
use Libraries.Game.InputSet

/*
    This class is used as a default display region for charts. It allows basic
    charts to be shown on screen, and accessible, without extending the game class.

    Attribute: Author Hannah Williams
*/
class MapDisplay is Game, TextInputListener
    boolean exitOnStart = false
    integer frame = 0

    GeoMap chart = undefined
    InputMonitor monitor
    KeyboardEvent keys

    action GetChart returns GeoMap
        return chart
    end

    action SetChart(GeoMap chart)
        me:chart = chart
        me:chart:SetInputGroup("Map")
    end

    action CreateGame
        ManualLayout layout
        SetLayout(layout)
        MakeInputTable()

        chart:SetPercentageWidth(1)
        chart:SetPercentageHeight(1)

        Add(chart)
        chart:Focus()
    end

    action TextInput(TextInputEvent event)
        chart:FirstLetterCharactersTyped(event:GetText())
    end

    action Update (number seconds)
        if monitor:IsKeyPressed(keys:ESCAPE)
            Exit()
        end

        if frame >= 1 and exitOnStart
            Exit()
        end

        frame = frame + 1
    end

    action GetExitOnStart returns boolean
        return exitOnStart
    end

    action SetExitOnStart(boolean exitOnStart)
        me:exitOnStart = exitOnStart
    end

    action MakeInputTable()
        InputTable chartTable
        chartTable:SetIdentifier("Map")
        MapNavigateBehavior mapNavigateBehavior
        InputSet leftArrow
        leftArrow:SetKeyboardInput(keys:LEFT)
        chartTable:Add(leftArrow, mapNavigateBehavior)
        InputSet rightArrow
        rightArrow:SetKeyboardInput(keys:RIGHT)
        chartTable:Add(rightArrow, mapNavigateBehavior)
        InputSet upArrow
        upArrow:SetKeyboardInput(keys:UP)
        chartTable:Add(upArrow, mapNavigateBehavior)
        InputSet downArrow
        downArrow:SetKeyboardInput(keys:DOWN)
        chartTable:Add(downArrow, mapNavigateBehavior)
        InputSet Tab
        Tab:SetKeyboardInput(keys:TAB)
        chartTable:Add(Tab, mapNavigateBehavior)
        InputSet enter
        enter:SetKeyboardInput(keys:ENTER)
        chartTable:Add(enter, mapNavigateBehavior)

        InputSet shiftTab
        shiftTab:SetKeyboardInput(keys:TAB)
        shiftTab:AddModifier(shiftTab:SHIFT)
        chartTable:Add(shiftTab, mapNavigateBehavior)

        InputSet shiftEnter
        shiftEnter:SetKeyboardInput(keys:ENTER)
        shiftEnter:AddModifier(shiftEnter:SHIFT)
        chartTable:Add(shiftEnter, mapNavigateBehavior)

        AddInputTable(chartTable)
        AddTextInputListener(me)
    end
end