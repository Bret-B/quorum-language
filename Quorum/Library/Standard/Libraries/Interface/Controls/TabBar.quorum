package Libraries.Interface.Controls

use Libraries.Containers.Array
use Libraries.Interface.Item2D
use Libraries.Interface.Controls.Control
use Libraries.Interface.Views.LabelBoxView
use Libraries.Interface.Views.View2D
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Layouts.TabFlowLayout
use Libraries.Interface.Events.TabChangeEvent
use Libraries.Interface.Events.TabChangeListener
use Libraries.Interface.Selections.TabPaneSelection
use Libraries.Game.GameStateManager
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.ColorGroup
use Libraries.Game.Graphics.Font

class TabBar is Control
    
    Array<Tab> tabs
    TabFlowLayout layout
    TabPane pane = undefined

    Array<TabChangeListener> listeners

    on create
        LayoutProperties layoutProperties = GetDefaultLayoutProperties()
        layoutProperties:SetHorizontalLayoutMode(layoutProperties:STANDARD)
        layoutProperties:SetVerticalLayoutMode(layoutProperties:FIT_CONTENTS)
        layoutProperties:SetPercentageWidth(1.0)

        SetLayout(layout)

        SetClipping(true)
        SetName("Tab Bar")
    end

    action Add(Array<Tab> array)
        integer counter = 0
        repeat while counter < array:GetSize()
            Add(array:Get(counter))
            counter = counter + 1
        end
    end

    action Add(Tab tab)
        tabs:Add(tab)
        parent:Item2D:Add(tab)
        tab:SetTabBar(me)
        Resize()

        TabChangeEvent event
        event:Set(tab, pane, event:OPENED)
        NotifyTabChangeListeners(event)
    end

    action Remove(Tab tab)
        if tab = undefined
            return now
        end

        tab:OnClose()

        TabPaneSelection selection = pane:GetSelection()
        boolean reselect = false

        
        integer index = selection:GetIndex()
        Tab currentTab = selection:GetTab()
        if currentTab = tab
            reselect = true
        end
        pane:Deselect()

        tabs:Remove(tab)
        parent:Item2D:Remove(tab)
        tab:SetTabBar(undefined)

        if reselect
            if tabs:GetSize() > 0
                index = index - 1
                if index < 0
                    index = 0
                end
                pane:Select(index)
            else
                pane:UpdateSelection(undefined)
            end
        else
            pane:Select(currentTab)
        end

        Resize()

        TabChangeEvent event
        event:Set(tab, pane, event:CLOSED)
        NotifyTabChangeListeners(event)
    end

    action Get(integer index) returns Tab
        return tabs:Get(index)
    end

    action GetTabs returns Array<Tab>
        return tabs
    end

    action SetTabPane(TabPane tabPane)
        pane = tabPane
        LayoutProperties paneProperties = GetDefaultLayoutProperties()
        SetFont(paneProperties:GetFont())
        SetFontSize(paneProperties:GetFontSize())
    end

    action GetTabPane returns TabPane
        return pane
    end

    action FitTab(Tab tab)
        integer index = GetChildren():GetFirstLocation(tab)
        if index = -1
            return now
        end

        if tab:GetX() <= 0
            LeftFitTab(index - 1.15)
        elseif tab:GetX() + tab:GetWidth() >= GetWidth()
            RightFitTab(index + 1.15)
        end
    end

    action LeftFitTab(number index)
        Array<Item2D> children = GetChildren()
        if index < 0
            index = 0
        elseif index >= children:GetSize()
            index = children:GetSize() - 1
        end

        integer counter = 0
        number offset = 0
        integer intIndex = cast(integer, index)
        repeat while counter < intIndex
            Item2D item = children:Get(counter)
            offset = offset + item:GetWidth()
            counter = counter + 1
        end

        // Include a percentage of the width of the next item, if we have any remainder.
        if counter < children:GetSize()
            offset = offset + (index - intIndex) * children:Get(counter):GetWidth()
        // If there's no next item, offset it by the last item, if there is one.
        elseif counter - 1 < children:GetSize()
            offset = offset + (index - intIndex) * children:Get(counter - 1):GetWidth()
        end

        layout:SetOffset(offset)
        Resize()
    end

    action RightFitTab(number index)
        Array<Item2D> children = GetChildren()
        if index < 0
            index = 0
        elseif index >= children:GetSize()
            index = children:GetSize() - 1
        end

        integer counter = 0
        number offset = 0
        integer intIndex = cast(integer, index)
        repeat while counter <= intIndex
            Item2D item = children:Get(counter)
            offset = offset + item:GetWidth()
            counter = counter + 1
        end

        // Include a percentage of the width of the next item, if we have any remainder.
        if counter < children:GetSize()
            offset = offset + (index - intIndex) * children:Get(counter):GetWidth()
        // If there's no next item, offset it by the last item, if there is one.
        elseif counter - 1 < children:GetSize()
            offset = offset + (index - intIndex) * children:Get(counter - 1):GetWidth()
        end

        layout:SetOffset(offset - GetWidth())
        Resize()
    end

    action GetSize returns integer
        return tabs:GetSize()
    end

    action IsEmpty returns boolean
        return tabs:IsEmpty()
    end

    action AddTabChangeListener(TabChangeListener listener)
        listeners:Add(listener)
    end

    action RemoveTabChangeListener(TabChangeListener listener)
        listeners:Remove(listener)
    end

    private action NotifyTabChangeListeners(TabChangeEvent event)
        GameStateManager manager
        manager:GetInput():NotifyTabChangeListeners(event)

        integer counter = 0
        if event:GetEventType() = event:OPENED
            repeat while counter < listeners:GetSize()
                listeners:Get(counter):OpenedTab(event)
                counter = counter + 1
            end
        elseif event:GetEventType() = event:CLOSED
            repeat while counter < listeners:GetSize()
                listeners:Get(counter):ClosedTab(event)
                counter = counter + 1
            end
        end
    end

    /*
    This action is used to load the graphical components of the Control. This is
    handled automatically by the Game engine as needed, and most users shouldn't
    need to use this action directly.
    */
    action LoadGraphics(LayoutProperties properties)
        if properties = undefined
            return now
        end

        View2D view = properties:GetView2D()
        if view = undefined
            if GetView2D() = undefined
                LabelBoxView content
                content:SetName("TabBar View")
                Color color
                ColorGroup background = properties:GetBackgroundColor()
                ColorGroup border = properties:GetBorderColor()
                number thickness = properties:GetBorderThickness()
                if background = undefined
                    background = color:DarkGray()
                end
                if border = undefined
                    border = color:Black()
                end
                content:SetBorderThickness(cast(integer, thickness))
                content:Initialize(background, border)
                view = content
            end
        end

        if view not= undefined
            SetView2D(view)
        end

        parent:Control:LoadGraphics(properties)
    end

    /*
    This action sets the Font to be used by this Control. How this Font is used
    varies between Controls, and in some cases the Control will ignore it 
    entirely (for example, ScrollPanes), but typically this sets the font that's
    used for the default label on many Controls, such as Buttons. If the Font
    is undefined, many Controls will omit their default label.

    Attribute: Parameter font The Font to use for this Control.
    */
    action SetFont(Font font)
        parent:Control:SetFont(font)
        integer counter = 0
        repeat while counter < tabs:GetSize()
            tabs:Get(counter):SetFont(font)
            counter = counter + 1
        end
    end

    /*
    This action sets the base Font size to be used by this Control, if the
    Control uses a Font.
    */
    action SetFontSize(integer size)
        parent:Control:SetFontSize(size)
        integer counter = 0
        repeat while counter < tabs:GetSize()
            tabs:Get(counter):SetFontSize(size)
            counter = counter + 1
        end
    end

    action DeselectAll
        integer i = 0
        repeat while i < tabs:GetSize()
            tabs:Get(i):SetToggleState(false)
            i = i + 1
        end
    end
end