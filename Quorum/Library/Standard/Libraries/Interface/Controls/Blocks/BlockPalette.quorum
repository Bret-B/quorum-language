package Libraries.Interface.Controls.Blocks

use Libraries.Interface.Controls.TabPane
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Forms.Form
use Libraries.Interface.Forms.Page
use Libraries.Interface.Pages.StackedRowPage
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.List
use Libraries.Interface.Controls.Button
use Libraries.Interface.Controls.Blocks.CodeEditor
use Libraries.Interface.Behaviors.Blocks.BlockPaletteInsertBehavior
use Libraries.Game.GameStateManager
use Libraries.Game.Layer2D
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Events.SelectionListener
use Libraries.Language.Compile.CodeCompletionResult
use Libraries.Language.Compile.CodeCompletionRequest
use Libraries.Containers.Array


class BlockPalette is TabPane

    CodeEditor editor = undefined
    Tab basicTab
    Tab controlTab
    Tab actionTab
    Tab suggestionTab
    
    SelectionListener suggestPage

    // Resources used to handle the visual component of dragging-and-dropping palette items.
    GameStateManager manager
    Layer2D draggedItemLayer = undefined
    boolean dragLayerInUse = false
    Drawable draggedItem = undefined

    action Setup
        LayoutProperties properties = GetDefaultLayoutProperties()
        properties:SetHorizontalLayoutMode(properties:FILL)
        properties:SetVerticalLayoutMode(properties:STANDARD)
//        properties:SetPercentageHeight(0.7)
        

        basicTab:SetName("Basic")
        basicTab:DisplayCloseButton(false)
        basicTab:SetRelatedItem(MakeBasicPage())

        controlTab:SetName("Control")
        controlTab:DisplayCloseButton(false)
        controlTab:SetRelatedItem(MakeControlPage())

        actionTab:SetName("Action")
        actionTab:DisplayCloseButton(false)
        actionTab:SetRelatedItem(MakeActionPage())
        
        suggestionTab:SetName("Suggestions")
        suggestionTab:DisplayCloseButton(false)
        suggestionTab:SetRelatedItem(MakeSuggestionPage())

        Add(basicTab)
        Add(controlTab)
        Add(actionTab)
        Add(suggestionTab)
    end

    action SetEditor(CodeEditor editor)
        if me:editor not= undefined
            me:editor:RemoveSelectionListener(suggestPage)
        end
        me:editor = editor
        if editor not= undefined
            editor:AddSelectionListener(suggestPage)
        end
    end

    action GetEditor returns CodeEditor
        return editor
    end

    action GetBasicTab returns Tab
        return basicTab
    end

    action GetControlTab returns Tab
        return basicTab
    end

    action GetActionTab returns Tab
        return basicTab
    end
    
    action GetSuggestionTab returns Tab
        return suggestionTab
    end

    action MakeBasicPage returns Page
        BlockPalettePage page
        page:SetPalette(me)
        text dq = ""
        dq = dq:GetDoubleQuote()
        page:AddBlockPaletteItem("Blank")
        page:AddBlockPaletteItem("integer a = 0")
        page:AddBlockPaletteItem("number b = 0.0")
        page:AddBlockPaletteItem("boolean bool = true")
        page:AddBlockPaletteItem("text string = "+dq+"words"+dq+"")
        page:AddBlockPaletteItem("output "+dq+"words"+dq+"")
        page:AddBlockPaletteItem("say "+dq+"words"+dq+"")
        page:AddBlockPaletteItem("text string = input()")
        page:AddBlockPaletteItem("// my comment")
        
        return page
    end

    action MakeControlPage returns Page
        BlockPalettePage page
        page:SetPalette(me)
        text dq = ""
        dq = dq:GetDoubleQuote()
        lf = dq:GetLineFeed()
        page:AddBlockPaletteItem("if true" + lf + "end")
        page:AddBlockPaletteItem("elseif")
        page:AddBlockPaletteItem("if true"+lf+"else"+lf+"end")
        page:AddBlockPaletteItem("else")
        page:AddBlockPaletteItem("repeat 1 times" + lf + "end")
        page:AddBlockPaletteItem("repeat until true" + lf + "end")
        page:AddBlockPaletteItem("repeat while false" + lf + "end")
        
        return page
    end

    action MakeActionPage returns Page
        BlockPalettePage page
        page:SetPalette(me)
        text dq = ""
        dq = dq:GetDoubleQuote()
        lf = dq:GetLineFeed()
        page:AddBlockPaletteItem("action myAction" + lf + "end")
        page:AddBlockPaletteItem("action myAction(parameter)" + lf + "end")
        page:AddBlockPaletteItem("action myAction returns integer" + lf + "return 0" + lf + "end")
        page:AddBlockPaletteItem("return")
        page:AddBlockPaletteItem("class MyClass" + lf + "end")
        page:AddBlockPaletteItem("use myLibrary")
        
        return page
    end
    
    action MakeSuggestionPage returns Page
        BlockSuggestionPage page
        page:SetPalette(me)
        me:suggestPage = page
        if GetEditor() not= undefined
            GetEditor():AddSelectionListener(page)
        end
        return page
    end

    action SetNewCodeCompletion(CodeCompletionResult result)
        BlockSuggestionPage page = cast(BlockSuggestionPage, suggestionTab:GetRelatedItem())
        page:SetCodeCompletionResult(result)
    end

    action SetNewCodeCompletion(Array<text> list)
        BlockSuggestionPage page = cast(BlockSuggestionPage, suggestionTab:GetRelatedItem())
        page:SetFromList(list)
    end

    action SetSuggestionResult(BlockSuggestionResult result)
        BlockSuggestionPage page = cast(BlockSuggestionPage, suggestionTab:GetRelatedItem())
        page:SetFromResult(result)
    end
    
    action UpdateSuggestion(Block block, CodeCompletionRequest request)
        BlockSuggestionHandler handler
        handler:Request(block, request, me)
    end

    /*
        This action is used when dragging an item from the palette into an editor. When an item
        is dragged a new layer is created so the item can be visible anywhere in the game and above
        other controls in the application. Setting the dragged item to be undefined also removes
        items in the layer so the layer does not persist and obstruct other controls after dragging
        is complete. 
    */
    action SetDraggedItem(Item item)
        // TO-DO: Handle 3D as well

        if draggedItemLayer = undefined
            Layer2D layer
            layer:SetName("Palette Drag Layer2D")
            draggedItemLayer = layer
        end

        if draggedItem not= undefined
            draggedItemLayer:Remove(draggedItem)
        end

        if item = undefined
            if dragLayerInUse
                manager:GetGame():RemoveLayer(draggedItemLayer)
            end

            dragLayerInUse = false
        else
            draggedItemLayer:Add(cast(Item2D, item))

            if dragLayerInUse = false
                manager:GetGame():AddLayer(draggedItemLayer)
            end

            dragLayerInUse = true
        end

        draggedItem = cast(Drawable, item)
    end

    /*
        Returns the item being dragged. 
    */
    action GetDraggedItem returns Item
        return draggedItem
    end

end