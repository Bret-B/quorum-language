package Libraries.Interface.Undo

use Libraries.Interface.Controls.Blocks.CodeEditor
use Libraries.Interface.Controls.Blocks.Block
use Libraries.Containers.MultipleLineText
use Libraries.Interface.Events.TextChangeEvent
use Libraries.Interface.Controls.Blocks.AssignmentBlock
use Libraries.Interface.Controls.Blocks.EditRegion
use Libraries.Interface.Controls.Blocks.EditBox
use Libraries.Interface.Controls.Blocks.EditField


class BlockInsertEdit is Edit
    text value = ""
    text deletedText = ""
    CodeEditor editor = undefined
    integer characterIndex = -1

    action GetEditor returns CodeEditor
        return editor
    end

    action SetEditor(CodeEditor editor)
        me:editor = editor
    end

    action SetCharacterIndex(integer index)
        me:characterIndex = index
    end

    action GetCharacterIndex returns integer
        return characterIndex
    end

    action SetText(text insertedText)
        me:value = insertedText
    end

    action GetText returns text
        return value
    end

    action SetDeletedText(text deletedText)
        me:deletedText = deletedText
    end

    action GetDeletedText returns text
        return deletedText
    end

    action Undo
        EditRegion region = editor:GetEditRegionAtTextIndex(characterIndex)
        EditRegion region2 = editor:GetEditRegionAtTextIndex(characterIndex + value:GetSize())
        if region not= undefined and region2 not=undefined and region = region2
            integer globalIndex = region:GetBlock():GetStartIndex() + region:GetStartIndex()
            if region is EditBox
                EditBox box = cast(EditBox, region)
                box:SetCaretPosition(characterIndex - globalIndex)
                box:Delete(characterIndex - globalIndex, characterIndex - globalIndex + value:GetSize())
                if deletedText not= ""
                    box:Insert(characterIndex - globalIndex, deletedText)
                    box:Select(characterIndex - globalIndex, characterIndex - globalIndex + deletedText:GetSize())
                end
            elseif region is EditField
                EditField box = cast(EditField, region)
                box:SetCaretPosition(characterIndex - globalIndex)
                box:Delete(characterIndex - globalIndex, characterIndex - globalIndex + value:GetSize())
                if deletedText not= ""
                    box:Insert(characterIndex - globalIndex, deletedText)
                    box:Select(characterIndex - globalIndex, characterIndex - globalIndex + deletedText:GetSize())
                end
            end
            return now
        end
        
        TextChangeEvent event
        event:Initialize(editor, deletedText,value, characterIndex)
        editor:OnTextChange(event, 0)
        editor:RegenerateBlocks()
        integer line = editor:GetLineIndex(characterIndex)
        editor:SelectBlockAtTextLine(line)
    end

    action Redo
        EditRegion region = editor:GetEditRegionAtTextIndex(characterIndex)
        EditRegion region2 = editor:GetEditRegionAtTextIndex(characterIndex + deletedText:GetSize())
        if region not= undefined and region2 not=undefined and region = region2
            integer globalIndex = region:GetBlock():GetStartIndex() + region:GetStartIndex()
            if region is EditBox
                EditBox box = cast(EditBox, region)
                box:SetCaretPosition(characterIndex - globalIndex)
                if deletedText not= ""
                    box:Select(characterIndex - globalIndex, characterIndex - globalIndex + deletedText:GetSize())
                end
                box:Insert(characterIndex - globalIndex, value)
            elseif region is EditField
                EditField box = cast(EditField, region)
                box:SetCaretPosition(characterIndex - globalIndex)
                if deletedText not= ""
                    box:Select(characterIndex - globalIndex, characterIndex - globalIndex + deletedText:GetSize())
                end
                box:Insert(characterIndex - globalIndex, value)
            end
            return now
        end
        TextChangeEvent event
        event:Initialize(editor, value,deletedText, characterIndex)
        editor:OnTextChange(event, 0)

        editor:RegenerateBlocks()
        integer line = editor:GetLineIndex(characterIndex)
        editor:SelectBlockAtTextLine(line)
    end

end