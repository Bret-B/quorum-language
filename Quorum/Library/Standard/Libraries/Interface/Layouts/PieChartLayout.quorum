package Libraries.Interface.Layouts

use Libraries.Interface.Controls.Control
use Libraries.Interface.Controls.Charts.Chart
use Libraries.Interface.Controls.Charts.PieChart
use Libraries.Interface.Controls.Charts.Graphics.PieBox
use Libraries.Interface.Controls.Charts.Graphics.PieSlice
use Libraries.Interface.Controls.ControlLabel
use Libraries.Game.Graphics.Label
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Color
use Libraries.Compute.Math
use Libraries.Containers.Array

/*
    This class determines the visual layout for a pie chart.

    Attribute: Author Hannah Williams

    Attribute: Example

    use Libraries.Compute.Statistics.DataFrame
    use Libraries.Compute.Statistics.Charts.Creator
    use Libraries.Interface.Controls.Charts.BoxPlot
    use Libraries.Game.Game
    
    class Main is Game
        action Main
            StartGame()
        end
    
        action CreateGame
            DataFrame frame
            frame:Load("Data/Data.csv")
            BoxPlotCreator creator
            creator:AddColumn(2)
        
            BoxPlot chart = cast(BoxPlot,frame:CreateChart(creator))
            chart:SetPercentageWidth(1)
            chart:SetPercentageHeight(1)
            chart:SetTitle("Sample Title")
            chart:SetXAxisTitle("X-axis Variable")
            chart:SetYAxisTitle("Y-axis Variable")
            Add(chart)
        end
    end
*/

class PieChartLayout is ChartLayout
    Color transparent
    Math math

    action LayoutChartContent(Chart control, Control chartAreaContainer)
        if not (control is PieChart)
            return now
        end

        PieChart chart = cast(PieChart, control)
        PieBox pieBox = chart:GetPieBoxes():Get(0)

        if chart:IsSeparated()
            integer i = 0
            repeat while i < chart:GetPieBoxes():GetSize()
                if chart:GetPieBoxes():Get(i):GetName() = chartAreaContainer:GetName()
                    pieBox = chart:GetPieBoxes():Get(i)
                end
                i = i + 1
            end
        end

        if pieBox not= undefined
            Array<PieSlice> slices = pieBox:GetSlices()
            number width = chartAreaContainer:GetWidth()
            number height = chartAreaContainer:GetHeight()
            number donutHolePercent = chart:GetDonutHolePercent()
    
            integer middleX = cast(integer, width/2)
            integer middleY = cast(integer, height/2)
            integer radius = middleY
            pieBox:SetPosition(middleX - radius, 0)
            if width < height
                radius = middleX
                pieBox:SetPosition(0, middleY - radius)
            end
            transparent:SetColor(0, 0, 0, 0)
            pieBox:LoadFilledRectangle(radius*2, radius*2, transparent)
    
            number currentTheta = 90 // This starts it at the top
            number currentPercent = 0
            integer i = 0
            repeat while i < slices:GetSize()
                PieSlice slice = slices:Get(i)
                number percent = math:Round(slice:GetValue()/cast(number, pieBox:GetValue()), 5)
                slice:Load(slice:GetColor())
                slice:SetSize(pieBox:GetWidth(), pieBox:GetHeight())
                slice:SetOuterRadius(radius)
                slice:SetInnerRadius(cast(integer, donutHolePercent*radius))
                
                slice:SetStartPercentage(currentPercent)
                currentPercent = currentPercent + percent
                slice:SetEndPercentage(currentPercent)
                pieBox:Add(slice)

                if slice:ShowAnnotation() or chart:ShowAnnotations() 
                    ControlLabel label = slice:GetAnnotation()
                    currentTheta = currentTheta - (percent/2) * 360 // Travel half way along the arc to place label
                    if label not= undefined
                        integer x = cast(integer, middleX + radius * math:Cosine(math:DegreesToRadians(currentTheta)))
                        integer y = cast(integer, middleY + radius * math:Sine(math:DegreesToRadians(currentTheta)))
                        number degrees = currentTheta mod 360
                        if degrees < 0
                            degrees = 360 + degrees
                        end
                        if degrees >= 0 and degrees < 90
                            label:SetPosition(x, y)
                        elseif degrees >= 90 and degrees < 180
                            label:SetPosition(x-label:GetWidth(), y)
                        elseif degrees >= 180 and degrees < 270
                            label:SetPosition(x-label:GetWidth(), y-label:GetHeight())
                        else
                            label:SetPosition(x, y-label:GetHeight())
                        end
                    end
                    currentTheta = currentTheta - (percent/2) * 360 // Account for the remaining half of the arc
                end
                i = i + 1
            end
        end
    end
end