package Libraries.Interface.Layouts

use Libraries.Interface.Item2D
use Libraries.Containers.Array
use Libraries.Interface.Controls.Control
use Libraries.Interface.Controls.Dialog
use Libraries.Interface.Controls.Cell

class ManualLayout is Layout

    boolean layoutContainer = false
    boolean ignoreInterfaceScale = false

    action ApplyToContainer(boolean shouldLayout)
        layoutContainer = shouldLayout
    end

    action IsAppliedToContainer returns boolean
        return layoutContainer
    end

    action SetIgnoreInterfaceScale(boolean ignore)
        ignoreInterfaceScale = ignore
    end

    action IsIgnoringInterfaceScale returns boolean
        return ignoreInterfaceScale
    end

    action Layout(Control container, Array<Item2D> items, number containerWidth, number containerHeight)

        if layoutContainer
            container:ResetLayoutFlag()
            LayoutProperties properties = container:GetLayoutProperties(containerWidth, containerHeight)
            Item2D containerParent = container:GetParent()

            if properties not= undefined
                if properties:NeedsRendering()
//                    output "Loading container graphics " + container:GetName()
                    container:LoadGraphics(properties)
                end

                number width = container:GetWidth()
                number height = container:GetHeight()
                number parentWidth = 0
                number parentHeight = 0
                if containerParent not= undefined
                    parentWidth = containerParent:GetWidth()
                    parentHeight = containerParent:GetHeight()
                end

                number interfaceScale = properties:GetInterfaceScale()
                if ignoreInterfaceScale
                    interfaceScale = 1.0
                end

                if properties:IsSettingWidth()
                    width = parentWidth * properties:GetPercentageWidth() + properties:GetPixelWidth() * interfaceScale
                end

                if properties:IsSettingHeight()
                    height = parentHeight * properties:GetPercentageHeight() + properties:GetPixelHeight() * interfaceScale
                end

                if width < properties:GetMinimumWidth()
                    width = properties:GetMinimumWidth()
                elseif width > properties:GetMaximumWidth()
                    width = properties:GetMaximumWidth()
                end

                if height < properties:GetMinimumHeight()
                    height = properties:GetMinimumHeight()
                elseif height > properties:GetMaximumHeight()
                    height = properties:GetMaximumHeight()
                end

                if properties:IsPositioningInX()
                    if properties:IsPositioningInY()
                        number targetX = parentWidth * properties:GetPercentageX() + properties:GetPixelX() - width * properties:GetPercentageOriginX()
                        number targetY = parentHeight * properties:GetPercentageY() + properties:GetPixelY() - height * properties:GetPercentageOriginY()
                        container:SetPosition(targetX, targetY)
                    else
                        number targetX = parentWidth * properties:GetPercentageX() + properties:GetPixelX() - width * properties:GetPercentageOriginX()
                        container:SetX(targetX)
                    end
                elseif properties:IsPositioningInY()
                    number targetY = parentHeight * properties:GetPercentageY() + properties:GetPixelY() - height * properties:GetPercentageOriginY()
                    container:SetY(targetY)
                end

                if properties:IsSettingWidth()
                    if properties:IsSettingHeight()
                        container:SetSize(width, height)
                    else
                        container:SetWidth(width)
                    end
                elseif properties:IsSettingHeight()
                    container:SetHeight(height)
                end
            end
        end

        integer counter = 0

        repeat while counter < items:GetSize()
            Item2D item = items:Get(counter)
            if item is Control
                Control control = cast(Control, item)
                control:ResetLayoutFlag()
                LayoutProperties properties = control:GetLayoutProperties(containerWidth, containerHeight)

                if properties not= undefined
                    if properties:NeedsRendering()
//                        output "Loading Control graphics " + control:GetName()
                        control:LoadGraphics(properties)
                    end

                    number width = control:GetWidth()
                    number height = control:GetHeight()

                    number interfaceScale = properties:GetInterfaceScale()
                    if ignoreInterfaceScale
                        interfaceScale = 1.0
                    end

                    if properties:IsSettingWidth()
                        width = containerWidth * properties:GetPercentageWidth() + properties:GetPixelWidth() * interfaceScale
                    end

                    if properties:IsSettingHeight()
                        height = containerHeight * properties:GetPercentageHeight() + properties:GetPixelHeight() * interfaceScale
                    end

                    if width < properties:GetMinimumWidth()
                        width = properties:GetMinimumWidth()
                    elseif width > properties:GetMaximumWidth()
                        width = properties:GetMaximumWidth()
                    end

                    if height < properties:GetMinimumHeight()
                        height = properties:GetMinimumHeight()
                    elseif height > properties:GetMaximumHeight()
                        height = properties:GetMaximumHeight()
                    end

                    if properties:IsPositioningInX()
                        if properties:IsPositioningInY()
                            number targetX = containerWidth * properties:GetPercentageX() + properties:GetPixelX() - width * properties:GetPercentageOriginX()
                            number targetY = containerHeight * properties:GetPercentageY() + properties:GetPixelY() - height * properties:GetPercentageOriginY()
                            control:SetPosition(targetX, targetY)
                        else
                            number targetX = containerWidth * properties:GetPercentageX() + properties:GetPixelX() - width * properties:GetPercentageOriginX()
                            control:SetX(targetX)
                        end
                    elseif properties:IsPositioningInY()
                        number targetY = containerHeight * properties:GetPercentageY() + properties:GetPixelY() - height * properties:GetPercentageOriginY() 
                        control:SetY(targetY)
                    end

                    if properties:IsSettingWidth()
                        if properties:IsSettingHeight()
                            control:SetSize(width, height)
                        else
                            control:SetWidth(width)
                        end
                    elseif properties:IsSettingHeight()
                        control:SetHeight(height)
                    else
                        /*
                        Setting the size of a control triggers its Resize. Even
                        if we're not changing its size, we need to ensure that
                        the Resize propagates all the way through the children
                        hierarchy.
                        */
                        control:Resize()
                    end
                end
            end

            counter = counter + 1
        end
    end

end