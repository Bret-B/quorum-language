package Libraries.Interface.Layouts
use Libraries.Interface.Controls.Charts.Chart
use Libraries.Interface.Controls.Control
use Libraries.Interface.Controls.Charts.ScatterPlot
use Libraries.Game.Graphics.Drawable
use Libraries.Containers.Array
use Libraries.Interface.Controls.Charts.ChartPoint
use Libraries.Interface.Controls.Icon

class ScatterPlotLayout is TwoVariableChartLayout
    action LayoutChartContent(Chart control, Control chartAreaContainer)
        if not (control is ScatterPlot)
            return now
        end
        
        ScatterPlot chart = cast(ScatterPlot, control)
        Array<ChartPoint> points = chart:GetChartPoints()
        if points = undefined
            return now
        end
        
        number width = chartAreaContainer:GetWidth() - 2*chart:GetHorizontalPanel():GetPaddingOffset()
        number height = chartAreaContainer:GetHeight() - 2*chart:GetVerticalPanel():GetPaddingOffset()
        number xMin = chart:GetXAxisMinimum()
        number xMax = chart:GetXAxisMaximum()
        number yMin = chart:GetYAxisMinimum()
        number yMax = chart:GetYAxisMaximum()

        // Set default point density
        integer pointRadius = chart:GetPointDensity()
        if pointRadius = 0
            pointRadius = cast(integer, width * 0.010 / 2) 
            if pointRadius < 4
                pointRadius = 4
            end
        end

        i = 0
        repeat while i < points:GetSize()
            ChartPoint point = points:Get(i)
            
            point:LoadFilledCircle(pointRadius, points:Get(i):GetColor())

            point:SetPercentX((point:GetValueX() - xMin) / (xMax - xMin))
            point:SetPercentY((point:GetValueY() - yMin) / (yMax - yMin))
            integer x = cast(integer, point:GetPercentX() * width - (point:GetWidth() / 2.0)) + chart:GetHorizontalPanel():GetPaddingOffset()
            integer y = cast(integer, point:GetPercentY() * height - (point:GetHeight() / 2.0)) + chart:GetVerticalPanel():GetPaddingOffset()
            point:SetPosition(x, y)
            i = i + 1
        end

        if chart:IsShowingLinearRegression()
            LayoutRegressionLines(chart, chartAreaContainer)
        end
    end
end