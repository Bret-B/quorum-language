package Libraries.Interface.Layouts

use Libraries.Interface.Layouts.Layout

use Libraries.Interface.Controls.Control
use Libraries.Containers.Array
use Libraries.Interface.Item2D
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Game.Graphics.Label
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Controls.Charts.Legend
use Libraries.Interface.Controls.Charts.Histogram
use Libraries.Interface.Controls.ControlLabel
use Libraries.Interface.Controls.Charts.SharedBarChartParent
use Libraries.Interface.Controls.Charts.Chart
use Libraries.Game.Graphics.Color
use Libraries.Compute.Math
use Libraries.Interface.Controls.Charts.HorizontalAxisPanel
use Libraries.Interface.Controls.Charts.VerticalAxisPanel
use Libraries.Interface.Controls.Charts.Series


class ChartLayout is Layout
    integer minimumLabelOffset = 35
    number yLabelHeight = 0.30
    Math math

    action Layout(Control container, Array<Item2D> items, number containerWidth, number containerHeight)
        LayoutProperties containerProperties
        integer originPointX = 0
        integer originPointY = 0
        
        if container not= undefined
            containerProperties = container:GetLayoutProperties(containerWidth, containerHeight)
            if containerProperties not= undefined
                if containerProperties:NeedsRendering()
                    container:LoadGraphics(containerProperties)
                end
            end
            if container is Chart
                if items:GetSize() = 0
                    return now
                end
                Chart chart = cast(Chart, container)
                
                //setting an origin to position containers 
                originPointX = cast(integer, chart:GetWidth()*chart:GetVerticalPanelWidthPercent())
                originPointY = cast(integer, chart:GetHeight()*chart:GetHorizontalPanelHeightPercent())

                // Title
                ControlLabel titleLabel = chart:GetTitleLabel()
                if titleLabel:GetDefaultLayoutProperties():NeedsRendering()
                    titleLabel:LoadGraphics(titleLabel:GetDefaultLayoutProperties())
                end
                number titleAreaWidth = chart:GetChartArea():GetWidth()
                number titleAreaHeight = containerHeight*0.10 //hardcoded right now.
                integer titleX = originPointX + cast(integer, titleAreaWidth/2 - titleLabel:GetWidth()/2)
                integer titleY = cast(integer, containerHeight- titleAreaHeight/2 - titleLabel:GetHeight()/2)
                titleLabel:SetPosition(titleX, titleY)
                
                //Get Panel Containers
                HorizontalAxisPanel horizontalContainer = chart:GetHorizontalPanel()
                VerticalAxisPanel verticalContainer = chart:GetVerticalPanel()
                Legend legendContainer = chart:GetLegend()
                Control chartAreaContainer = chart:GetChartArea()

                originPointX = 0
                originPointY = 0

                if legendContainer:DisplayOnRight() // This is default.. even when legend is not showing.
                    verticalContainer:SetPosition(originPointX, originPointY + horizontalContainer:GetHeight())
                    chartAreaContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY + horizontalContainer:GetHeight())
                    legendContainer:SetPosition(originPointX + verticalContainer:GetWidth() + chartAreaContainer:GetWidth(), originPointY + horizontalContainer:GetHeight())

                    horizontalContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY)
                    
                elseif legendContainer:DisplayOnLeft()
                    if chart:IsShowingYAxis()
                        legendContainer:SetPosition(originPointX, originPointY + horizontalContainer:GetHeight())
                        verticalContainer:SetPosition(originPointX + legendContainer:GetWidth(), originPointY + horizontalContainer:GetHeight())
                    else
                        verticalContainer:SetPosition(originPointX, originPointY + horizontalContainer:GetHeight())
                        legendContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY + horizontalContainer:GetHeight())
                    end

                    chartAreaContainer:SetPosition(originPointX + legendContainer:GetWidth() + verticalContainer:GetWidth(), originPointY + horizontalContainer:GetHeight())                 
                    horizontalContainer:SetPosition(originPointX + legendContainer:GetWidth() + verticalContainer:GetWidth(), originPointY)

                elseif legendContainer:DisplayOnTop()
                    verticalContainer:SetPosition(originPointX, originPointY + horizontalContainer:GetHeight())
                    chartAreaContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY + horizontalContainer:GetHeight())
                    
                    horizontalContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY)
                    legendContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY + horizontalContainer:GetHeight() + verticalContainer:GetHeight())

                else // legendContainer:DisplayOnBottom()
                    verticalContainer:SetPosition(originPointX, originPointY + legendContainer:GetHeight() + horizontalContainer:GetHeight())
                    chartAreaContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY + legendContainer:GetHeight() + horizontalContainer:GetHeight())
                    
                    if chart:IsShowingXAxis()
                        horizontalContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY + legendContainer:GetHeight())
                        legendContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY)
                    else
                        legendContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY + horizontalContainer:GetHeight())
                        horizontalContainer:SetPosition(originPointX + verticalContainer:GetWidth(), originPointY)
                    end
                end
                

                //panel Chart Area origin
                originPointX = 0
                originPointY = 0

                LayoutChartContent(chart, chartAreaContainer)

                if chart:IsShowingLegend()
                    LayoutLegend(chart, legendContainer)
                end

                if chart:IsShowingXAxis()
                    LayoutHorizontalAxis(chart, horizontalContainer)
                end

                if chart:IsShowingYAxis()
                    LayoutVerticalAxis(chart, verticalContainer)
                end
                
            end

            //Reset Layout flags so Layout doesn't called more than needed
            ResetLayoutFlags(container)
        end
    end

    action LayoutVerticalAxis(Chart chart, VerticalAxisPanel verticalContainer)
        boolean usingGrouping = (chart:GetGroupPanels():GetSize() > 0) and verticalContainer:UseTextLabelOverride()
        originPointX = cast(integer, verticalContainer:GetWidth())
        originPointY = 0
        Color color

        //This is the ticks on the Y axis
        number groupOffset = 0
        number scaleDivWidth = 0      
        integer tickHeight = 0
        integer tickWidth = 0
        Array<Drawable> ticks = verticalContainer:GetTicks()
        Array<Drawable> brackets = verticalContainer:GetBrackets()
        Array<Drawable> lines = verticalContainer:GetMajorGridlines()
        Array<Drawable> minorlines = verticalContainer:GetMinorGridlines()

        if not ticks:IsEmpty()
            Drawable currentTick = ticks:Get(0)
            tickHeight = cast(integer, currentTick:GetHeight())
            tickWidth = cast(integer, currentTick:GetWidth())

            Drawable axis = verticalContainer:GetAxis()
            axis:LoadFilledRectangle(tickHeight, cast(integer, chart:GetChartArea():GetHeight()))
            axis:SetPosition(0, 0) //This line is in the chart area.
            if usingGrouping
                groupOffset = cast(number, chart:GetGroupPanels():Get(0):GetHeight())/2.0
                originPointY = cast(integer, groupOffset) + chart:GetPaddedChartOffset()
                scaleDivWidth = cast(number, chart:GetGroupPanels():Get(0):GetHeight())
                chart:HideAllYGridLines()
            else
                scaleDivWidth = (axis:GetHeight() - ticks:Get(0):GetHeight()) / (ticks:GetSize() - 1)
            end
            
            number minorScaleDivWidth = scaleDivWidth/verticalContainer:GetMinorGridlineCount()
            integer bracketY = 0
            integer tickX = originPointX - tickWidth
            number tickY = originPointY

            integer i = 0
            integer j = 0
            repeat while i < ticks:GetSize()
                if usingGrouping
                    scaleDivWidth = cast(number, chart:GetGroupPanels():Get(i):GetHeight())
                    if verticalContainer:ShowGroupBrackets()
                        bracketY = cast(integer, chart:GetGroupPanels():Get(i):GetY())
                        if brackets:GetSize() >= (i*3)+2
                            integer index = i*3
                            brackets:Get(index):LoadFilledRectangle(tickWidth, tickHeight)
                            brackets:Get(index):SetPosition(tickX-tickHeight, bracketY+tickHeight)
                            
                            brackets:Get(index+1):LoadFilledRectangle(tickHeight, cast(integer, scaleDivWidth)-tickHeight*2)
                            brackets:Get(index+1):SetPosition(tickX-tickHeight, bracketY+tickHeight)

                            bracketY = bracketY + cast(integer, scaleDivWidth)

                            brackets:Get(index+2):LoadFilledRectangle(tickWidth, tickHeight)
                            brackets:Get(index+2):SetPosition(tickX-tickHeight, bracketY-tickHeight*2)
                            tickX = tickX - tickHeight - tickWidth
                        end
                    end
                end
                currentTick = ticks:Get(i)
                currentTick:SetPosition(tickX, cast(integer, tickY))

                Drawable majorLine
                if chart:IsShowingMajorYGridLines() and i < lines:GetSize()
                    if i = 0  // This will not draw the gridline that overlaps with the x-axis
                        if not chart:IsShowingXAxis()
                            majorLine = lines:Get(i)
                        end
                    else
                        majorLine = lines:Get(i)
                    end
                end
                integer thin = cast(integer, currentTick:GetHeight()*0.75)
                majorLine:SetPosition(axis:GetWidth(), cast(integer, tickY+(thin/2)))
                majorLine:LoadFilledRectangle(cast(integer, chart:GetChartArea():GetWidth() - axis:GetWidth()), thin, color:LightGray())

                if chart:IsShowingMinorYGridLines() and j < minorlines:GetSize() 
                    integer thinner = cast(integer, majorLine:GetHeight()/2)
                    number minorY = majorLine:GetY() + minorScaleDivWidth

                    repeat while j < (i+1)*verticalContainer:GetMinorGridlineCount() and minorY < axis:GetHeight()
                        Drawable minorLine = minorlines:Get(j)
                        minorLine:SetPosition(axis:GetWidth(), cast(integer, minorY+thinner))
                        minorLine:LoadFilledRectangle(cast(integer, chart:GetChartArea():GetWidth() - axis:GetWidth()), thinner, color:LightGray())
                        j = j + 1
                        minorY = minorY + minorScaleDivWidth
                    end
                end

                tickX = originPointX - tickWidth
                tickY = tickY + (scaleDivWidth*verticalContainer:GetIntervalOverride())
                i = i + 1
            end
        end

        //This code represents the labels on the Y axis 
        originPointX = cast(integer, verticalContainer:GetWidth())
        originPointY = 0
        if usingGrouping
            originPointY = cast(integer, groupOffset) + chart:GetPaddedChartOffset()
            if verticalContainer:ShowGroupBrackets()
                originPointX = originPointX - tickHeight - tickWidth
            end
        end
        integer labelX = 0
        integer labelY = 0
        number labelMinimumX = originPointX
        Array<ControlLabel> scaleLabels = verticalContainer:GetLabels()
        if not scaleLabels:IsEmpty()
            ControlLabel currentLabel

            integer i = 0
            repeat while i < scaleLabels:GetSize()
                currentLabel = scaleLabels:Get(i) 
                if currentLabel:GetDefaultLayoutProperties():NeedsRendering()
                    currentLabel:LoadGraphics(currentLabel:GetDefaultLayoutProperties())
                end

                labelX = cast(integer, originPointX - currentLabel:GetWidth() - (1.5*tickWidth))
                labelY = cast(integer, originPointY - (currentLabel:GetHeight()/2) + (tickHeight/2)  + ((scaleDivWidth*verticalContainer:GetIntervalOverride())*i))

                currentLabel:SetPosition(labelX , labelY)

                if labelX < labelMinimumX
                    labelMinimumX = labelX
                end
                i = i + 1
            end
        end

        //This is the actual rotated label on the Y axis.      
        originPointX = cast(integer, labelMinimumX)
        originPointY = 0
        if verticalContainer:GetTitleLabel() not= undefined
            ControlLabel axisLabel = verticalContainer:GetTitleLabel()
            Item2D labelAnchor = axisLabel:GetParent()
            if axisLabel:GetDefaultLayoutProperties():NeedsRendering()       
                axisLabel:LoadGraphics(axisLabel:GetDefaultLayoutProperties())
            end

            number axisLabelWidth = axisLabel:GetWidth()
            number axisLabelHeight = axisLabel:GetHeight()

            integer titleX = cast(integer, labelMinimumX - (1.5*tickWidth))
            integer titleY = cast(integer, (verticalContainer:GetHeight() - axisLabelWidth) / 2.0)

            labelAnchor:SetPosition(titleX, titleY)
            labelAnchor:SetRotation(270)
        end
    end

    /*
        Chart layouts do not have a way to determine the ticks of a subclass because charts are very different in style.
        As such, every chart needs to override this with ticks relevant for its own chart type.
    */
    action LayoutHorizontalAxis(Chart chart, HorizontalAxisPanel horizontalContainer)
        boolean usingGrouping = (chart:GetGroupPanels():GetSize() > 0) and horizontalContainer:UseTextLabelOverride()
        originPointX = 0
        originPointY = cast(integer, horizontalContainer:GetHeight())
        Color color

        // This is the ticks on the X axis
        number groupOffset = 0
        number scaleDivWidth = 0      
        integer tickHeight = 0
        integer tickWidth = 0
        Array<Drawable> ticks = horizontalContainer:GetTicks()
        Array<Drawable> brackets = horizontalContainer:GetBrackets()
        Array<Drawable> lines = horizontalContainer:GetMajorGridlines()
        Array<Drawable> minorlines = horizontalContainer:GetMinorGridlines()

        if not ticks:IsEmpty()   
            Drawable currentTick = ticks:Get(0)
            tickHeight = cast(integer, currentTick:GetHeight())
            tickWidth = cast(integer, currentTick:GetWidth())
 
            Drawable axis = horizontalContainer:GetAxis()
            axis:LoadFilledRectangle(cast(integer, chart:GetChartArea():GetWidth()), tickWidth)
            axis:SetPosition(0, 0)

            if usingGrouping
                groupOffset = cast(number, chart:GetGroupPanels():Get(0):GetWidth())/2.0
                originPointX = cast(integer, groupOffset) + chart:GetPaddedChartOffset()
                scaleDivWidth = cast(number, chart:GetGroupPanels():Get(0):GetWidth())
                chart:HideAllXGridLines()
            else
                scaleDivWidth = (axis:GetWidth() - tickWidth) / (ticks:GetSize() - 1)
            end
            number minorScaleDivWidth = scaleDivWidth/horizontalContainer:GetMinorGridlineCount()
            integer bracketX = 0
            number tickX = originPointX
            integer tickY = originPointY - tickHeight       

            integer i = 0
            integer j = 0
            repeat while i < ticks:GetSize()
                if usingGrouping
                    scaleDivWidth = cast(number, chart:GetGroupPanels():Get(i):GetWidth())
                    if horizontalContainer:ShowGroupBrackets()
                        bracketX = cast(integer, chart:GetGroupPanels():Get(i):GetX())
                        if brackets:GetSize() >= (i*3)+2
                            integer index = i*3
                            brackets:Get(index):LoadFilledRectangle(tickWidth, tickHeight)
                            brackets:Get(index):SetPosition(bracketX+tickWidth, tickY- tickWidth)
                            
                            brackets:Get(index+1):LoadFilledRectangle(cast(integer, scaleDivWidth)-tickWidth*2, tickWidth)
                            brackets:Get(index+1):SetPosition(bracketX+tickWidth, tickY- tickWidth)

                            bracketX = bracketX + cast(integer, scaleDivWidth)

                            brackets:Get(index+2):LoadFilledRectangle(tickWidth, tickHeight)
                            brackets:Get(index+2):SetPosition(bracketX-tickWidth*2, tickY- tickWidth)
                            tickY = tickY - tickHeight - tickWidth
                        end
                    end
                end
                currentTick = ticks:Get(i)
                currentTick:SetPosition(cast(integer, tickX-(tickWidth/2)), tickY)

                Drawable majorLine
                if chart:IsShowingMajorXGridLines() and i < lines:GetSize()
                    majorLine = lines:Get(i)
                end
                integer thin = cast(integer, currentTick:GetWidth()*0.75)
                majorLine:SetPosition(cast(integer, tickX+(thin/2)), axis:GetHeight())
                majorLine:LoadFilledRectangle(thin, cast(integer, chart:GetChartArea():GetHeight() - axis:GetHeight()), color:LightGray())

                if chart:IsShowingMinorXGridLines() and j < minorlines:GetSize() 
                    integer thinner = cast(integer, majorLine:GetWidth()/2)
                    number minorX = majorLine:GetX() + minorScaleDivWidth

                    repeat while j < (i+1)*horizontalContainer:GetMinorGridlineCount() and minorX < axis:GetWidth()
                        Drawable minorLine = minorlines:Get(j)
                        minorLine:SetPosition(cast(integer, minorX+thinner), axis:GetHeight())
                        minorLine:LoadFilledRectangle(thinner, cast(integer, chart:GetChartArea():GetHeight() - axis:GetHeight()), color:LightGray())
                        j = j + 1
                        minorX = minorX + minorScaleDivWidth
                    end
                end
                tickX = tickX + (scaleDivWidth*horizontalContainer:GetIntervalOverride())
                tickY = originPointY - tickHeight
                i = i + 1
            end
        end

        // This code represents the labels on the X axis 
        originPointX = 0
        if usingGrouping
            originPointX = cast(integer, groupOffset) + chart:GetPaddedChartOffset()
        end
        originPointY = originPointY - tickHeight
        if horizontalContainer:ShowGroupBrackets()
            originPointY = originPointY - tickHeight - tickWidth
        end

        number labelWidth = 0
        number labelHeight = 0
        number labelMinimumY = originPointY
        
        integer degrees = chart:GetXLabelsRotation() mod 360
        number xLabelSpace = horizontalContainer:GetSumOfLabelWidths() + horizontalContainer:GetLabels():GetSize()*5 + groupOffset
        if xLabelSpace > cast(number, chart:GetChartArea():GetWidth()) and not chart:GetRotationOverride()
            degrees = 270 //If labels overlap, the default will tilt them 90 degrees.
        end
        number cos = math:Cosine(math:DegreesToRadians(degrees))
        number sin = math:Sine(math:DegreesToRadians(degrees))

        integer maxXLabelOffset = 0
        Array<ControlLabel> xLabels = horizontalContainer:GetLabels()
        if not xLabels:IsEmpty()
            ControlLabel currentLabel = xLabels:Get(0)
            Item2D labelAnchor = currentLabel:GetParent()
            if currentLabel:GetDefaultLayoutProperties():NeedsRendering()
                currentLabel:LoadGraphics(currentLabel:GetDefaultLayoutProperties())
            end

            integer i = 0
            repeat while i < xLabels:GetSize()
                currentLabel = xLabels:Get(i)
                labelAnchor = currentLabel:GetParent()
                if currentLabel:GetDefaultLayoutProperties():NeedsRendering()
                    currentLabel:LoadGraphics(currentLabel:GetDefaultLayoutProperties())
                end
                labelWidth = currentLabel:GetWidth()
                labelHeight = currentLabel:GetHeight()

                integer xCurrentLabel = cast(integer, originPointX + ((scaleDivWidth*horizontalContainer:GetIntervalOverride()) * i) - (labelWidth / 2))
                integer yCurrentLabel = cast(integer, originPointY - labelHeight)
                if degrees not= 0
                    number percent = cast(number, degrees mod 90)/90.0 
                    integer startx = cast(integer, xCurrentLabel + (labelWidth/2))
                    integer starty = yCurrentLabel
                    integer deltaX = 0
                    integer deltaY = 0                

                    if degrees > 0 and degrees < 90
                        deltaY = cast(integer, percent*labelHeight)
                        if (starty - (labelWidth)*sin)  < labelMinimumY
                            labelMinimumY = (starty - (labelWidth)*sin)
                        end
                    elseif degrees = 90
                        deltaX = cast(integer, (labelHeight/2))
                        deltaX = 0
                        deltaY = cast(integer, labelHeight)
                        if (starty - labelWidth) < labelMinimumY
                            labelMinimumY = (starty - labelWidth)
                        end
                    elseif degrees > 90 and degrees < 180
                        deltaY = cast(integer, labelHeight)
                        if (starty - (labelWidth)*sin - (labelHeight)*sin)  < labelMinimumY
                            labelMinimumY = (starty - (labelWidth)*sin - (labelHeight)*sin)
                        end
                    elseif degrees = 180
                        deltaX = cast(integer, (labelWidth/2))
                        deltaY = cast(integer, labelHeight)
                        if (starty - labelHeight)  < labelMinimumY
                            labelMinimumY = (starty - labelHeight)
                        end
                    elseif degrees > 180 and degrees < 270
                        deltaX = cast(integer, -(labelWidth*cos))
                        deltaY = cast(integer, labelWidth*sin + labelHeight*sin + labelHeight)
                        if (starty + deltaY + labelHeight*sin)  < labelMinimumY
                            labelMinimumY = (starty + deltaY + labelHeight*sin) - labelHeight/2
                        end
                    elseif degrees = 270
                        deltaX = cast(integer, (labelHeight/2) + (tickWidth/2))
                        deltaY = cast(integer, -labelWidth + labelHeight - (0.5*tickHeight))
                    elseif degrees > 270 and degrees < 360
                        deltaX = cast(integer, -(labelWidth*cos))
                        deltaY = cast(integer, labelWidth*sin + labelHeight*sin + labelHeight)
                        if (starty + deltaY) < labelMinimumY
                            labelMinimumY = (starty + deltaY - labelHeight/2)
                        end
                    end

                    xCurrentLabel = cast(integer, startx + deltaX)
                    yCurrentLabel = cast(integer, starty + deltaY)
                end
                labelAnchor:SetPosition(xCurrentLabel, yCurrentLabel)
                labelAnchor:SetRotation(degrees)
                if labelAnchor:GetY() < labelMinimumY
                    labelMinimumY = labelAnchor:GetY()
                end
                i = i + 1
            end
        end

        //This is the label itself on the X axis
        originPointX = 0
        originPointY = cast(integer, labelMinimumY)
        if horizontalContainer:GetTitleLabel() not= undefined
            ControlLabel axisLabel = horizontalContainer:GetTitleLabel()
            Item2D labelAnchor = axisLabel:GetParent()
            axisLabel:LoadGraphics(axisLabel:GetDefaultLayoutProperties())

            number axisLabelWidth = axisLabel:GetWidth()
            number axisLabelHeight = axisLabel:GetHeight()

            integer titleX = cast(integer, (horizontalContainer:GetWidth() - axisLabelWidth)/2.0)
            integer titleY = cast(integer, labelMinimumY - axisLabelHeight - (1.5*tickWidth))
            labelAnchor:SetPosition(titleX, titleY)
        end
    end

    action LayoutLegend(Chart chart, Legend legendContainer)
        if legendContainer:DisplayOnLeft() or legendContainer:DisplayOnRight()
            integer originPointX = 0
            integer originPointY = cast(integer, legendContainer:GetHeight()*0.90)
        
            //This code represents the label entries in the legend
            integer iconX = originPointX
            integer iconY = originPointY
    
            integer labelX = 0
            integer labelY = 0
            Array<Series> seriesList = legendContainer:GetSeriesList()
            if not seriesList:IsEmpty()
                ControlLabel currentLabel
                Icon currentIcon
    
                integer i = 0
                repeat while i < seriesList:GetSize()
                    currentLabel = seriesList:Get(i):GetLabel()
                    if currentLabel:GetDefaultLayoutProperties():NeedsRendering()
                        currentLabel:LoadGraphics(currentLabel:GetDefaultLayoutProperties())
                    end
    
                    currentIcon = seriesList:Get(i):GetIcon()
                    integer iconSize = cast(integer, currentLabel:GetHeight())
                    currentIcon:LoadFilledRectangle(iconSize, iconSize, currentIcon:GetColor())
    
                    iconX = iconSize
                    iconY = cast(integer, iconY - (1.5*iconSize))
                    currentIcon:SetPosition(iconX , iconY)
                    
                    currentLabel:SetPosition(cast(integer, iconX + (iconSize*1.5)), cast(integer, iconY+(0.5*iconSize)-(currentLabel:GetHeight()/2)))
                    i = i + 1
                end
            end

            //This is the actual title of the legend.      
            if legendContainer:GetTitleLabel() not= undefined
                ControlLabel axisLabel = legendContainer:GetTitleLabel()
                Item2D labelAnchor = axisLabel:GetParent()
                if axisLabel:GetDefaultLayoutProperties():NeedsRendering()       
                    axisLabel:LoadGraphics(axisLabel:GetDefaultLayoutProperties())
                end
    
                number axisLabelWidth = axisLabel:GetWidth()
                number axisLabelHeight = axisLabel:GetHeight()
    
                integer titleX = cast(integer, iconX)
                integer titleY = cast(integer, originPointY)
    
                labelAnchor:SetPosition(titleX, titleY)
            end
        else //legendContainer:DisplayOnTop() or legendContainer:DisplayOnBottom()
            integer originPointX = 0
            integer originPointY = cast(integer, legendContainer:GetHeight()*0.50)
    
    
            //This is the actual title of the legend     
            if legendContainer:GetTitleLabel() not= undefined
                ControlLabel axisLabel = legendContainer:GetTitleLabel()
                Item2D labelAnchor = axisLabel:GetParent()
                if axisLabel:GetDefaultLayoutProperties():NeedsRendering()       
                    axisLabel:LoadGraphics(axisLabel:GetDefaultLayoutProperties())
                end
    
                number axisLabelWidth = axisLabel:GetWidth()
                number axisLabelHeight = axisLabel:GetHeight()
    
                integer titleX = cast(integer, originPointX)
                integer titleY = cast(integer, originPointY - (axisLabelHeight/2))
    
                labelAnchor:SetPosition(titleX, titleY)
                originPointX = originPointX + cast(integer, axisLabelWidth)
            end    

            //This code represents the label entries in the legend
            integer iconX = originPointX
            integer iconY = originPointY
    
            integer labelX = 0
            integer labelY = 0
            Array<Series> seriesList = legendContainer:GetSeriesList()
            if not seriesList:IsEmpty()
                ControlLabel currentLabel
                Icon currentIcon
    
                integer i = 0
                repeat while i < seriesList:GetSize()
                    currentLabel = seriesList:Get(i):GetLabel()
                    if currentLabel:GetDefaultLayoutProperties():NeedsRendering()
                        currentLabel:LoadGraphics(currentLabel:GetDefaultLayoutProperties())
                    end
    
                    currentIcon = seriesList:Get(i):GetIcon()
                    integer iconSize = cast(integer, currentLabel:GetHeight())
                    currentIcon:LoadFilledRectangle(iconSize, iconSize, currentIcon:GetColor())
    
                    iconX = cast(integer, iconX + (1.5*iconSize))
                    iconY = originPointY - cast(integer, currentLabel:GetHeight()/2)
                    currentIcon:SetPosition(iconX , iconY)
                    
                    currentLabel:SetPosition(iconX + (1.5*iconSize), iconY)
                    iconX = cast(integer, iconX + currentLabel:GetWidth() + (1.5*iconSize))
                    i = i + 1
                end
            end
        end
    end

    // Resets all layout flags for the item and its entire children hierarchy.
    private action ResetLayoutFlags(Item2D item)
        if item is Control
            Control control = cast(Control, item)
            control:ResetLayoutFlag()
        end

        Array<Item2D> children = item:GetChildren()
        integer counter = 0
        repeat while counter < children:GetSize()
            ResetLayoutFlags(children:Get(counter))
            counter = counter + 1
        end
    end

    blueprint action LayoutChartContent(Chart chart, Control chartAreaContainer)
end