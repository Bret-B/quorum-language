package Libraries.Interface.Layouts

use Libraries.Containers.Array
use Libraries.Interface.Item2D
use Libraries.Interface.Controls.Control
use Libraries.Interface.Forms.FormContainerControl
use Libraries.Interface.Forms.Grouping
use Libraries.Interface.Forms.Banner
use Libraries.Game.Graphics.Font
use Libraries.Interface.Controls.ScrollPaneRendererAnchor
use Libraries.Interface.Forms.FormConstants
use Libraries.Game.Graphics.Label

class FormRowLayout is Layout

    // Default values for the layout. These will likely be moved elsewhere as the design matures.

    // How wide each row is, as a percentage of the screen, by default. Any space not taken by the width is applied as padding (half on the left, half on the right)
    constant number DEFAULT_ROW_PERCENTAGE_WIDTH = 0.8

    // How tall each row is, as a percentage of the screen, if the row doesn't otherwise provide any height information (e.g., a size dictated by the font in a Label, for example)
    constant number DEFAULT_ROW_PERCENTAGE_HEIGHT = 0.05

    // How much vertical padding to add between rows, as a percentage of the screen height.
    constant number DEFAULT_ROW_VERTICAL_PADDING = 0.02

    // How much space should be between elements in a row by default, as a percentage of the ROW'S width.
    constant number DEFAULT_IN_ROW_PERCENTAGE_PADDING = 0.05

    constant integer TOP_AREA = 0
    constant integer BOTTOM_AREA = 1
    constant integer CENTER_AREA = 2

    // Items are separated into three sets, based on whether they're positioned from the top, center, or bottom.
    Array<Array<Item2D>> areaItems
    Array<number> areaHeights

    FormConstants constants

    on create
        integer i = 0
        repeat while i < 3
            Array<Item2D> items
            areaItems:Add(items)
            i = i + 1
        end

        areaHeights:SetSize(3)
    end

    action Layout(Control parentControl, Array<Item2D> childItems, number containerWidth, number containerHeight)

        FormContainerControl container = undefined

        number minimumResultHeight = containerHeight

        if parentControl is FormContainerControl
            container = cast(FormContainerControl, parentControl)
        elseif parentControl is ScrollPaneRendererAnchor
            ScrollPaneRendererAnchor anchor = cast(ScrollPaneRendererAnchor, parentControl)
            container = cast(FormContainerControl, cast(Object, anchor:GetScrollPane()))
            minimumResultHeight = container:GetHeight()
        end
        
        integer titleAlignment = container:GetTitleAlignment()
        integer contentAlignment = container:GetContentAlignment()

        if contentAlignment = constants:DEFAULT_ALIGN
            contentAlignment = constants:CENTER_ALIGN
        end
        if titleAlignment = constants:DEFAULT_ALIGN
            titleAlignment = contentAlignment
        end

        Font containerFont = container:GetDefaultFont():Copy()
        containerFont:SetSize(constants:LARGE_FONT_SIZE)

        number defaultRowWidth = DEFAULT_ROW_PERCENTAGE_WIDTH * containerWidth
        number defaultRowStartX = (containerWidth - defaultRowWidth) / 2.0
        number defaultRowEndX = defaultRowStartX + defaultRowWidth
        number defaultInRowPadding = defaultRowWidth * DEFAULT_IN_ROW_PERCENTAGE_PADDING
        number defaultRowHeight = containerFont:GetLineHeight()
        number defaultRowVerticalPadding = cast(integer, defaultRowHeight * 0.4)

        // Reset the area arrays to defaults before assigning items to them.
        integer counter = 0
        repeat while counter < 3
            areaItems:Get(counter):Empty()
            areaHeights:Set(counter, 0)
            counter = counter + 1
        end
        
        // Separate items into their areas.
        counter = 0
        repeat while counter < childItems:GetSize()
            Item2D item = childItems:Get(counter)
            
            if item is Grouping
                Grouping group = cast(Grouping, item)
                if group:GetVerticalPosition() = group:BOTTOM_POSITION
                    areaItems:Get(BOTTOM_AREA):Add(group)
                elseif group:GetVerticalPosition() = group:CENTER_POSITION
                    areaItems:Get(CENTER_AREA):Add(group)
                else
                    areaItems:Get(TOP_AREA):Add(group)
                end
            else
                // By default, everything is positioned from the top.
                areaItems:Get(TOP_AREA):Add(item)
            end

            counter = counter + 1
        end

        integer areaCounter = 0
        repeat while areaCounter < areaItems:GetSize()
            Array<Item2D> items = areaItems:Get(areaCounter)

            integer i = 0
            number rowTopY = containerHeight - defaultRowHeight
            repeat while i < items:GetSize()
                Item2D item = items:Get(i)
                boolean isTitle = (i = 0 and container:GetTitle() not= "")
                boolean isGrouping = (item is Grouping)
        
                integer currentAlignment = contentAlignment
                if isTitle
                    currentAlignment = titleAlignment
                end
    
                if isGrouping
                    // If the item is a group, layout its title on a row, if it has one, then place all following children on a single row.
                    Grouping group = cast(Grouping, item)
    
                    if group:GetDefaultLayoutProperties():NeedsRendering()
                        group:LoadGraphics(group:GetDefaultLayoutProperties())
                    end

                    Array<Item2D> groupItems = group:GetChildren()
                    integer j = 0
    
                    number groupTitleHeightOffset = 0

                    // The numberOfRowItems is how many items go into each row.
                    // The totalItemsInRows is the number of items across all of the rows (i.e. how many items there are other other than the title)
                    integer numberOfRowItems = group:GetColumns()
                    integer totalItemsInRows = groupItems:GetSize()

                    if group:GetTitle() not= ""
                        // The title doesn't contribute to the number of elements in rows, because it's handled as its own special case.
                        totalItemsInRows = totalItemsInRows - 1
                    end
                    
                    // If there aren't a set number of columns, everything goes in one row, so the number of row items and total items is the same.
                    if numberOfRowItems <= 0
                        numberOfRowItems = totalItemsInRows
                    end
    
                    number rowWidth = defaultRowWidth
                    if group:GetPixelWidth() not= 0 or group:GetPercentageWidth() not= 0
                        rowWidth = group:GetPixelWidth() + containerWidth * group:GetPercentageWidth()
                    end

                    number elementWidth = (rowWidth - defaultInRowPadding * (numberOfRowItems - 1)) / numberOfRowItems

                    // Groupings can separate elements into multiple rows if there's more elements than columns.
                    // We'll need to track which elements are in which row, and what the height of each row is.
                    Array<Array<Item2D>> groupRowItems
                    Array<number> groupRowHeights
                    number totalRowHeights = 0

                    // Figure out how many rows it takes to store our items. If the division has a remainder, then we have an additional, partially-filled row
                    integer rowCount = 0
                    if numberOfRowItems > 0
                        rowCount = totalItemsInRows / numberOfRowItems
                        if totalItemsInRows mod numberOfRowItems not= 0
                            rowCount = rowCount + 1
                        end
                    end

                    // Initialize the row arrays.
                    repeat rowCount times
                        Array<Item2D> rowItemArray
                        groupRowItems:Add(rowItemArray)
                        groupRowHeights:Add(0)
                    end

                    // Now that the potential title has been handled, handle the content that shares the rows.
                    // Unfortunately, we have to iterate quickly through the elements to determine the tallest in each row.
                    integer k = 0
                    integer titleIndexingOffset = 0

                    // Skip the title, if there is one
                    if group:GetTitle() not= ""
                        k = k + 1
                        titleIndexingOffset = 1
                    end

                    repeat while k < groupItems:GetSize()
                        integer rowArrayIndex = (k - titleIndexingOffset) / numberOfRowItems
                        Array<Item2D> rowItemArray = groupRowItems:Get(rowArrayIndex)
                        rowItemArray:Add(groupItems:Get(k))

                        if groupItems:Get(k) is Control
                            Control control = cast(Control, groupItems:Get(k))
                            Font font = control:GetFont()
                            if font = undefined
                                font = containerFont:Copy()
                            else
                                font = font:Copy()
                            end
        
                            font:SetSize(cast(integer, control:GetFontSize() * control:GetInterfaceScale()))
        
                            number height = font:GetLineHeight()
        
                            if control:GetPixelHeight() not= 0 or control:GetPercentageHeight() not= 0
                                height = control:GetPixelHeight() + containerHeight * control:GetPercentageHeight()
                            end
        
                            height = height * control:GetLayoutRows() + control:GetTopPadding() + control:GetBottomPadding()

                            // As a special case, if the grouping contains only 1 item per row, allow controls to set themselves to use a percentage of the width, if they request it.
                            if numberOfRowItems = 1 and control:GetPercentageWidth() > 0 and control:GetHorizontalLayoutMode() not= control:MAINTAIN_ASPECT_RATIO
                                
if control is Label
                                    Label label = cast(Label, control)
                                    label:SetMaximumWidth(cast(integer, elementWidth * control:GetPercentageWidth()))
                                end
                                
                                control:SetSize(cast(integer, elementWidth * control:GetPercentageWidth()), cast(integer, height))

                            // If the control is trying to maintain an aspect ratio, do our best to respect that.
                            elseif control:GetHorizontalLayoutMode() = control:MAINTAIN_ASPECT_RATIO and control:GetPercentageWidth() > 0
                                number idealWidth = height * control:GetPercentageWidth()

                                if idealWidth <= elementWidth
                                    control:SetSize(cast(integer, idealWidth), cast(integer, height))
                                else
                                    control:SetSize(cast(integer, elementWidth), cast(integer, elementWidth / control:GetPercentageWidth()))
                                end
                            else
                                if control is Label
                                    Label label = cast(Label, control)
                                    label:SetMaximumWidth(cast(integer, elementWidth))
                                end
                                
                                control:SetSize(cast(integer, elementWidth), cast(integer, height))
                            end

                            // Set the height to match the Control's actual height.
                            // This might differ from what we expected if the Control has its own special height calculations, e.g. a multi-line Label.
                            height = control:GetHeight()

                            if height > groupRowHeights:Get(rowArrayIndex)
                                groupRowHeights:Set(rowArrayIndex, height)
                            end
                        end

                        k = k + 1
                    end

                    k = 0
                    repeat while k < groupRowHeights:GetSize()
                        if groupRowHeights:Get(k) <= 0
                            groupRowHeights:Set(k, defaultRowHeight)
                        end

                        totalRowHeights = totalRowHeights + groupRowHeights:Get(k)

                        // If there's more than one row, we need to also include the padding between rows.
                        if k > 0
                            totalRowHeights = totalRowHeights + defaultRowVerticalPadding
                        end

                        k = k + 1
                    end

                    if group:GetPixelHeight() not= 0 or group:GetPercentageHeight() not= 0
                        number calcHeight = group:GetPixelHeight() + containerHeight * group:GetPercentageHeight()
                        if calcHeight > totalRowHeights
                            totalRowHeights = calcHeight
                        end
                    end

                    number bannerPadding = 0

                    // The title gets its own row, if there is one.
                    if group:GetTitle() not= ""
                        currentAlignment = group:GetTitleAlignment()
    
                        if currentAlignment = constants:DEFAULT_ALIGN
                            currentAlignment = titleAlignment
                        end
    
                        Control control = cast(Control, groupItems:Get(0))
                        if control:GetDefaultLayoutProperties():NeedsRendering()
                            control:LoadGraphics(control:GetDefaultLayoutProperties())
                        end
                        
if control is Label
                                    Label label = cast(Label, control)
                                    label:SetMaximumWidth(rowWidth)
                                end
    
                        // The label's width and height are known quantities. There's nothing to set, so just fetch them now.
                        number width = control:GetWidth()
                        number height = control:GetHeight()
        
                        number x = 0
                        if currentAlignment = constants:LEFT_ALIGN
                            if width > rowWidth
                                x = (rowWidth - width) / 2.0
                            else
                                x = 0
                            end
                        elseif currentAlignment = constants:CENTER_ALIGN
                            x = (rowWidth - width) / 2.0
                        elseif currentAlignment = constants:RIGHT_ALIGN
                            if width > rowWidth
                                x = (rowWidth - width) / 2.0
                            else
                                x = rowWidth - width
                            end
                        end

                        // We need to offset the title of a Banner since it'll stretch all the way left-to-right, and we'd like a little extra space at the bottom
                        if group is Banner
                            x = x + (containerWidth - rowWidth) / 2
                            bannerPadding = defaultRowVerticalPadding
                        end
        
                        groupTitleHeightOffset = rowTopY
                        number y = rowTopY - height - bannerPadding
                        
                        number labelMultiLineOffset = 0
                        if control is Label
                            Label label = cast(Label, control)
                            if label:GetLineCount() > 1
                                labelMultiLineOffset = label:GetLineHeight() * (label:GetLineCount() - 1)
                            end
                        end
        
                        // If the title is the only item in the grouping, there won't be a row beneath it, so it doesn't need to be pushed further up.
                        if groupItems:GetSize() = 1
                            control:SetPosition(x, defaultRowVerticalPadding + bannerPadding + labelMultiLineOffset)
                        else
                            control:SetPosition(x, totalRowHeights + defaultRowVerticalPadding + bannerPadding + labelMultiLineOffset)
                        end
        
                        rowTopY = y - defaultRowVerticalPadding
                        groupTitleHeightOffset = groupTitleHeightOffset - rowTopY
    
                        j = j + 1
                    end
    
                    currentAlignment = group:GetContentAlignment()
    
                    if currentAlignment = constants:DEFAULT_ALIGN
                        currentAlignment = contentAlignment
                    end
    
                    number groupX = 0
                    if currentAlignment = constants:LEFT_ALIGN
                        if rowWidth > defaultRowWidth
                            x = (containerWidth - rowWidth) / 2.0
                        else
                            x = defaultRowStartX
                        end
                    elseif currentAlignment = constants:CENTER_ALIGN
                        x = (containerWidth - rowWidth) / 2.0
                    elseif currentAlignment = constants:RIGHT_ALIGN
                        if rowWidth > defaultRowWidth
                            x = (containerWidth - rowWidth) / 2.0
                        else
                            x = defaultRowEndX - rowWidth
                        end
                    end
    
                    // If there's no content besides the title, the rowHeight won't contribute to the height
                    // of the group beyond what's already calculated in the groupTitleHeightOffset
                    number contentRowHeight = totalRowHeights
                    if j = 1 and j = groupItems:GetSize()
                        contentRowHeight = 0
                    end
                    

                    // Banners have special rules about their positioning. They always stretch the whole width of their container.
                    // Their positioning info is actually used to offset the children within them.
                    if group is Banner
                        group:SetSize(containerWidth, contentRowHeight + groupTitleHeightOffset)
                        group:SetPosition(0, rowTopY - contentRowHeight)
                    else
                        group:SetSize(rowWidth, contentRowHeight + groupTitleHeightOffset)
                        group:SetPosition((containerWidth - rowWidth) / 2, rowTopY - contentRowHeight)
                    end
    
                    rowTopY = rowTopY - contentRowHeight - defaultRowVerticalPadding
  
                    integer subRowCounter = groupRowItems:GetSize()
                    number previousRowHeights = 0
                    repeat while subRowCounter > 0
                        subRowCounter = subRowCounter - 1
                        Array<Item2D> currentRowItems = groupRowItems:Get(subRowCounter)
                        number currentRowHeight = groupRowHeights:Get(subRowCounter)

                        number elementX = 0

                        if group is Banner
                            elementX = (containerWidth - rowWidth) / 2
                        end

                        integer rowItemCounter = 0
                        repeat while rowItemCounter < currentRowItems:GetSize()
                            if currentRowItems:Get(rowItemCounter) is Control
                                Control control = cast(Control, currentRowItems:Get(rowItemCounter))
                                if control:GetDefaultLayoutProperties():NeedsRendering()
                                    control:LoadGraphics(control:GetDefaultLayoutProperties())
                                end
        
                                control:SetPosition(cast(integer, elementX + (elementWidth - control:GetWidth()) / 2.0), cast(integer, (currentRowHeight - control:GetHeight()) / 2.0) + bannerPadding + previousRowHeights)
    
                                elementX = elementX + elementWidth + defaultInRowPadding
                            end
        
                            rowItemCounter = rowItemCounter + 1
                        end

                        previousRowHeights = previousRowHeights + currentRowHeight + defaultRowVerticalPadding
                    end
    
                elseif item is Control
                    Control control = cast(Control, item)
                    if control:GetDefaultLayoutProperties():NeedsRendering()
                        control:LoadGraphics(control:GetDefaultLayoutProperties())
                    end
    
                    number width = defaultRowWidth
                    if control:GetPixelWidth() not= 0 or control:GetPercentageWidth() not= 0
                        width = control:GetPixelWidth() + containerWidth * control:GetPercentageWidth()
                    end

                    Font font = control:GetFont()
                    if font = undefined
                        font = containerFont:Copy()
                    else
                        font = font:Copy()
                    end

                    font:SetSize(cast(integer, control:GetFontSize() * control:GetInterfaceScale()))

                    number height = font:GetLineHeight()

                    if height <= 0
                        height = defaultRowHeight
                    end

                    if control:GetPixelHeight() not= 0 or control:GetPercentageHeight() not= 0
                        height = control:GetPixelHeight() + containerHeight * control:GetPercentageHeight()
                    end

                    height = height * control:GetLayoutRows() + control:GetTopPadding() + control:GetBottomPadding()
    
                    if control:GetHorizontalLayoutMode() = control:MAINTAIN_ASPECT_RATIO
                        width = height * control:GetPercentageWidth()
                    elseif control:GetVerticalLayoutMode() = control:MAINTAIN_ASPECT_RATIO
                        height = width * control:GetPercentageHeight()
                    end

                    if control is Label
                        Label label = cast(Label, control)
                        label:SetMaximumWidth(width)
                    end
                    
                    control:SetSize(width, height)

                    // Set the height to match the Control's actual height.
                    // This might differ from what we expected if the Control has its own special height calculations, e.g. a multi-line Label.
                    height = control:GetHeight()
    
                    /*
                    Reassign the width and height based on the "real" width and height, which might differ from our request based on
                    the control's own ability to lay itself out during the resize. For example, Labels will entirely ignore our height
                    request, as they strictly derive their height from their text and how many lines the text requires.
                    */
                    width = control:GetWidth()
    
                    number x = 0
                    if currentAlignment = constants:LEFT_ALIGN
                        if width > defaultRowWidth
                            x = (containerWidth - width) / 2.0
                        else
                            x = defaultRowStartX
                        end
                    elseif currentAlignment = constants:CENTER_ALIGN
                        x = (containerWidth - width) / 2.0
                    elseif currentAlignment = constants:RIGHT_ALIGN
                        if width > defaultRowWidth
                            x = (containerWidth - width) / 2.0
                        else
                            x = defaultRowEndX - width
                        end
                    end
    
                    number y = rowTopY - height
    
                    control:SetPosition(x, y)

                    rowTopY = y - defaultRowVerticalPadding
                end
    
                i = i + 1
            end

            /*
            The height can be determined from how far we've traveled away from the top of the container.
            */
            number areaHeight = containerHeight - rowTopY

            areaHeights:Set(areaCounter, areaHeight)
            areaCounter = areaCounter + 1
        end

        number resultHeight = containerHeight
        number newContainerHeightOffset = 0

        // If the container is depending on us to figure out the height, use our calculated heights instead of assuming the containerHeight is trustworthy.
        if parentControl:GetVerticalLayoutMode() = parentControl:FIT_CONTENTS
            resultHeight = areaHeights:Get(TOP_AREA) + areaHeights:Get(CENTER_AREA) + areaHeights:Get(BOTTOM_AREA)
            if resultHeight < minimumResultHeight
                resultHeight = minimumResultHeight
            end

            // This is how much each item needs to be moved to maintain positions relative to the new top of the control.
            newContainerHeightOffset = resultHeight - parentControl:GetHeight()

            // Set the parent control to match the new height, if it isn't already.
            if parentControl:GetHeight() not= resultHeight
                parentControl:SetHeight(resultHeight)
            end
        end

        // After setting the regions, the top set only needs to move if the height of the container changed.
        if newContainerHeightOffset not= 0
            Array<Item2D> topItems = areaItems:Get(TOP_AREA)
            integer i = 0
            repeat while i < topItems:GetSize()
                Item2D item = topItems:Get(i)
                item:MoveY(newContainerHeightOffset)
                i = i + 1
            end
        end

        
        // The bottom and center areas need adjusting regardless of whether or not the container height changed.
        Array<Item2D> bottomItems = areaItems:Get(BOTTOM_AREA)
        integer i = 0
        number bottomOffset = newContainerHeightOffset - cast(integer, resultHeight - areaHeights:Get(BOTTOM_AREA))
        repeat while i < bottomItems:GetSize()
            Item2D item = bottomItems:Get(i)
            item:MoveY(bottomOffset)
            i = i + 1
        end

        // Finally position the center items between the top and bottom areas.
        Array<Item2D> centerItems = areaItems:Get(CENTER_AREA)
        i = 0
        number centerTarget = (resultHeight - areaHeights:Get(TOP_AREA) + areaHeights:Get(BOTTOM_AREA)) / 2.0
        number centerOfItems = resultHeight - areaHeights:Get(CENTER_AREA) / 2.0
        number centerOffset = newContainerHeightOffset - cast(integer, centerOfItems - centerTarget)
        repeat while i < centerItems:GetSize()
            Item2D item = centerItems:Get(i)
            item:MoveY(centerOffset)
            i = i + 1
        end
    end

end