package Libraries.Interface.Selections
use Libraries.Interface.Controls.Charts.ChartItem

/*
    This class represents a selection system for any chart class. 
    By default, Charts have a ChartItem, which contains children, parent, 
    and next/previous items. This allows the system to navigate the overall 
    visual structure, in each case also allowing the system to know which 
    item should have the focus when this change occurs. Besides this, selection 
    works similarly to any other control, where one can get or set the selection 
    and query it for information. 

    Attribute: Author Andreas Stefik

    Attribute: Example

    use Libraries.Interface.Controls.Charts
    use Libraries.Game.Game

    class Main is Game
        action Main
            StartGame()
        end

        action CreateGame
            BarChart chart
            ChartSelection selection = chart:GetSelection()

            //the selection will be defined, but the item it points at (e.g., GetChartItem()) will not.
            Add(chart)
        end
    end
*/
class ChartSelection is Selection
    /* The item that is currently selected, possibly undefined. */
    ChartItem item = undefined

    /*
        Sets the item that should be selected.

        Attribute: Parameter item if this is undefined, the system is told the display name is the empty string.
    */
    action Set(ChartItem item)
        if item not= undefined
            if me:item not= undefined
                me:item:LostSelection()
            end
            me:item = item
            text value = item:GetDisplayName()
            if value not= undefined
                SetDisplayName(value)
            end
            me:item:GainedSelection()
        else 
            if me:item not= undefined
                me:item:LostSelection()
            end
            me:item = undefined
            SetDisplayName("")
        end
    end

    /*
        Returns the part of the chart that is selected.

        Attribute: Returns the chart item that is selected, possibly undefined if nothing is selected.
    */
    action GetChartItem returns ChartItem
        return item
    end

    action IsEmpty returns boolean
        return item = undefined
    end

    /*
        This action clears the current selection.
    */
    action Empty
        Set(undefined)
    end
end