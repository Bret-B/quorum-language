package Libraries.Interface.Selections
use Libraries.Interface.Item
use Libraries.Interface.Item2D
use Libraries.Interface.Controls.TreeTableCell
use Libraries.Interface.Controls.TreeTable
use Libraries.Containers.Support.Pair

class TreeTableSelection is Selection
    integer row = -1
    integer column = -1

    action Initialize(Item item)
        Initialize(item, "")
    end

    action IsEmpty returns boolean
        return column < 0 and row < 0
    end

    /*
        This action selects a single cell in the TreeTable. In this case, 
        the x represents the column number and the y represents the row.
    */
    action Set(integer row, integer column)
        TreeTable table = GetTreeTable()
        if table not= undefined
            TreeTableCell cell = table:GetCell(row, column)
            if cell not= undefined
                me:row = row
                me:column = column
                SetDisplayName(cell:GetText())
                return now
            end
        end

        // If the TreeTable or Cell couldn't be found, set selection to empty.
        Empty()
    end

    /*
        This action selects a single cell in the TreeTable.
    */
    action Set(TreeTableCell cell)
        if cell not= undefined
            TreeTable table = GetTreeTable()
            if table not= undefined
                Pair<integer> coords = table:GetCellCoordinates(cell)
                if coords:GetFirstValue() > -1 and coords:GetSecondValue() > -1
                    row = coords:GetFirstValue()
                    column = coords:GetSecondValue()
                    SetDisplayName(cell:GetText())
                    return now
                end
            end
        end

        // If we couldn't set it to the cell, empty the selection.
        Empty()
    end

    action Get returns TreeTableCell
        TreeTable table = GetTreeTable()
        if table not= undefined and IsEmpty() = false
            return table:GetCell(row, column)
        end
        return undefined
    end

    action GetTreeTable returns TreeTable
        Item item = GetItem()
        if item is TreeTable
            return cast(TreeTable, item)
        end
        return undefined
    end

    /*
    The Empty action sets the selection to be empty, or in other words, there is
    no cell actively selected. When the selection is empty, the x and y 
    coordinates will be -1.
    */
    action Empty
        row = -1
        column = -1
        SetDisplayName("")
    end

    action GetRow returns integer
        return row
    end

    action GetColumn returns integer
        return column
    end
end