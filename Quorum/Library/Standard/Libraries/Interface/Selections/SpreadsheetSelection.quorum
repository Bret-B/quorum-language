package Libraries.Interface.Selections
use Libraries.Interface.Item
use Libraries.Interface.Item2D
use Libraries.Interface.Controls.Cell
use Libraries.Interface.Controls.Spreadsheet
use Libraries.Containers.Support.Pair

class SpreadsheetSelection is Selection
    integer row = -1
    integer column = -1

    action Initialize(Item item)
        Initialize(item, "")
    end

    action IsEmpty returns boolean
        return row < 0 or column < 0
    end

    /*
        This action selects a single cell in the spreadsheet. In this case, 
        the x represents the column number and the y represents the row.
    */
    action Set(integer row, integer column)
        Spreadsheet sheet = GetSpreadsheet()
        if sheet not= undefined
            Cell cell = sheet:GetCell(row, column)
            if cell not= undefined
                me:row = row
                me:column = column
                SetDisplayName(cell:GetText())
                return now
            end
        end

        // If the Spreadsheet or Cell couldn't be found, set selection to empty.
        Empty()
    end

    /*
        This action selects a single cell in the spreadsheet.
    */
    action Set(Cell cell)
        if cell not= undefined
            Spreadsheet sheet = GetSpreadsheet()
            if sheet not= undefined
                Pair<integer> coords = sheet:GetCellCoordinates(cell)
                if coords:GetFirstValue() > -1 and coords:GetSecondValue() > -1
                    row = coords:GetFirstValue()
                    column = coords:GetSecondValue()
                    SetDisplayName(cell:GetText())
                    return now
                end
            end
        end

        // If we couldn't set it to the cell, empty the selection.
        Empty()
    end

    action Get returns Cell
        Spreadsheet sheet = GetSpreadsheet()
        if sheet not= undefined and IsEmpty() = false
            return sheet:GetCell(row, column)
        end
        return undefined
    end

    action GetSpreadsheet returns Spreadsheet
        Item item = GetItem()
        if item is Spreadsheet
            return cast(Spreadsheet, item)
        end
        return undefined
    end

    /*
    The Empty action sets the selection to be empty, or in other words, there is
    no cell actively selected. When the selection is empty, the x and y 
    coordinates will be -1.
    */
    action Empty
        row = -1
        column = -1
        SetDisplayName("")
    end

    action GetRow returns integer
        return row
    end

    action GetColumn returns integer
        return column
    end
end