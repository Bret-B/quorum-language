package Libraries.Interface.Behaviors.Scenes.Properties

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Controls.Scenes.Dialogs.PropertiesDialog
use Libraries.Interface.Controls.Scenes.SceneEditor
use Libraries.Interface.Behaviors.Behavior

/*
    This behavior is used in the scene editor from any properties dialog. This behavior 
    moves focus away from the dialog and focuses either on the scene editor or the 
    selected item. Note that this does not hide the dialog but moves focus away from it. 
    This allows for the properties dialog to still be available to view or jump to but 
    allow the user to interact with other parts of the editor.
*/
class UnfocusPropertiesBehavior is Behavior
    action Run(BehaviorEvent event)
        SceneEditor controller = undefined

        if event:GetItem() is PropertiesDialog
            PropertiesDialog dialog = cast(PropertiesDialog, event:GetItem())
            controller = dialog:GetSceneEditor()
        end

        if controller = undefined
            return now
        end

        if controller:GetSelectedItem() = undefined
            controller:Focus()
        else
            controller:GetSelectedItem():Focus()
        end
    end
end