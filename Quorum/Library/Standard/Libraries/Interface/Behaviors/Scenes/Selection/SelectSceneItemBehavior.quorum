package Libraries.Interface.Behaviors.Scenes.Selection

use Libraries.Containers.Array
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Item
use Libraries.Game.Layer
use Libraries.Interface.Item2D
use Libraries.Interface.Item3D
use Libraries.Interface.Controls.Scenes.Layers.EditorLayer
use Libraries.Interface.Controls.Scenes.SceneEditor
use Libraries.Interface.Behaviors.Behavior

/*
    This behavior is used to select an item in the scene editor. The input expected 
    by the scene editor to use this behavior is a left mouse click. If the user clicks 
    on the scene editor and there is a scene item there then the scene editor will try 
    to select that item. 
*/
class SelectSceneItemBehavior is Behavior

    action Run(BehaviorEvent event)
        Layer layer = undefined
        if event:GetItem() is Item2D
            Item2D item = cast(Item2D, event:GetItem())
            layer = item:GetLayer()
        elseif event:GetItem() is Item3D
            Item3D item = cast(Item3D, event:GetItem())
            layer = item:GetLayer()
        end

        SceneEditor controller = undefined

        if layer is EditorLayer
            EditorLayer editorLayer = cast(EditorLayer, layer)
            controller = editorLayer:GetSceneEditor()
        else
            return now
        end

        Array<Item> array
        array:Add(event:GetItem())
        controller:SetFilteredItems(array, true)        
    end

end