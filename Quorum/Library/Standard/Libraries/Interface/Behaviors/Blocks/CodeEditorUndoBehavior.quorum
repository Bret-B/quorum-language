package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Controls.Blocks.CodeEditor
use Libraries.Interface.Controls.Blocks.BlockItem
use Libraries.Interface.Controls.Blocks.Block
use Libraries.Interface.Undo.BlockEditManager

class CodeEditorUndoBehavior is Behavior

    action Run(BehaviorEvent event)
        CodeEditor editor = undefined
        if event:GetItem() is CodeEditor
            editor = cast(CodeEditor, event:GetItem())
        elseif event:GetItem() is Block
            Block block = cast(Block, event:GetItem())
            editor = block:GetEditor()
        elseif event:GetItem() is BlockItem
            BlockItem item = cast(BlockItem, event:GetItem())
            editor = item:GetEditor()
        else
            // If we can't find the editor, abort.
            return now
        end
        BlockEditManager editManager = editor:GetBlockEditManager()
        editManager:Undo()
        editor:RequestViewRefresh()
    end

end