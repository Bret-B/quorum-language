package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Item
use Libraries.Interface.Controls.TextField
use Libraries.Interface.Selections.TextFieldSelection

class TextFieldSelectToMouseClickBehavior is Behavior
    action Run(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextField)
            return now
        end

        
        /* When the mouse is clicked (with the SHIFT key being pressed) we will use
        the previousCaretPosition (set by the SHIFT key press) and the current mouse-click
        caret position to set the textbox selection. 
        */
        TextField textField = cast(TextField, item) 
        integer previousCaretPosition = textField:GetPreviousCaretPosition()
        integer caretPosition = textField:GetCaretPosition()

        if previousCaretPosition < caretPosition
            if caretPosition = textField:GetSize()
                textField:Select(previousCaretPosition, caretPosition)
            //the caret was placed at the end of the word
            elseif IsWhiteSpace(textField:GetText(caretPosition, caretPosition + 1)) 
                textField:Select(previousCaretPosition, caretPosition, true)
            //the caret is at the beginning or in the middle of a word 
            else
                textField:Select(previousCaretPosition, caretPosition, false)
            end
        else
            if previousCaretPosition = textField:GetSize()
                textField:Select(caretPosition, previousCaretPosition)
            elseif IsWhiteSpace(textField:GetText(previousCaretPosition, previousCaretPosition + 1))
                textField:Select(caretPosition, previousCaretPosition, true)
            else
                textField:Select(caretPosition, previousCaretPosition, false)
            end    
        end
        
    end

    action IsFinished returns boolean
        return true
    end

    private action IsWhiteSpace(text value) returns boolean
        text tab = value:GetTab()
        text cr = value:GetCarriageReturn()
        text lf = value:GetLineFeed()
        text space = " "

        if value = tab or value = cr or value = lf or value = space
            return true
        end

        return false
    end
end