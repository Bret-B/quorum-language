package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Controls.Spreadsheet
use Libraries.Interface.Selections.SpreadsheetSelection
use Libraries.Interface.Controls.Cell
use Libraries.Game.GameStateManager
use Libraries.Game.Game

class SpreadsheetCutBehavior is Behavior 
    action Run(BehaviorEvent event)
        if event:GetItem() is Spreadsheet
            Spreadsheet sheet = cast(Spreadsheet, event:GetItem())
            
            SpreadsheetSelection selection = sheet:GetSelection()
            Cell cell = selection:Get()
            if cell = undefined
                return now
            end
            text value = cell:GetText()
            if value = undefined
                return now
            end
            GameStateManager manager
            Game game = manager:GetGame()
            game:SetClipboard(value)
            cell:SetText("")
        end
    end
end