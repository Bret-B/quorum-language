package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Item
use Libraries.Interface.Controls.TextBox

class TextBoxPageDownBehavior is Behavior
    action Run(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextBox)
            return now
        end

        TextBox textBox = cast(TextBox, item)
        integer totalLines = textBox:GetLineCount()
        integer visibleLines = textBox:GetVisibleLines()
        integer startLine = textBox:GetStartLine()

        integer newStart = visibleLines + startLine
        //caret goes to the position, now need to update the view to display the caret position at the top
        if newStart < totalLines - 1
            textBox:SetStartLine(newStart)
            textBox:GoToLine(newStart + (visibleLines / 2))
            textBox:SetCaretPositionToLine(newStart)
        else
            textBox:GoToLine(totalLines - 1)
            textBox:SetCaretPositionToLine(totalLines - 1)
        end

    end

    action IsFinished returns boolean
        return true
    end
end