package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Item
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Selections.TextBoxSelection

class TextBoxSelectToMouseClickBehavior is Behavior
    action Run(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextBox)
            return now
        end

        
        /* When the mouse is clicked (with the SHIFT key being pressed) we will use
        the previousCaretPosition (set by the SHIFT key press) and the current mouse-click
        caret position to set the textbox selection. 
        */
        TextBox textBox = cast(TextBox, item) 
        integer previousCaretPosition = textBox:GetCaretPosition()

        textBox:MousePositionCaret(event:GetMouseEvent(), false)

        integer caretPosition = textBox:GetCaretPosition()

        if previousCaretPosition < caretPosition
            if caretPosition = textBox:GetSize()
                textBox:Select(previousCaretPosition, caretPosition)
            //the caret was placed at the end of the word
            elseif IsWhiteSpace(textBox:GetText(caretPosition, caretPosition + 1)) 
                textBox:Select(previousCaretPosition, caretPosition, true)
            //the caret is at the beginning or in the middle of a word 
            else
                textBox:Select(previousCaretPosition, caretPosition, false)
            end
        else
            if previousCaretPosition = textBox:GetSize()
                textBox:Select(caretPosition, previousCaretPosition)
            elseif IsWhiteSpace(textBox:GetText(previousCaretPosition, previousCaretPosition + 1))
                textBox:Select(caretPosition, previousCaretPosition, true)
            else
                textBox:Select(caretPosition, previousCaretPosition, false)
            end    
        end
        
    end

    action IsFinished returns boolean
        return true
    end

    private action IsWhiteSpace(text value) returns boolean
        text tab = value:GetTab()
        text cr = value:GetCarriageReturn()
        text lf = value:GetLineFeed()
        text space = " "

        if value = tab or value = cr or value = lf or value = space
            return true
        end

        return false
    end
end