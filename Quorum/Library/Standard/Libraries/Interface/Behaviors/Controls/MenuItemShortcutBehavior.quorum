package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Controls.MenuItem
use Libraries.Interface.Item
use Libraries.Containers.Iterator

class MenuItemShortcutBehavior is Behavior

    MenuItem menuItem = undefined

    action Run(BehaviorEvent event)
        if menuItem:IsMenu()
            menuItem:GetMenuRoot():Focus()
            OpenParentMenu(menuItem)

            Iterator<MenuItem> children = menuItem:GetMenuItems()
            MenuItem first = children:Next()
            first:Select()
        else
            menuItem:Activate()
        end
    end

    private action OpenParentMenu(MenuItem item)
        if item not= undefined
            OpenParentMenu(item:GetParentMenu())
            item:Open()
        end
    end

    action IsFinished returns boolean
        return true
    end

    action SetMenuItem(MenuItem menuItem)
        me:menuItem = menuItem
    end

    action GetMenuItem returns MenuItem
        return menuItem
    end

end