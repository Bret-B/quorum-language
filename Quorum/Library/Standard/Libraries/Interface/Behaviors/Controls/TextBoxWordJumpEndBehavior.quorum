package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Item
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Selections.TextBoxSelection

class TextBoxWordJumpEndBehavior is Behavior
    action Run(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextBox)
            return now
        end

        TextBox textBox = cast(TextBox, item)
        TextBoxSelection selection = textBox:GetSelection()

        integer size = textBox:GetSize()
        integer start = selection:GetStartIndex()
        integer finish = selection:GetEndIndex()

        //if the selection is before the caret, we are going to lengthen it
        //it the selection is after the caret, we are going to shorten it.
        integer caretPosition = textBox:GetCaretPosition()
        boolean done = false
        boolean firstWord = false 
        integer i = 0

        if caretPosition >= finish
            i = caretPosition
        else
            i = finish
        end
        repeat while i + 1 <= size and not done
            text value = textBox:GetText(i, i+1)
            if IsWhiteSpace(value)
                if firstWord
                    done = true
                    i = i - 1
                end
            else 
                firstWord = true
            end
            i = i + 1
        end

        if i < 0
            i = 0
        end

        textBox:Select(i, i, false)
        textBox:SetCaretPosition(i)
    end

    private action IsWhiteSpace(text value) returns boolean
        text tab = value:GetTab()
        text cr = value:GetCarriageReturn()
        text lf = value:GetLineFeed()
        text space = " "

        if value = tab or value = cr or value = lf or value = space
            return true
        end

        return false
    end

    action IsFinished returns boolean
        return true
    end
end