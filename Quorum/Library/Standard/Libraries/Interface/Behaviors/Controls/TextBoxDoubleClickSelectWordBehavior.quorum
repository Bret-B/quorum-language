package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Item
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Selections.TextBoxSelection

class TextBoxDoubleClickSelectWordBehavior is Behavior
    action Run(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextBox)
            return now
        end

        TextBox textBox = cast(TextBox, item)  
        integer endOfLineIndex = 0
        integer caretPosition = textBox:GetCaretPosition()
        integer lineCount = textBox:GetLineCount()
        integer lineNumber = textBox:GetCaretLine()
        integer beginOfLineIndex = textBox:GetIndexOfLine(lineNumber)

        /* if the double-click is occurring on the last line of the textbox, then the 
        endOfLineIndex should just be the textBox size */
        if lineNumber = (lineCount - 1)
            endOfLineIndex = textBox:GetSize()
        else
            integer nextLineIndex = textBox:GetIndexOfLine(lineNumber + 1)      
            endOfLineIndex = nextLineIndex - 1
        end

        
        boolean beginWordIsFinished = false
        integer beginIndex = caretPosition
        boolean selectionIsSpecialCharacter = false

        /* find the index of the beginning of the selection determined by whitespace or a special character, 
        if the selection is a special character, then set the flag to determine the number of special characters to 
        include in the selection */
        repeat while beginIndex >= beginOfLineIndex and not(beginWordIsFinished)  
            if beginIndex = beginOfLineIndex
                beginWordIsFinished = true
            else
                text previousCharacter = textBox:GetText(beginIndex - 1, beginIndex)
                if IsSpecialCharacter(previousCharacter)
                    if beginIndex = caretPosition
                        selectionIsSpecialCharacter = true
                    end
                    beginWordIsFinished = true
                elseif IsWhiteSpace(previousCharacter)
                    if beginIndex = caretPosition and IsSpecialCharacter(textBox:GetText(beginIndex, beginIndex + 1))
                        selectionIsSpecialCharacter = true
                    end
                    beginWordIsFinished = true
                else
                    beginIndex = beginIndex - 1
                end
            end
        end
        
        /* find the index of the end of the word determined by whitespace or a special character */
        boolean endWordIsFinished = false
        integer endIndex = caretPosition
        repeat while endIndex <= endOfLineIndex and not(endWordIsFinished) and not(selectionIsSpecialCharacter)
            if endIndex = endOfLineIndex
                endWordIsFinished = true
            else
                text nextCharacter = textBox:GetText(endIndex, endIndex + 1)
                if(IsSpecialCharacter(nextCharacter) or IsWhiteSpace(nextCharacter))
                    endWordIsFinished = true
                else
                    endIndex = endIndex + 1 
                end
            end
        end

        /* If the selection is a special character, then check to see if there needs to be a single 
        character selection (only one special character) or if there needs to be a multiple special character
        selection in the case that there are two or more special characters in a row (eg //, /*, "", or ()). */
        if selectionIsSpecialCharacter
            endIndex = caretPosition
            beginIndex = caretPosition - 1
            repeat while IsSpecialCharacter(textBox:GetText(beginIndex - 1, beginIndex))
                beginIndex = beginIndex - 1
            end 
            //In the case that the Special character is the first character on the line, remove the leading white space from the selection
            if IsWhiteSpace(textBox:GetText(beginIndex, beginIndex + 1))
                beginIndex = beginIndex + 1
            end
            repeat while IsSpecialCharacter(textBox:GetText(endIndex, endIndex + 1))
                endIndex = endIndex + 1
            end
        end

        textBox:Select(beginIndex, endIndex, true)
    end

    private action IsWhiteSpace(text value) returns boolean
        text tab = value:GetTab()
        text cr = value:GetCarriageReturn()
        text lf = value:GetLineFeed()
        text space = " "

        if value = tab or value = cr or value = lf or value = space
            return true
        end

        return false
    end

    private action IsSpecialCharacter(text value) returns boolean
        boolean isSpecialCharacter = false
        if value = "." or value = "/" or value = "*" or value = "-" or value = "+"
        or value = "(" or value = ")" or value = ":" or value = "<" or value = ">"
        or value = ";" or value = "," or value = value:GetDoubleQuote()
            isSpecialCharacter = true
        end
        return isSpecialCharacter
    end

    action IsFinished returns boolean
        return true
    end
end