package Libraries.Interface.Behaviors

use Libraries.Interface.Events.BehaviorEvent

/*
This class represents a generic way for an item in the user interface, whether 
it be a game component or 2D or 3D item, to conduct a behavior. By design, it 
can run itself, with a Run action, can take action over time using its Update 
action, can cleanup any items when it is finished using Dispose, and can 
determine when it is completed with its IsFinished action. Behavior itself 
does not do anything itself, but is heavily subclassed for specific kinds of 
actions. 

The classes that use Behaviors of one kind or another the most are the user 
interface controls, like Button, TextBox, or others, and the InputTable class. 
Together, these classes provide a way to efficiently route user input throughout 
the system.
*/
class Behavior 

    boolean runOnUpdate = false
    boolean hasBeenRun = false

    /*
        When an action is taken with this Behavior, run is called. For example, 
        if a button is clicked in the user interface, Run would be called.
    */
    action Run(BehaviorEvent event)

    end

    /*
        Behaviors can optionally be set to Update on every frame of a running 
        program, depending on context. By default, however, not all behaviors do this. 
        For example, in a button, Update is never called on its default behavior, just 
        Run. In contrast, if you call AddBehavior on an item, Update would be called on
        every frame.
    */
    action Update(number seconds)

    end

    /*
        Dispose does nothing by default, but can be overridden to conduct 
        cleanup actions when a behavior is finished. Note that, as discussed in 
        the comment for the Update action, Dispose is only called if the behavior 
        is added through AddBehavior, not if its set as a default Behavior on an 
        interface element (e.g., a Buttonâ€™s default clicking behavior).
    */
    action Dispose
        
    end

    /*
        This action states if a behavior has completed its work.
    */
    action IsFinished returns boolean
        return false
    end

    /*
        The SetRunOnUpdate action determines when a behavior should be first run
        when the behavior is added to an Item. If the value is false, then the
        Behavior's Run action will be called immediately when it is added to an
        Item. If it is true, it will be called during the next update loop.
    */
    action SetRunOnUpdate(boolean run)
        runOnUpdate = run
    end

    /*
        The IsRunningOnUpdate action returns true if this Behavior calls its Run
        action during the Game's update loop, and returns false if the Behavior
        calls its Run action immediately upon being added to an Item. The default
        value is false.
    */
    action IsRunningOnUpdate returns boolean
        return runOnUpdate
    end

    /*
        This action returns true if the Behavior has been run by a current item that
        it has been added to. This is an internal flag used by the engine, and most
        users will never need this functionality.
    */
    action HasBeenRun returns boolean
        return hasBeenRun
    end

    /*
        This action sets a flag indicating if the Behavior has been run by the
        current Item this Behavior has been added to. This is used internally by
        the engine, and most users should never need this functionality.
    */
    action SetHasRunFlag(boolean hasRun)
        hasBeenRun = hasRun
    end
end