package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class BooleanToTextCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        VisitLineNumber()
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()

        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "java/lang/Boolean", "toString", "(Z)Ljava/lang/String;", false)
    end
    
    action WritePython returns text
        text result = ""
        text DQ = result:GetDoubleQuote()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + "__builtins__.str(" + opcodeToCast:WritePython() + ")"
        end
        return result
    end

    action WriteJavaScript returns text
        text result = ""
        text DQ = result:GetDoubleQuote()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + opcodeToCast:WriteJavaScript() + " + " + DQ + DQ
        end
        return result
    end

    action GetTemplateCopy returns QuorumOpcode
        BooleanToTextCast op
        op:SetType(me:GetType())
        return op
    end
end