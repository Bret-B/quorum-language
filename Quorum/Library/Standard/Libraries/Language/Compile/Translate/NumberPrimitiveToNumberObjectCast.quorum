package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class NumberPrimitiveToNumberObjectCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        VisitLineNumber()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "plugins/quorum/Libraries/Language/Types/Number", "ConvertNumberToNumberObject",
                "(D)Lquorum/Libraries/Language/Types/Number_;", false)
    end
    
    action WritePython returns text
        text result = ""
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + "quorum_Libraries_Language_Types_Number_(0, " + opcodeToCast:WritePython() + ")"
        end
        return result
    end

    action WriteJavaScript returns text
        text result = ""
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + "new quorum_Libraries_Language_Types_Number_(" + "false, " + opcodeToCast:WriteJavaScript() + ")"
        end
        return result
    end

    action GetTemplateCopy returns QuorumOpcode
        NumberPrimitiveToNumberObjectCast op
        op:SetType(me:GetType())
        return op
    end
end