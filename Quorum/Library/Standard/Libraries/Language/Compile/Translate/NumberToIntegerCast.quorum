package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class NumberToIntegerCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        VisitLineNumber()
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()

        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitInstruction(opcodes:GetDoubleToInteger())
    end

    action WritePython returns text
        text result = ""
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            text temp = opcodeToCast:WritePython()
            result = result + " Global_Cast_Number_To_Integer(" + temp + ")"
        end
        return result
    end
    
    action WriteJavaScript returns text
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            return "global_truncate_(" + opcodeToCast:WriteJavaScript() + ")"
        end
        return ""
    end

    action GetTemplateCopy returns QuorumOpcode
        NumberToIntegerCast op
        op:SetType(me:GetType())
        return op
    end
end