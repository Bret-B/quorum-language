package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class BooleanToIntegerCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        VisitLineNumber()
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()

        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "plugins/quorum/Libraries/Language/Types", "BooleanToIntegerCast",
                "(Z)I", false)
    end

    action WritePython returns text
        text result = ""
        text DQ = result:GetDoubleQuote()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + "__builtins__.int(" + opcodeToCast:WritePython() + ")"
        end
        return result
    end
    
    action WriteJavaScript returns text
        text result = ""
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + opcodeToCast:WriteJavaScript() + "? 1 : 0"
        end
        return result
    end

    action GetTemplateCopy returns QuorumOpcode
        BooleanToIntegerCast op
        op:SetType(me:GetType())
        return op
    end
end