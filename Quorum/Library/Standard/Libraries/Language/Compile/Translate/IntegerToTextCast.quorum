package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Interpreter.Runtime
use Libraries.Language.Compile.Interpreter.Result

class IntegerToTextCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        VisitLineNumber()
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()

        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "java/lang/Integer", "toString",
                "(I)Ljava/lang/String;", false)
    end
    
    action WritePython returns text
        text result = ""
        text DQ = result:GetDoubleQuote()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + "Global_Cast_Integer_To_Text(" + opcodeToCast:WritePython() + ")"
        end
        return result
    end

    action WriteJavaScript returns text
        text result = ""
        text DQ = result:GetDoubleQuote()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + opcodeToCast:WriteJavaScript() + " + " + DQ + DQ
        end
        return result
    end

    action Evaluate(Runtime runtime)
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            Result left = runtime:PopFromStack()

            Result result
            Type type
            
            result:type = type
            type:SetToText()

            if left not= undefined
                result:textValue = "" + left:integerValue
                runtime:PushToStack(result)
            end
        end
    end

    action GetTemplateCopy returns QuorumOpcode
        IntegerToTextCast op
        op:SetType(me:GetType())
        return op
    end
end