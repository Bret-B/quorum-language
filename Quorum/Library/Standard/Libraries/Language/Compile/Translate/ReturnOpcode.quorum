package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Symbol.ActionCallResolution
use Libraries.Language.Compile.Symbol.Action
use Libraries.Language.Compile.Symbol.Class

class ReturnOpcode is QuorumOpcode
    QuorumOpcode returnValue = undefined
    JavaBytecodeOpcodes opcodes
    integer opcode = -1
    integer opcode2 = -1

    action SetBytecodeWriteOffsetToAllChildren(integer offset)
        SetBytecodeWriteOffset(offset)
        if returnValue not= undefined
            returnValue:SetBytecodeWriteOffsetToAllChildren(offset)
        end
    end

    action SetMethodWriterToAllChildren(JavaBytecodeMethodWriter writer)
        SetMethodWriter(writer)
        if returnValue not= undefined
            returnValue:SetMethodWriterToAllChildren(writer)
        end
    end

    action SetReturnValue(QuorumOpcode op)
        returnValue = op
    end
    
    action GetReturnValue returns QuorumOpcode
        return returnValue
    end

    action GetJavaOpcode returns integer
        return opcode
    end

    action SetJavaOpcode(integer op)
        opcode = op
    end

    action GetJavaOpcode2 returns integer
        return opcode2
    end

    action SetJavaOpcode2(integer op)
        opcode2 = op
    end

    action Write
        Type type = GetType()
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        VisitLineNumber()
        if returnValue not= undefined
            returnValue:Write()
        end

        if type:IsVoid()
            integer op = opcodes:GetReturnOpcode(type)
            methodWriter:VisitInstruction(op)
        else
            integer op = opcode
            methodWriter:VisitInstruction(op)

            integer op2 = opcode2
            if op2 not= -1
                methodWriter:VisitInstruction(op2)
            end
        end
    end

    action WritePython returns text 
        text result = ""
        EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        TranslateConstants constants
        text tabs = constants:GetTabText()

        Type type = GetType()
        if not type:IsVoid()
            if returnValue not= undefined
                //in the python version if the cast is implicit, we need to make it explicit
                Type returnValueType = returnValue:GetType()
                if returnValueType:IsInteger() and type:IsNumber()
                    result = tabs + "Global_Cast_Integer_To_Number(" + returnValue:WritePython() + ")" + EOL
                else
                    result = tabs + "return " + returnValue:WritePython() + EOL
                end
            end
        else 
            result = tabs + "return" + EOL
        end
        return result
    end

    action WriteJavaScript returns text 
        text result = ""
        EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        Type type = GetType()
        if not type:IsVoid()
            if returnValue not= undefined
                
                result = "return " + returnValue:WriteJavaScript() + ";" + EOL
            end
        else 
            result = "return;" + EOL
        end
        return result
    end

    action GetTemplateCopy returns QuorumOpcode
        ReturnOpcode op
        op:SetType(GetType())
        op:SetJavaOpcode(me:GetJavaOpcode())
        op:SetJavaOpcode2(me:GetJavaOpcode2())
        return op
    end
end