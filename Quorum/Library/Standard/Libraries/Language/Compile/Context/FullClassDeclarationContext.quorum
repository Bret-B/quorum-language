package Libraries.Language.Compile.Context

use Libraries.Language.Compile.QualifiedName
use Libraries.Language.Compile.Location
use Libraries.Containers.Array
use Libraries.Language.Compile.QuorumSourceListener

class FullClassDeclarationContext is ParseContext
    public text className = ""
    public boolean staticClass = false
    public QualifiedName name = undefined
    public Location classNameLocation

    public GenericDeclarationContext generic = undefined
    public InheritStatementsContext inherit = undefined
    Array<ClassStatementsContext> statements = undefined
    /*
        In the Antlr parser, there is a bug related to documentation where it only fires
        under certain conditions. This can be used to mimic the bug, but should be removed
        once the hand parser replaces the antlr one.
    */
    public boolean fireDocumentation = true

    action Traverse(QuorumSourceListener listener)
        //I think we want these. This might be a flaw in the antlr parser
        if fireDocumentation
            EnterDocumentation(listener)
        end
        listener:EnterFullClassDeclaration(me)
        if generic not= undefined generic:Traverse(listener) end
        if inherit not= undefined inherit:Traverse(listener) end

        if statements not= undefined
            i = 0
            repeat while i < statements:GetSize()
                statements:Get(i):Traverse(listener)
                i = i + 1
            end
        end
        if fireDocumentation
            ExitDocumentation(listener)
        end
        listener:ExitFullClassDeclaration(me)
    end
end