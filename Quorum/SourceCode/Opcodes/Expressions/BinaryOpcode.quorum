package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Symbol.Operation
use Libraries.Language.Interpreter.Runtime
use Libraries.Language.Interpreter.Result

class BinaryOpcode is QuorumOpcode
    QuorumOpcode left = undefined
    QuorumOpcode right = undefined
    Operation op = undefined
    integer opcode = -1
    integer leftOpcode = -1
    integer rightOpcode = -1
    integer leftOpcode2 = -1
    integer rightOpcode2 = -1

    action SetOperation(Operation op)
        me:op = op
    end

    action GetOperation returns Operation
        return op
    end

    action SetBytecodeWriteOffsetToAllChildren(integer offset)
        SetBytecodeWriteOffset(offset)
        if left not= undefined
            left:SetBytecodeWriteOffsetToAllChildren(offset)
        end

        if right not= undefined
            right:SetBytecodeWriteOffsetToAllChildren(offset)
        end
    end

    action SetMethodWriterToAllChildren(JavaBytecodeMethodWriter writer)
        SetMethodWriter(writer)
        if left not= undefined
            left:SetMethodWriterToAllChildren(writer)
        end

        if right not= undefined
            right:SetMethodWriterToAllChildren(writer)
        end
    end

    

    private action RuntimeConvert(integer opcode, Result value)
        if opcode = -1
            return now
        end

        JavaBytecodeOpcodes opcodes
        if opcode = opcodes:GetIntegerToDouble()
            value:numberValue = value:integerValue
            value:integerValue = 0
            Type type
            type:SetToNumber()
            type:numberConstant = value:integerValue
            value:type = type
        end
    end

    action Evaluate(Runtime runtime)
        me:left:Evaluate(runtime)
        Result left = runtime:PopFromStack()
        RuntimeConvert(leftOpcode, left)
        integer current = op:GetOperation()

        Result result
        Type type
        
        result:type = type

        //short circuit at runtime
        if current = op:AND
            type:SetToBoolean()
            if not left:booleanValue //if it's false, push false and bail 
                result:booleanValue = false
            else //evaluate the right hand side
                me:right:Evaluate(runtime)
                Result right = runtime:PopFromStack()
                RuntimeConvert(rightOpcode, right)
                if right:booleanValue
                    result:booleanValue = true
                else
                    result:booleanValue = false
                end
            end

            runtime:PushToStack(result)
            return now
        elseif current = op:OR
            type:SetToBoolean()
            if left:booleanValue //if it's true, push true and bail 
                result:booleanValue = true
            else //evaluate the right hand side, becaue the left is false
                me:right:Evaluate(runtime)
                Result right = runtime:PopFromStack()
                RuntimeConvert(rightOpcode, right)
                if right:booleanValue
                    result:booleanValue = true
                else
                    result:booleanValue = false
                end
            end

            runtime:PushToStack(result)
            return now
        end
        me:right:Evaluate(runtime)
        Result right = runtime:PopFromStack()
        RuntimeConvert(rightOpcode, right)
        
        JavaBytecodeOpcodes opcodes
        //for now assume they are integers and go for it
        if current = op:PLUS
            if opcodes:GetDoubleAdd() = opcode
                result:numberValue = left:numberValue + right:numberValue
                type:SetToNumber()
            elseif opcodes:GetIntegerAdd() = opcode
                result:integerValue = left:integerValue + right:integerValue
                type:SetToInteger()
            end
        elseif current = op:MINUS
            if opcodes:GetDoubleSubtract() = opcode
                result:numberValue = left:numberValue - right:numberValue
                type:SetToNumber()
            elseif opcodes:GetIntegerSubtract() = opcode
                result:integerValue = left:integerValue - right:integerValue
                type:SetToInteger()
            end
        elseif current = op:TIMES
            if opcodes:GetDoubleMultiply() = opcode
                result:numberValue = left:numberValue * right:numberValue
                type:SetToNumber()
            elseif opcodes:GetIntegerMultiply() = opcode
                result:integerValue = left:integerValue * right:integerValue
                type:SetToInteger()
            end
        elseif current = op:DIVIDE
            if opcodes:GetDoubleDivide() = opcode
                result:numberValue = left:numberValue / right:numberValue
                type:SetToNumber()
            elseif opcodes:GetIntegerDivide() = opcode
                result:integerValue = left:integerValue / right:integerValue
                type:SetToInteger()
            end
        elseif current = op:MODULUS
            if opcodes:GetDoubleModulus() = opcode
                result:numberValue = left:numberValue mod right:numberValue
                type:SetToNumber()
            elseif opcodes:GetIntegerModulus() = opcode
                result:integerValue = left:integerValue mod right:integerValue
                type:SetToInteger()
            end
        elseif current = op:EQUALS
            if left:type:IsNumber() //types have been converted
                result:booleanValue = left:numberValue = right:numberValue
            elseif left:type:IsInteger()
                result:booleanValue = left:integerValue = right:integerValue
            elseif left:type:IsBoolean()
                result:booleanValue = left:booleanValue = right:booleanValue
            elseif left:type:IsText()
                result:booleanValue = left:textValue = right:textValue
            end
            type:SetToBoolean()
        elseif current = op:NOT_EQUALS
            if left:type:IsNumber() //types have been converted
                result:booleanValue = left:numberValue not= right:numberValue
            elseif left:type:IsInteger()
                result:booleanValue = left:integerValue not= right:integerValue
            elseif left:type:IsBoolean()
                result:booleanValue = left:booleanValue not= right:booleanValue
            elseif left:type:IsText()
                result:booleanValue = left:textValue not= right:textValue
            end
            type:SetToBoolean()
        elseif current = op:GREATER_EQUALS
            if left:type:IsNumber() //types have been converted
                result:booleanValue = left:numberValue >= right:numberValue
            elseif left:type:IsInteger()
                result:booleanValue = left:integerValue >= right:integerValue
            elseif left:type:IsText()
                result:booleanValue = left:textValue >= right:textValue
            end
            type:SetToBoolean()
        elseif current = op:GREATER
            if left:type:IsNumber()
                result:booleanValue = left:numberValue > right:numberValue
            elseif left:type:IsInteger()
                result:booleanValue = left:integerValue > right:integerValue
            elseif left:type:IsText()
                result:booleanValue = left:textValue > right:textValue
            end
            type:SetToBoolean()
        elseif current = op:LESS_EQUALS
            if left:type:IsNumber() //types have been converted
                result:booleanValue = left:numberValue = right:numberValue
            elseif left:type:IsInteger()
                result:booleanValue = left:integerValue = right:integerValue
            elseif left:type:IsBoolean()
                result:booleanValue = left:booleanValue = right:booleanValue
            elseif left:type:IsText()
                result:booleanValue = left:textValue = right:textValue
            end
            type:SetToBoolean()
        elseif current = op:LESS
            if left:type:IsNumber() 
                result:booleanValue = left:numberValue < right:numberValue
            elseif left:type:IsInteger()
                result:booleanValue = left:integerValue < right:integerValue
            elseif left:type:IsText()
                result:booleanValue = left:textValue < right:textValue
            end
            type:SetToBoolean()
        end

        runtime:PushToStack(result)
    end

    action WriteJavaScript returns text
        integer current = op:GetOperation()
        text result = ""
        text leftResult = left:WriteJavaScript()
        text operator = ""
        

        if current = op:PLUS
            operator =  "+"
        elseif current = op:MINUS
            operator =  "-"
        elseif current = op:TIMES
            operator =  "*"
        elseif current = op:DIVIDE
            operator =  "/"
        elseif current = op:MODULUS
            operator =  "%"
        elseif current = op:AND
            operator =  "&&"
        elseif current = op:OR
            operator =  "||"
        elseif current = op:EQUALS
            operator =  "=="
        elseif current = op:NOT_EQUALS
            operator =  "!="
        elseif current = op:GREATER_EQUALS
            operator =  ">="
        elseif current = op:GREATER
            operator =  ">"
        elseif current = op:LESS_EQUALS
            operator =  "<="
        elseif current = op:LESS
            operator =  "<"
        elseif current = op:NOT
            operator =  "!"
        end

        text rightResult = right:WriteJavaScript()

        Type leftType = left:GetType()
        Type rightType = right:GetType()
        if leftType:IsText() and rightType:IsNumber()
            rightResult = "(global_number_converter_(" + rightResult + "))"
        elseif leftType:IsNumber() and rightType:IsText()
            leftResult = "(global_number_converter_(" + leftResult + "))"
        end

        //we have to take the floor, because JavaScript doesn't have integers
        if leftType:IsInteger() and rightType:IsInteger() and current = op:DIVIDE
            result = result + "global_truncate_(" + leftResult + " " + operator + " " + rightResult + ")"
        else
            result = result + "(" + leftResult + " " + operator + " " + rightResult + ")"
        end
        return result
    end

    action GetJavaOpcode returns integer
        return opcode
    end

    action GetJavaLeftOpcode returns integer
        return leftOpcode
    end

    action GetJavaRightOpcode returns integer
        return rightOpcode
    end

    action GetJavaLeftOpcode2 returns integer
        return leftOpcode2
    end

    action GetJavaRightOpcode2 returns integer
        return rightOpcode2
    end

    action SetJavaOpcode(integer op)
        opcode = op
    end

    action SetJavaLeftOpcode(integer op)
        leftOpcode = op
    end

    action SetJavaRightOpcode(integer op)
        rightOpcode = op
    end

    action SetJavaLeftOpcode2(integer op)
        leftOpcode2 = op
    end

    action SetJavaRightOpcode2(integer op)
        rightOpcode2 = op
    end

    action GetLeftOpcode returns QuorumOpcode
        return left
    end

    action GetRightOpcode returns QuorumOpcode
        return right
    end

    action SetLeftOpcode(QuorumOpcode op)
        left = op
    end

    action SetRightOpcode(QuorumOpcode op)
        right = op
    end

    action Write
        VisitLineNumber()
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        left:Write()
        WriteLeft()
        right:Write()
        WriteRight()

        if opcode not= -1
            methodWriter:VisitInstruction(opcode)
        end
    end

    action WriteLeft
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        if leftOpcode not= -1
            methodWriter:VisitInstruction(leftOpcode)
        end
    end

    action WriteRight
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        if rightOpcode not= -1
            methodWriter:VisitInstruction(rightOpcode)
        end
    end

    action GetTemplateCopy returns QuorumOpcode
        BinaryOpcode bop
        Type t = GetType()
        me:SetType(t:Copy())
        bop:opcode = opcode
        bop:leftOpcode = leftOpcode
        bop:rightOpcode = rightOpcode

        Operation newOp
        newOp:SetOperation(op:GetOperation())
        bop:SetOperation(newOp)

        return bop
    end
end