package Science.Tests

use Libraries.Testing.Test
use Libraries.Compute.Statistics.DataFrame
use Libraries.Compute.Statistics.Tests.CompareMeans
use Libraries.Compute.Statistics.Reporting.CompareMeansResult

/*
    This class tests a two-sample t-test (welch and student)
*/
class UndefinedCompareTwoMeansTest is Test

    on create
        SetName("Calculate UndefinedCompareTwoMeansTest")
    end

    action Run
        DataFrame frame
        frame:LoadFromCommaSeparatedValue("y,x1,x2,x3
        2.0,4.0,-9.0,9
        6.0,7,-19.0,19
        3.0,,-18.0,18
        9.0,8.0,-16.0,16
        15.0,17.0,-2.0,2
        1.0,3.0,-4.0,4")
        frame:AddSelectedColumnRange(0,1)

        // Welch t-test (unequal variances)
        CompareMeans compare
        frame:Calculate(compare)
        
        CompareMeansResult result = compare:GetResult()
        Check(result:GetTestStatistic(), -0.5475535855568276)      
        Check(result:GetDegreesOfFreedom(), 8.474614208045049)        
        Check(result:GetInformation():GetValue(result:GetGroupsFrame():GetColumn(0):GetHeader()):GetValue("mean"), 6.0)        
        Check(result:GetInformation():GetValue(result:GetGroupsFrame():GetColumn(1):GetHeader()):GetValue("mean"), 7.8)   
        Check(result:GetProbabilityValue(), 0.5981202198797242)        
        Check(result:GetEffectSize(), -0.332252649090927)

        text summary = result:GetSummary()
        text formalsummary = result:GetFormalSummary()
        Check(true, summary not= "")
        Check(true, formalsummary not= "")

        // Student t-test (equal variances)
        CompareMeans compare2
        compare2:AssumeEqualVariances(true)
        frame:Calculate(compare2)
        
        CompareMeansResult result2 = compare2:GetResult()
        Check(result2:GetTestStatistic(), -0.5501047109652415)        
        Check(result2:GetDegreesOfFreedom(), 9.0)        
        Check(result2:GetProbabilityValue(), 0.5956339683083105)
        Check(result2:GetEffectSize(), -0.3331049445889223)

        text summary2 = result2:GetSummary()
        text formalsummary2 = result2:GetFormalSummary()
        Check(true, summary2 not= "")
        Check(true, formalsummary2 not= "")
    end
end