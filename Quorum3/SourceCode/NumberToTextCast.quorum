package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class NumberToTextCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        VisitLineNumber()
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()

        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "java/lang/Double", "toString",
                "(D)Ljava/lang/String;", false)
    end
    
    action WriteJavaScript returns text
        text result = ""
        text DQ = result:GetDoubleQuote()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            text temp = opcodeToCast:WriteJavaScript()
            result = result + "(" + temp  + " % 1 == 0? "+temp + ".toFixed(1) : " + temp + ") + " + DQ + DQ
        end
        return result
    end

    action GetTemplateCopy returns QuorumOpcode
        NumberToTextCast op
        op:SetType(me:GetType())
        return op
    end
end