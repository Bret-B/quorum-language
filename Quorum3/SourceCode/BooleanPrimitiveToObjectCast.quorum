package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class BooleanPrimitiveToObjectCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        VisitLineNumber()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "plugins/quorum/Libraries/Language/Types/Boolean", "ConvertBooleanToObject",
                "(Z)Lquorum/Libraries/Language/Object_;", false)
    end
    
    action WriteJavaScript returns text
        text result = "new quorum_Libraries_Language_Types_Boolean_("
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + opcodeToCast:WriteJavaScript()
        end
        return result + ")"
    end

    action GetTemplateCopy returns QuorumOpcode
        BooleanPrimitiveToObjectCast op
        op:SetType(me:GetType())
        return op
    end
end