package Libraries.Language.Compile

use Libraries.Language.Compile.Parser
use Libraries.System.File
use Libraries.System.DateTime
use Libraries.Containers.Iterator
use Libraries.Language.Compile.Translate.JarGenerator
use Libraries.Web.WebResponder
use Libraries.Web.WebResponse
use Libraries.Web.WebRequest
use Libraries.Containers.Array
use Libraries.System.Console
use Libraries.Compute.Math
use Libraries.Containers.HashTable
use Libraries.Language.Errors.OutOfBoundsError
use Libraries.Language.Compile.Test.CompilerTestSuite
use Libraries.System.SystemHelper
use Libraries.Language.Compile.Symbol.SymbolTable
use Libraries.Language.Compile.Symbol.Class
use Libraries.Language.Compile.Symbol.Action
use Libraries.Game.all

class Main is WebResponder
    public constant text NAME_FLAG = "-name"
    public constant text COMPILE_FLAG = "-compile"
    public constant text WEB_FLAG = "-web"
    public constant text JAVASCRIPT_FLAG = "-javascript"
    public constant text LIBRARY_FLAG = "-library"
    public constant text REGENERATE_STANDARD_LIBRARY = "-setup"
    public constant text TEST_FLAG = "-test"
    public constant text HELP_FLAG = "-help"
    HashTable<text, Array<text>> flagValues

    action Main
        //ProcessConsoleFlags()
        //GenerateDocumentation()
        //CompileHardcodedFile()
        CompileTest()
    end

    action GenerateDocumentation
        Compiler compiler
        File standardLibraryFolder = compiler:GetStandardLibraryFolder()
        compiler:SetStandardLibraryFolder(standardLibraryFolder)
        output compiler:GetVersion()
        File root
        root:SetPath("Output")
        compiler:SetOutputFolder(root)
        File main
        main:SetPath("Library/Tests/Use/Pass/GenerateAllClasses.quorum")
        Array<File> files
        files:Add(main)
        compiler:SetOutputType(compiler:DOCUMENT)
        compiler:SetMain(main)
        compiler:Compile(files)

        if compiler:IsCompilationErrorFree()
            output "Build Successful"
            if compiler:GetOutputType() = compiler:JAVASCRIPT
                output compiler:GetCompiledJavaScript()
            end
        else
            output compiler:GetCompilerErrorsAsText()
        end
    end

    private action CompileHardcodedFile
        Compiler compiler
        File standardLibraryFolder = compiler:GetStandardLibraryFolder()
        compiler:SetStandardLibraryFolder(standardLibraryFolder)
        output compiler:GetVersion()
        compiler:ScanStandardLibrary()
        File root
        root:SetPath("Output")
        compiler:SetOutputFolder(root)
        File main
        //main:SetWorkingDirectory("/Users/stefika/NetBeansProjects/BlankQuorumApplication/SourceCode/")
        //main:SetWorkingDirectory("/Users/stefika/NetBeansProjects/QUOR455/SourceCode/")
        //main:SetPath("Library/Tests/Expressions/Pass/OrAndBooleanBoolean.quorum") //CallMethodCallingBlueprint
        main:SetPath("Library/Tests/Expressions/Pass/BooleanAndOrAnd.quorum")

        File class2
        //class2:SetWorkingDirectory("/Users/stefika/NetBeansProjects/BlankQuorumApplication/SourceCode/")
        class2:SetPath("Library/Tests/Inheritance/Pass/LandVehicle.quorum")
        
        File class3
        class3:SetPath("Library/Tests/Inheritance/Pass/GasPoweredVehicle.quorum")

        File class4
        class4:SetPath("Library/Tests/Inheritance/Pass/Truck.quorum")

        File class5
        class5:SetPath("Library/Tests/Inheritance/Pass/GrandChildWithMultiple.quorum")
        Array<File> files

        files:Add(main)
        //files:Add(class2)
        //files:Add(class3)
        //files:Add(class4)
        //files:Add(class5)

        //compiler:SetOutputSpeech(false) //don't speak while in test mode
        //compiler:SetIsWebApplication(true)
        //compiler:SetOutputType(compiler:JAVASCRIPT)
        compiler:SetMain(main)
        compiler:Compile(files)

        CodeCompletionRequest request
        request:line = "use Libraries.Containers.all"
        request:lineNumber = 1
        request:fileKey = main:GetAbsolutePath()
        request:startOffset = 5
        CodeCompletionResult result = compiler:Request(request)


        if compiler:IsCompilationErrorFree()
            output "Build Successful"
            if compiler:GetOutputType() = compiler:JAVASCRIPT
                File myOutput
                myOutput:SetWorkingDirectory("/Users/alleew/Desktop/WebGLTester/public_html")
                myOutput:SetPath("TestQuorum.js")
                myOutput:Write(compiler:GetCompiledJavaScript())
                output "File written."
            end
        else
            output compiler:GetCompilerErrorsAsText()
        end
    end

    private action CompileTest
        Compiler compiler

        DateTime time
        File standardLibraryFolder = compiler:GetStandardLibraryFolder()
        compiler:SetStandardLibraryFolder(standardLibraryFolder)
        output compiler:GetVersion() + " Compile Test"


        File outputLocation
        outputLocation:SetPath("Library/Compiled")
        output "Scanning"
        number start = time:GetEpochTime()
        Library library
        library:SetCachingLibraryOpcodes(true)
        library:SetLocation(standardLibraryFolder)
        library:SetOutputFolder(outputLocation)
        library:Scan()
        output ""
        number finish = time:GetEpochTime()
        output "Time: " + ((finish - start) / 1000.0) + " seconds"

        output "Writing Standard Library"
        start = time:GetEpochTime()
        library:Write()
        finish = time:GetEpochTime()
        output "Time: " + ((finish - start) / 1000.0) + " seconds"

        compiler:SetStandardLibrary(library)
        

        output "Compiling"
        start = time:GetEpochTime()
        File root
        root:SetPath("Output")
        compiler:SetOutputFolder(root)
        File main
        //main:SetPath("Library/Tests/Use/Pass/GenerateAllClasses.quorum")
        main:SetPath("Library/Tests/Game/Pass/LoadCylinders.quorum")

        Array<File> files
        files:Add(main)
        compiler:SetMain(main)

        CompilerResult result = compiler:Compile(files, compiler:GetStandardLibrary())
        finish = time:GetEpochTime()
        output "Time: " + ((finish - start) / 1000.0) + " seconds"

        CompilerErrorManager errors = result:compilerErrorManager
        if errors:IsCompilationErrorFree()
            output "Build Successful"
            if compiler:GetOutputType() = compiler:JAVASCRIPT
                File myOutput
                myOutput:SetWorkingDirectory("/Users/alleew/Desktop/WebGLTester/public_html")
                myOutput:SetPath("TestQuorum.js")
                myOutput:Write(compiler:GetCompiledJavaScript())
                output "File written."
            end
        else
            Iterator<CompilerError> iterator = errors:GetIterator()
            repeat while iterator:HasNext()
                CompilerError error = iterator:Next()
                output error:GetErrorMessage()
            end
            //output errors:G
        end
    end

    private action ProcessConsoleFlags
        Console console
        Array<text> flags = console:GetConsoleArguments()

        if flags:IsEmpty()
            output GetHelpText()
            return now
        end

        Array<text> values = undefined
        i = 0
        repeat while i < flags:GetSize()
            text flag = flags:Get(i)
            if flag:GetCharacter(0) = "-" //it's a flag, process commands until next flag.
                Array<text> v
                values = v
                //put this new array in the hash. It's empty, but we can still
                //add to it.

                //duplicate?
                if flagValues:HasKey(flag)
                    output "The flag " + flag + " has two or more values, which I do not understand. Each flag can be used only once."
                    return now
                end
                flagValues:Add(flag, values)
            else 

                //is this the first one? If it is, they may be trying to compile
                if i = 0
                    Array<text> newValues
                    flagValues:Add(COMPILE_FLAG, newValues)
                    values = newValues
                end
                values:Add(flag)
            end
            i = i + 1
        end
        DoFlagActions()
        //DisplayFlagValues()
    end

    /*
        Flags are processed by Quorum in a particular order and within certain
        constraints. The -help flag overrides everything, so it is requested, 
        other flags are ignored. If -compile is requested, then -web cannot be. 
        The converse is also true. If the -test flag is issued, there can be no other flags. 
        The name flag can be used in combination with either -web or -compile, 
        but not both.
    */
    private action DoFlagActions
        //first check if the help flag is in there
        if flagValues:HasKey(HELP_FLAG)
            output GetHelpText()
            return now
        end
        

        if flagValues:HasKey(TEST_FLAG)
            if flagValues:GetSize() = 1
                RunTestSuite()
                return now
            else
                output "I noticed a -test flag was issued, but this flag can only be requested on its own, not with other flags."
                return now
            end
        end

        if flagValues:HasKey(REGENERATE_STANDARD_LIBRARY)
            Array<text> names = flagValues:GetValue(REGENERATE_STANDARD_LIBRARY)
            if names:GetSize() not= 1
                output "I noticed a -setup flag was issued, but this flag requires a path to where to rebuild the standard library."
                return now
            else
                text compilePath = names:Get(0)
                File libraryPath
                libraryPath:SetWorkingDirectory(compilePath)
                CompileStandardLibrary(libraryPath)
            end
        end

        text name = "Default"
        if flagValues:HasKey(NAME_FLAG)
            Array<text> names = flagValues:GetValue(NAME_FLAG)
            if names:GetSize() not= 1
                output "I noticed the name flag was used, but it does not have exactly 1 value."
                return now
            else
                name = names:Get(0)
            end
        end

        if flagValues:HasKey(COMPILE_FLAG)
            if flagValues:HasKey(WEB_FLAG)
                output "I noticed the compile flag and the web flag were both used. Only one can be passed, otherwise I cannot determine how to package the program."
                return now
            end

            Array<text> names = flagValues:GetValue(COMPILE_FLAG)

            if names:IsEmpty()
                output "I noticed you asked for a program to be compiled, but that no files were passed."
                output GetHelpText()
                return now
            end

            Compile(name, names, false)
            return now
        end

        if flagValues:HasKey(JAVASCRIPT_FLAG)
            Array<text> names = flagValues:GetValue(JAVASCRIPT_FLAG)

            if flagValues:HasKey(WEB_FLAG) or flagValues:HasKey(COMPILE_FLAG)
                output "I noticed the javascript flag was used with the compile flag or the web flag. Only one can be passed, otherwise I cannot determine how to package the program."
                return now
            end

            if names:IsEmpty()
                output "I noticed you asked for a program to be compiled to JavaScript, but that no files were passed."
                output GetHelpText()
                return now
            end

            Compile(name, names, false)
            return now
        end

        if flagValues:HasKey(WEB_FLAG)
            if flagValues:HasKey(COMPILE_FLAG)
                output "I noticed the compile flag and the web flag were both used. Only one can be passed, otherwise I cannot determine how to package the program."
                return now
            end

            Array<text> names = flagValues:GetValue(WEB_FLAG)

            if names:IsEmpty()
                output "I noticed you asked for a program to be compiled to the web, but that no files were passed."
                output GetHelpText()
                return now
            end

            Compile(name, names, true)
            return now
        end
    end

    private action CompileStandardLibrary(File compilePath)
        Compiler compiler
        text version = compiler:GetVersion()
        Library library
        library:SetOutputFolder(compilePath)

        if flagValues:HasKey(LIBRARY_FLAG)
            Array<text> names = flagValues:GetValue(LIBRARY_FLAG)
            if names:GetSize() not= 1
                output "I noticed the library flag was used, but it does not have exactly 1 value."
                return now
            else
                path = names:Get(0)
                File libraryPath
                libraryPath:SetWorkingDirectory(path)
                libraryPath:SetPath("Standard")
                library:SetLocation(libraryPath)
            end
        else 
            File standardLibraryFolder = compiler:GetStandardLibraryFolder()
            library:SetLocation(standardLibraryFolder)
        end

        DateTime time
        number start = time:GetEpochTime()
        output version + " Standard Library Setup"
        library:SetCachingLibraryOpcodes(true)

        output "Scanning Standard Library. Please wait..."
        library:Scan()

        output "Writing to disk at: " + compilePath:GetAbsolutePath()
        library:Write()

        number finish = time:GetEpochTime()
        number seconds = (finish - start) / 1000.0
        output "Complete in " + seconds + " seconds." 
    end

    private action Compile(text name, Array<text> fileNames, boolean isWebApplication)
        Array<File> files
        i = 0
        repeat fileNames:GetSize() times
            text fileName = fileNames:Get(i)
            File file
            file:SetPath(fileName)
            files:Add(file)
            i = i + 1
        end

        Compiler compiler
        compiler:SetName(name)
        File root
        root:SetPath("")
        compiler:SetOutputFolder(root)

        if flagValues:HasKey(LIBRARY_FLAG)
            Array<text> names = flagValues:GetValue(LIBRARY_FLAG)
            if names:GetSize() not= 1
                output "I noticed the library flag was used, but it does not have exactly 1 value."
                return now
            else
                path = names:Get(0)
                File libraryPath
                libraryPath:SetWorkingDirectory(path)
                libraryPath:SetPath("Standard")
                compiler:SetStandardLibraryFolder(libraryPath)
            end
        else 
            File standardLibraryFolder = compiler:GetStandardLibraryFolder()
            compiler:SetStandardLibraryFolder(standardLibraryFolder)
        end

        boolean javascript = false
        if flagValues:HasKey(JAVASCRIPT_FLAG)
            compiler:SetOutputType(compiler:JAVASCRIPT)
            compiler:SetWritingJavaScriptToDisk(true)
        end
        
        output compiler:GetVersion()

        File main = files:Get(0)
        compiler:SetMain(main)
        compiler:SetIsWebApplication(isWebApplication)
        compiler:Compile(files)

        if compiler:IsCompilationErrorFree()
            output "Build Successful"
        else
            output compiler:GetCompilerErrorsAsText()
        end
    end

    private action RunTestSuite
        CompilerTestSuite suite
        suite:Main()
    end

    private action DisplayFlagValues
        Iterator<text> keys = flagValues:GetKeyIterator()
        repeat while keys:HasNext()
            text key = keys:Next()
            Array<text> values = flagValues:GetValue(key)
            text result = key
            i = 0
            repeat values:GetSize() times
                if i = 0
                    result = result + ": " + values:Get(i)
                else
                    result = result + ", " + values:Get(i)
                end
                i = i + 1
            end

            output result
        end
    end

    private action GetHelpText returns text
        Compiler compiler
        text help = 
"Quorum " + compiler:GetVersion() + "

Quorum is a computer programming language, which you can use either " +
"from the console using flags (the program you just ran) or from a development environment (like NetBeans). " +
"For this version, the commands that Quorum knows take the following format:

    java -jar Quorum.jar (-flag value*)*

What this means is that you can pass a flag to Quorum combined with any number of values. " +
"The legal flags are listed as follows:
    -name This flag tells Quorum to change the name the file is outputs.
    -compile This flag tells Quorum to compile a set of files. If no flag is passed, this is the default.
    -web This flag tells Quorum to convert to a server-side application
    -javascript This flag converts the source code to JavaScript, so it can be run in a browser.
    -test This flag tells Quorum to run its test suite on itself.
    -library If a file path is passed to this flag, we can tell Quorum we have placed its standard library in a different folder.
    -setup If a file path is passed to this flag, it tells Quorum to recompile its standard library. Generally, unless you have modified the standard library, this flag should not be used.
    -help This flag tells Quorum to output this help screen. 


Here are a few examples of how you can use this program:
        java -jar Quorum.jar Hello.quorum 

    This would request that Quorum compiles the source file Hello.quorum.

        java -jar Quorum.jar -compile Hello.quorum" +
    "

        java -jar Quorum.jar -name MyProgram -compile Hello.quorum

    This would cause Quorum to compile Hello.quorum and name the output MyProgram." +
    "

        java -jar Quorum.jar Hello.quorum Goodbye.quorum

    This would cause Quorum to Compile two source code files. The first file must have a Main action.

For more information on writing programs in Quorum, visit www.quorumlanguage.com."     
        return help
    end

    action Respond(WebRequest request) returns WebResponse
        text result = ""
        WebResponse response
        constant text CODE_REQUEST = "code"
        Compiler compiler
        

        File library
        //To deploy to a particular server, change this line to the 
        //parent directory for where the standard library is housed.
        library:SetWorkingDirectory("/usr/share/tomcat")
        //library:SetPath("Library/Standard")
        //library:SetWorkingDirectory("/Users/stefika/Repositories/quorum-language/Quorum3")
        library:SetPath("Library/Standard")
        //output library:GetAbsolutePath()

        compiler:SetStandardLibraryFolder(library)
        
        text directory = library:GetWorkingDirectory()
        //if there is no code tag, then just ignore the request
        if not request:HasParameter(CODE_REQUEST)
            
            response:SetPageText("No Quorum code was sent to the server.")
            //response:SetPageText(directory)
            return response
        end

        text source = request:GetParameter("code")
        compiler:SetOutputType(compiler:JAVASCRIPT)
        compiler:CompileSingle(source)

        if compiler:IsCompilationErrorFree()
            response:SetPageText(compiler:GetCompiledJavaScript())
        else
            response:SetPageText(compiler:GetCompilerErrorsAsHTML())
        end
        return response
    end
end
