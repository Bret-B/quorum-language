package Libraries.Game.Graphics

use Libraries.Game.Graphics.BaseLight
use Libraries.Compute.Vector3

class DirectionalLight is BaseLight
    
    public Vector3 direction

    /*
    This action will set the direction the light points in. It takes three
    number values, which represent the three values of a 3-dimensional vector.
    */
    action SetDirection(number directionX, number directionY, number directionZ)
        direction:Set(directionX, directionY, directionZ)
    end

    /*
    This action will set the direction the light points in. The light will point
    in the same direction as the supplied Vector3 object.
    */
    action SetDirection(Vector3 newDirection)
        direction:Set(newDirection)
    end

    /*
    This action will return the current direction of this DirectionalLight.
    */
    action GetDirection returns Vector3
        return direction
    end

    /*
    This action will set the color and direction of this DirectionalLight by
    copying the color and direction of the given DirectionalLight object.
    */
    action SetLight(DirectionalLight copyFrom)
        SetLight(copyFrom:GetColor(), copyFrom:GetDirection())
    end

    /*
    This action will set both the color and direction of this light using the
    given values.
    */
    action SetLight(Color newColor, Vector3 newDirection)
        if newColor not= undefined
            SetColor(newColor)
        end

        if newDirection not= undefined
            SetDirection(newDirection)
            direction:Normalize()
        end
    end

    /*
    This action will set both the color and direction of this light. The first
    three values are the red, green, and blue values, respectively, of the color
    to set the light to. The following three values are the x, y, and z values
    of the direction vector of the light.
    */
    action SetLight(number red, number green, number blue, number x, number y, number z)
        SetColor(red, green, blue, 1)
        direction:Set(x, y, z)
        direction:Normalize()
    end

end