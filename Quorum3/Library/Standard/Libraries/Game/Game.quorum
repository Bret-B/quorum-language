/*
    The foundation of any game. Any user created game should inherit from this
    and must implement CreateGame and Update.
*/

package Libraries.Game

use Libraries.Game.Graphics.Painter
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.GraphicsManager
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.Drawable
use Libraries.Containers.List
use Libraries.Containers.Blueprints.Iterator
use Libraries.System.File
use Libraries.System.Properties
use Libraries.Interface.Panel
use Libraries.Interface.Events.KeyboardProcessor
use Libraries.Interface.Events.KeyboardListener
use Libraries.Interface.Events.MouseProcessor
use Libraries.Interface.Events.MouseListener
use Libraries.Interface.Events.MouseMovementListener
use Libraries.Interface.Events.MouseWheelListener
use Libraries.Interface.Events.MouseEvent
use Libraries.Interface.Item
use Libraries.Interface.Events.CollisionEvent

/*
The Game class is the heart of any game in Quorum. The basic Game class will
just display a gray screen. To make your own game, you should make a new class
that inherits from Game, and make your own versions of the CreateGame() and
Update(number) actions. The example below will pop up an empty gray window.

Attribute: Example

    use Libraries.Game.Game

    class Main
        action main
            Game game
            Game:StartGame()
        end
    end
*/
class Game

    public Painter batch = undefined
    ApplicationConfiguration configuration
    GraphicsManager gl20

    public boolean exitRequested = false

    // Variables used for Panels and the event system.
    private Panel activePanel = undefined
    private KeyboardProcessor keyboardProcessor
    private MouseProcessor mouseProcessor
    private List<Item> collisionList
    private List<CollisionEvent> currentCollisions

    on create
        Color color
        color = color:Black()

        configuration:title = "Game"
        configuration:width = 800
        configuration:height = 600

        configuration:initialBackgroundColor = color

        Properties properties
        text os = properties:GetOperatingSystemName()

        text path = ""
        File file = properties:GetRunLocation()
        file:SetPath("jni")
        
        path = file:GetAbsolutePath()

        properties:SetProperty("org.lwjgl.librarypath", path)

        // Setting the path to load the native C files, reusing the file and
        // path variables.
        path = ""
        if os:Contains("Mac OS X")
            file:SetPath("jni/libGameEngineCPlugins.so")
        elseif os:Contains("Windows")
            text bits = properties:GetProperty("os.arch")
            if bits = "x86"
                file:SetPath("jni/libGameEngineCPlugins32.dll")
            else
                file:SetPath("jni/libGameEngineCPlugins64.dll")
            end
        end

        
        path = file:GetAbsolutePath()
        
        GameStateManager manager
        manager:SetNativePath(path)
        manager:SetOperatingSystem(os)

        Panel panel
        panel:SetName("Default Active Panel")
        SetActivePanel(panel)
    end

    /*
    The StartGame action will start your game. Specifically, it does this:
    1. Creates the Game window.
    2. Calls the CreateGame() action.
    3. Repeatedly calls Update() until Exit() is called or the window is closed.

    If you are using any actions to change the starting settings of the window,
    call those before calling StartGame().

    Attribute: Example

        use Libraries.Game.Game

        class Main
            action main
                Game game
                Game:StartGame()
            end
        end
    */
    action StartGame
    
        GameStateManager manager

        GameDisplay display
        manager:SetGameDisplay(display)
        
        display:SetConfig(configuration)
        display:SetupDisplay()

        Application application
        application:Setup(me, configuration, display)

    end

    /*
    The Add action will add an item to the Game. This will make the game handle
    the item for many tasks. For example, adding a Drawable to the game will
    make it draw on the screen.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Game.Graphics.Drawable

        class Main is Game
            Drawable bunny

            action Main
                StartGame()
            end

            action CreateGame
                bunny:Load("Images/Rabbit.png")
                Add(bunny)
            end
        end
    */
    action Add(Item item)
        activePanel:Add(item)
    end

    /*
    The Remove action will remove an item that was put into the game using the
    Add action. For example, removing a Drawable will make the game engine stop
    drawing it.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Game.Graphics.Drawable

        class Main is Game
            Drawable bunny

            action Main
                StartGame()
            end

            action CreateGame
                bunny:Load("Images/Rabbit.png")
                Add(bunny)

                // If we don't have this remove command, the bunny will draw.
                Remove(bunny)
            end
        end
    */
    action Remove(Item item)
        activePanel:Remove(item)
    end

    action DrawAll

        batch:Begin()

        activePanel:Draw(batch)

        batch:End()

    end

    action ClearScreen
        gl20:ClearScreenColor(0.85,0.85,0.85,0)
        gl20:ClearScreen(16384)
    end

    action ContinueGame
        ProcessInputEvents()
        TestForCollisions()
        UpdateAll()
        Update(GetSecondsBetweenFrames())
        ClearScreen()
        DrawAll()
    end

    action SetConfiguration(ApplicationConfiguration config)
        configuration = config
    end

    action GetConfiguration returns ApplicationConfiguration
        return configuration
    end

    /*
    The GetScreenWidth action will return the width of the screen as an integer.

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                output GetScreenWidth()
            end
        end
    */
    action GetScreenWidth returns integer
        return configuration:width
    end

    /*
    The GetScreenHeight action will return the height of the screen as an integer.

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                output GetScreenHeight()
            end
        end
    */
    action GetScreenHeight returns integer
        return configuration:height
    end

    /*
    Using SetColorFilter will tint all drawn objects on the screen that do not
    have their own custom color tint. For example, using a red color filter will
    make all objects drawn in the game appear to be more red.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Game.Graphics.Drawable
        use Libraries.Game.Graphics.Color

        class Main is Game
        
            Drawable bunny
            Color tint

            action Main
                StartGame()
            end

            action CreateGame
                bunny:Load("Rabbit.png")
                Add(bunny)
                
                tint = tint:Maroon()

                SetColorFilter(tint)
            end
        end
    */
    action SetColorFilter(Color newColor)
        batch:SetColor(newColor)
    end

    /*
    SetColorFilter can also be called using four number parameters instead of a
    color object. The four parameters are the red, green, blue, and opacity
    of the filter, respectively. All four of the parameters should be between 0
    and 1, representing between 0% and 100% of that color component. For example,
    a value of 0 for red means that the tinting color will have no red, while a
    value of 1 will have all red components. An opacity of 0 is totally
    transparent, while an opacity of 1 will be totally visible. 

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Game.Graphics.Drawable

        class Main is Game
        
            Drawable bunny

            action Main
                StartGame()
            end

            action CreateGame
                bunny:Load("Rabbit.png")
                Add(bunny)
                
                // Our color will have 75% red, 0% green, 75% blue, and be
                // totally opaque. This will tint our bunny image purple.

                SetColorFilter(0.75, 0, 0.75, 1)
            end
        end
    */
    action SetColorFilter(number r, number g, number b, number a)
        batch:SetColor(r, g, b, a)
    end

    /*
    GetSecondsBetweenFrames will return the number of seconds that has passed
    between the current frame and the previous one. Frames are the still images
    drawn on the screen in rapid succession to make the images appear to move.
    Note that the value from this field is approximately the same value as the
    parameter in the Update action.

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action Update(number time)
                output "Seconds since last frame: " + GetSecondsBetweenFrames()
            end
        end
    */
    system action GetSecondsBetweenFrames returns number

    /*
    The Update action is where most of the game's logic goes. The code inside
    Update will be called on every frame of the game. In other words, for
    every time a picture is drawn on the screen, the Update action is called.
    The number parameter is the amount of seconds that has passed since the last
    time Update was called.

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            // It is very important when making our own Update action that it has
            // the time parameter! If we don't have it, it won't be called.
            action Update(number time)
                output "Update was called!"
            end
        end
    */
    action Update(number time)

    end

    /*
    CreateGame is called when you first start the game, and is useful for setting
    up things that need to be initialized before they can be used, like loading
    Drawables.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Game.Graphics.Drawable

        class Main is Game

            Drawable bunny

            action Main
                StartGame()
            end

            action CreateGame
                bunny:Load("Rabbit.png")
                Add(bunny)
            end
            
        end
    */
    action CreateGame

    end

    /*
    The Exit action is used to tell the Game class to close the application
    after it completes the current iteration of the main loop.

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            number timer = 0

            action Main
                StartGame()
            end

            action Update(number time)
                timer = timer + time
                if timer > 3
                    Exit()
                    output "Exiting the game!"
                end
            end
            
        end
    */
    action Exit
        exitRequested = true
    end

    /*
    GetActivePanel returns the currently active Panel object. The Game class
    always has one Panel that is considered active. The active Panel will have
    all of its children drawn and updated. When Items are added to the Game,
    they are actually added to the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Panel

        class Main is Game

            Panel myPanel = undefined

            action Main
                StartGame()
            end

            action CreateGame
                myPanel = GetActivePanel()
            end
            
        end
    */
    action GetActivePanel returns Panel
        return activePanel
    end

    /*
    SetActivePanel sets the currently active Panel object. The Game class
    always has one Panel that is considered active. The active Panel will have
    all of its children drawn and updated. When Items are added to the Game,
    they are actually added to the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Panel

        class Main is Game

            Panel myPanel

            action Main
                StartGame()
            end

            action CreateGame
                SetActivePanel(myPanel)
            end
            
        end
    */
    action SetActivePanel(Panel panel)
        if activePanel not= undefined
            //activePanel:keyboardProcessor = undefined
            //activePanel:mouseProcessor = undefined
            activePanel:Disable()
        end
        //panel:keyboardProcessor = keyboardProcessor
        //panel:mouseProcessor = mouseProcessor
        panel:SetPosition(0,0)
        panel:SetWidth(GetScreenWidth())
        panel:SetHeight(GetScreenHeight())
        activePanel = panel
        activePanel:Enable()
    end

    /*
    This action updates the active Panel and all of the Items that have been
    added to the active Panel. This is called automatically by the Game class 
    as part of the main loop, so most users will never need to use this action
    directly.
    */
    private action UpdateAll
        activePanel:UpdateAll(GetSecondsBetweenFrames())
    end

    /*
    This action looks for input events and notifies any event handlers that
    have been added to the Game class as necessary. This action is called
    automatically by the Game class as part of the main loop, so most users will
    never need to use this action directly.
    */
    private action ProcessInputEvents
        List<MouseEvent> mouseEvents = mouseProcessor:GetCurrentEvents()
        MouseEvent mouseEvent = undefined
        repeat mouseEvents:GetSize() times
            mouseEvent = mouseEvents:RemoveFromFront()
            Item temp = activePanel:GetItemAt(mouseEvent:GetX(), mouseEvent:GetY())
            if temp not= undefined
                temp:ProcessMouseEvent(mouseEvent)
            end
        end

        keyboardProcessor:Update()
        keyboardProcessor:ProcessEvents()
    end

    /*
    This action adds a MouseListener to the Game's active Panel. The mouse
    listener will be notified of mouse buttons being clicked and released 
    so long as it is a part of the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Events.MouseListener

        class Main is Game

            MouseListener listener

            action Main
                StartGame()
            end

            action CreateGame
                AddMouseListener(listener)
            end
            
        end
    */
    action AddMouseListener(MouseListener listener)
        activePanel:AddMouseListener(listener)
    end

    /*
    This action adds a MouseMovementListener to the Game's active Panel. The
    listener will be notified of mouse movement and dragging so long as it is a
    part of the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Events.MouseMovementListener

        class Main is Game

            MouseMovementListener listener

            action Main
                StartGame()
            end

            action CreateGame
                AddMouseMovementListener(listener)
            end
            
        end
    */
    action AddMouseMovementListener(MouseMovementListener listener)
        activePanel:AddMouseMovementListener(listener)
    end

    /*
    This action adds a MouseWheelListener to the Game's active Panel. The
    listener will be notified of mouse wheel scrolling so long as it is a
    part of the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Events.MouseWheelListener

        class Main is Game

            MouseWheelListener listener

            action Main
                StartGame()
            end

            action CreateGame
                AddMouseWheelListener(listener)
            end
            
        end
    */
    action AddMouseWheelListener(MouseWheelListener listener)
        activePanel:AddMouseWheelListener(listener)
    end

    /*
    This action adds a KeyboardListener to the Game's active Panel. The
    listener will be notified of keys being pressed and released so long as it
    is a part of the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Events.KeyboardListener

        class Main is Game

            Keyboard listener

            action Main
                StartGame()
            end

            action CreateGame
                AddKeyboardListener(listener)
            end
            
        end
    */
    action AddKeyboardListener(KeyboardListener listener)
        keyboardProcessor:AddListener(listener)
    end

    /*
    This action removes a MouseListener from the Game's active Panel. The
    listener will no longer receive events for mouse clicks and releases from
    the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Events.MouseListener

        class Main is Game

            MouseListener listener

            action Main
                StartGame()
            end

            action CreateGame
                AddMouseListener(listener)

                RemoveMouseListener(listener)
            end
            
        end
    */
    action RemoveMouseListener(MouseListener listener)
        activePanel:RemoveMouseListener(listener)
    end

    /*
    This action removes a MouseMovementListener from the Game's active Panel.
    The listener will no longer receive events for mouse movement and dragging
    from the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Events.MouseMovementListener

        class Main is Game

            MouseMovementListener listener

            action Main
                StartGame()
            end

            action CreateGame
                AddMouseMovementListener(listener)

                RemoveMouseMovementListener(listener)
            end
            
        end
    */
    action RemoveMouseMovementListener(MouseMovementListener listener)
        activePanel:RemoveMouseMovementListener(listener)
    end

    /*
    This action removes a MouseWheelListener to the Game's active Panel. The
    listener will no longer receive events for mouse wheel scrolling from
    the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Events.MouseWheelListener

        class Main is Game

            MouseWheelListener listener

            action Main
                StartGame()
            end

            action CreateGame
                AddMouseWheelListener(listener)

                RemoveMouseWheelListener(listener)
            end
            
        end
    */
    action RemoveMouseWheelListener(MouseWheelListener listener)
        activePanel:RemoveMouseWheelListener(listener)
    end

    /*
    This action removes a KeyboardListener to the Game's active Panel. The
    listener will no longer receive events for key presses and releases from
    the active Panel.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Events.KeyboardListener

        class Main is Game

            KeyboardListener listener

            action Main
                StartGame()
            end

            action CreateGame
                AddKeyboardListener(listener)

                RemoveKeyboardListener(listener)
            end
            
        end
    */
    action RemoveKeyboardListener(KeyboardListener listener)
        keyboardProcessor:RemoveListener(listener)
    end

    /*
    This action sets the starting screen size of the game. The default screen
    size is 800 by 600. This action should be called before calling StartGame().

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                SetScreenSize(1024, 800)
                StartGame()
            end
        end
    */
    action SetScreenSize(integer width, integer height)
        configuration:width = width
        configuration:height = height
    end

    /*
    This action enables or disables vSync in the game. Passing a value of true
    enables it, a value of false disables it. vSync is enabled by default. 
    This action should be called before calling StartGame().

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                SetVSync(false)
                StartGame()
            end
        end
    */
    action SetVSync(boolean vSync)
        configuration:vSyncEnabled = vSync
    end

    /*
    This action enables or disables resizing the game window by dragging the
    edges of the screen. Passing a value of true enables it, a value of false
    disables it. Resizing is disabled by default. 
    This action should be called before calling StartGame().

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                EnableResizing(true)
                StartGame()
            end
        end
    */
    action EnableResizing(boolean resize)
        configuration:resizable = resize
    end

    /*
    This action sets the name of the game displayed at the top of the window. 
    The default name is "Game". This action should be called before calling
    StartGame().

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                SetGameName("Mariachi Band Simulator 3000")
                StartGame()
            end
        end
    */
    action SetGameName(text name)
        configuration:title = name
    end

    /*
    This action enables or disables auto resizing of textures if the game window
    is resized. Passing a value of true enables it, a value of false disables it.
    Texture auto resizing is enabled by default. This action should be called
    before calling StartGame().

    Attribute: Example

        use Libraries.Game.Game

        class Main is Game

            action Main
                EnableTextureAutoResizing(false)
                StartGame()
            end
        end
    */
    action EnableTextureAutoResizing(boolean resize)
        configuration:autoResizeTextures = resize
    end

    /*
    Adds an Item to the Game to be tested for collisions. Items added in this
    way will produce collision events if they overlap another Item that has been
    added as a collision listener. Items can react to collisions via the 
    BeginCollision and FinishCollision actions in Item.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Item

        class Main is Game

            Item item1
            Item item2

            action Main
                StartGame()
            end

            action CreateGame
                AddCollisionListener(item1)
                AddCollisionListener(item2)
            end

        end
    */
    action AddCollisionListener(Item item)
        collisionList:Add(item)
    end

    /*
    Removes an Item from the Game for testing for collisions. Items that have
    been removed as collision listeners will no longer receive collision events,
    and can no longer cause collision events in other Items.

    Attribute: Example

        use Libraries.Game.Game
        use Libraries.Interface.Item

        class Main is Game

            Item item1
            Item item2

            action Main
                StartGame()
            end

            action CreateGame
                AddCollisionListener(item1)
                AddCollisionListener(item2)

                RemoveCollisionListener(item1)
            end

        end
    */
    action RemoveCollisionListener(Item item)
        collisionList:Remove(item)
    end

    private action TestForCollisions
        integer counter1 = 0
        integer counter2 = 1
        Item item1 = undefined
        Item item2 = undefined

        repeat collisionList:GetSize() times
            item1 = collisionList:Get(counter1)
            number x1 = item1:GetX()
            number x2 = item1:GetX() + item1:GetWidth()
            number y1 = item1:GetY()
            number y2 = item1:GetY() + item1:GetHeight()
            counter2 = counter1 + 1
            repeat while counter2 < collisionList:GetSize()
                item2 = collisionList:Get(counter2)
                if x1 < item2:GetX() + item2:GetWidth() and x2 > item2:GetX() and y1 < item2:GetY() + item2:GetHeight() and y2 > item2:GetY()
                    integer collisionCounter = 0
                    boolean found = false
                    CollisionEvent temp = undefined
                    repeat while collisionCounter < currentCollisions:GetSize() and found = false
                        temp = currentCollisions:Get(collisionCounter)
                        if temp:Contains(item1) and temp:Contains(item2)
                            found = true
                        end
                    end

                    if found = false
                        CollisionEvent newCollision
                        newCollision:SetCollision(item1, item2)
                        currentCollisions:Add(newCollision)
                        newCollision:BeginCollision()
                    end
                else
                    integer collisionCounter = 0
                    boolean found = false
                    CollisionEvent temp = undefined
                    repeat while collisionCounter < currentCollisions:GetSize() and found = false
                        temp = currentCollisions:Get(collisionCounter)
                        if temp:Contains(item1) and temp:Contains(item2)
                            found = true
                        end
                    end

                    if found = true
                        currentCollisions:Remove(temp)
                        temp:FinishCollision()
                    end
                end
                
                counter2 = counter2 + 1
            end

            counter1 = counter1 + 1
        end
    end

end