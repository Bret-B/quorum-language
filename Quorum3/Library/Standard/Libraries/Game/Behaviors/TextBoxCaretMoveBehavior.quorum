package Libraries.Game.Behaviors

use Libraries.Interface.Item
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.TextBox

class TextBoxCaretMoveBehavior is TimedBehavior
    
    public constant integer LEFT = 1
    public constant integer RIGHT = 2
    public constant integer DOWN = 3
    public constant integer UP = 4
 
    TextBox textBox = undefined

    integer direction = 0

    action SetDirection(integer direction)
        me:direction = direction
    end

    action GetDirection returns integer
        return direction
    end

    action Begin(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextBox)
            return now
        end

        textBox = cast(TextBox, item)

        if direction = LEFT
            textBox:MoveCaretLeft()
        elseif direction = RIGHT
            textBox:MoveCaretRight()
        elseif direction = DOWN
            textBox:MoveCaretDown()
        elseif direction = UP
            textBox:MoveCaretUp()
        end
    end

    action Finish
        if textBox = undefined
            return now
        end

        SetTimer(textBox:GetCaretMoveTime())
        textBox:AddBehavior(me)
    end
end