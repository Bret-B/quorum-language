use Libraries.Game.Layer3D
use Libraries.Containers.Array
use Libraries.Interface.Item3D
use Libraries.Game.Collision.BroadphaseCollision3D
use Libraries.Interface.Events.CollisionEvent3D
use Libraries.Interface.Events.CollisionListener3D
use Libraries.Language.Object
use Libraries.Game.Collision.Item3DNode
use Libraries.Game.Collision.ContactEdge3D
use Libraries.Game.Collision.Item3DNode

package Libraries.Game.Collision

class CollisionManager3D
    BroadphaseCollision3D broadphase
    CollisionEvent3D collisionList = undefined
    integer collisionCount = 0
    Array<CollisionListener3D> listeners

    Layer3D myLayer = undefined
    
    action SetLayer(Layer3D layer)
        myLayer = layer
    end

    action AddPair(Object objectA, Object objectB)
        Item3DNode nodeA = cast(Item3DNode, objectA)
        Item3DNode nodeB = cast(Item3DNode, objectB)

        Item3D itemA = nodeA:item
        Item3D itemB = nodeB:item

        integer indexA = nodeA:childIndex
        integer indexB = nodeB:childIndex

        if itemA = itemB
            return now
        end

        ContactEdge3D edge = itemB:contactList
        repeat while edge not= undefined
            if edge:GetOther() = itemA
                Item3D iA = edge:GetCollision():GetItemA()
                Item3D iB = edge:GetCollision():GetItemB()
                integer a = edge:GetCollision():GetChildIndexA()
                integer b = edge:GetCollision():GetChildIndexB()
                
                if iA = itemA and a = indexA and iB = itemB and b = indexB
                    return now
                end

                if iA = itemB and a = indexB and iB = itemA and b = indexA
                    return now
                end
            end

            edge = edge:GetNext()
        end

        // create collision event and add to collision event list
        CollisionEvent3D collision
        collision:Initialize(itemA, indexA, itemB, indexB)
        
        collision:SetPrevious(undefined)
        collision:SetNext(collisionList)
        if collisionList not= undefined
            collisionList:SetPrevious(collision)
        end
        collisionList = collision

        // connect collision to itemA
        collision:nodeA:SetCollision(collision)
        collision:nodeA:SetOther(itemB)

        collision:nodeA:SetPrevious(undefined)
        collision:nodeA:SetNext(itemA:contactList)
        if itemA:contactList not= undefined
            itemA:contactList:SetPrevious(collision:nodeA)
        end
        itemA:contactList = collision:nodeA

        // connect collision to itemB
        collision:nodeB:SetCollision(collision)
        collision:nodeB:SetOther(itemA)

        collision:nodeB:SetPrevious(undefined)
        collision:nodeB:SetNext(itemB:contactList)
        if itemB:contactList not= undefined
            itemB:contactList:SetPrevious(collision:nodeB)
        end
        itemB:contactList = collision:nodeB

        collisionCount = collisionCount + 1
    end

    action FindNewCollisions
        broadphase:UpdatePairs(me)
    end

    action Destroy(CollisionEvent3D collision)
        Item3D itemA = collision:GetItemA()
        Item3D itemB = collision:GetItemB()

        if collision:IsTouching()
            integer i = 0
            repeat while i < listeners:GetSize()
                CollisionListener3D listener = listeners:Get(i)
                listener:FinishCollision(collision)
                i = i + 1
            end
        end

        if collision:GetPrevious() not= undefined
            collision:GetPrevious():SetNext(collision:GetNext())
        end

        if collision:GetNext() not= undefined
            collision:GetNext():SetPrevious(collision:GetPrevious())
        end

        if collision = collisionList
            collisionList = collision:GetNext()
        end

        // remove collision from itemA
        if collision:nodeA:GetPrevious() not= undefined
            collision:nodeA:GetPrevious():SetNext(collision:nodeA:GetNext())
        end

        if collision:nodeA:GetNext() not= undefined
            collision:nodeA:GetNext():SetPrevious(collision:nodeA:GetPrevious())
        end

        if collision:nodeA = itemA:contactList
            itemA:contactList = collision:nodeA:GetNext()
        end

        // remove collision from itemB
        if collision:nodeB:GetPrevious() not= undefined
            collision:nodeB:GetPrevious():SetNext(collision:nodeB:GetNext())
        end

        if collision:nodeB:GetNext() not= undefined
            collision:nodeB:GetNext():SetPrevious(collision:nodeB:GetPrevious())
        end

        if collision:nodeB = itemB:contactList
            itemB:contactList = collision:nodeB:GetNext()
        end

        collisionCount = collisionCount - 1
    end

    action Collide
        CollisionEvent3D collision = collisionList
        repeat while collision not= undefined
            boolean jump = false
            Item3D itemA = collision:GetItemA()
            Item3D itemB = collision:GetItemB()
            integer indexA = collision:GetChildIndexA()
            integer indexB = collision:GetChildIndexB()

            if not itemA:IsCollidable() or not itemB:IsCollidable()
                CollisionEvent3D cNuke = collision
                collision = cNuke:GetNext()
                Destroy(cNuke)
                jump = true
            end

            // collision filtering
            // this involves, for example, ignoring collisions where both items
            // are declared as static bodies
            // should we allow user-defined collision filtering?

            if not jump
                integer nodeIDA = itemA:GetNode(indexA):nodeID
                integer nodeIDB = itemB:GetNode(indexB):nodeID

                boolean overlap = broadphase:TestOverlap(nodeIDA, nodeIDB)

                if not overlap
                    CollisionEvent3D cNuke = collision
                    collision = cNuke:GetNext()
                    Destroy(cNuke)
                else
                    collision:DispatchCollision(listeners)
                    collision = collision:GetNext()
                end
            end
        end
    end

    action GetBroadphase returns BroadphaseCollision3D
        return broadphase
    end

    action GetCollisionList returns CollisionEvent3D
        return collisionList
    end

    action GetCollisionCount returns integer
        return collisionCount
    end

    action AddCollisionListener(CollisionListener3D listener)
        listeners:Add(listener)
    end

    action RemoveCollisionListener(CollisionListener3D listener)
        listeners:Remove(listener)
    end
end