package Libraries.Interface.Controls

use Libraries.Containers.Array
use Libraries.Interface.Item2D
use Libraries.Interface.LabelBoxView
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.TabBar

class TabPane is Item2D
    
    TabBar bar = undefined
    Item2D currentItem = undefined
    Item2D viewArea
    Layout viewLayout

    on create
        SetName("Tab Pane")
        viewArea:SetName("Tab View Area")
    end

    action Initialize(number width, number height, Array<Tab> tabs)
        TabBar tabBar
        bar = tabBar
        bar:SetTabPane(me)
        bar:Initialize(tabs)
        
        viewLayout:SetPercentageX(0)
        viewLayout:SetPercentageY(0)
        viewLayout:SetPercentageWidth(1)
        viewLayout:SetPercentageHeight(1)
        viewLayout:SetHeightOffset(-bar:GetHeight())
        viewArea:AddLayout(viewLayout)

        viewArea:SetClipping(true)

        Add(viewArea)
        Add(bar)

        SetSize(width, height)

        if tabs:GetSize() > 0
            tabs:Get(0):SetToggleState(true)
        end
    end

    action SetCurrentItem(Item2D item)
        if currentItem not= undefined
            viewArea:Remove(currentItem)
        end
        if item not= undefined
            viewArea:Add(item)
        end
        currentItem = item

        viewLayout:SetHeightOffset(-bar:GetHeight())
        viewArea:Resize()
    end

    action GetCurrentItem returns Item2D
        return currentItem
    end

    action Add(Array<Tab> array)
        bar:Add(array)
    end

    action Add(Tab tab)
        bar:Add(tab)
    end

    action Remove(Tab tab)
        bar:Remove(tab)
    end
end