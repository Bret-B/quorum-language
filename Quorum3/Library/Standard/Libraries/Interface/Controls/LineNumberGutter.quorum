package Libraries.Interface.Controls

use Libraries.Containers.Array
use Libraries.Game.Graphics.Label

class LineNumberGutter is Gutter
   
    Array<Label> labels

    action UpdateLines(integer startLine, integer totalLines, number lineHeight)
        // Currently regenerates everything each time.
        // This should preserve whatever objects and information are useful
        // between each call.

        Empty()

        TextBox textBox = GetTextBox()
        integer fontSize = cast(integer, textBox:GetFontSize() * textBox:GetInterfaceScale())
        // Temporary hard-coded value
        integer width = 50

        integer counter = 0
        number y = textBox:GetHeight()
        repeat while counter < totalLines
            y = y - lineHeight
            Label label = GetLabel(counter)
            label:SetSize(fontSize)
            label:SetText("" + (startLine + counter))
            label:SetPosition(width - label:GetWidth(), y)
            Add(label)

            counter = counter + 1
        end

        SetSize(width, textBox:GetHeight() + lineHeight)
    end
 
    private action GetLabel(integer index) returns Label
        if labels:GetSize() <= index or labels:Get(index) = undefined
            Label label
            labels:Add(index, label)
            return label
        else
            return labels:Get(index)
        end
    end
end