package Libraries.Interface.Selections

use Libraries.Containers.Support.Pair
use Libraries.Containers.MultipleLineText
use Libraries.Interface.Controls.TextBox

class TextBoxSelection is Selection

    Pair<integer> indices
    Pair<integer> lines
    Pair<integer> lineIndices
    boolean caretAtEnd = true

    on create
        lines:Set(0, 0)
        indices:Set(0, 0)
        lineIndices:Set(0, 0)
    end

    action Set(integer startIndex, integer endIndex, boolean caretAtEnd)
        TextBox textBox = GetTextBox()
        if textBox not= undefined
            MultipleLineText multi = textBox:GetLines()
            integer startLine = multi:LineIndexFromCharacterIndex(startIndex)
            integer startLinePosition = startIndex - multi:CharacterIndexFromLineIndex(startLine)

            integer endLine = multi:LineIndexFromCharacterIndex(endIndex)
            integer endLinePosition = endIndex - multi:CharacterIndexFromLineIndex(endLine)

            indices:Set(startIndex, endIndex)
            lines:Set(startLine, endLine)
            lineIndices:Set(startLinePosition, endLinePosition)

            me:caretAtEnd = caretAtEnd

            SetDisplayName(multi:GetText(startIndex, endIndex))
        end
    end

    action GetTextBox returns TextBox
        if GetItem() is TextBox
            TextBox textBox = cast(TextBox, GetItem())
            return textBox
        end
        return undefined
    end

    action GetStartIndex returns integer
        return indices:GetFirstValue()
    end

    action GetEndIndex returns integer
        return indices:GetSecondValue()
    end

    action GetFirstLine returns integer
        return lines:GetFirstValue()
    end

    action GetLastLine returns integer
        return lines:GetSecondValue()
    end

    action GetFirstLineIndex returns integer
        return lineIndices:GetFirstValue()
    end

    action GetLastLineIndex returns integer
        return lineIndices:GetSecondValue()
    end

    action IsCaretAtEnd returns boolean
        return caretAtEnd
    end

    action IsEmpty returns boolean
        return indices:GetFirstValue() = indices:GetSecondValue()
    end
end