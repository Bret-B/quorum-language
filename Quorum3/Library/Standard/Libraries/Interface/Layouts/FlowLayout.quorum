package Libraries.Interface.Layouts

use Libraries.Interface.Item2D
use Libraries.Containers.Array

class FlowLayout is Layout
    
    boolean wrapping = true

    action Layout(Item2D item)
        number x = 0
        number y = 0

        number tallestHeight = 0
        Array<Item2D> kids = item:GetChildren()
        i = 0
        repeat while i < kids:GetSize()
            Item2D kid = kids:Get(i)

            if x + kid:GetWidth() > item:GetWidth() and wrapping
                y = y - tallestHeight
                x = 0
                tallestHeight = 0
            end

            if kid:GetHeight() > tallestHeight
                tallestHeight = kid:GetHeight()
            end

            kid:SetPosition(x, y)
            x = x + kid:GetWidth()
            i = i + 1
        end

        if not wrapping
            item:SetWidth(x)
        end
        
        number oldHeight = item:GetHeight()
        item:SetHeight(tallestHeight - y)
        item:MoveY(oldHeight - (tallestHeight - y))
    end

    action EnableWrapping(boolean wrap)
        wrapping = wrap
    end

    action WrappingEnabled returns boolean
        return wrapping
    end

end