package Libraries.Interface.Layouts

use Libraries.Interface.Item2D
use Libraries.Containers.Array
use Libraries.Interface.Controls.Control

class FlowLayout is Layout
    
    boolean wrapping = false
    boolean padFront = false
    boolean ignoreHidden = false
    number padding = 0

    action Layout(Control container, Array<Item2D> items, number containerWidth, number containerHeight)
        number x = 0
        number y = 0

        if padFront
            x = padding
        end

        number tallestHeight = 0
        i = 0
        repeat while i < items:GetSize()
            Item2D item = items:Get(i)
            if item is Control and ((not ignoreHidden) or item:IsShowing())
                Control control = cast(Control, item)
                if x + control:GetWidth() > containerWidth and wrapping
                    y = y - tallestHeight
                    x = 0
                    tallestHeight = 0
                end

                if control:GetHeight() > tallestHeight
                    tallestHeight = control:GetHeight()
                end

                control:SetPosition(x, y)
                x = x + control:GetWidth() + padding
            end
            i = i + 1
        end

        if not wrapping
            container:SetWidth(x)
        else
            number oldHeight = container:GetHeight()
            container:SetHeight(tallestHeight - y)
            container:MoveY(oldHeight - (tallestHeight - y))
        end
    end

    action SetWrapping(boolean wrap)
        wrapping = wrap
    end

    action WrappingEnabled returns boolean
        return wrapping
    end

    action SetPadding(number value)
        padding = value
    end

    action GetPadding returns number
        return padding
    end

    action SetFrontPadding(boolean pad)
        padFront = pad
    end

    action IsFrontPadding returns boolean
        return padFront
    end

    action SetIgnoreHidden(boolean ignore)
        ignoreHidden = ignore
    end

    action IsIgnoringHidden returns boolean
        return ignoreHidden
    end
end