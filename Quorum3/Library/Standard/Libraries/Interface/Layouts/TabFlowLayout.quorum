package Libraries.Interface.Layouts

use Libraries.Interface.Item2D
use Libraries.Containers.Array
use Libraries.Game.Graphics.Label
use Libraries.Game.Graphics.Font
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.Control
use Libraries.Interface.Controls.Tab

class TabFlowLayout is Layout
    
    boolean padFront = false
    boolean ignoreHidden = false
    number padding = 0
    number offset = 0

    action Layout(Control container, Array<Item2D> items, number containerWidth, number containerHeight)
        number x = -offset
        number y = 0

        integer counter = 0
        repeat while counter < items:GetSize()
            Item2D item = items:Get(counter)
            if item is Control
                Control control = cast(Control, item)
                control:Resize()
                LayoutProperties properties = control:GetLayoutProperties(containerWidth, containerHeight)
                control:SetHeight(properties:GetHeightOffset())
            end

            counter = counter + 1
        end

        if padFront
            x = padding
        end

        // 1.4 is currently hard-coded. This should be modified in the future
        // to be more reflective of different padding options.
        number textArea = container:GetHeight() / 1.4
        i = 0
        repeat while i < items:GetSize()
            Item2D kid = items:Get(i)

            if (not ignoreHidden) or kid:IsShowing()
                if kid is Tab
                    Tab tab = cast(Tab, kid)
                    Label label = tab:GetLabel()
                    Font font = label:GetFont()
                    counter = 0
                    number lineHeight = 0
                    repeat while lineHeight <= textArea
                        counter = counter + 1
                        font:SetSize(counter)
                        lineHeight = font:GetLineHeight()
                    end

                    label:SetSize(counter - 1)
                end

                kid:SetHeight(container:GetHeight())
                kid:SetPosition(x, y)
                x = x + kid:GetWidth() + padding
            end
            i = i + 1
        end
    end

    action SetPadding(number value)
        padding = value
    end

    action GetPadding returns number
        return padding
    end

    action SetFrontPadding(boolean pad)
        padFront = pad
    end

    action IsFrontPadding returns boolean
        return padFront
    end

    action SetIgnoreHidden(boolean ignore)
        ignoreHidden = ignore
    end

    action IsIgnoringHidden returns boolean
        return ignoreHidden
    end

    action SetOffset(number value)
        offset = value
    end

    action GetOffset returns number
        return offset
    end
end