package Libraries.Interface.Layouts

use Libraries.Interface.Item2D
use Libraries.Containers.Array
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.TreeItem
use Libraries.Interface.Controls.Control

class TreeLayout is Layout
    
    number padding = 0
    boolean isTopAligned = false

    action Layout(Control container, Array<Item2D> items, number containerWidth, number containerHeight)
        number x = 0
        number y = 0

        number totalHeight = 0
        number longestWidth = 0
        integer i = 0

        repeat while i < items:GetSize()
            Item2D kid = items:Get(i)
            kid:Resize()

            number height = kid:GetHeight()
            number treeHeight = 0
            if kid is TreeItem
                TreeItem treeItem = cast(TreeItem, kid)
                treeHeight = treeItem:GetTreeHeight() - height
            end


            if kid:GetWidth() > longestWidth
                longestWidth = kid:GetWidth()
            end

            if isTopAligned = true
                y = y - height
            end

            kid:SetPosition(x, y)

            if isTopAligned = false
                y = y - height - treeHeight - padding
            else
                y = y - treeHeight - padding
            end

            i = i + 1
        end

        if container not= undefined
            LayoutProperties containerProperties = container:GetLayoutProperties(containerWidth, containerHeight)
            if containerProperties not= undefined
                if containerProperties:GetHorizontalLayoutMode() = containerProperties:FIT_CONTENTS
                    container:SetWidth(longestWidth + x)
                end
                if containerProperties:GetVerticalLayoutMode() = containerProperties:FIT_CONTENTS
                    number oldHeight = container:GetHeight()
                    number oldTop = container:GetY() + container:GetHeight()
                    container:SetHeight(-y)
                    container:SetY(oldTop - container:GetHeight())
                end
            end
        end
    end

    action IsTopAligned returns boolean
        return isTopAligned
    end

    action SetTopAligned(boolean topAligned)
        isTopAligned = topAligned
    end

    action SetPadding(number value)
        padding = value
    end

    action GetPadding returns number
        return padding
    end
end