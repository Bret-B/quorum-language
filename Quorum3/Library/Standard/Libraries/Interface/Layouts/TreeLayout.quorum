package Libraries.Interface.Layouts

use Libraries.Interface.Item2D
use Libraries.Containers.Array
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.TreeItem
use Libraries.Interface.Controls.Control

class TreeLayout is Layout
    
    boolean wrapping = false
    number padding = 0

    action Layout(Control container, Array<Item2D> items, number containerWidth, number containerHeight)
        number x = 0
        number y = 0

        number longestWidth = 0
        i = items:GetSize()

        repeat while i > 0
            i = i - 1
            Item2D kid = items:Get(i)
            kid:Resize()

            number height = 0
            if kid is TreeItem
                TreeItem treeItem = cast(TreeItem, kid)
                height = treeItem:GetTreeHeight()
            else
                height = kid:GetHeight()
            end

            if y + height > containerHeight and wrapping
                y = 0
                x = x + longestWidth
                longestWidth = 0
            end

            if kid:GetWidth() > longestWidth
                longestWidth = kid:GetWidth()
            end

            kid:SetPosition(x, y + height - kid:GetHeight())

            y = y + height + padding
        end

        container:SetWidth(longestWidth + x)
        if not wrapping
            number oldHeight = container:GetHeight()
            container:SetHeight(y)
            container:SetY(-y)
        else
            container:MoveY(container:GetHeight() - y)
        end
    end

    action SetWrapping(boolean wrap)
        wrapping = wrap
    end

    action WrappingEnabled returns boolean
        return wrapping
    end

    action SetPadding(number value)
        padding = value
    end

    action GetPadding returns number
        return padding
    end
end