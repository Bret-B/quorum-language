package Libraries.Interface

use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.Texture
use Libraries.Game.Graphics.Drawable

class Panel is Item
    
    boolean hasBorder = false
    boolean hasBackground = false

    Color borderColor
    Color backgroundColor

    on create
        borderColor = borderColor:Gray()
        backgroundColor = backgroundColor:LightGray()
    end

    action Initialize(number width, number height)
        SetWidth(width)
        SetHeight(height)
        EnableBackground()
        EnableBorder()
        SetName("Panel")
    end

    action EnableBackground
        if hasBackground = true
            return now
        end

        Texture backTexture
        backTexture:LoadFilledRectangle(cast(integer, GetWidth()), cast(integer, GetHeight()), backgroundColor)

        Drawable background
        background:Load(backTexture)
        background:SetName("Panel Background")

        Add(background, 0)

        hasBackground = true
    end

    action DisableBackground
        if hasBackground = false
            return now
        end

        Item background = Remove(0)
        background:Dispose()
        
        hasBackground = false
    end

    action SetBackgroundColor(Color color)
        backgroundColor = color

        if hasBackground = true
            DisableBackground()
            EnableBackground()
        end
    end

    action EnableBorder
        if hasBorder = true
            return now
        end

        integer borderIndex = 0

        if hasBackground
            borderIndex = 1
        end

        Texture borderTexture
        borderTexture:LoadRectangle(cast(integer, GetWidth()), cast(integer, GetHeight()), borderColor)

        Drawable border
        border:Load(borderTexture)
        border:SetName("Panel Border")

        Add(border, borderIndex)

        hasBorder = true
    end

    action DisableBorder
        if hasBorder = false
            return now
        end

        integer borderIndex = 0

        if hasBackground
            borderIndex = 1
        end

        Item border = Remove(borderIndex)
        border:Dispose()
        
        hasBorder = false
    end

    action SetBorderColor(Color color)
        borderColor = color

        if hasBorder = true
            DisableBorder()
            EnableBorder()
        end
    end

    action IsBackgroundEnabled returns boolean
        return hasBackground
    end

    action IsBorderEnabled returns boolean
        return hasBorder
    end

end