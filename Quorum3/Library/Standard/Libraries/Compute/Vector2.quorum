/* A class representing a vector in 2D space. */

package Libraries.Compute

use Libraries.Compute.all
use Libraries.Containers.Array

class Vector2

    Math math

    number x = 0
    number y = 0
    constant number radiansToDegrees = 180 / math:pi
    constant number degreesToRadians = math:pi / 180

    action GetX returns number
        return x
    end

    action GetY returns number
        return y
    end

    // set the calling vectors coordinates
    action Set(Vector2 vector) returns Vector2
        x = vector:GetX()
        y = vector:GetY()
        return me
    end

    action Set(number xValue, number yValue) returns Vector2
        x = xValue
        y = yValue
        return me
    end

    // returns a copy of the calling vector
    action Copy() returns Vector2
        Vector2 vector
        vector:Set(x,y)
        return vector
    end

    action Length(number x, number y) returns number
        return math:SquareRoot(x * x + y * y)
    end

    action Length() returns number
        return math:SquareRoot(x * x + y * y)
    end

    action LengthSquared(number x, number y) returns number
        return x * x + y * y
    end

    action LengthSquared() returns number
        return x * x + y * y
    end

    action Subtract(Vector2 vector) returns Vector2
        x = x - vector:GetX()
        y = y - vector:GetY()
        return me
    end

    action Subtract(number xValue, number yValue) returns Vector2
        x = x - xValue
        y = y - yValue
        return me
    end

    action Normalize() returns Vector2
        number length = Length()
        if length not= 0
            x = x / length
            y = y / length
        end
        return me
    end

    action Add(Vector2 vector) returns Vector2
        x = x + vector:GetX()
        y = y + vector:GetY()
        return me
    end

    action Add(number xValue, number yValue) returns Vector2
        x = x + xValue
        y = y + yValue
        return me
    end

    action DotProduct(number x1, number y1, number x2, number y2) returns number
        return x1 * x2 + y1 * y2
    end

    action DotProduct(Vector2 vector) returns number
        return x * vector:GetX() + y * vector:GetY()
    end

    action DotProduct(number xValue, number yValue) returns number
        return x * xValue + y * yValue
    end

    action Scale(number scalar) returns Vector2
        x = x * scalar
        y = y * scalar
        return me
    end

    action Scale(number xValue, number yValue) returns Vector2
        x = x * xValue
        y = y * yValue
        return me
    end

    action Scale(Vector2 vector) returns Vector2
        x = x * vector:GetX()
        y = y * vector:GetY()
        return me
    end

    action MultiplyAndAdd (Vector2 vector, number scalar) returns Vector2
        x = x + vector:GetX() * scalar
        y = y + vector:GetY() * scalar
        return me
    end

    action MultiplyAndAdd(Vector2 addVector, Vector2 multiplyVector) returns Vector2
        x = x + addVector:GetX() * multiplyVector:GetX()
        y = y + addVector:GetY() * multiplyVector:GetY()
        return me
    end

    action Distance (number x1, number y1, number x2, number y2) returns number
        constant number x_d = x2 - x1
        constant number y_d = y2 - y1
        return math:SquareRoot(x_d * x_d + y_d * y_d)
    end

    action Distance (Vector2 vector) returns number
        constant number x_d = vector:GetX() - x
        constant number y_d = vector:GetY() - y
        return math:SquareRoot(x_d * x_d + y_d * y_d)
    end

    action Distance (number xValue, number yValue) returns number
        constant number x_d = xValue - x
        constant number y_d = yValue - y
        return math:SquareRoot(x_d * x_d + y_d * y_d)
    end

    action DistanceSquared (number x1, number y1, number x2, number y2) returns number
        constant number x_d = x2 - x1
        constant number y_d = y2 - y1
        return x_d * x_d + y_d * y_d
    end

    action DistanceSquared (Vector2 vector) returns number
        constant number x_d = vector:GetX() - x
        constant number y_d = vector:GetY() - y
        return x_d * x_d + y_d * y_d
    end

    action DistanceSquared (number xValue, number yValue) returns number
        constant number x_d = xValue - x
        constant number y_d = yValue - y
        return x_d * x_d + y_d * y_d
    end

    action Limit (number limit) returns Vector2
        return LimitSquared(limit * limit)
    end

    action LimitSquared(number limit2) returns Vector2
        number length2 = LengthSquared()
        if length2 > limit2
            return Scale(math:SquareRoot(limit2 / length2))
        end
        return me
    end

    action Clamp (number min, number max) returns Vector2
        constant number lengthSquared = LengthSquared()

        if lengthSquared = 0.0
            return me
        end

        number max2 = max * max

        if lengthSquared > max2
            return Scale(math:SquareRoot(max2 /lengthSquared))
        end

        number min2 = min * min

        if lengthSquared < min2
            return Scale(math:SquareRoot(min2 /lengthSquared))
        end

        return me
    end

    action ToText() returns text
        return "[" + x + ":" + y + "]"
    end

    action SetLength(number length) returns Vector2
        return SetLengthSquared(length * length)
    end

    action SetLengthSquared(number lengthSquared) returns Vector2
        number oldLengthSquared = LengthSquared()
        if oldLengthSquared = 0 or oldLengthSquared = lengthSquared
            return me
        else
            return Scale(math:SquareRoot(lengthSquared / oldLengthSquared))
        end
    end

    action Multiply(Matrix3 matrix) returns Vector2
        number x = me:x + matrix:values:Get(0) + me:y * matrix:values:Get(3) + matrix:values:Get(6)
        number y = me:x + matrix:values:Get(1) + me:y * matrix:values:Get(4) + matrix:values:Get(7)
        me:x = x
        me:y = y
        return me
    end

    action CrossProduct (Vector2 vector) returns number
        return x * vector:GetY() - y * vector:GetY()
    end

    action CrossProduct (number xValue, number yValue) returns number
        return x * yValue - y * xValue
    end

   action Angle () returns number
        number angle = math:InverseTangent(y, x) / radiansToDegrees
        if angle < 0
            angle = angle + 360
        end
        return angle
    end

    action Angle (Vector2 vector) returns number
	return math:InverseTangent(CrossProduct(vector), DotProduct(vector)) / radiansToDegrees
    end

    action AngleInRadians () returns number
        return math:InverseTangent(y, x)
    end

    action AngleInRadians(Vector2 vector) returns number
        return math:InverseTangent(CrossProduct(vector), DotProduct(vector))
    end

    action SetAngle (number degrees) returns Vector2
        return SetAngleInRadians(degrees * degreesToRadians)
    end

    action SetAngleInRadians (number radians) returns Vector2
        Set(Length(), 0.0)
        RotateRadians(radians)
        return me
    end

    action Rotate (number degrees) returns Vector2
        return RotateRadians(degrees * degreesToRadians)
    end
    
   /* check this */
   action RotateRadians (number radians) returns Vector2
        number cos = math:Cosine(radians)
        number sin = math:Sine(radians)
        number newX = x * cos - y * sin
        number newY = x * sin + y * cos
        x = newX
        y = newY
        return me
   end

    action Rotate90Degrees (integer direction) returns Vector2
        number xValue = x
        if direction >= 0
            x = y * -1.0
            y = xValue
        else
            x = y
            y = xValue * -1.0
        end

        return me
    end

    action LinearInterpolation (Vector2 tangent, number alpha) returns Vector2
        constant number invAlpha = 1.0 - alpha
        x = (x * invAlpha) + (tangent:GetX() * alpha)
        y = (y * invAlpha) + (tangent:GetY() * alpha)
        return me
    end

    /* needs Interpolation class
    action interpolate (Vector2 target, number alpha, Interpolation interpolation)
        return LinearInterpolation(target, interpolation:Apply(alpha))
    end */

   /* action hashCode() returns integer
        integer prime = 31
        integer result = 1
        result = prime * result + numberUtils:floatToIntBits(x)
        result = prime * result + numerUtils:floatToIntBits(y)
        return result
    end */

    /* action equals (Object object) returns boolean
        if me = object
            return true
        end
        
        if object = undefined
            return false
        end */

    action EqualAtPrecision (Vector2 vector, number precision) returns boolean

        if vector = undefined
            return false

        elseif math:AbsoluteValue(vector:GetX() - x) > precision
            return false

        elseif math:AbsoluteValue(vector:GetY() - y) > precision
            return false

        end

        return true

    end

    action EqualAtPrecision (number xValue, number yValue, number precision) returns boolean
        if math:AbsoluteValue(xValue - x) > precision
            return false
        elseif math:AbsoluteValue(yValue - y) > precision
            return false
        end
        return true
    end

    action IsUnit() returns boolean 
        return IsUnit(0.000000001)
    end

    action IsUnit(number margin) returns boolean
        if math:AbsoluteValue(LengthSquared() - 1.0) < margin
	    return true
        else
	    return false
        end
    end

    action IsZero() returns boolean
        return x = 0 and y = 0
    end

    action IsZero (number margin)  returns boolean
        return LengthSquared() < margin
    end

    action IsOnLine (Vector2 other) returns boolean
        return math:AbsoluteValue(x * other:GetY() - y * other:GetX()) <= 0.000001
    end
    
   action IsOnLine (Vector2 other, number epsilon) returns boolean
        return math:AbsoluteValue(x * other:GetY() - y * other:GetX()) <= epsilon
    end

    action IsCollinear (Vector2 other, number epsilon) returns boolean
        return IsOnLine(other, epsilon) and DotProduct(other) > 0.0
    end

    action IsCollinear (Vector2 other) returns boolean
        return IsOnLine(other) and DotProduct(other) > 0.0
    end

    action IsCollinearOpposite (Vector2 other, number epsilon) returns boolean
        return IsOnLine(other, epsilon) and DotProduct(other) < 0.0
    end

    action IsCollinearOpposite (Vector2 other) returns boolean
        return IsOnLine(other) and DotProduct(other) < 0.0
    end

    action IsPerpendicular (Vector2 vector) returns boolean
        return math:AbsoluteValue(DotProduct(vector)) <= 0.000001
    end

    action IsPerpendicular (Vector2 vector, number epsilon) returns boolean
        //return mathUtils:IsZero(DotProduct(vector), epsilon)
        return math:AbsoluteValue(DotProduct(vector)) <= epsilon
    end

    action HasSameDirection (Vector2 vector) returns boolean
        return DotProduct(vector) > 0
    end

    action HasOppositeDirection (Vector2 vector) returns boolean
        return DotProduct(vector) < 0
    end

    action SetZero returns Vector2
        x = 0
        y = 0
        return me
    end
    
end