package Libraries.Compute

use Libraries.Containers.Array
use Libraries.Compute.Math
use Libraries.Compute.Vector2
use Libraries.Compute.Vector3
use Libraries.Compute.Matrix3
use Libraries.Compute.Matrix4

/*
Affine2 is a specialized three-by-three matrix that represents a series of 2D
transformations, such as translations, scales, flips, rotations, and shears.

Attribute: Example

*/
class Affine2 
    
    public number M00 = 1.0
    public number M01 = 0.0
    public number M02 = 0.0
    public number M10 = 0.0
    public number M11 = 1.0
    public number M12 = 0.0
    Math math


    /*
    This action constructs an identity matrix. The resulting affine transformation
    is:
        1.0  |   0.0  |   0.0
        0.0  |   1.0  |   0.0
        0.0  |   0.0  |   1.0

    Attribute: Returns The calling Affine2

    Attribute: Example

    */
    action Identity returns Affine2
        M00 = 1
        M01 = 0
        M02 = 0
        M10 = 0
        M11 = 1
        M12 = 0

        return me
    end

    /*
    This action sets the affine transformation to the passed affine transformation.
    
    Attribute: Parameter other The affine matrix to use to set as the affine matrix

    Attribute: Returns The calling Affine2

    Attribute: Example

    */
    action Set(Affine2 other) returns Affine2

        M00 = other:M00
        M01 = other:M01
        M02 = other:M02
        M10 = other:M10
        M11 = other:M11
        M12 = other:M12

        return me
    end

    /*
    This action sets the affine matrix to the passed 3x3 matrix.

    Attribute: Parameter matrix The matrix to set as the affine matrix

    Attribute: Returns The caling Affine2

    Attribute: Example

    */
    action Set(Matrix3 matrix) returns Affine2

        Array<number> matrixValues = matrix:values

        M00 = matrixValues:Get(matrix:M00)
        M01 = matrixValues:Get(matrix:M01)
        M02 = matrixValues:Get(matrix:M02)
        M10 = matrixValues:Get(matrix:M10)
        M11 = matrixValues:Get(matrix:M11)
        M12 = matrixValues:Get(matrix:M12)

        return me
    end

    /*
    This action sets the affine matrix to the 2D transformation components of the
    passed 4x4 matrix.

    Attribute: Parameter matrix The matrix to set as the affine matrix

    Attribute: Returns The calling Affine2
    
    Attribute: Example
    */
    action Set(Matrix4 matrix) returns Affine2

        Array<number> matrixValues = matrix:values

        M00 = matrixValues:Get(matrix:M00)
        M01 = matrixValues:Get(matrix:M01)
        M02 = matrixValues:Get(matrix:M03)
        M10 = matrixValues:Get(matrix:M10)
        M11 = matrixValues:Get(matrix:M11)
        M12 = matrixValues:Get(matrix:M13)

        return me
    end

    /*
    This action sets the affine matrix to the translation matrix with the
    given x and y translations.

    Attribute: Parameter x The translation x-coordinate
    Attribute: Parameter y The translation y-coordinate

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToTranslation (number x, number y) returns Affine2

        M00 = 1
        M01 = 0
        M02 = x
        M10 = 0
        M11 = 1
        M12 = y
        
        return me
    end

    /*
    This action sets the affine matrix to the translation matrix with the given
    vector.

    Attribute: Parameter vector The translation vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToTranslation (Vector2 vector) returns Affine2

        return SetToTranslation(vector:GetX(), vector:GetY())
    
    end

    /*
    This action sets the affine matrix to the scaling matrix with the given
    x and y scales.

    Attribute: Parameter scaleX The amount to scale in the x-direction
    Attribute: Parameter scaleY The amount to scale in the y-direction

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToScaling (number scaleX, number scaleY) returns Affine2

        M00 = scaleX
        M01 = 0
        M02 = 0
        M10 = 0
        M11 = scaleY
        M12 = 0

        return me
    end

    /*
    This action sets the affine matrix to the scaling matrix with the given
    scaling vector.
    
    Attribute: Parameter scale The scaling vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToScaling (Vector2 scale) returns Affine2
        
        return SetToScaling(scale:GetX(), scale:GetY())
     
    end

    /*
    This action sets the affine matrix as the rotation matrix with the given
    rotation angle in degrees.

    Attribute: Parameter degrees The rotation angle in degrees

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToRotation (number degrees) returns Affine2
        
        number cosine = math:Cosine(math:DegreesToRadians(degrees))
        number sine = math:Sine(math:DegreesToRadians(degrees))

        M00 = cosine
        M01 = sine * -1
        M02 = 0
        M10 = sine
        M11 = cosine
        M12 = 0

        return me
    end

    /*
    This action sets the affine matrix to the rotation matrix with the given
    rotation angle in radians.

    Attribute: Parameter radians The rotation angle in radians

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToRotationInRadians (number radians) returns Affine2

        number cosine = math:Cosine(radians)
        number sine = math:Sine(radians)

        M00 = cosine
        M01 = sine * -1
        M02 = 0
        M10 = sine
        M11 = cosine
        M12 = 0

        return me
    end


    /*
    This action sets the affine matrix to the rotation matrix that will rotate any
    vector in the counter-clockwise direction around the z-axis (the axis coming
    out of the screen).

    Attribute: cosine The cosine of the angle
    Attribute: sine The sine of the angle

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToRotation (number cosine, number sine) returns Affine2

        M00 = cosine
        M01 = sine * -1
        M02 = 0
        M10 = sine
        M11 = cosine
        M12 = 0
        
        return me

    end

    /*
    This action sets the affine matrix to the shearing matrix with the passed
    shear in x and y.

    Attribute: Parameter shearX The shear in x
    Attribute: Parameter shearY The shear in y

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToShearing (number shearX, number shearY) returns Affine2
        
        M00 = 1
        M01 = shearX
        M02 = 0
        M10 = shearY
        M11 = 1
        M12 = 0

        return me
    end

    /*
    This action sets the affine matrix to the shearing matrix with the passed
    shear vector.

    Attribute: Parameter vector The shear vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToShearing (Vector2 vector) returns Affine2

        return SetToShearing (vector:GetX(), vector:GetY())
        
    end


    /*
    This action sets the affine matrix to the concatenation of translation, rotation,
    and scale. This is more efficient than doing all three transformations
    separately.

    Attribute: Parameter x The translation in x
    Attribute: Parameter y The translation in y
    Attribute: Parameter degrees The angle in degrees to rotate
    Attribute: Parameter scaleX The amount to scale in the x-direction
    Attribute: Parameter scaleY The amount to scale in the y-direction

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToTranslationRotationScale(number x, number y, number degrees, number scaleX, number scaleY) returns Affine2
    
        M02 = x
        M12 = y

        if degrees = 0

            M00 = scaleX
            M01 = 0
            M10 = 0
            M11 = scaleY

        else
            
            number cosine = math:Cosine(math:DegreesToRadians(degrees))
	    number sine = math:Sine(math:DegreesToRadians(degrees))

            M00 = cosine * scaleX
            M01 = -1 * sine * scaleY
            M10 = sine * scaleX
            M11 = cosine * scaleY
            
        end

        return me
    end

    /*
    This action sets the affine matrix to the concatenation of translation, rotation,
    and scale. This is more efficient than doing all three transformations
    separately.

    Attribute: Parameter translation The translation vector
    Attribute: Parameter degrees The angle in degrees to rotate
    Attribute: Parameter scale The scale vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToTranslationRotationScale(Vector2 translation, number degrees, Vector2 scale) returns Affine2
    
        return SetToTranslationRotationScale(translation:GetX(), translation:GetY(), degrees, scale:GetX(), scale:GetY())

    end

    /*
    This action sets the affine matrix to the concatenation of translation, rotation,
    and scale. This is more efficient than doing all three transformations
    separately.

    Attribute: Parameter x The translation in x
    Attribute: Parameter y The translation in y
    Attribute: Parameter degrees The angle in radians to rotate
    Attribute: Parameter scaleX The amount to scale in the x-direction
    Attribute: Parameter scaleY The amount to scale in the y-direction

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToTranslationRotationRadiansScale (number x, number y, number radians, number scaleX, number scaleY) returns Affine2
        
        M02 = x
        M12 = y

        if radians = 0

            M00 = scaleX
            M01 = 0
            M10 = 0
            M11 = scaleY

        else
            
            sine = math:Sine(radians)
            cosine = math:Cosine(radians)

            M00 = cosine * scaleX
            M01 = -1 * sine * scaleY
            M10 = sine * scaleX
            M11 = cosine * scaleY
            
        end
        
        return me
    end

    /*
    This action sets the affine matrix to the concatenation of translation, rotation,
    and scale. This is more efficient than doing all three transformations
    separately.

    Attribute: Parameter translation The translation vector
    Attribute: Parameter degrees The angle in radians to rotate
    Attribute: Parameter scale The scale vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToTranslationRotationRadiansScale(Vector2 translation, number radians, Vector2 scale) returns Affine2
    
        return SetToTranslationRotationRadiansScale(translation:GetX(), translation:GetY(), radians, scale:GetX(), scale:GetY())

    end

    /*
    This action sets the affine matrix to the concatenation of translation and
    scale. This is more efficient than doing both separately.

    Attribute: Parameter x The translation in x
    Attribute: Parameter y The translation in y
    Attribute: Parameter scaleX The scale in the x-direction
    Attribute: Parameter scaleY The scale in the y-direction

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToTranslationScale (number x, number y, number scaleX, number scaleY) returns Affine2
        
        M00 = scaleX
        M01 = 0
        M02 = x
        M10 = 0
        M11 = scaleY
        M12 = y
        
        return me
    end


    /*
    This action sets the affine matrix to the concatenation of translation and
    scale. This is more efficient than doing both separately.

    Attribute: Parameter translation The translation vector
    Attribute: Parameter scale The scale vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToTranslationScale (Vector2 translation, Vector2 scale) returns Affine2
        
        return SetToTranslationScale (translation:GetX(), translation:GetY(), scale:GetX(), scale:GetY())
       
    end

    /*
    This action sets the affine matrix to the product of the passed affine
    matrices.

    Attribute: Parameter left The left affine matrix
    Attribute: Parameter right The right affine matrix

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action SetToProduct (Affine2 left, Affine2 right) returns Affine2

        M00 = left:M00 * right:M00 + left:M01 * right:M10
        M01 = left:M00 * right:M01 + left:M01 * right:M11
        M02 = left:M00 * right:M02 + left:M01 * right:M12 + left:M02
        M10 = left:M10 * right:M00 + left:M11 * right:M10
        M11 = left:M10 * right:M01 + left:M11 * right:M11
        M12 = left:M10 * right:M02 + left:M11 * right:M12 + left:M12

        return me
    end

    /*
    This action inverts the affine matrix.

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Invert returns Affine2

        number determinant = Determinant()

        // if determinant = 0
        // throw exception
        // end

        number inverseDeterminant = 1.0 / determinant

        number tmp00 = M11
        number tmp01 = -1 * M01
        number tmp02 = M01 * M12 - M11 * M02
        number tmp10 = -1 * M10
        number tmp11 = M00
        number tmp12 = M10 * M02 - M00 * M12

        M00 = inverseDeterminant * tmp00
        M01 = inverseDeterminant * tmp01
        M02 = inverseDeterminant * tmp02
        M10 = inverseDeterminant * tmp10
        M11 = inverseDeterminant * tmp11
        M12 = inverseDeterminant * tmp12

        return me
    end


    /*
    This action postmultiplies the affine matrix with the passed affine matrix. Calling
    A:Multiply(B) will result in A = AB.

    Attribute: Parameter affine The affine matrix to multiply by
    
    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Multiply (Affine2 affine) returns Affine2
        
        number tmp00 = M00 * affine:M00 + M01 * affine:M10
        number tmp01 = M00 * affine:M01 + M01 * affine:M11
        number tmp02 = M00 * affine:M02 + M01 * affine:M12 + M02
        number tmp10 = M10 * affine:M00 + M11 * affine:M10
        number tmp11 = M10 * affine:M01 + M11 * affine:M11
        number tmp12 = M10 * affine:M02 + M11 * affine:M12 + M12

        M00 = tmp00
        M01 = tmp01
        M02 = tmp02
        M10 = tmp10
        M11 = tmp11
        M12 = tmp12

        return me
    end

    /*
    This action premultiplies the affine matrix with the passed affine matrix. Calling
    A:Multiply(B) will result in A = BA.

    Attribute: Parameter affine The affine matrix to multiply by

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreMultiply (Affine2 affine) returns Affine2

        number tmp00 = affine:M00 * M00 + affine:M01 * M10
        number tmp01 = affine:M00 * M01 + affine:M01 * M11
        number tmp02 = affine:M00 * M02 + affine:M01 * M12 + affine:M02
        number tmp10 = affine:M10 * M00 + affine:M11 * M10
        number tmp11 = affine:M10 * M01 + affine:M11 * M11
        number tmp12 = affine:M10 * M02 + affine:M11 * M12 + affine:M12

        M00 = tmp00
        M01 = tmp01
        M02 = tmp02
        M10 = tmp10
        M11 = tmp11
        M12 = tmp12

        return me
    end

    /*
    This action postmultiplies the affine matrix with the translation matrix.

    Attribute: Parameter x The x-component of the translation vector
    Attribute: Parameter y The y-component of the translation vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Translate (number x, number y) returns Affine2

        M02 = M02 + (M00 * x + M01 * y)
        M12 = M12 + (M10 * x + M11 * y)
        
        return me
    end

    /*
    This action postmultiplies the affine matrix with the translation matrix.

    Attribute: Parameter translate The translation vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Translate (Vector2 translate) returns Affine2
        
        return Translate (translate:GetX(), translate:GetY())

    end

    /*
    This action premultiplies the affine matrix with the translation matrix.

    Attribute: Parameter x The x-component of the translation vector
    Attribute: Parameter y The y-component of the translation vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreTranslate (number x, number y) returns Affine2

        M02 = M02 + x
        M12 = M12 + y
        
        return me
    end

    /*
    This action premultiplies the affine matrix with the translation matrix.

    Attribute: Parameter translate The translation vector
    
    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreTranslate (Vector2 translate) returns Affine2

        return PreTranslate (translate:GetX(), translate:GetY())

    end

    /*
    This action postmultiplies the affine matrix with the scale matrix.
    
    Attribute: Parameter scaleX The scale in the x-direction
    Attribute: Parameter scaleY The scale in the y-direction

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Scale (number scaleX, number scaleY) returns Affine2

        M00 = M00 * scaleX
        M01 = M01 * scaleY
        M10 = M10 * scaleX
        M11 = M11 * scaleY

        return me
    end

    /*
    This action postmultiplies the affine matrix with the scale matrix.

    Attribute: Parameter scale The scale vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Scale (Vector2 scale) returns Affine2

        return Scale(scale:GetX(), scale:GetY())
        
    end

    /*
    This action premultiplies the affine matrix with the scale matrix.

    Attribute: Parameter scaleX The scale in the x-direction
    Attribute: Parameter scaleY The scale in the y-direction

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreScale (number scaleX, number scaleY) returns Affine2

        M00 = M00 * scaleX
        M01 = M01 * scaleX
        M02 = M02 * scaleX
        M10 = M10 * scaleY
        M11 = M11 * scaleY
        M12 = M12 * scaleY

        return me
   end

    /*
    This action premultiplies the affine matrix with the scale matrix.

    Attribute: Parameter scale The scale vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreScale (Vector2 scale) returns Affine2
        
        return PreScale (scale:GetX(), scale:GetY())

    end

    /*
    This action postmultiplies the affine matrix with the (counter-clockwise)
    rotation matrix.

    Attribute: Parameter degrees The angle in degrees
    
    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Rotate (number degrees) returns Affine2

        if degrees = 0
            return me
        end
            
        number cosine = math:Cosine(math:DegreesToRadians(degrees))
        number sine = math:Sine(math:DegreesToRadians(degrees))

        number tmp00 = M00 * cosine + M01 * sine
        number tmp01 = M00 * (-1 * sine) + M01 * cosine
        number tmp10 = M10 * cosine + M11 * sine
        number tmp11 = M10 * (-1 * sine) + M11 * cosine

        M00 = tmp00
        M01 = tmp01
        M10 = tmp10
        M11 = tmp11

        return me
   end

    /*
    This action postmultiplies the affine matrix with the (counter-clockwise)
    rotation matrix.

    Attribute: Parameter radians The angle in radians
    
    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action RotateInRadians (number radians) returns Affine2

        if radians = 0
            return me
        end
            
        number cosine = math:Cosine(radians)
        number sine = math:Sine(radians)

        number tmp00 = M00 * cosine + M01 * sine
        number tmp01 = M00 * (-1 * sine) + M01 * cosine
        number tmp10 = M10 * cosine + M11 * sine
        number tmp11 = M10 * (-1 * sine) + M11 * cosine

        M00 = tmp00
        M01 = tmp01
        M10 = tmp10
        M11 = tmp11

        return me
    end

    /*
    This action premultiplies the affine matrix with the (counter-clockwise)
    rotation matrix.

    Attribute: Parameter degrees The angle in degrees
    
    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreRotate (number degrees) returns Affine2

        if degrees = 0
            return me
        end

        number cosine = math:Cosine(math:DegreesToRadians(degrees))
        number sine = math:Sine(math:DegreesToRadians(degrees))

        number tmp00 = cosine * M00 - sine * M10
        number tmp01 = cosine * M01 - sine * M11
        number tmp02 = cosine * M02 - sine * M12
        number tmp10 = sine * M00 + cosine * M10
        number tmp11 = sine * M01 + cosine * M11
        number tmp12 = sine * M02 + cosine * M12

        M00 = tmp00
        M01 = tmp01
        M02 = tmp02
        M10 = tmp10
        M11 = tmp11
        M12 = tmp12

        return me

    end

    /*
    This action premultiplies the affine matrix with the (counter-clockwise)
    rotation matrix.

    Attribute: Parameter radians The angle in radians
    
    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreRotateInRadians (number radians) returns Affine2

        if radians = 0
            return me
        end

        number cosine = math:Cosine(radians)
        number sine = math:Sine(radians)

        number tmp00 = cosine * M00 - sine * M10
        number tmp01 = cosine * M01 - sine * M11
        number tmp02 = cosine * M02 - sine * M12
        number tmp10 = sine * M00 + cosine * M10
        number tmp11 = sine * M01 + cosine * M11
        number tmp12 = sine * M02 + cosine * M12

        M00 = tmp00
        M01 = tmp01
        M02 = tmp02
        M10 = tmp10
        M11 = tmp11
        M12 = tmp12

        return me

    end

    /*
    This action postmultiplies the affine matrix by the shear matrix.
    
    Attribute: Parameter shearX The shear in the x-direction
    Attribute: Parameter shearY The shear in the y-direction

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Shear (number shearX, number shearY) returns Affine2

        number tmp0 = M00 + shearY * M01
        number tmp1 = M01 + shearX * M00
        M00 = tmp0
        M01 = tmp1

        tmp0 = M10 + shearY * M11
        tmp1 = M11 + shearX * M10
        M10 = tmp0
        M11 = tmp1

        return me
    end

    /*
    This action postmultiplies the affine matrix by the shear matrix.
    
    Attribute: Parameter shear The shear vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action Shear (Vector2 shear) returns Affine2

        return Shear (shear:GetX(), shear:GetY())

    end

    /*
    This action premultiplies the affine matrix by the shear matrix.
    
    Attribute: Parameter shearX The shear in the x-direction
    Attribute: Parameter shearY The shear in the y-direction

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreShear (number shearX, number shearY) returns Affine2

        number tmp00 = M00 + shearX * M10
        number tmp01 = M01 + shearX * M11
        number tmp02 = M02 + shearX * M12
        number tmp10 = M10 + shearY * M00
        number tmp11 = M11 + shearY * M01
        number tmp12 = M12 + shearY * M02

        M00 = tmp00
        M01 = tmp01
        M02 = tmp02
        M10 = tmp10
        M11 = tmp11
        M12 = tmp12

        return me
    end

    /*
    This action premultiplies the affine matrix by the shear matrix.
    
    Attribute: Parameter shear The shear vector

    Attribute: Returns The calling Affine2

    Attribute: Example
    */
    action PreShear (Vector2 shear) returns Affine2

        return PreShear (shear:GetX(), shear:GetY())

    end

    /*
    This action calculates the determinant of the affine matrix.

    Attribute: Returns The determinant of the affine matrix

    Attribute: Example
    */
    action Determinant returns number

        return M00 * M11 - M01 * M10

    end


    /*
    This action gets the x and y translation component of the affine matrix.

    Attribute: Returns the position vector of the translation

    Attribute: Example
    */
    action GetTranslation() returns Vector2
        Vector2 position
        position:Set(M02, M12)

        return position

    end

    /*
    This action determines whether this affine matrix is a translation matrix.
    
    Attribute: Returns true if matrix is a translation matrix and false if it is not

    Attribute: Example
    */
    action IsTranslation () returns boolean

        return M00 = 1 and M11 = 1 and M01 = 0 and M10 = 0

    end

    /*
    This action determines whether this affine matrix is the identity matrix.
    
    Attribute: Returns true if matrix is the identity matrix and false if it is not

    Attribute: Example
    */
    action IsIdentityMatrix () returns boolean

        return M00 = 1 and M02 = 0 and M12 = 0 and M11 = 1 and M01 = 0 and M10 = 0

    end
    
    /*
    This action applies the affine transformation(s) to the passed point.

    Attribute: Parameter point The point to apply the transformations to

    Attribute: Example
    */
    action ApplyAffineTransformation (Vector2 point)
    
        number x = point:GetX()
        number y = point:GetY()

        point:Set(M00 * x + M01 * y + M02, M10 * x + M11 * y + M12)

    end

    /*
    This action returns the text-representation of the affine matrix.

    Attribute: Returns The text representation of the matrix

    Attribute: Example
    */
    action ToText () returns text

        return "[" + M00 + "|" + M01 + "|" + M02 + "]
["+ M10 + "|" + M11 + "|" + M12 + "]
[0.0|0.0|0.1]"

    end    

end