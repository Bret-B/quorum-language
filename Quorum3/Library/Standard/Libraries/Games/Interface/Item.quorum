package Libraries.Games.Interface

use Libraries.Containers.List
use Libraries.Containers.HashTable
use Libraries.Containers.Blueprints.Iterator
use Libraries.Games.Interface.Events.MouseEventListener
use Libraries.Games.Interface.Events.MouseEvent

class Item 

    public number x = 0
    public number y = 0

    public number width = 0
    public number height = 0

    List<MouseEventListener> mouseListeners

    private HashTable<text, State> states
    State currentState = undefined
    text currentStateName = ""

    action SetX(number newX)
        x = newX
    end
   
    action SetY(number newY)
        y = newY
    end

    action GetX returns number
        return x
    end

    action GetY returns number
        return y
    end

    action SetPosition(number newX, number newY)
        x = newX
        y = newY
    end

    action SetWidth(number newWidth)
        width = newWidth
    end

    action SetHeight(number newHeight)
        height = newHeight
    end
    
    action GetWidth returns number
        return width
    end

    action GetHeight returns number
        return height
    end

    action AddMouseEventListener(MouseEventListener listener)
        mouseListeners:Add(listener)
    end

    action RemoveMouseEventListener(MouseEventListener listener)
        mouseListeners:Remove(listener)
    end

    action ProcessMouseEvent(MouseEvent event)
        Iterator<MouseEventListener> iterator = mouseListeners:GetIterator()
        if event:eventType = event:CLICKED_MOUSE
            repeat while iterator:HasNext()
                iterator:Next():ClickedMouse(event)
            end
        elseif event:eventType = event:MOVED_MOUSE
            repeat while iterator:HasNext()
                iterator:Next():MovedMouse(event)
            end
        elseif event:eventType = event:DRAGGED_MOUSE
            repeat while iterator:HasNext()
                iterator:Next():DraggedMouse(event)
            end
        elseif event:eventType = event:RELEASED_MOUSE
            repeat while iterator:HasNext()
                iterator:Next():ReleasedMouse(event)
            end
        elseif event:eventType = event:SCROLLED_MOUSE
            repeat while iterator:HasNext()
                iterator:Next():ScrolledMouse(event)
            end
        end

    end

    action Update(number secondsSinceUpdate)
        if currentState not= undefined
            currentState:Update(secondsSinceUpdate)
        end
    end

    action AddState(text stateName, State state)
        states:Add(stateName, state)
        state:SetParent(me)
    end

    action RemoveState(text stateName)
        states:RemoveKey(stateName)
    end

    action SetState(text stateName)
        currentState = states:GetValue(stateName)
        currentStateName = stateName
    end

    action GetCurrentStateName returns text
        return currentStateName
    end

    action BeginCollision(Item item)
        
    end

    action FinishCollision(Item item)

    end

end