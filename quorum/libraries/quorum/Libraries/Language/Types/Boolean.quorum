package Libraries.Language.Types

use Libraries.Language.Object
use Libraries.Language.Support.CompareResult

/*
The Boolean class is the object representation of the primitive type boolean. 


Attribute: Example

class Main
   action Main
      boolean isTrue = true
      Boolean result = test(isTrue)
   end
   action test(Boolean bool) returns Boolean
        return bool
   end
end
*/
class Boolean is a Object
    //The value this Boolean class represents
    private boolean value = false

    /*
        This action sets the value of the boolean object.

        Attribute: Parameter i The boolean value(true or false).

        Attribute: Example

        Boolean isTrue
        isTrue:SetValue(true)
    */ 
    action SetValue(boolean i)
        value = i
    end

    /*
        This action gets the value from the boolean object.

        Attribute: Returns The value of the object.

        Attribute: Example

        Boolean isTrue
        boolean result = isTrue:GetValue()
    */ 
    action GetValue returns boolean
        return value
    end

    /*
        This action gets the value from the boolean object and casts it to
        a number.

        Attribute: Returns The value of the object converted to a number.

        Attribute: Example

        Boolean isTrue
        number result = isTrue:GetNumberValue()
    */ 
    action GetNumberValue returns number
        return cast(number, value)
    end

    /*
        This action gets the value from the boolean object and casts it to
        a text value.

        Attribute: Returns The value of the object converted to text.

        Attribute: Example

        Boolean isTrue
        text result = isTrue:GetTextValue()
    */ 
    action GetTextValue returns text
        return cast(text, value)
    end

    /*
        This action gets the value from the boolean object and casts it to
        an integer.

        Attribute: Returns The value of the object converted to an integer.

        Attribute: Example

        Boolean isTrue
        integer result = isTrue:GetIntegerValue()
    */ 
    action GetIntegerValue returns integer
        return cast(integer, value)
    end

    /*
        This action compares two object values and returns a CompareResult.
        The compare result is either larger if this hash code is larger than
        the object passed as a parameter, smaller, or equal.

        Attribute: Parameter object The object to compare to.
        Attribute: Returns The Comprare result, Smaller, Equal, or Larger. 

        Attribute: Example

        Boolean o
        Boolean t
        CompareResult result = o:Compare(t)
    */  
    action Compare(Object object) returns CompareResult
        CompareResult result

        boolean thisOne = me:value
        Boolean b = cast(Boolean, object)

        boolean other = b:GetValue()

        if thisOne = other
            result:result = result:EQUAL
        elseif thisOne = false and other = true
            result:result = result:SMALLER
        else
            result:result = result:LARGER
        end
        return result
    end

    /*
        This action determines if two objects are equal based on their
        values(true or false).

        Attribute: Parameter object The to be compared.

        Attribute: Returns True if the values are equal and false if they
        are not equal. 

        Attribute: Example

        Boolean o
        Boolean t
        boolean result = o:Equals(t)
    */ 
    action Equals(Object object) returns boolean
        boolean myValue = GetValue()

        Boolean t = undefined
        t = cast(Boolean, object)
        boolean otherValue = t:GetValue()

        return myValue = otherValue
    end
end